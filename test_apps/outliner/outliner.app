application outliner

//generic imports
imports widgets/widgets
imports widgets/tree
imports widgets/masterdetail
imports widgets/popup

//application specific imports
imports data
imports documentmanagement
imports toolbar
imports nodeview
imports json
imports style

section pages

define page root() {
  var d: Document := null;
  outliner_contents(d)
}

define page outliner(doc: Document) {
  outliner_contents(doc)
}

define outliner_contents(doc: Document) {
  //a hook for the popup windows
  placeholder popup {}
  
  block[id:= header, width := "100%"] {
    block[id:= maintitle] { "Outliner" }
    toolbar(doc)
  }

  block[style := "width:100%; height: 8px; background-color: gray; text-align: center"]{
    navigate()[onclick := action { visibility (header , toggle); }]{ "^^^" }
  }

  main(doc)
  
  spacer
  "application generated by WebDSLx"
}

define main(doc: Document) {
  action selectHeader(id: String) {
    var n: HeaderNode := loadHeaderNode(UUIDFromString(id));
    replace(detailView, detailView(n));
  }

  navigate(documentoutline(doc)){ "JSON version" }
  spacer
  
  if (doc == null) {
    "(No document loaded)" 
  }
  else {

    masterdetail() with {
      masterview() {
        form {
          dojoTree(navigate(documentoutline(doc)), doc.root.id.toString())
            [onselect:=selectHeader(null)]
        }
      }
      detailview() {
        detailView(doc.root)
      }
    }

  }
} 
