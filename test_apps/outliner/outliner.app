application outliner

//generic imports
imports widgets/widgets
imports widgets/tree
imports widgets/masterdetail
imports widgets/popup

//application specific imports
imports data
imports documentmanagement
imports toolbar
imports nodeview
imports json
imports style

section pages

define page root() {
  var d: Document := null;
  outliner_contents(d)
}

define page outliner(doc: Document) {
  outliner_contents(doc)
}

define outliner_contents(doc: Document) {
  //a hook for the popup windows
  placeholder popup {}
  
  collapseUp() {
    block[id:= header, width := "100%"] {
      block[id:= maintitle] { "Outliner" }
      toolbar(doc)
    }
  }

  main(doc)
  
  spacer
  "application generated by WebDSLx"
}

define main(doc: Document) {
  if (doc == null) {
    "(No document loaded)" 
  }
  else {
    masterdetail() with {
      masterview() {
        documentTree(doc)
      }
      detailview() {
        detailView(doc.root)
      }
    }

  }
} 

define template documentTree(doc: Document) {
  dojoTree(navigate(documentoutline(doc)), doc.root.id.toString())
    [onselect:=selectHeader(null), ondrop:= ondrop(null,null)]
  
  action selectHeader(id: String) {
    var n: HeaderNode := loadHeaderNode(UUIDFromString(id));
    replace(detailView, detailView(n));
  }
  action ondrop(item: String, target: String) {
    var targetNode: HeaderNode := loadHeaderNode(UUIDFromString(target));
    var itemNode: HeaderNode := loadHeaderNode(UUIDFromString(item));
    itemNode.parent := targetNode;
    relocate(outliner(doc));
  }
}