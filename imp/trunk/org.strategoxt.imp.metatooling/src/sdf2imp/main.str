module sdf2imp/main

imports
  libstratego-lib
  libstratego-xtc // TODO2: XTC detox?
  libjava-front
  sdf-options
  
  EditorService
  
  sdf2imp/parser/-
  sdf2imp/project/-
  sdf2imp/services/-
  sdf2imp/util/-
  sdf2imp/lib/-

overlays

  tname_ASTNODE      = tname |[ org.strategoxt.imp.runtime.parser.ast.AstNode ]|
  tname_VISITOR      = tname |[ org.strategoxt.imp.runtime.parser.ast.AbstractVisitor ]|
  tname_OUTLINERBASE = tname |[ org.strategoxt.imp.runtime.services.OutlinerBase ]|
  tname_FOLDINGBASE  = tname |[ org.strategoxt.imp.runtime.services.FoldingBase ]|
  tname_ITOKEN       = tname |[ lpg.runtime.IToken ]|

strategies

  main-sdf2imp =
    rules(BasePackage := ""); // default output dir
    
    xtc-input-wrap(
      sdf2imp-options
    , sdf2imp
    )

strategies

  sdf2imp =
    check-options;
  
    create-or-copy-parse-table;
    
    verbose-msg(!"Analyzing grammar...");
    sdf2rtg;
    read-from => RTG(_, ProdRules(<id>));
    register-rtg;

    create-default-descriptors;
    create-java-implementation
  
  /** @type FILE -> _ */
  create-default-descriptors =
    // TODO: Only build default descriptor files if they do not exist
    //       or perhaps they shouldn't be built if they aren't imported?
    
    create-folding-descriptor;
    create-outliner-descriptor;
    create-colorer-descriptor;
    create-main-descriptor
    
  create-java-implementation =    
    // Create modules from the descriptors
    create-folding;
    create-outliner;
    create-colorer;
    
    // Create other plugin classes
    create-parse-controller;
    create-build-properties;
    create-project-file;
    create-activator;
    create-classpath;
    create-manifest;
    create-colorer;
    create-plugin-xml
  
  create-or-copy-parse-table =
    where(
      print-filename(|[], <parse-table-target>);
      <eq> (<ParseTable>, <parse-table-target>)
    <+
      ParseTable;
      risky(file-exists |"File not found");
      <copy-file> (<id>, <parse-table-target>)
    )
  <+
    xtc-ensure-file => FILE(input);
    where(
      <file-exists <+ mkdir> "syntax";
      <xtc-command(!"sdf2table")> ["-i", input, "-o", <parse-table-target>,
                                   "-m", <get-sdf-main-module>]
    )

  parse-table-target =
    <conc-strings> ("syntax/", <get-sdf-main-module>, ".tbl")

  sdf2rtg =
    xtc-transform(!"sdf2rtg", !["--ignore-missing-cons", "-m", <get-sdf-main-module> | <pass-verbose>]);
    xtc-transform(!"sglri", !["-p", <xtc-find> "rtg.tbl", "--start", "RTG"])
