module sdf2imp/main

imports
  libstratego-lib
  libstratego-xtc // TODO2: XTC detox?
  libjava-front
  sdf-options
  
  EditorService
  
  sdf2imp/project/-
  sdf2imp/services/-
  sdf2imp/util/-
  sdf2imp/lib/-

overlays

  tname_ASTNODE      = tname |[ org.strategoxt.imp.runtime.parser.ast.AstNode ]|
  tname_VISITOR      = tname |[ org.strategoxt.imp.runtime.parser.ast.AbstractVisitor ]|
  tname_OUTLINERBASE = tname |[ org.strategoxt.imp.runtime.services.OutlinerBase ]|
  tname_FOLDINGBASE  = tname |[ org.strategoxt.imp.runtime.services.FoldingBase ]|
  tname_ITOKEN       = tname |[ lpg.runtime.IToken ]|

strategies

  main-sdf2imp =
    rules(BasePackage := ""); // default output dir
    
    ctree-include(input-descriptor-file + set-input-dir-prefix);
    
    xtc-input-wrap(
      sdf2imp-options
    , sdf2imp
    )
  
  ctree-include(s) = // ensure strategies s are not optimized away when packed to a ctree
    id <+ s

strategies

  sdf2imp =
    create-or-copy-parse-table;
    
    verbose-msg(!"Analyzing grammar...");
    sdf2rtg;
    read-from => RTG(_, ProdRules(<id>));
    
    where(
      create-main-descriptor
    <+
      read-main-descriptor // descriptor already exists, analyze it
    );
    
    register-rtg;
    
    create-syntax-descriptor;
    create-folding-descriptor;
    create-outliner-descriptor;
    create-colorer-descriptor;
    create-references-descriptor;

    create-packed-descriptor-file;
    
    create-java-implementation
    
  create-java-implementation =    
    // Create other plugin classes
    create-parse-controller;
    create-build-properties;
    create-project-file;
    create-activator;
    create-classpath;
    create-manifest;
    create-plugin-xml
  
  create-or-copy-parse-table =
    where(
      print-filename(|[], <parse-table-target>);
      <eq> (<ParseTable>, <parse-table-target; file-exists>)
    <+
      ParseTable;
      risky(file-exists |"File not found");
      <copy-file> (<id>, <parse-table-target>)
    )
  <+
    xtc-ensure-file => FILE(input);
    where(
      <file-exists <+ mkdir> "syntax";
      <xtc-command(!"sdf2table")> ["-i", input, "-o", <parse-table-target>,
                                   "-m", <get-sdf-main-module>]
    )

  parse-table-target =
    <conc-strings> ("syntax/", <get-sdf-main-module>, ".tbl")
    
  // TODO: Cache rtg like parse table (specifying both the rtg and table would make -i optional)

  sdf2rtg =
    xtc-transform(!"sdf2rtg", !["--ignore-missing-cons", "-m", <get-sdf-main-module> | <pass-verbose>]);
    xtc-transform(!"sglri", !["-p", <xtc-find> "rtg.tbl", "--start", "RTG"])
