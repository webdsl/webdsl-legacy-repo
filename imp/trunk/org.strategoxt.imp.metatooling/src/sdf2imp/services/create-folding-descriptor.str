module sdf2imp/services/create-folding-descriptor

imports
  libstratego-lib
  
  sdf2imp/util/-
  sdf2imp/services/ast-form-heuristic
  
strategies

  create-folding-descriptor =
    
    output-default-descriptor-file(
      <descriptor-name> "-Folding.generated"
    , 
      heuristic-folding-productions;
      map(create-folding-or-outliner-rule);
      !|[
        module <descriptor-name> "-Folding.generated"
        
        folding Default folding definitions
        ~
        ~// Folding rules indicate grammar productions to be foldable,
        ~// and take one of the following forms, and :
        ~//
        ~//   - <sort>.<constructor>
        ~//   - <sort>._,
        ~//   - _.<constructor>
        ~
          ~*<id>
      ]|
    )
  
  heuristic-folding-productions =
    StartSymbol;
    
    heuristic-collect-all-rtg(
      // UNDONE: not(is-list-production);
      where(
        heuristic-child-sorts;
        one(heuristic-sort-is-list)
      );
      
      where(
        heuristic-production-has-no-same-sort-descendant
      );
      
      verbose-msg(!"Folding match:", heuristic-asfix-sort-name, constructor-name)
    )
  
  create-folding-or-outliner-rule =
    where(sort := <heuristic-asfix-sort-name>);
  
    if is-list-production then
      !descriptor |[ ~(sort)*.<constructor-name> ]|
    <+
      !descriptor |[ ~(sort)* ]|
    else
      !descriptor |[ ~sort.<constructor-name> ]|
    <+
      !descriptor |[ ~sort ]|
    end
