module normalize-ast

imports
  libstratego-lib
  libstratego-xtc
  libwebdsl-front
  remove-annos

strategies
  main =
    xtc-io-wrap(
      webdslc-options
    , webdslc-usage
    , webdslc-about
    , ![]
    , xtc-normalize
    )

  xtc-normalize =
    id
    ; set-appname
    ; set-default-config
    ; xtc-parse-webdsl
    ; normalize
    ; remove-position-annos
    ; output-webdsl

strategies

  innermost-rep(s) =
    all-consnil(innermost-rep(s))
    ; try(s ; innermost-rep(s))

  all-consnil(s) =
    ?[_|_] < [s|s] + all(s)

signature
  constructors
    RemoveMe : Annotation

strategies
  normalize =
    innermost-rep(desugar)

  desugar :
    [RemoveMe()|lst] -> lst

  desugar =
    normalize-action

  desugar =
    normalize-ui

rules

  normalize-action :
    FunctionNoReturn(x, farg*, Block(stat*)) -> 
    Function(x, farg*, SimpleSort("Void"), Block(stat*))

  normalize-action :
    IfNoElse(e, Block(stat*)) -> If(e, Block(stat*), Block([]))

  normalize-ui :
    IfNoElseTempl(e, elem*) -> IfTempl(e, elem*, [])

rules

  normalize-ui :
    TemplateCallNoArgs(x) -> TemplateCall(x, [], [])

  normalize-ui :
    TemplateCallNoBody(x, e*) -> TemplateCall(x, e*, [])

  normalize-ui :
    TemplateCallBody(x, elem*) -> TemplateCall(x, [], elem*)

  normalize-ui :
    [ TemplateCall("dummy", [], elem1*), elem2*] -> [elem1*, elem2*]

rules

  normalize-ui :
    TemplateCall("navigate", [ThisCall("url", [e])], []) -> 
    TemplateCall("navigate", [ThisCall("url", [e])], [TemplateCallNoBody("output", [e])])

  normalize-ui :
    TemplateCallNoBody("navigate", [ThisCall("url", [e])]) -> 
    TemplateCall("navigate", [ThisCall("url", [e])], [TemplateCallNoBody("url", [e])])

  normalize-ui :
    TemplateCall("navigate", [e1|[e2]], []) -> 
    TemplateCall("navigate", [e2], [TemplateCallNoBody("text", [e1])])

rules

  normalize-ui :
    TemplateCall("section", [e], elem*) -> 
    TemplateCall("section", [], 
      [TemplateCallBody("header", [TemplateCallNoBody("output", [e])])|elem*])

rules

  normalize-ui :
    TemplateCall("text", [Var(x)], []) -> TemplateCall("text", [Var(y)], [])
    where y := <un-double-quote> x

  normalize-ui :
    Text(str) -> TemplateCall("text", [String(str)], [])

  normalize-ui :
    [TemplateCall("text", [e1|[e2|e*]], [])|elem*] -> 
    [TemplateCall("text", [e1], [])|[TemplateCall("text", [e2|e*], [])|elem*]]

  normalize-ui :
    TemplateCall("text", [e], []) -> TemplateCall("output", [e], [])
    where <not(?String(_))> e
