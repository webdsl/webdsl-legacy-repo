module generate

// parses a NanoWebDSL file, 
// applies syntactic normalizations to the AST,
// and pretty-prints the result

imports
  libstratego-lib
  libstratego-xtc
  libwebdsl-front
  remove-annos
  write-files
  java-concat-ids

strategies

  main =
    xtc-io-wrap(
        webdslc-options
      , webdslc-usage
      , webdslc-about
      , ![] // xtc dependencies
      , xtc-normalize
    )

  xtc-normalize = id
    ; set-appname
    ; set-default-config
    ; xtc-parse-webdsl
    ; remove-position-annos
    ; generate
    ; java-concat-ids
    ; output-generated-files
    ; <exit> 0

strategies

  generate =
    collect(generate-code)

strategies

  generate-code = entity-to-java

  entity-to-java : 
    |[ entity x_class { prop* } ]| -> 
    JavaFile(["src"],
      compilation-unit|[
        package domain; 
        @Entity class x_class { 
          ~*cbd*
       } 
      ]|)
    where cbd* := <filter(property-to-java); concat> prop*

  property-to-java-conc :
    |[ x :: String (ann*) ]| ->
    class-body-dec*|[ 
      protected String x = "";

      public String x_get() {
        return x;
      }

      public void x_set(String newitem) {
        x = newitem;
      }
    ]|
    where x_get := <concat-strings> ["get", x]
        ; x_set := <concat-strings> ["set", x]

  property-to-java :
    |[ x :: String (ann*) ]| ->
    class-body-dec*|[ 
      protected String _#x = "";

      public String get#x() {
        return _#x;
      }

      public void set#x(String newitem) {
        _#x = newitem;
      }
    ]|

