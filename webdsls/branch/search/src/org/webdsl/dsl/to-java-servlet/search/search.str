module org/webdsl/dsl/to-java-servlet/search/search

imports
  libstrategolib
  libjava-front

imports
  libwebdsl-front
  libback-end-transformations
  org/webdsl/dsl/to-java-servlet/-

strategies

  property-to-index-annos :
    Property(x, propkind, _, anno*) -> index-annos
    where not (<?Ref()<+?Comp() > propkind)
    	; searchable-annos := <filter(is-searchable-anno;try(add-field-name-arg(|x)))> anno*
    	; not(?[])
  	with rules(CurrentAnalyzerAnnoDef := []) 
	    ; fields-anno := Anno(TypeName(Id("org.hibernate.search.annotations.Fields")), [ElemValArrayInit(<map(searchable-to-field-anno);flatten-list> searchable-annos)])
	    ; analyzer-def-annos := <CurrentAnalyzerAnnoDef<+![]>
	    ; index-annos := [fields-anno, analyzer-def-annos]
  	  	  
  //Reference/Composite properties are embedded index fields, one level of depth right now
  property-to-index-annos :
    Property(x, propkind, _, anno*) -> [anno|[ @org.hibernate.search.annotations.IndexedEmbedded(depth=1, prefix="~x" + ".") ]|]
    where  <?Ref()<+?Comp() > propkind
    	; <fetch(?SearchableAnno(_))> anno*     

  property-to-index-annos :
    DerivedProperty(x, _, _, anno*, _) -> <flatten-list>[anno|[ @org.hibernate.search.annotations.Field(name="~x") ]|, analyzeranno]
    where s := <fetch-elem(?SearchableAnno(_))> anno*  
    with analyzeranno := <analyzer-anno-from-searchableAnno <+ ![]> s

  property-to-index-annos :
    DerivedProperty(x, _, _, anno*, _) -> []
    where not(<fetch(?SearchableAnno(_))> anno*)
  
  //Other side (inverse) of embedded index field  
  property-to-contained-in:
  	p@Property(_, _, _, anno*) -> [anno|[ @org.hibernate.search.annotations.ContainedIn]|]
	where <has-anno(InverseOfSearchable)> p

strategies //field annotations
	
  searchable-to-field-anno :
  	SearchableAnno(sa-args) -> field-anno
  	with  field-args := <map(to-java-field-arg)> sa-args
  		; field-anno := Anno(TypeName(Id("org.hibernate.search.annotations.Field")),field-args)  
  
  add-field-name-arg(|fieldname):
  	SearchableAnno(sa-args) -> SearchableAnno([SA-Argument("name", fieldname) | sa-args])
  	where not (<fetch(?SA-Argument("name", _))> sa-args)  
  
  to-java-field-arg :
  	SA-Argument("analyzer", analyzer-name) -> ElemValPair(Id("analyzer"),anno|[ @org.hibernate.search.annotations.Analyzer(definition="~analyzer-name") ]|)
  	with <do-once(create-and-record-analyzer-definition|"create-and-record-analyzer-definition")<+ ![]> analyzer-name

  to-java-field-arg :
  	SA-Argument("name", field-name) -> ElemValPair(Id("name"),Lit(String([Chars(field-name)])))

strategies //searchconfigs, analyzers

  create-and-record-analyzer-definition = 
  	SearchConfigFromName; analyzer-def-anno := <search-config-to-analyzer-def>; rules(CurrentAnalyzerAnnoDef :+= analyzer-def-anno)
	
  analyzer-anno-from-searchableAnno:
  	SearchableAnno(x) -> [anno|[ @org.hibernate.search.annotations.Analyzer(definition="~x") ]|, analyzerDefAnno]
  	where not(<?""> x )
  		; analyzerDefAnno := <do-once(SearchConfigFromName; search-config-to-analyzer-def|"define analyzer")<+ ![]> x
  	  
  search-config-to-analyzer-def:
  	SearchConfig(name, body) -> <to-analyzer-def(|name)> body
    
  to-analyzer-def(|x_AnalyzerName):
  	SearchConfigBody(charFilters*, tokenizer, tokenFilters*) -> result
  	with mod_charFiltersDefs* := <map(to-char-filter-def)> charFilters*
  	   ; mod_tokenizerDef := <to-tokenizer-def <+ ![]> tokenizer
  	   ; mod_tokenFilterDefs* := <map(to-token-filter-def)> tokenFilters*
  	   ; name_arg := ElemValPair(Id("name"),Lit(String([Chars(x_AnalyzerName)])))
  	   ; charfilter_arg := ElemValPair(Id("charFilters"),ElemValArrayInit(mod_charFiltersDefs*))
  	   ; tokenizer_arg := ElemValPair(Id("tokenizer"), mod_tokenizerDef)
  	   ; tokenfilter_arg := ElemValPair(Id("filters"),ElemValArrayInit(mod_tokenFilterDefs*))
  	   ; if (<?None()>tokenizer) then result := Anno(TypeName(Id("org.hibernate.search.annotations.AnalyzerDef")),[name_arg, charfilter_arg,tokenfilter_arg])
  	     else result := Anno(TypeName(Id("org.hibernate.search.annotations.AnalyzerDef")),[name_arg, charfilter_arg,tokenizer_arg,tokenfilter_arg]) 
  	     end

  to-tokenizer-def:
  	Tokenizer(tokenizername, args) -> result
  	with   e_analyzerargs := <map(to-analyzer-arg)> args
  		; tokenizername' := <to-fully-qlfd-name> tokenizername
  		; result := Anno(TypeName(Id("org.hibernate.search.annotations.TokenizerDef")), [ ElemValPair(Id("factory"), Lit(Class(ClassOrInterfaceType(TypeName(Id(tokenizername')),None()))))
                              , ElemValPair(Id("params"), ElemValArrayInit(e_analyzerargs))
                              ])
  
  to-token-filter-def:
  	TokenFilter(filtername, args) -> result
  	with  e_analyzerargs := <map(to-analyzer-arg)> args
		; filtername' := <to-fully-qlfd-name> filtername
  		; result := Anno(TypeName(Id("org.hibernate.search.annotations.TokenFilterDef")), [ ElemValPair(Id("factory"), Lit(Class(ClassOrInterfaceType(TypeName(Id(filtername')),None()))))
                              , ElemValPair(Id("params"), ElemValArrayInit(e_analyzerargs))
                              ])
  	
  to-char-filter-def:
  	CharFilter(filtername, args) -> result
  	with  e_analyzerargs := <map(to-analyzer-arg)> args
  		; filtername' := <to-fully-qlfd-name> filtername
  		; result := Anno(TypeName(Id("org.hibernate.search.annotations.CharFilterDef")), [ ElemValPair(Id("factory"), Lit(Class(ClassOrInterfaceType(TypeName(Id(filtername')),None()))))
                              , ElemValPair(Id("params"), ElemValArrayInit(e_analyzerargs))
                              ])
                                
  to-analyzer-arg:
  	Argument(x_name, x_value) ->   anno|[ @org.hibernate.search.annotations.Parameter(name = "~x_name", value = x_value) ]|
  
  to-fully-qlfd-name:
  	name -> name'
  	with
  		if (<string-starts-with(|String("org.apache.solr.analysis."))>name) then name' := name
  		else name' := <conc-strings>("org.apache.solr.analysis.",name) end

strategies

  GenerateCodeJavaServlet = generate-java-search-functions; fail
  GenerateCodeJavaServlet = generate-java-searchquery-class ; fail


  generate-java-searchquery-class :
  	ent -> <emit-java-code-local> compilation-unit |[
  	package pkgname;
  	
  	import utils.*;
  	
	public class x_class#SearchQuery extends org.webdsl.search.SearchQuery<x_class> {
		
		public x_class#SearchQuery (){
			entityClass = x_Class.class;
			searchFields = new String[]{ e_sf* };
			fulltextsession = org.hibernate.search.Search.getFullTextSession(e_HibSession);
		}
	}	
  	]|
    where ((x_class, _) := <get-entity-def> ent
       ; x_props := <get-anno(?IndexedAnno(<id>))> ent)
    with e_sf* := <map(\name -> expr |[ "~name" ]| \)> x_props
       ; pkgname := <DomainPackage>
       ; x_Class := <concat-strings> [<pp-java5-to-string> <DomainPackage>, ".", x_class]

  generate-java-search-functions:
    ent -> <emit-java-code-local> compilation-unit |[
  
      package pkgname;
      
      @Partial
      public class x_class {
      
        public static java.util.List<x_Class> x#_(String searchQuery)  {
          return x#_(searchQuery, 1000, 0);
        }
        public static java.util.List<x_Class> x#_(String searchQuery, int limit)  {
          return x#_(searchQuery, limit, 0);
        }
        public static java.util.List<x_Class> x#_(String searchQuery, int limit, int offset)  {
           return new x_Class#SearchQuery().setSearchTerms(searchQuery).setFirstResult(offset).setMaxResults(limit).getResultList();
        }
      }
    ]|
    where (
        (x_class, _) := <get-entity-def> ent
      ; x_props := <get-anno(?IndexedAnno(<id>))> ent
    )
    with x := <concat-strings> ["_static_", "search", x_class]
       ; e_sf* := <map(\name -> expr |[ "~name" ]| \)> x_props
       ; pkgname := <DomainPackage>
       ; x_Class := <concat-strings> [<pp-java5-to-string> <DomainPackage>, ".", x_class]

strategies

  generate-code-java-servlet-once =
      bagof-IndexedEntities
    ; string-sort-annos
    ; cached-generate(generate-java-servlet-indexmanager | "internal_IndexManager")
    ; fail


  generate-java-servlet-indexmanager :
    x_class* ->
    <emit-java-code-local> compilation-unit|[
      package utils;
  
      import java.util.List;
  
      import utils.*;
      import pkgname.*;
      
      import org.webdsl.lang.*;
  
      @SuppressWarnings("all")
      public class IndexManager {
        public static void main(String[] args) {
        	reindex();
        	System.exit(0);
        }
        
        private static void reindex() {
          java.io.PrintWriter out = new java.io.PrintWriter(System.out);
          ThreadLocalOut.push(out);
          // @TODO Globals are not loaded yet
          System.out.println("Starting reindexing of all searchable data...");
          long time = System.currentTimeMillis();
          
          org.hibernate.search.FullTextSession session = org.hibernate.search.Search.getFullTextSession(HibernateUtilConfigured.getSessionFactory().getCurrentSession());
		  
		  bstm*
			 			
		  time = System.currentTimeMillis() - time;
	      System.out.println("Reindexing finished in " + time + "ms total.");
        }
        
        private static boolean reindexEntityClass(Class c, org.hibernate.search.FullTextSession session){
        	System.out.println("reindexing: " + c + "...");
        	long time = System.currentTimeMillis();
        	try {
        		session.createIndexer(c)
 				.batchSizeToLoadObjects( 50 )
				.threadsToLoadObjects( 3 )
 				.threadsForSubsequentFetching( 4 )
 				.startAndWait();
 			}
 			catch(java.lang.InterruptedException ex){
 				System.out.println(ex);
 				return false;
 			}
 			time = System.currentTimeMillis() - time;
 			System.out.println("done in " + time + "ms.");
 			return true;
        }
        public static void optimizeIndex(){
        	System.out.println(new java.util.Date() + ": Optimizing search index started.");
        	org.hibernate.search.Search.getFullTextSession(HibernateUtilConfigured.getSessionFactory().getCurrentSession()).getSearchFactory().optimize();
        	System.out.println(new java.util.Date() + ": Optimizing search index finished succesfully.");
        }
      }
    ]|
    with pkgname := <DomainPackage>
	    ; x_class_set := <make-set> x_class*
	    ; bstm* := <map(\ x_name -> java|[ reindexEntityClass(x_name.class, session); ]|\)> x_class_set 

strategies

is-searchable-anno = ?SearchableAnno(_)
