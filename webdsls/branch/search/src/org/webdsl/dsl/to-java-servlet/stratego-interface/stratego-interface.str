module stratego-interface

imports
  libstrategolib
  libwebdsl-front

strategies

  call-to-java-servlet :
    exp  |[ Stratego(e) ]| ->
    java |[ org.webdsl.tools.strategoxt.StrategoProgram.get(e2, utils.ApplicationContextProperty.class) ]|
    with e2 := <expression-to-java-servlet> e
  
  call-to-java-servlet :
    exp  |[ e.invoke(e_strategy, e_input) ]| ->
    java |[ e2.invoke(e_strategy2, e_input2) ]|
    where SimpleSort("Stratego") := <get-type-anno> e
    with e2          := <expression-to-java-servlet> e
       ; e_input2    := <expression-to-java-servlet> e_input
       ; e_strategy2 := <expression-to-java-servlet> e_strategy
  
  builtin-java-type :
    SimpleSort("Stratego") -> type |[ org.webdsl.tools.strategoxt.StrategoProgram ]|
  
  