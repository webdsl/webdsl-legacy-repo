module org/webdsl/dsl/to-java-servlet/session-variables

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

rules

  GenerateCodeJavaServlet = generate-java-servlet-session-vars-template; fail

  generate-java-servlet-session-vars-template  :
    def@Define(_,_,_,_,_) ->
    <generate-java-servlet-template-partial> 
    class-body-dec* |[
      ~*cbds
    ]|
    with  sents := <get-anno(?SessionEntities(<id>))> def
        ; cbds  := <map(generate-java-servlet-session-vars-template-helper);concat> sents

  generate-java-servlet-session-vars-template-helper :
    x_ident ->
        class-body-dec*
        |[ 
           private t_ent x_ident = null;
                             
           @Partial
           protected void initialize()
           {
             x_ident = (x_ent) env.getVariable("~x_ident");
           }
   
        ]|
    with  t_ent := <capitalize-string; java-type-entity> x_ident
        ; x_ent := <java-type-to-java-name> t_ent
    
  GenerateCodeJavaServlet = page-class-generation-test; generate-java-servlet-session-vars-page; fail
  
  //merge reference properties of session entities at page initialization
  generate-java-servlet-session-vars-page  :
    def@Define(mod*,_,_,_,_) ->
    <generate-java-servlet-partial> 
    class-body-dec* |[
      ~*cbds*
    ]|
    where <fetch(Page)> mod* <+ <fetch(AjaxTemplate)> mod*
    with  sents := <get-anno(?SessionEntities(<id>))> def
        ; cbds*  := <map(generate-java-servlet-session-vars-page-helper-one);concat> sents 
  
  generate-java-servlet-session-vars-page-helper-one:
    x_ident -> 
    class-body-dec* |[    
           //when cancel occurs, session vars need to be reinitialized if they were just created, to remove all pointers to evicted hibernate objects
         //  boolean x_ident#JustCreated = false;
           
           @Partial
           protected void initialize()
           {
             String x_ident#id = null;
             x_ident#id = (String) session.getAttribute("~x_ident");
             t_ent x_ident;
             try{
               x_ident =(x_ent) hibSession.get(x_ent.class,java.util.UUID.fromString(x_ident#id));
               if(x_ident == null){
                 throw new RuntimeException();
               }
             }
             catch(Exception ex){
               ~*<![];if-debug(!bstm*|[  System.out.println("session entity could not be loaded, creating new");  ]|)>
               
               x_ident = new x_ent();
               e_HibSession.save(x_ident);
             }
             env.putVariable("~x_ident",x_ident);
           }
           
           @Partial
           private void storeSessionEntities(){
             t_ent x_ident = (x_ent) env.getVariable("~x_ident");
             //e_HibSession.save(x_ident);
             session.setAttribute("~x_ident",x_ident.getId().toString());
           }  
    ]|
    with  t_ent := <capitalize-string; java-type-entity> x_ident
        ; x_ent := <java-type-to-java-name> t_ent
    
    //    ; bstm* := <filter(generate-java-servlet-session-vars-page-helper-update(|x_ident))> prop*
  //      ; bstm1* := <filter(generate-java-servlet-session-vars-page-helper-store(|x_ident))> prop*
/*
  generate-java-servlet-session-vars-page-helper-update(|x_ident):
    Property(x, k, s, annos) -> java|[
      if (x_ident.get#x#ident() != null) {
        //x_ident.set#x((x_class) e_HibSession.load(x_class.class,x_ident.get#x().getId()));
        x_ident.set#x((x_class) e_HibSession.load(x_class.class,x_ident.get#x#ident()));
      }
    ]|
    where <defined-java-type> s
        ; SimpleSort(t) := s
        ; x_class := t
        ; x_type := "java.util.UUID"
        
  generate-java-servlet-session-vars-page-helper-store(|x_ident):
    Property(x, k, s, annos) -> java|[
      if (x_ident.get#x() != null) {
        //x_ident.set#x((x_class) e_HibSession.load(x_class.class,x_ident.get#x().getId()));
        x_ident.set#x#ident(x_ident.get#x().getId());
        x_ident.set#x(null);
      }
    ]|
    where <defined-java-type> s
        ; SimpleSort(t) := s
        ; x_class := t
        */