module org/webdsl/dsl/languages/search/desugar

imports
  libstrategolib

imports
  libwebdsl-front
  org/webdsl/dsl/languages/search/-
  org/webdsl/dsl/transformation/desugar

rules
	
  desugar-search:
  	TokenFilterNoArgs(t) -> TokenFilter(t, [])
  	
  desugar-search:
  	TokenizerNoArgs(t) -> Tokenizer(t, [])
  	
  desugar-search:
  	CharFilterNoArgs(t) -> CharFilter(t, [])
  
  desugar-search:
  	CharFilter(name,args) -> CharFilter(name',args)
  	where not (<string-ends-with(|"Factory")> name)
  		; name' := <concat-strings> [name,"Factory"]
  		  
  desugar-search:
  	Tokenizer(name,args) -> Tokenizer(name',args)
  	where not (<string-ends-with(|"Factory")> name)
  		; name' := <concat-strings> [name,"Factory"]
  		  
  desugar-search:
  	TokenFilter(name,args) -> TokenFilter(name',args)
  	where not (<string-ends-with(|"Factory")> name)
  		; name' := <concat-strings> [name,"Factory"]

  desugar-search:
    EntityNoSuper(name,entbodydecs){anno*} -> EntityNoSuper(name,entbodydecs2){anno*}
	where entbodydecs2 := <desugar-entity-search(|name)> entbodydecs
    
  desugar-search : 
    Entity(name,super,entbodydecs){anno*} -> Entity(name,super,entbodydecs2){anno*}
	where entbodydecs2 := <desugar-entity-search(|name)> entbodydecs

  desugar-entity-search(|x_class) :
  	entbodydecs -> entbodydecs2
  	where <IndexedEntity> x_class //if it is an entity with searchmapping or searchable annos
    	; <bagof-EntityNameToSearchableProps; flatten-list; ?[]> x_class //and searchable properties are not yet recorded
    	; entbodydecs2 := <desugar-entbody-search(|x_class)> entbodydecs // record the searchable properties and try to desugar
  	
  desugar-entbody-search(|entityName):
    entbodydecs -> entbodydecs2
    with mappings := <bagof-SearchMappingForEnt> entityName
    	; (props,functions,validations,_,rest) := <split-entity-body> entbodydecs
        ; props1 := <try(mapping-to-searchable-anno(|mappings))> props
        ; props2 := <map(repeat(desugar-search-prop))> props1 //desugar the searchable properties 
        ; searchableProps := <filter(is-searchable-prop)> props2
        ; rules(EntityNameToSearchableProps : entityName -> searchableProps ) //record the searchable properties
        ; entbodydecs2 := <concat> [props2,functions,validations,rest]  
  	
  mapping-to-searchable-anno(|mappings):
    props -> props2
    where <?[SearchMappingEmbedded(fieldmappings)|rest]> mappings
    with props1 := <map(try(add-searchable-anno(|fieldmappings)))> props 
    ; props2 := <try(mapping-to-searchable-anno(|rest))> props1
  
  add-searchable-anno(|fieldmappings):
    Property(name, propkind, type, annos) -> Property(name, propkind, type, annos1)
    where mappings := <filter(?SearchFieldMapping(name,_))> fieldmappings
    ; not(?[])
    ; annos1 := <concat> [annos, <map(to-searchable-anno)> mappings]
      
  add-searchable-anno(|fieldmappings):
    DerivedProperty(name, propkind, type, annos, ex) -> DerivedProperty(name, propkind, type, annos1, ex)
    where mappings := <filter(?SearchFieldMapping(name,_))> fieldmappings
    ; not(?[])
    ; annos1 := <concat> [annos, <map(to-searchable-anno)> mappings]
      
  add-searchable-anno(|fieldmappings):
    DerivedPropertyNoAnno(name, propkind, type, ex)  -> DerivedProperty(name, propkind, type, annos, ex)
    where mappings := <filter(?SearchFieldMapping(name,_))> fieldmappings
    ; not(?[])
    ; annos := <map(to-searchable-anno)> mappings
  
  to-searchable-anno:
    SearchFieldMapping(_,parts) -> SearchableAnno(sa-args)
	with sa-args := <map(to-sa-arg <+ to-sa-arg-list) ; flatten-list> parts
  
  to-sa-arg: FieldName(x) -> SA-Argument("name", x)
  to-sa-arg: AnalyzerName(x) -> SA-Argument("analyzer", x)
  to-sa-arg: Boost(x) -> SA-Argument("boost", x)
  to-sa-arg: "autocomplete" -> SA-Argument("autocomplete", "")
  to-sa-arg: "spellcheck" -> SA-Argument("spellcheck", "")
  to-sa-arg-list: SearchMappingAnno(kw*) -> <map(to-sa-arg)> kw*

  desugar-search-prop:
	p@Property(name,propkind,srt,anno) -> Property(name,propkind,srt,anno')
	where(<is-untokenized-default-type> srt; <fetch(anno-needs-desugaring)> anno)		
	with if (<is-numeric-type> srt) 
	  then anno' := <map(try(desugar-searchable-anno-numeric <+ desugar-searchable-anno(|name)))> anno
	  else anno' := <map(try(desugar-searchable-anno-untokenized <+ desugar-searchable-anno(|name)))> anno end		
	   
  desugar-search-prop:
	p@Property(name,propkind,srt,anno) -> Property(name,propkind,srt,anno')
	where(not(<is-untokenized-default-type> srt); <fetch(anno-needs-desugaring)> anno
	 ; anno' := <map(try(desugar-searchable-anno(|name)))> anno)   

  desugar-search-prop:
	p@DerivedProperty(name, propkind, srt, anno, ex) -> DerivedProperty(name, propkind, srt, anno', ex) 
	where(<is-untokenized-default-type> srt; <fetch(anno-needs-desugaring)> anno)
	with if (<is-numeric-type> srt) 
	  then anno' := <map(try(desugar-searchable-anno-numeric <+ desugar-searchable-anno(|name)))> anno
	  else anno' := <map(try(desugar-searchable-anno-untokenized <+ desugar-searchable-anno(|name)))> anno end

  desugar-search-prop:
	p@DerivedProperty(name, propkind, srt, anno, ex)  -> DerivedProperty(name, propkind, srt, anno', ex)
	where(not(<is-untokenized-default-type> srt); <fetch(anno-needs-desugaring)> anno
	 ; anno' := <map(try(desugar-searchable-anno(|name)))> anno)
  
  desugar-searchable-anno(|name):
    SearchableAnno() -> SearchableAnno([SA-Argument("name", name)])

  desugar-searchable-anno(|name):
  	SearchableAnnoBoost(SearchableAnno(sa-args),boost) -> SearchableAnno([SA-Argument("boost", boost) | sa-args])
  
  desugar-searchable-anno(|name):
  	SearchableAnnoBoost(SearchableAnno() ,boost) -> SearchableAnno([SA-Argument("boost", boost)])  
  	
  desugar-searchable-anno(|name):
  	SearchableAnno(sa-args) -> SearchableAnno([SA-Argument("name", name) | sa-args])
  	where not (<fetch(?SA-Argument("name", _))> sa-args)
  	
  desugar-searchable-anno(|name):
  	SearchableAnno(sa-args) -> SearchableAnno([SA-Argument("analyzer", "default") | sa-args])
  	where not (<fetch(?SA-Argument("analyzer", _))> sa-args)
 
  desugar-searchable-anno-untokenized:
 	SearchableAnno(sa-args) -> SearchableAnno([SA-Argument("analyzer", "no") | sa-args])
  	where not (<fetch(?SA-Argument("analyzer", _))> sa-args)
 
  desugar-searchable-anno-numeric:
 	SearchableAnno(sa-args) -> SearchableAnno([SA-Argument("analyzer", "no"), SA-Argument("numeric", "") | sa-args])
  	where not (<fetch(?SA-Argument("analyzer", _))> sa-args)
  
  anno-needs-desugaring= ?SearchableAnno()
  anno-needs-desugaring= ?SearchableAnno(sa-args);not (<fetch(?SA-Argument("name", _))> sa-args;<fetch(?SA-Argument("analyzer", _))> sa-args)
  anno-needs-desugaring= ?SearchableAnnoBoost(_,_)
  
  is-untokenized-default-type = is-numeric-type <+ is-date-type
  is-numeric-type = ?SimpleSort("Int")
  is-numeric-type = ?SimpleSort("Float")
  is-date-type = ?SimpleSort("Date")
  is-date-type = ?SimpleSort("DateTime")
  is-date-type = ?SimpleSort("Time")