/**

 Access control for WebDSL

*/

module org/webdsl/dsl/transformation/access-control-declarations

imports
  libstrategolib

imports
  libwebdsl-front
  org/webdsl/dsl/transformation/-
rules

  declare-definition =
    (oncetd(declare-ac-policy) <+ rules( AC-Policy-Expression := Name("anonymous") ))//default policy
    ; declare-access-control 

  declare-ac-policy=
    ?ACPolicy(pexp)
  ; rules(AC-Policy-Expression := pexp)

  declare-access-control:
    AccessControlDefinition(name,defs) -> AccessControlDefinition(name,defs)
    where rules (
            Security-Check-Types := 
            [
             ["page"]
            ,["action"]
            ,["page" , "action"]
            ,["template"]
            //,["page","template"]
            ,["template","action"]
            //,["template","template"]
            //,["function"]
            ]
          )
        ; <map(declare-ac-rule
            <+ declare-ac-pointcuts
            <+ ?x;info(|["map failed in ac declarations: ",x]))> defs
        
  // declare-ac-principal :
  //  AccessControlPrincipal(ident,props) -> AccessControlPrincipal(ident,props)
  //nothing to declare, gets translated to securityContext which should be declared
 
  declare-ac-rule = 
    ?AccessControlRule(checktype,matchstring,fargs,check,acrules)
    
  declare-ac-pointcuts:
    AccessControlPointcut(pname,fa,elems) -> AccessControlPointcut(pname,fa,elems) 
    where {| pointcut-formal-argument :
             <filter(\ ar@Arg(ident,thesort)->ar where rules (pointcut-formal-argument : ident -> ar) \)> fa
           ; <filter(declare-ac-pointcut-element(|pname,fa))> elems
          |}  
          
  declare-ac-pointcut-element(|pname,fa):
    AccessControlPointcutElement(ident,matchident,faidents,wc) -> AccessControlPointcutElement(ident,matchident,faidents,wc)
    where faidentsargs := MatchArgs(<map(pointcut-formal-argument)> faidents,wc)
        ; newPointcutList := <concat> [<AccessControlPointcuts<+![]> (pname,fa),[(ident,matchident,faidentsargs)]]
        ; rules(
            AccessControlPointcuts: (pname,fa) -> newPointcutList
            is-access-control-pointcut: (pname,fa)
          )   
          
  // uses: declare-global-funcs = ?fu@Function(f, args, s, b) ...
  declare-definition:
    p@Predicate(f,args,b) -> p//<declare-global-funcs> Function(f, args, SimpleSort("Bool"), b)
    where rules (IsPredicate : f)