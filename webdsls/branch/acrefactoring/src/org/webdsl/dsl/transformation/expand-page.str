module expand-page

imports 
  libstrategolib
  
imports 
  libwebdsl-front

rules

  expand-page-templates =
    alltd-l(expand-page)

  expand-page =
    //?Define([Page()], name, args, body1)
    [ ?tdef|[ define page x(farg*) { elem* } ]| 
      ; expand-template-definition | expand-page-templates ]
    
  expand-page =
    ?[tdef|[ define x(farg*) { elem* } ]| | <expand-page-templates>]
           
  expand-template-definition :
    tdef|[ define mod* x(farg*){elem1*} ]| -> tdef|[ define mod* x(farg*){elem2*} ]|
    where {| TemplateDefinition, TemplateArguments, IsPage, IsEmail
           : elem2* := <filter(not(declare-template-definition)); map(expand-element)> elem1*
           |}
    
  expand-template-definition-old :
    Define(mods, name, args, body1) -> <dbg(|"expanded")> Define(mods, name, args, body2)
    where dbg(|"expanding definition")
        ; {| TemplateDefinition, TemplateArguments, IsPage, IsEmail
           : elems := <filter(not(declare-template-definition))> body1
           ; body2 := <map(expand-element)> elems
           |}
           
  expand-element = dbg(|"expand-element: "); (
    expand-template-call
    <+ Subst
    <+ all(expand-element)
  )
             
  expand-template-call :
    elem|[ x(e*){elem1*} ]| -> elem|[ dummy() {elem2*} ]| // @todo: fixme!!!!!
    where tdef|[ define mod* x(farg*) { elem3* } ]| := <TemplateDefinition; rename-bound-variables> x
        ; {| Subst, TypeOf, Elems
           : <zip(bind-variable)> (farg*, <alltd(Subst)> e*)
           ; rules( Elems := elem1* )
           ; elem2* := <map(expand-element)> elem3*
           ; str := x
           |}
  
  expand-template-call-old :
    call@TemplateCall(name, args, elems1) -> <dbg(|"expanded")> TemplateCall("div", [String(name)], elems2)
    where <dbg(|"expanding")> call
        ; Define(mods, name, fargs, body) := <TemplateDefinition; dbg(|"rename in"); rename-bound-variables; dbg(|"rename out")> name
        ; {| Subst, TypeOf, Elems
           : <zip(bind-variable)> (fargs, <alltd(Subst)> args)
           ; rules( Elems := elems1 )
           ; elems2 := <map(expand-element)> body
           |}
           
    // @todo template definitions in included definitions are not used
          
   bind-variable =
     ?(Arg(x, s), e)
     ; rules( 
         Subst  : Var(x) -> e 
         Subst  : Qualified(x, y) -> Qualified(e, y)
         TypeOf : x -> s 
       )

strategies // bound variables
  
  rename-bound-variables = 
    RenameTemplateVar
    <+ rename-template-definition
    <+ rename-vardecl 
    <+ rename-for
    <+ rename-forall
    <+ rename-select
    <+ rename-action-definition
    <+ all(rename-bound-variables)
  
  rename-template-definition :
    Define(mods, name, args1, body1) -> Define(mods, name, args2, body2)
    where {| RenameTemplateVar
           : args2 := <map(rename-arg)> args1
           ; body2 := <rename-bound-variables> body1
           |}
           
  rename-action-definition :
    Action(name, args1, body1) -> Action(name, args2, body2)
    where {| RenameTemplateVar
           : args2 := <map(rename-arg)> args1
           ; body2 := <rename-bound-variables> body1
           |}

  rename-action-definition :
    InitAction(body1) -> InitAction(body2)
    where {| RenameTemplateVar
           : body2 := <rename-bound-variables> body1
           |}
           
  rename-arg :
    Arg(x, s) -> Arg(y, s)
    where y := <rename-var> x

  rename-vardecl :
    VarDecl(x, s) -> VarDecl(y, s)
    where y := <rename-var> x
        
  rename-vardecl :
    VarDeclInit(x, s, e1) -> VarDeclInit(y, s, e2)
    where e2 := <rename-bound-variables> e1
        ; y := <rename-var> x
        
  rename-for :
    For(x, s, e1, Filter(we1, obe1), elems1) -> For(y, s, e2, Filter(we2, obe2), elems2)
    where {| RenameTemplateVar
           : y := <rename-var> x
           ; e2 := <rename-bound-variables> e1
           ; elems2 := <rename-bound-variables> elems1
           ; we2 := <rename-bound-variables> we1
           ; obe2 := <rename-bound-variables> obe1
           |}
           
  rename-forall :
    ForAll(x, s, Filter(we1, obe1), elems1) -> ForAll(y, s, Filter(we2, obe2), elems2)
    where {| RenameTemplateVar
           : y := <rename-var> x
           ; elems2 := <rename-bound-variables> elems1
           ; we2 := <rename-bound-variables> we1
           ; obe2 := <rename-bound-variables> obe1
           |}

  rename-select :
    Select(x, s, lab, e1) -> Select(y, s, lab, e2)
    where {| RenameTemplateVar
           : y := <rename-var> x
           ; e2 := <rename-bound-variables> e1
           |}

  rename-var :
    x -> y
    where y := <newname> x
        ; rules(
            RenameTemplateVar : Var(x)           -> Var(y)
            RenameTemplateVar : Qualified(x, f)  -> Qualified(y, f)
            RenameTemplateVar : Assignment(x, e) -> Assignment(y, e2) where e2 := <try(sometd(rename-bound-variables))> e
          )
     
strategies

  propagate-component-names =
    alltd(propagate-component-names-in-page)
    
  propagate-component-names-in-page :
    Define([Page()], name, args, body1) -> Define([Page()], name, args, body2)
    where {| PrefixComponent, SpecializeActionCall
           : <map(declare-component-prefix(|name))> args
           ; <get-page-vardecls; map(declare-component-prefix(|name))> body1
           ; declare-specialize-action-call(|name)
           ; body2 := <propagate-component-names-in-expression> body1
           |}

  propagate-component-names-in-expression =
    rec x (alltd(PrefixComponent <+ not-init-action <+ specialize-action-call(x)))
    
  specialize-action-call(x) =
    TemplateCall("action" <+ "actionLink", [id, try(SpecializeActionCall); x], [])

  not-init-action =
    ?InitAction(_)

  not-in-call =
    ?Call(_, _, _)
    
  declare-component-prefix(|name) =
    ?Arg(x, s); where(<declare-component-prefix(|name)> x)
    
  declare-component-prefix(|name) =
    ?VarDecl(x, s); where(<declare-component-prefix(|name)> x)
    
  declare-component-prefix(|name) =
    ?VarDeclInit(x, s, e); where(<declare-component-prefix(|name)> x)
    
  declare-component-prefix(|name) =
    ?x
    ; rules( 
        PrefixComponent : 
          Var(x){Type(t)} -> FieldAccess(Var(name){Type("Component")}, x){Type(t)}
          where not(<?"Component"> t)
      )

  declare-specialize-action-call(|name) =
    rules (
      SpecializeActionCall :
        ThisCall(f, args) -> <dbg(|"specialized: ")> Call(Var(name){Type("Component")}, f, args)
        where dbg(|"SpecializeActionCall: ") 
    )
    
strategies

  get-page-vardecls = 
    collect(fail
      <+ \ d@VarDecl(_,_) -> [d] \
      <+ \ d@VarDeclInit(_,_,_) -> [d] \
      <+ \ Action(_,_,_) -> [] \
      <+ \ InitAction(_) -> [] \
    ); concat
    
    
    