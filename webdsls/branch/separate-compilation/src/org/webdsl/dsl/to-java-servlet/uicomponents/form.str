module org/webdsl/dsl/to-java-servlet/uicomponents/form

imports
  libstratego-lib
  libjava-front
  libwebdsl-front
  org/webdsl/dsl/to-java-servlet/uicomponents/navigate
  org/webdsl/dsl/to-java-servlet/template-render
  org/webdsl/dsl/to-java-servlet/template-action-inputs
  org/webdsl/dsl/to-java-servlet/ajax/html-utils
  org/webdsl/dsl/to-java-servlet/core-to-java-servlet
  org/webdsl/dsl/to-java-servlet/control-flow-to-java-servlet
   
rules // forms

  elem-to-java-servlet : 
    tc@TemplateCall("form", [],passign*, elems) ->
    bstm* |[
      ident = e_ident;
      ThreadLocalPage.get().setFormIdent(ident);//for actionLink javascript code and ajax actions  //TODO breaks with nested forms, use stack
      if(ThreadLocalPage.get().getParammap().get(ident)!=null)
      {
        ThreadLocalPage.get().setInSubmittedForm(true); // ok, because no nested forms, otherwise need to count just like inForLoop variable
      }
      ThreadLocalPage.get().formRequiresMultipartEnc = false;
      out.print("<form name=\""+ident+"\" id=\""+ident+
        "\" action=\""+e_resulturl+
        "\" accept-charset=\"UTF-8\" method=\"POST\" "+e_attrs1);
      //create temporary out stream so the decision for multipart enctype can be made after handling the contents of the form
      java.io.StringWriter x_stringwr = new java.io.StringWriter();
      out = new java.io.PrintWriter(x_stringwr);
      ThreadLocalOut.push(out);
       
      out.print(">");
      out.print("<input type=\"hidden\" name=\""+ident+"\" value=\"1\" />");
      out.print(ThreadLocalPage.get().getHiddenParams()); // page arguments
      bstm*
      out.print("</form>");
      
      //set 'out' back to the original out stream
      ThreadLocalOut.popChecked(out);
      out = ThreadLocalOut.peek();
      if(ThreadLocalPage.get().formRequiresMultipartEnc){
        out.write(" enctype=\"multipart/form-data\"");
      }
      out.write(x_stringwr.toString());
  
      ThreadLocalPage.get().formRequiresMultipartEnc = false;
      ThreadLocalPage.get().setInSubmittedForm(false);
      ThreadLocalPage.get().setFormIdent("");
    ]|
    with  bstm* := <elems-to-java-servlet> elems
        ; formident := <get-formnumber> tc
        ; e_attrs1 := <to-html-attrs> passign*
        ; x_stringwr := <newname> "stringwriter"
        ; x_out := <newname> "out"
        ; e_url := java:expr |[ utils.HTMLFilter.filter(ThreadLocalPage.get().getPageUrlWithParams()) ]|
        ; e_resulturl := <convert-abs-url-based-on-secure-modifier(|passign*)> e_url
        ; e_ident := <for-loop-counter-wrap-code-expr> java:expr|["~formident"+uniqueid]|
      
rules

  handle-actions-to-java-servlet: 
    tc@TemplateCall("form", [],passign*, TemplateBody(elems)) ->
    <for-loop-counter-wrap-code>
    bstm*|[
        ident += "~formident"+uniqueid;
        ThreadLocalPage.get().setFormIdent(ident);//for actionLink javascript code and ajax actions 
        //if(ThreadLocalPage.get().getParammap().get(ident)!=null)
        //{
        bstm1*
        //}
    ]|
    with // bstm* := <collect-om(handle-actions-input-to-java-servlet)> elems
         bstm1* := <collect-om(handle-actions-to-java-servlet); concat-with-sublists> elems
        ; formident := <get-formnumber> tc   
