module org/webdsl/dsl/transformation/separation

imports 
  libstrategolib 
  libwebdsl-front
  libsepcomp
  org/webdsl/dsl/transformation/-
  org/webdsl/dsl/typechecker/typechecker

rules
  
  is-emitted-mod = string-starts-with(|"(gen)")
  
  get-toplevel-definition = TopLevelDefinitions
  
  is-imported = where(ModuleToDefs)
  
  all-modules = all-keys-ModuleToDefs
  is-module-modified = where(ModifiedFile)
  
  all-definitions = all-modules; mapconcat(module-to-definitions)
  is-definition-loaded = where(get-toplevel-definition)
  
  notify-rules-loaded =
    rules(NewDefinitionsLoaded := True()) // something changed, need new desugar phase
  
  ignore-dr-tracking =
      ?"CurrentDefinitionKey"
   <+ ?"MemoOpenParseTable" <+ ?"WebDslPpTable"    // whole parse table!
   <+ ?"TopLevelDefinitions"
   <+ ?"ProcedureEntities"
   <+ ?"DoneOnce"
   <+ ?"InTypeCheckingPhase"
   <+ ?"InPage"
   <+ ?"InnermostNormalForm"
   <+ ?"DesugarInDefineContextApplied"
   <+ ?"InTemplate"
   <+ ?"InnermostApplied"
   <+ ?"LogTimeSection"
   <+ ?"LogTime"
   <+ ?"AddWebDSLDefs"
   
  trigger-recompilation =
    ?defkey;
    load-unmodified-def;    // get ast
    in-typechecking-phase(rename-and-store);
    <is-definition-loaded <+ fatal-err(|"Definition not loaded?")> defkey;
    <add-typecheck-emits> defkey;
    rules(NewDefinitionsLoaded := True())
    
  module-to-definitions = ModuleToDefs
  
  all-modified-files = all-keys-ModifiedFile
  
  store-signatures =
    with(
      all-modified-files;
      map({
          ?mod;
          module-to-definitions;
          mapconcat(bagof-CreatedRules; concat) => defrules;
          content := (<get-webdsl-version>, (<module-to-definitions> mod, <ModuleImports> mod, defrules));
          cachefile := <path-to-cache-path> mod;
          <write-file(|cachefile)> content
      })
    )
    
  load-dependent-typecheck-modules =
    find-dependent-defs;
    map(load-unmodified-def; store-module-def)
  
  get-module-signature = CachedVersion; ?(_,_,<id>)
  
  def-key-to-module = def-key-to-file
  
  get-definition-owner: key{owner} -> owner
  
  def-key-to-string: At(mod, line, col) -> $[[mod]_[<int-to-string> line]_[<int-to-string> col]]
  