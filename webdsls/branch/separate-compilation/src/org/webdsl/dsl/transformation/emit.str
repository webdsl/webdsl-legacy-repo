module org/webdsl/dsl/transformation/emit

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front
  
signature constructors
  
  IsGenerated : Term
  
rules	// emit top-level definition

	// @TODO: rename to emit-webdsl-def
	emit-webdsl-code = 
      declare-all
    ; emit-webdsl-code-no-declare
    
  // Used for separate declare / emit phase
	emit-webdsl-code-no-declare =
      where(InTypeCheckingPhase); emit-webdsl-code-no-rename
  <+  with-toplevel-name(
	        rename-all
	      ; emit-webdsl-code-no-rename
	    )
	  
	//used to support syntax variations and backwards compatibility, rename strategy will need to cope with the variations, desugar can normalize to one
  // renaming is not needed for these one-to-one transformations that have the same semantics
	emit-webdsl-code-no-rename =
			?def
    ; (CurrentDefinitionKey <+ !"") => key
    ; rules(AddWebDSLDefs :+= (key, def))
    
	desugar-emit-all-defs =
      bagof-AddWebDSLDefs
    ; where(length => n1)
    ; emit-for-loaded-defs
    ; where(length => n2)
    ; not(<eq> (n1, n2))                // nothing applied? fail
  
  // Emit for definitions that are selected for recompilation.
  // Fails if nothing could be emitted. 
  emit-for-loaded-defs: emits -> remaining
    with 
      remove-all({
          ?(owner,def)
        ; <emit-with-owner(|owner)> def
      })
    ; ?remaining
    ; rules(AddWebDSLDefs :- _)
    ; map(\x -> <rules(AddWebDSLDefs :+= x)> \)
      
  emit-with-owner(|owner): def -> def
    where
      if <?At(x,y,z)> owner; <not(is-emitted-mod)> x then
        // Emitted from an ast definition
        if <is-definition-loaded> owner then
            // Definition is recompiling, emit
            key := At(x,"emit",<newname>"emit"){owner}
          ; <store-module-def(|key)> def
        else
            // Some declare rules emit definitions for each entity/page, these emit should not be
            // emitted until the originating definition is loaded. 
            fail
        end
      else
        // Emitted from another compiler emit
        <store-module-def> def
      end
		
rules // emit section
  
  // TODO: dr is never actually applied
  emit-webdsl-section =
      declare-all
    ; rename-section
    ; ?sec
    ; rules(
        AddWebDSLSection :+ Application(qid, sections) -> Application(qid, [sec|sections])
      )
  
rules // emit builtins (during declare)
	
	add-builtin-rule = ?rule; rules(AddWebDSLRules :+= rule)
	
	add-builtins-to-ast =
			bagof-AddWebDSLDefs
	    // TODO: track rules during rename ?
    ; map(\(owner, def) -> (owner, <rename-top> def) \)
	  ; try(emit-for-loaded-defs)
		  // TODO: same as defs
		; !AccessControlDefinition(None(), <bagof-AddWebDSLRules>)
		; rename-and-store
		; rules(AddWebDSLRules :- _)
	
	/*
	get-builtin-sections: _ -> [defsec,rulesec]
    with  defsec := Section("builtins", <bagof-AddWebDSLDefs; map(rename-toplevel)>)
        ; rulesec := AccessControlDefinition(None(), <bagof-AddWebDSLRules; map(rename-toplevel)>)
        ; rules(AddWebDSLDefs :- _)
        ; rules(AddWebDSLRules :- _)
	*/