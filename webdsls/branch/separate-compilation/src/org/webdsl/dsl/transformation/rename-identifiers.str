module org/webdsl/dsl/transformation/rename-identifiers

imports 
  libstrategolib
  
imports 
  libwebdsl-front
  org/webdsl/dsl/transformation/-
  org/webdsl/dsl/typechecker/-
  org/webdsl/dsl/languages/data-model/-
  org/webdsl/dsl/languages/ui/-

rules

  rename-identifiers-top =
    transform-defs-tracked(
      {| ExistingRenamings, GlobalToLocalMap:
          with-toplevel-name(rename-identifiers)
      |}
    )
    
	rename-identifiers = alltd(
      rename-bound-variables; try(rename-annotations)
   <+ rename-annotations; try(some(rename-identifiers))
  )
   
  rename-annotations = ?x{<not(?[]); ?anno*>}; !x{<rename-identifiers> anno*}

  rename-bound-variables =
       exclude-from-var-renaming
    <+ ExcludeFromVarRenaming  
    //<+ Define(id,id,rename-identifiers,rename-identifiers) // otherwise local template definitions get renamed and dynamic scoping breaks (eg the local body will be inserted as body12 in the lookup hashmap)
    <+ (
        check-for-templatebody-cons
      <+ rename-template-def-id
      <+ rename-template-call-id
      <+ rename-action-def-id
      <+ rename-test-id
      <+ rename-global-var
     ); try(some(rename-identifiers)) // traverse
    <+ use-local-renaming
  
  exclude-from-var-renaming = ?"this"
  
  use-local-renaming :
    x{anno*} -> z{new-anno*}
    where <is-string> x
        ; y := <fetch-elem(is-string)> anno*
        ; <remove-all(?y)> anno*
        ; ![OriginalNameAnno(x{}) | <id>]
        ; ?new-anno*
        ; z := <global-to-local-name(|<strip-annos> x)> y
        
  // Make a locally unique name from a globally unique name
  global-to-local-name(|x) = GlobalToLocalMap
    
  global-to-local-name(|x): y -> z
    where TopLevelDefineName => toplevel
        ; n := <ExistingRenamings; inc <+ !0> x
    with  z := <concat-strings> [x, <int-to-string> n]
        ; rules(
            ExistingRenamings : x -> n
            GlobalToLocalMap : y -> z
          )
          
   use-global-renaming: x{anno*} -> y{anno2*, OriginalNameAnno(x)}
    with  y := <fetch-elem(is-string)> anno*
        ; anno2* := <remove-all(?y)> anno*
          
rules

  rename-template-def-id :
    Define(mod*, x, farg*, targs, elem*){anno*} ->
    Define(mod*, x1{orig}, farg*, targs, elem*){anno*}
    where <fetch(?Page())> mod*
       <+ <fetch(?Email())> mod* //no overloading for emails yet
    with  x1 := <if <fetch(TemplateModOverride)> mod* then <concat-strings>["$Override$",x] else !x end>
    with  orig := OriginalNameAnno(x{})
                 
  rename-template-def-id :
    Define(mod*, x, farg*, targs, elem*){anno*} ->
    Define(mod*, y{orig}, farg*, targs, elem*){anno*}
    where not(<fetch(?Page())> mod*)
        ; not(<fetch(?Email())> mod*)
    with  xsa := <strip-annos> x
        ; y := <TemplateNewName> (xsa,<template-formal-arguments-to-types;strip-annos> farg*)
        ; orig := OriginalNameAnno(x{})
    
  check-for-templatebody-cons : // catches typical omission in desugaring rules: missing TemplateBody
    TemplateCall(x, arg*, passign*, elem*) -> <debug(!"INTERNAL ERROR: found template that is missing TemplateBody constructor around its elements: "); <exit> 1> 
    where not(<?TemplateBody(_)> elem*) 
    
  rename-template-call-id :
    tc@TemplateCall(x, arg*, passign*, TemplateBody(elem*)){anno*}
    -> TemplateCall(y{orig}, arg*, passign*, TemplateBody(elem*)){new-anno*}
    where y := <CallToDefinedTemplate> x
    with  orig := OriginalNameAnno(x{})
        ; ![CallToDefinedAnno() | anno*]
        ; ![CalledTemplateArgs(<TemplateArguments> y) | <id>]
        ; ?new-anno*

  rename-template-call-id :
    tc@TemplateCall(x, arg*, passign*, TemplateBody(elem*)){anno*} 
    -> TemplateCall(x{orig}, arg*, passign*, TemplateBody(elem*)){new-anno*} 
    where types := <CallToRequiredTemplate> x
    with  orig := OriginalNameAnno(x{})
        ; ![CallToRequiredAnno() | anno*]
        ; ![CalledTemplateArgs(<map(!Arg("dummyname",<id>))> types) | <id>]
        ; ?new-anno*

  rename-template-call-id :
    tc@TemplateCall(x, arg*, passign*, body)
    -> TemplateCall(y, arg*, passign*, body)
    where <CallToBuiltInTemplate> x
    with  y := <strip-annos> x //code generator assumes that built-in templates don't have annotations on the name
  
  /** many desugarings, such as output(String) -> text(String) currently do not rename the resulting call, 
   *  therefore this sanity check is disabled for now. 
   *  @TODO call rename on all generated templatecalls and enable this rule, it will catch template lookup issues early
  rename-template-call-id :
    tc@TemplateCall(x, arg*, passign*, TemplateBody(elem*)){anno*} -> tc
    where not(<CallToBuiltInTemplate <+ CallToBuiltInTemplate <+ CallToRequiredTemplate> x)
    with  debug(!"Internal error in transformation/rename-identifiers.str: templatecall could not be resolved as declared, built-in, or required: ");<exit> 1
   */ 
      
  // Actions are lifted to toplevel elements, so their names should remain globally unique
  rename-action-def-id: 
      Action(amod*, nm1, args, b) ->
      Action(amod*, <use-global-renaming> nm1, args, b)

  rename-action-def-id:
      ActionCall(name, args){anno*} -> ActionCall(<use-global-renaming> name, args){anno*}
       
  rename-test-id:
      Test(nm1, body) -> Test(<use-global-renaming> nm1, body)

  rename-global-var:
      v@VarDeclInit(x,_,_) -> <fail>
    where <IsGlobal> x
    with  rules(GlobalVarInits :+= v)
    
  rename-global-var:
      s@SessionEntity(_,_) -> <fail>
    with  rules(SessionEntitiesBackend :+= s)