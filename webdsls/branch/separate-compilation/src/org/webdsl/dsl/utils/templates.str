module org/webdsl/dsl/utils/templates

imports
  libstrategolib
    
rules     
    
  template-formal-arguments-to-types =
    map(?Arg(_,<id>); strip-ref-sort); strip-all-annos
    //type could contain annotation, e.g. SimpleSort("URL"){IsRefSort}, need strip-annos because these types are used in LHS of DR
    
rules //utility rules to get the unique name generated for a template

  generate-template-name-fargs :
    (n,fargs) -> name
    with t* := <template-formal-arguments-to-types> fargs
       ; name := <generate-template-name> (n,t*) 

  generate-template-name :
    (n,t*) -> name
    with arg_part := <map(generate-template-name-helper)> t*
       ; name := <concat-strings> [n | arg_part]
       
  generate-template-name-helper:
    GenericSort("SDFInput",[tablename]) -> <concat-strings> ["SDFInput",tablename] //no SimpleSort cons around 'tablename'
       
  generate-template-name-helper:
    SimpleSort(s) -> s
    
  generate-template-name-helper:
    GenericSort(x,[srt]) -> <concat-strings> [x,<generate-template-name-helper> srt]