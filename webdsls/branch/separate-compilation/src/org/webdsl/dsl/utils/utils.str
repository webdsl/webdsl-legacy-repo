module org/webdsl/dsl/utils/utils

imports
  libstrategolib
  libjava-front
  libstratego-gpp
  libstratego-xtc // TODO: XTC detox
  libstratego-aterm

imports
  org/webdsl/dsl/syntax/-
  org/webdsl/dsl/utils/xml-doc2abox

strategies

  info(|msg) = log(|Info(), msg)

strategies

  filter-warn(s | msg) =
    filter(s <+ fatal-err(|msg))

  filter-concat-warn(s | msg) =
    filter(s; if not(is-list) then debug(!"should be a list: "); fail end <+ debug(!msg); fail); concat

  concat-with-sublists=
    foldr(![], (is-list, id) < conc + MkCons)   
  
strategies

  remove-leading-underscore = try(string-starts-with(|"_");explode-string;?[_|<id>];implode-string)

  capitalize-string =
    explode-string
    ; [to-upper | id]
    ; implode-string

  decapitalize-string =
    explode-string
    ; [to-lower | id]
    ; implode-string

  list-to-separated-string(|separator) =
    ?[head|tail]; where (not(<?[]> tail))
    ; !<concat-strings> [head, separator, <list-to-separated-string(|separator)> tail]

  list-to-separated-string(|separator) =
    ?[head]; !head

  make-human-readable =
    explode-string
    ; make-human-readable-internal
    ; concat
    ; implode-string
    ; if string-starts-with(|" ") then
        explode-string
        ; Tl
        ; implode-string
      end
    ; capitalize-string

  make-human-readable-internal :
    [f|r] -> [[f]|<make-human-readable-internal> r]
    where <not(is-upper)> f

  make-human-readable-internal :
    [F|r] -> [[' ', f]|<make-human-readable-internal> r]
    where <is-upper> F
        ; f := <to-lower> F

  make-human-readable-internal :
    [] -> []
    
strategies // do-once is applied on the same term only once for each action

  do-once(s|action) = 
    ?elem
    ; not(<DoneOnce> (action, elem))
    ; s
    ; rules( DoneOnce : (action, elem) )
    
strategies // print a pretty-printed aterm

  pp-aterm =
    pp-aterm-box; box2text-string(|80); debug

  pp-aterm(|msg) =
    where(<try(is-list; concat-strings); debug> msg)
  ; pp-aterm
  
  pp-aterm(msg) =
    where(msg; debug)
  ; pp-aterm
  
  pp-aterm(|msg1,msg2) = 
    pp-aterm(|<conc-strings> (msg1, msg2))
    
rules 
  find-doubles :
    [] -> []

  find-doubles :
    [a|k] -> <find-doubles> k
    where not(<elem> (a, k))

  find-doubles :
    [a|k] -> [a|<find-doubles> k]
    where <elem> (a, k)
    

strategies
  // @TODO: Take scopes into account (see POPL course)

  rename-var(|from, to) =
    topdown(try(rename-var-aux(|from, to)))

  rename-var-aux(|from, to) :
    Var(from) -> Var(to)

  rename-var-aux(|from, to) :
    Qualified(from, f) -> Qualified(to, f)

  rename-var-aux(|from, to) :
    Assignment(from, e) -> Assignment(to, e)

rules //concatenates a list of java expressions to a single expression using the + operator (useful for string or numeric expressions)
  generate-addition-exp :
    [x|xs] ->  e|[ e0 + e1 ]|
    where not([] := xs)
        ; e0 := x
        ; e1 := <generate-addition-exp> xs

  generate-addition-exp :
    [e0] -> e|[ e0 ]|
    
  generate-addition-exp :
    [] -> e|[ "" ]|
    
strategies

  trim-initial-dots-slashes = ltrim-chars(?'.'<+?'/')
  
strategies

  GeneratedOutputDir = !"src-generated"
  