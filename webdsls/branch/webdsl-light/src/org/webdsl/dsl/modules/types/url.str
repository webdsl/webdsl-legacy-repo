module org/webdsl/dsl/modules/types/url

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  elem-to-xhtml :
    elem|[ inputURL(e){} ]| ->
    %>
      <s:decorate>
        <h:inputText styleClass="inputURL" value="<%= estring %>" required="<%= req %>"/>
      </s:decorate>
    <%
    where estring := <arg-to-value-string> e
        ; req := <input-of-required-property(|e)>

    // @todo validate URL

rules // types

  builtin-java-type :
    SimpleSort("URL") -> type|[ String  ]|

  builtin-java-type-default-value :
    SimpleSort("URL") -> java|[ "" ]|

  type-compatible =
    ?(SimpleSort("URL"), SimpleSort("String"))

rules // desugar

  DeriveInput :
    elem|[ input(e){} ]| -> elem|[ inputURL(e){} ]|
    where SimpleSort("URL") := <type-of> e

  DeriveOutput :
    elem|[ output(e){} ]| -> elem|[ navigate(url(e)){url(e)} ]|
    where SimpleSort("URL") := <type-of> e

