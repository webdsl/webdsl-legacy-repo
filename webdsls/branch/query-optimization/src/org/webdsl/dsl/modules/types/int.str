module org/webdsl/dsl/modules/types/int

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  is-simple-sort = ?SimpleSort("Int")  
  extends-builtin : "Int" -> "Object"
  built-in-template-rule-setup = <declare-built-in-template-rules> ("inputInt",[SimpleSort("Int")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputInt",[SimpleSort("Int")]);fail

rules // type

  type-default-value :
    SimpleSort("Int") -> exp|[ 0 ]|

  eq-type-of :
    Int(x) -> SimpleSort("Int")

rules // desugar
  DeriveInput :
    elem|[ input(e)[passign*]{elem*} ]| -> elem|[ inputInt(e)[passign*]{elem*} ]|
    where SimpleSort("Int") := <type-of> e

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputInt(e)[passign*]{} ]|
    where SimpleSort("Int") := <type-of> e
    
rules // conversion

 check-builtin-signature :
    (SimpleSort("Int"), "floatValue", []) ->  SimpleSort("Float")