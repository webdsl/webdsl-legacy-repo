module org/webdsl/dsl/modules/types/success-messages

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
 
rules //message function

  check-builtin-signature :
    (None(), "message", [s]) ->  SimpleSort("Void")
    where <type-compatible> (s, SimpleSort("String"))
    
  // messages template call
  built-in-template-rule-setup = <declare-built-in-template-rules> ("messages",[]);fail
  
rules //message template

  get-template-success = !<TemplateSignature> ("templateSuccess",[GenericSort("List",[SimpleSort("String")])])
  
  default-template-success =
    <emit-webdsl-code> def|[    
      define ignore-access-control templateSuccess(messages : List<String>){
        block()[style := "border: 1px solid #BB8800; margin-left: -5px; margin-top: 5px; margin-bottom: 5px; padding: 4px"]{
          for(ve: String in messages){
            block()[style := "width:100%; color: #BB8800; margin-top: 5px;"]{
              text(ve)   
            }    
          }
        }
      }
    ]|