module org/webdsl/dsl/languages/action/request-var

imports
	org/webdsl/dsl/generation/webdsl-generator

rules 

  declare-request-var =
      ?(v, x, srt)
    ; rules( 
        IsRequestScopedVarDecl : x
        TypeOf : x -> srt
        VarDecl :+ x -> v
      )

  declare-action =
      ?RequestScope(var)
    ; <?VarDecl(x, srt) <+ ?VarDeclInit(x,srt,_)> var
    ; <declare-request-var> (var, x, srt)
    
  declare-action =
      ?RequestScope(var@VarDeclInitInferred(x, e))
    ; srt := <type-of <+ !"unknown"> e  // type-of can fail if the expression cannot be typed, in that case the var will not be declared and constraint error triggers
    ; <declare-request-var> (var, x, srt) 
      
rules
  
  //assert
  desugar-action = ?RequestScope(VarDeclInitInferred(x, e)); debug(!"Assertion failed: VarDeclInitInferred should have been desugared already!"); <exit> 1

  desugar-action :    
    VarDeclInit(x, srt, e) -> RequestScopeVarDeclInit(x, srt, e)
    where <IsRequestScopedVarDecl> x
    
  desugar-action :    
    VarDecl(x, srt) -> RequestScopeVarDecl(x, srt)
    where <IsRequestScopedVarDecl> x
        
  desugar-action :    
    Var(x) -> RequestVar(x)
    where <IsRequestScopedVarDecl> x

rules

  eq-type-of-action :
    RequestVar(x) -> <TypeOf> x
    