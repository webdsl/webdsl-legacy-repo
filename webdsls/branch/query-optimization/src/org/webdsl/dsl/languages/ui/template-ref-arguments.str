module org/webdsl/dsl/languages/ui/template-ref-arguments

imports
  libstrategolib
  libwebdsl-front
  org/webdsl/dsl/languages/ui/templatecall-utils

rules
  
  //RefSort only allowed in template arguments	
  constraint-error-ui :
    Arg(_,s@RefSort(_)){anno*} -> <add-error(|["Reference argument '",pps,"' only allowed in formal parameters of pages, templates, or ajax templates"])>
    where <not(fetch(IsTemplateArg))> anno*
    with  pps := <pp-webdsl-to-string> s 
  
  //only pass lvalue to ref argument
  constraint-error-ui :
    x -> <fail>
    where uniqtname := <is-defined-tc-get-called-tc> x
        ; fargs := <TemplateArguments> uniqtname
        ; args := <get-tc-args> x
        ; <zip(try(constraint-error-ui-ref-arg))> (fargs,args)
          
  constraint-error-ui-ref-arg :
    (Arg(_,RefSort(_)), e) -> <add-error(|["Invalid argument: '",epp,"' is not assignable, which is required for reference argument"])> e
    where type := <type-of> e
        ; (not(<is-lvalue> e) <+ <has-immutable-anno> type)
    with  epp := <pp-webdsl-to-string> e
    
rules
  
  check-ref-argument-entity-access-sig :
    (t, "getEntity", []) -> SimpleSort("Entity")
    where <is-ref-type> t
    
  check-ref-argument-entity-access-sig :
    (t, "getValidationErrors", []) -> GenericSort("List",[SimpleSort("String")])
    where <is-ref-type> t
    
  check-ref-argument-entity-access-sig :
    (t, "getReflectionProperty", []) -> SimpleSort("ReflectionProperty")
    where <is-ref-type> t
    