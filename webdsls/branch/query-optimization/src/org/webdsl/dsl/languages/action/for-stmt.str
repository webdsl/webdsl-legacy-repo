module org/webdsl/dsl/languages/action/for-stmt

imports
  libstrategolib

imports
  libwebdsl-front

rules

  normalize-action :
    ForStmtNoFilter(x, s, e, block){anno*} -> ForStmt(x, s, e, Filter(True(), OrderAscending(Null()), Limit(Int("1000"), Int("0"))), block){anno*}

  normalize-action :
    ForAllStmtNoFilter(x, s, block){anno*} -> ForAllStmt(x, s, Filter(True(), OrderAscending(Null()), Limit(Int("1000"), Int("0"))), block){anno*}

rules

  rename-action :
    fs@ForStmt(x, srt, e1, f1, b1) -> ForStmt(y, srt2, e2, f2, b2)
    with e2 := <rename-all> e1
       ; {| Rename, VarInTemplateContext
          : srt2 := <rename-all> srt
          ; y := <rename-bound-immutable(|srt2)> x
          ; rules(VarDecl : y -> fs)
          ; <declare-vars-in-entity-context> y
          ; f2 := <rename-all> f1
          ; b2 := <rename-all> b1
          |}

  rename-action :
    fs@ForStmtNoFilter(x, srt, e1, b1) -> ForStmtNoFilter(y, srt2, e2, b2)
    with e2 := <rename-all> e1
       ; {| Rename, VarInTemplateContext
          : srt2 := <rename-all> srt
          ; y := <rename-bound-immutable(|srt2)> x
          ; rules(VarDecl : y -> fs)
          ; <declare-vars-in-entity-context> y
          ; b2 := <rename-all> b1
          |}

  rename-action :
    fs@ForAllStmt(x, srt, f1, b1){anno*} -> ForAllStmt(y, srt2, f2, b2){newanno*}
    with {| Rename, VarInTemplateContext
          : srt2 := <rename-all> srt
          ; y := <rename-bound-immutable(|srt2)> x
          ; rules(VarDecl : y -> fs)
          ; <declare-vars-in-entity-context> y
          ; f2 := <rename-all> f1
          ; b2 := <rename-all> b1
          ; newanno* := <forallstmt-annotations-for-query-optimization> anno*
          |}

  rename-action :
    fs@ForAllStmtNoFilter(x, srt, b1){anno*} -> ForAllStmtNoFilter(y, srt2, b2){newanno*}
    with {| Rename, VarInTemplateContext
          : srt2 := <rename-all> srt
          ; y := <rename-bound-immutable(|srt2)> x
          ; rules(VarDecl : y -> fs)
          ; <declare-vars-in-entity-context> y
          ; b2 := <rename-all> b1
          ; newanno* := <forallstmt-annotations-for-query-optimization> anno*
          |}

  forallstmt-annotations-for-query-optimization:
    anno* -> newanno*
    where plain-anno := <remove-all(?FunctionArgs(_) <+ ?QueryOptimizations(_, _, _, _))> anno*
    ; qopt := <(fetch-elem(?QueryOptimizations(_, _, _, _)); rename-all; ![<id>]) <+ ![]> anno*
    ; args := <(FunctionArg; ![FunctionArgs(<id>)]) <+ ![]>
    ; newanno* := <concat> [ qopt, args, plain-anno ]

  rename-action :
    fs@ForCountStmt(x, e1, e2, b1) -> ForCountStmt(y, e3, e4, b2)
    with e3 := <rename-all> e1
       ; e4 := <rename-all> e2
       ; {| Rename, VarInTemplateContext
          : y := <rename-bound-immutable(|SimpleSort("Int"))> x
          ; rules(VarDecl : y -> fs)
          ; <declare-vars-in-entity-context> y
          ; b2 := <rename-all> b1
          |}

rules

  desugar-action :
    def@ForAllStmt(x, srt, Filter(cond, _, _), _){anno*} -> def{QueryOptimizations(joincandidates, joincandidatesgen, querycondition, conditionjoins), anno*}
    where not(<fetch-elem(?QueryOptimizations(_, _, _, _))> anno*)
    with args := <fetch-elem(?FunctionArgs(<id>)) <+ []> anno* // Can fail for an action
    ; (joincandidates, joincandidatesgen, querycondition, conditionjoins) := <get-query-optimizations>(def, x, srt, args, cond)

  desugar-action :
    def@ForAllStmtNoFilter(x, srt, _){anno*} -> def{QueryOptimizations(joincandidates, joincandidatesgen, querycondition, conditionjoins), anno*}
    where not(<fetch-elem(?QueryOptimizations(_, _, _, _))> anno*)
    with args := <fetch-elem(?FunctionArgs(<id>)) <+ []> anno* // Can fail for an action
    ; (joincandidates, joincandidatesgen, querycondition, conditionjoins) := <get-query-optimizations>(def, x, srt, args, True())

rules

  constraint-error-action : ForCountStmt(x, e1, e2, b) -> <expression-type-constraint(|SimpleSort("Int"))> e1
  constraint-error-action : ForCountStmt(x, e1, e2, b) -> <expression-type-constraint(|SimpleSort("Int"))> e2

  expression-type-constraint(|type) :
    e -> <add-error(|["Expression should be of type ",typename])>
    with  SimpleSort(typename) := type
    where not(<type-compatible> (type, <type-of> e))

  constraint-error-action : ForStmt(_, srt, e, _, _)      -> <for-constraint-error(|srt, e)>
  constraint-error-action : ForStmtNoFilter(_, srt, e, _) -> <for-constraint-error(|srt, e)>
