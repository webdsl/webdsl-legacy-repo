module org/webdsl/dsl/to-java-servlet/types/timeinterval

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules // java type

  builtin-java-type :
    SimpleSort("TimeInterval") -> type|[ Long ]|
    
rules // Int literal

   expression-to-java-servlet :
     TimeInterval(ti*) -> val
     with val := <map(ti-to-ms); foldr(!expr|[ 0 ]|, \ (e1, e2) -> expr|[ e1 + e2 ]| \)> ti*

   ti-to-ms :
     Milliseconds(e) -> <expression-to-java-servlet> e

   ti-to-ms :
     Seconds(e1) -> expr|[ 1000 * e2 ]|
     with e2 := <expression-to-java-servlet> e1

   ti-to-ms :
     Minutes(e1) -> expr|[ 60 * 1000 * e2 ]|
     with e2 := <expression-to-java-servlet> e1

   ti-to-ms :
     Hours(e1) -> expr|[ 60 * 60 * 1000 * e2 ]|
     with e2 := <expression-to-java-servlet> e1

   ti-to-ms :
     Days(e1) -> expr|[ 24 * 60 * 60 * 1000 * e2 ]|
     with e2 := <expression-to-java-servlet> e1

   ti-to-ms :
     Weeks(e1) -> expr|[ 7 * 24 * 60 * 60 * 1000 * e2 ]|
     with e2 := <expression-to-java-servlet> e1
  
rules
  
  generate-global-var-initializer-method = 
    bstm* := <map({\ elem |[ var x : srt := e ]| -> srt\}); make-set; map(generate-global-var-initializer); concat> <bagof-AllGlobalVars <+ ![]>
    ; bstm2*  := <map(generate-session-var-initializer); concat> <bagof-SessionEntities<+![]>
    ; t := type|[ boolean ]|
    ; !|[
        public static t initGlobalVars(Environment env, org.hibernate.Session hibSession) {
          bstm2*
          bstm*
          return true;
        }
      ]|

  generate-session-var-initializer :
    sent@def|[ session x_ident { ebd* } ]| -> bstm*|[ 
      env.putVariable("~x_ident", new x_Ident());
    ]| 
    with x_Ident := <capitalize-string> x_ident

  generate-global-var-initializer :
    srt@SimpleSort(x_typename) -> bstm*|[ 
      env.putVariable("~x", GlobalVariables.get#x(hibSession)); 
      if(env.getVariable("~x") == null) {
        hibSession.getTransaction().commit();
        return false;
      }
    ]| 
    with vardeclinits := <(bagof-AllGlobalVars <+ ![]); filter(where(?VarDeclInit(_, srt, _)))>
    where [singlevar] := vardeclinits
    ; <?elem |[ var x : srt := e ]|> singlevar

  generate-global-var-initializer :
    srt@SimpleSort(x_typename) -> bstm*|[ 
      GlobalVariables.initGlobal#x_typename(env, hibSession); 
      bstm*
    ]| 
    with vardeclinits := <(bagof-AllGlobalVars <+ ![]); filter(where(?VarDeclInit(_, srt, _)))>
    where not([singlevar] := vardeclinits)
    ; bstm* := <map({\
                      elem |[ var x : srt := e ]| ->
                      bstm |[ 
									      if(env.getVariable("~x") == null) {
									        hibSession.getTransaction().commit();
									        return false;
									      }
                      ]|
                      \})> vardeclinits

   invoke-every-to-init :
    ie@InvokeEvery(e, ti) -> bstm*|[
      java.util.TimerTask x1 = new java.util.TimerTask() {
          public void run() {
            if(utils.GlobalVariables.globalvarsChecked && utils.GlobalInit.initChecked){
              org.hibernate.Session hibSession = null;
              try {
                hibSession = HibernateUtilConfigured.getSessionFactory().getCurrentSession();
                hibSession.beginTransaction();
                Environment env = new Environment(PageServlet.staticEnv);
                if(DispatchServlet.initGlobalVars(env, hibSession)) {
                  AbstractPageServlet ps = new GlobalsPageServlet(env);
                  ps.setHibSession(hibSession);
                  ThreadLocalPage.set(ps);
                  java.io.PrintWriter out = new java.io.PrintWriter(System.out);
                  ThreadLocalOut.push(out);
                  e1;
                  hibSession.getTransaction().commit();
                  ThreadLocalOut.popChecked(out);
                }
              } catch (Exception ex) {
                System.out.println("exception occured while executing timed function: "+"~ppinvoke");	
                System.out.println("exception message: "+ex.getMessage());	
                ex.printStackTrace();
                hibSession.getTransaction().rollback();
              }  
              finally {
                ThreadLocalPage.set(null);
              }
            }
          }
      };
      timer.scheduleAtFixedRate(x1, 0, e2);
    ]|
    with x1 := <newname> "timer"
       ; e1 := <expression-to-java-servlet> e
       ; e2 := <expression-to-java-servlet> ti
       ; ppinvoke := <pp-webdsl-to-string; escape> ie
