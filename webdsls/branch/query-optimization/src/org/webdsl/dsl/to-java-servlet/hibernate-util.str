module org/webdsl/dsl/to-java-servlet/hibernate-util

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

overlays

  e_HibSession =
    java:expr |[ HibernateUtilConfigured.getSessionFactory().getCurrentSession() ]|

rules

  hibernate-forall-criteria :
    (term{anno*}, t, x, srt, Filter(wh,ob,lim)) -> e_criteria
    with <fetch-elem(?QueryOptimizations(joinproperties, joinpropertiesgen, querycondition))> anno*
    ; e_criteria := <foldr(!expr|[ e_HibSession.createCriteria(t.class).setResultTransformer(org.hibernate.Criteria.DISTINCT_ROOT_ENTITY) ]|,hibernate-criteria-join); hibernate-criteria-add-condition(|querycondition,x,srt)> joinproperties
        //try on next line since not all ordering can be translated to query
        /** ; e_criteria := <foldr(!e0,hibernate-forall-criteria-filter(|x,srt));where(rules(OrderAndLimitApplied:=True())) <+ !e0> [lim,ob]  */

   hibernate-extra-criteria :
   	(term{anno*}, t, x, srt) -> e_criterias*
   	with e_criterias* := <hibernate-extra-criteria>(term{anno*}, t, x, srt, e_HibSession)
    
   hibernate-extra-criteria :
   	(term{anno*}, t, x, srt, e_Session) -> e_criterias*
   	with <fetch-elem(?QueryOptimizations(joinproperties, joinpropertiesgen, querycondition))> anno*
   	; e_base := expr|[ e_Session.createCriteria(t.class) ]|
   	; e_criterias* := <map(hibernate-extra-criteria-helper(|e_base, x, srt, querycondition))> joinpropertiesgen

  hibernate-extra-criteria-helper(|e_base, x, srt, querycondition) :
  	joinproperties -> e_criteria
  	with e_criteria := <foldr(!e_base, hibernate-criteria-join); hibernate-criteria-add-condition(|querycondition,x,srt)> joinproperties 
    
  hibernate-criteria-join :
    //(str, e) -> expr|[ e.setFetchMode("~str", org.hibernate.FetchMode.JOIN) ]|
  	//((str, _, alias), e) -> expr|[ e.createAlias("~str", "~alias") ]|
  	(str, e) -> expr|[ e.createAlias("~assaciation", "~alias", org.hibernate.criterion.CriteriaSpecification.LEFT_JOIN) ]|
  	with varList := <string-tokenize(|['.'])> str
  	; prevAliasList := <reverse; ?[joinProp|<id>]; reverse> varList
  	; prevAlias := <?[] < !"" + (separate-by(!"_"); !["_"|<id>]; concat-strings)> prevAliasList
  	; alias := <concat-strings>[prevAlias, "_", joinProp]
  	; assaciation := <?"" < !joinProp + <concat-strings>[prevAlias, ".", joinProp]> prevAlias

  hibernate-criteria-add-condition(|cond,x,srt) :
  	e_criteria -> <id>
  	where <?True()> cond

  hibernate-criteria-add-condition(|cond,x,srt) :
  	e_criteria -> expr|[ e_criteria.add(e_Cond) ]|
  	where <not(?True())> cond
  	with {| IsRootVar:
    		rules( IsRootVar : Var(x) -> srt )
        	; e_Cond := <hibernate-criteria-transform-condition> cond
        	; info(|["e_Cond: ", <pp-java5-to-string> e_Cond])
          |}

  property-pair-to-string =
    ?FieldAccess(l, fld)
    ; if Var(x) := l then
        !<concat-strings> ["_", fld]
      else
        !<concat-strings> [<property-pair-to-string> l, "._", fld]
      end
  
  property-to-string:
  	fa@FieldAccess(_, _) -> expr|[ "~str" ]|
  	with str := <property-pair-to-string> fa

  property-to-string:
  	var@Var(_) -> expr|[ "id" ]|
  	where <IsRootVar> var

  hibernate-criteria-transform-condition:
    Not(expr1) -> expr|[ org.hibernate.criterion.Restrictions.not(e_expr1new) ]|
    with e_expr1new := <hibernate-criteria-transform-condition> expr1

  hibernate-criteria-transform-condition:
    Or(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.or(e_expr1new, e_expr2new) ]|
    with e_expr1new := <hibernate-criteria-transform-condition> expr1
    ; e_expr2new := <hibernate-criteria-transform-condition> expr2

  hibernate-criteria-transform-condition:
    And(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.and(e_expr1new, e_expr2new) ]|
    with e_expr1new := <hibernate-criteria-transform-condition> expr1
    ; e_expr2new := <hibernate-criteria-transform-condition> expr2

  hibernate-criteria-transform-condition:
    SmallerThanOrEqual(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.leProperty(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-both-props>(expr1, expr2) // Both expressions are database fields

  hibernate-criteria-transform-condition:
    SmallerThanOrEqual(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.le(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-param1-prop>(expr1, expr2) // expr1 is a database field, expr2 is not

  hibernate-criteria-transform-condition:
    SmallerThanOrEqual(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.gt(e_expr2new, e_expr1new) ]|
    where (e_expr1new, e_expr2new) := <transform-param2-prop>(expr1, expr2) // expr2 is a database field, expr1 is not

  hibernate-criteria-transform-condition:
    SmallerThan(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.ltProperty(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-both-props>(expr1, expr2)

  hibernate-criteria-transform-condition:
    SmallerThan(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.lt(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-param1-prop>(expr1, expr2)

  hibernate-criteria-transform-condition:
    SmallerThan(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.ge(e_expr2new, e_expr1new) ]|
    where (e_expr1new, e_expr2new) := <transform-param2-prop>(expr1, expr2)

  hibernate-criteria-transform-condition:
    LargerThanOrEqual(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.geProperty(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-both-props>(expr1, expr2)

  hibernate-criteria-transform-condition:
    LargerThanOrEqual(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.ge(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-param1-prop>(expr1, expr2)

  hibernate-criteria-transform-condition:
    LargerThanOrEqual(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.lt(e_expr2new, e_expr1new) ]|
    where (e_expr1new, e_expr2new) := <transform-param2-prop>(expr1, expr2)

  hibernate-criteria-transform-condition:
    LargerThan(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.gtProperty(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-both-props>(expr1, expr2)

  hibernate-criteria-transform-condition:
    LargerThan(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.gt(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-param1-prop>(expr1, expr2)

  hibernate-criteria-transform-condition:
    LargerThan(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.le(e_expr2new, e_expr1new) ]|
    where (e_expr1new, e_expr2new) := <transform-param2-prop>(expr1, expr2)

  hibernate-criteria-transform-condition:
    NotEq(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.neProperty(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-both-props>(expr1, expr2)

  hibernate-criteria-transform-condition:
    NotEq(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.ne(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-param1-prop>(expr1, expr2)

  hibernate-criteria-transform-condition:
    NotEq(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.ne(e_expr2new, e_expr1new) ]|
    where (e_expr1new, e_expr2new) := <transform-param2-prop>(expr1, expr2)

  hibernate-criteria-transform-condition:
    Eq(expr1, expr2) -> expr|[ org.hibernate.criterion.Restrictions.eqProperty(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-both-props>(expr1, expr2)

  hibernate-criteria-transform-condition:
    Eq(expr1{anno*}, expr2) -> expr|[ org.hibernate.criterion.Restrictions.eq(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-param1-prop>(expr1, expr2); <not(fetch-elem(?SimpleSort("UUID")))> anno*

  hibernate-criteria-transform-condition:
    Eq(expr1{anno*}, expr2) -> expr|[ org.hibernate.criterion.Restrictions.naturalId().set(e_expr1new, e_expr2new) ]|
    where (e_expr1new, e_expr2new) := <transform-param1-prop>(expr1, expr2); <fetch-elem(?SimpleSort("UUID"))> anno*

  hibernate-criteria-transform-condition:
    Eq(expr1, expr2{anno*}) -> expr|[ org.hibernate.criterion.Restrictions.eq(e_expr2new, e_expr1new) ]|
    where (e_expr1new, e_expr2new) := <transform-param2-prop>(expr1, expr2); <not(fetch-elem(?SimpleSort("UUID")))> anno*

  hibernate-criteria-transform-condition:
    Eq(expr1, expr2{anno*}) -> expr|[ org.hibernate.criterion.Restrictions.naturalId().set(e_expr2new, e_expr1new) ]|
    where (e_expr1new, e_expr2new) := <transform-param2-prop>(expr1, expr2); <fetch-elem(?SimpleSort("UUID"))> anno*

  transform-both-props:
  	(expr1, expr2) -> (e_expr1new, e_expr2new)
    where not([] := <collect(IsRootVar)>expr1); not([] := <collect(IsRootVar)>expr2) 
    with e_expr1new := <property-to-string> expr1
    ; e_expr2new := <property-to-string> expr2

  transform-param1-prop:
    (expr1, expr2) -> (e_expr1new, e_expr2new)
    where not([] := <collect(IsRootVar)>expr1); [] := <collect(IsRootVar)>expr2
    with e_expr1new := <property-to-string> expr1
    ; e_expr2new := <expression-to-java-servlet> expr2

  transform-param2-prop:
    (expr1, expr2) -> (e_expr2new, e_expr1new)
    where [] := <collect(IsRootVar)>expr1; not([] := <collect(IsRootVar)>expr2) 
    with e_expr1new := <expression-to-java-servlet> expr1
    ; e_expr2new := <property-to-string> expr2

  /**
   * Hibernate optimization below on hold, Zef is working on a replacement ORM
   */
  /* 
  hibernate-forall-criteria-filter(|x,srt) = hibernate-forall-criteria-filter
  
  hibernate-forall-criteria-filter: 
    (Limit(limit,first), e) -> expr|[ e.setFirstResult(e_first).setMaxResults(e_limit) ]|
    with  e_limit := <expression-to-java-servlet> limit
        ; e_first := <expression-to-java-servlet> first

  hibernate-forall-criteria-filter(|x,srt) =
    // if the order clause is simply a property, then the query can do the ordering   
    hibernate-forall-criteria-order-is-prop(|x,srt)
  <+\(OrderDescending(Null()), e) -> e\
  <+\(OrderAscending(Null()), e) -> e\
  
  hibernate-forall-criteria-order-is-prop(|x,srt) :
    (OrderDescending(orderexp), e) -> <hibernate-forall-criteria-order-is-prop-helper(|x,srt,"desc")> (orderexp, e)
     
  hibernate-forall-criteria-order-is-prop(|x,srt) :
    (OrderAscending(orderexp), e) -> <hibernate-forall-criteria-order-is-prop-helper(|x,srt,"asc")> (orderexp, e)

  hibernate-forall-criteria-order-is-prop-helper(|x,srt,x_order) :
    (orderexp, e) -> expr|[ e.addOrder(org.hibernate.criterion.Order.x_order("~<concat-strings> ["_",prop]")) ]|
    with  SimpleSort(ent) := srt
    where FieldAccess(var,prop) := orderexp
        ; Var(x) := var
        ; <type-of-property> (ent,prop)
  */

rules

  generate-code-java-servlet-once = 
      bagof-AllEntityNames; string-sort-annos
    ; cached-generate(generate-java-servlet-hibernate-util | "internal_HibernateUtil")
    ; fail
    
  generate-java-servlet-hibernate-util :
    all-entity-names -> <emit-java-code-local> compilation-unit|[
    package utils;
    
    import java.util.Properties;
    
    import org.hibernate.*;
    import org.hibernate.cfg.*;
    import org.webdsl.*;
    import org.hibernate.event.*;
    import org.hibernate.event.def.*;
    import java.util.*;
    
    import pkgname.*;
    @SuppressWarnings("unused") 
    public class HibernateUtilConfigured {
        private static final SessionFactory sessionFactory;
        private static AnnotationConfiguration annotationConfiguration;
        static
        { 
          try
          { 
                annotationConfiguration = new AnnotationConfiguration();
    
                //Properties prop = HibernateUtil.getProperties();

                annotationConfiguration.addPackage("utils");    
                annotationConfiguration.addAnnotatedClass(utils.ApplicationContextProperty.class);
                annotationConfiguration.addAnnotatedClass(utils.File.class);

                annotationConfiguration.addPackage("~domainpack");
                //annotationConfiguration.addAnnotatedClass(User.class);
                bstm*
    
                //annotationConfiguration.addProperties(prop);
                //bstm1*
                annotationConfiguration.setListener("save-update", new SetVersionSaveOrUpdateEventListener());
                    
                annotationConfiguration.setListener("flush-entity", new SetValidationEventListener());
    
                sessionFactory = annotationConfiguration.buildSessionFactory();
                
            } catch (Throwable ex) {
                throw new ExceptionInInitializerError(ex);
            }
        }
    
        public static SessionFactory getSessionFactory() {
            return sessionFactory;
        }
        
        public static AnnotationConfiguration getAnnotationConfiguration()
        { 
          return annotationConfiguration;
        }
        
        // references that will be implicitly stored in the db need version set to at least 1 as well to indicate a persisted entity     
        @SuppressWarnings("serial")
        private static class SetVersionSaveOrUpdateEventListener extends DefaultSaveOrUpdateEventListener 
        {
          public void onSaveOrUpdate(SaveOrUpdateEvent event)  throws HibernateException
          { 
            setVersion(event.getObject());
            super.onSaveOrUpdate(event);
          }
      
          public void setVersion(Object o)
          { 
            if(o instanceof WebDSLEntity)
            { 
              WebDSLEntity we = (WebDSLEntity)o;
              if(we.getVersion() <= 0)
              { 
                we.setVersion(1);
              }
            }
          }
        } 
        
        //register which objects need to be validated at the end of an action
        @SuppressWarnings("serial")
        private static class SetValidationEventListener extends DefaultFlushEntityEventListener
        { 
            public void onFlushEntity(FlushEntityEvent event) throws HibernateException
            { 
              validate(event.getEntity());
              super.onFlushEntity(event);
            }
            
            public void validate(Object o)
            { 
                if(o instanceof WebDSLEntity)
                { 
                    WebDSLEntity we = (WebDSLEntity)o;
                    AbstractPageServlet aps = ThreadLocalPage.get();
                    if(aps != null){
                      aps.addEntityToValidateAfterAction(we); 
                    }
                }
            }
        }
    }
    ]|
    with pkgname := <DomainPackage>
       ; domainpack := <pp-java5-to-string> <DomainPackage>
       ; bstm* := <map({\ 
                           x -> bstm |[ annotationConfiguration.addAnnotatedClass(x_class); ]| 
                           where x_class := <concat-strings> [<DomainPackage;pp-java5-to-string>,".",x,".class"]
                       \})> all-entity-names
      /* ; if not([] := <bagof-AllGlobalVars>) 
         then bstm1* := [ bstm |[ annotationConfiguration.addAnnotatedClass(ApplicationContextProperty.class); ]| ]
         else bstm1* := [] end
 */
