module org/webdsl/dsl/to-java-servlet/ajax/html-utils

imports
  libstrategolib
  libjava-front

rules

  to-html-attrs(|classattr) :
    passign* -> java:expr|[" class=\""+"~classattr"+e_attrs2+"\" "+e_attrs1+" "]|
    with (e_attrs1, e_attrs2) := <to-html-attrs-separate-class> passign*

  to-html-attrs-separate-class :
    passign* -> (<to-html-attrs> noclass, class)
    with  noclass := <remove-all(?PropertyAssignment("class", _))> passign*
        ; class-attr-list := <filter(    ?PropertyAssignment("class", _)
                               ; expression-assignment-to-html-attr
                               ; ?(_,<id>))> passign*
        ; class := <?[];!java:expr|[ "" ]| <+ ![java:expr|[" "]| |<id;separate-by(|java:expr|[" "]|)>]; generate-addition-exp> class-attr-list

  to-html-attrs :
    attrs -> attrs3
    with  attrs1 := <generate-property-assignments> attrs
        ; attrs3 := <generate-addition-exp > attrs1

  generate-property-assignments=
    remove-all(exclude-from-property-assignment)
  ; map(   convert-single-property-assignment-to-java-exp
        <+ convert-attribute-selectors-to-java-exp
        <+ fatal-err(|"Cannot convert propertyassignment: "))

  convert-single-property-assignment-to-java-exp =
  (   actioncall-to-html-attr 
   <+ expression-assignment-to-html-attr
  )
  ; assignmentexp-to-html-attr
  
  exclude-from-property-assignment = TemplateCallPropertyAjax
  exclude-from-property-assignment = TemplateCallPropertySecure
  exclude-from-property-assignment = TemplateCallPropertyNotSecure
  exclude-from-property-assignment = TemplateCallPropertyIgnoreValidation
  exclude-from-property-assignment = ?PropertyAssignment("templateContext",_)
    
  //final conversion: converts a String assignment to html    
  assignmentexp-to-html-attr :
    (x, e_y) -> e |[ org.webdsl.tools.Utils.showAttributeEscapeHtml("~x",e_y) ]|
   
  expression-assignment-to-html-attr :
    PropertyAssignment(x, e) -> (x, e1)
    where e1 := <expression-to-java-servlet> e
    
  call-to-java-servlet :
    exp |[ baseURL() ]| -> expr |[ ThreadLocalServlet.getContextPath() ]|
