module org/webdsl/dsl/to-java-servlet/data-validation/success-messages
imports 
  libstrategolib  
  Java-15
  libjava-front 
  
imports 
  libwebdsl-front
  
/**
 *  Success messages
 *  IncomingSuccessMessages partly implemented in dispatch-servlet.str and java-page.str as well
 */  
  
rules //show messages

  elem-to-java-servlet :
    tc@elem|[ messages()[passign*]{elem*} ]| -> result
    with  result := <render-success-messages-with-template> <TemplateSuccess>
       
  /**
   *  calls render of templateSuccess
   */
  render-success-messages-with-template :  
    messagestemplate -> 
    bstm* |[  
      java.util.List<String> x_list;// = new java.util.LinkedList<String>();
      x_list = ThreadLocalPage.get().getIncomingSuccessMessages();
      if(x_list.size() > 0){
         Class ts = env.getTemplate("templateSuccessListString");
         try{
           Object[] args = {ThreadLocalPage.get().getIncomingSuccessMessages()};
           ((TemplateServlet)ts.newInstance()).render(args, env, null, null);
         }
         catch(InstantiationException ie){
             ie.printStackTrace();
         }
         catch(IllegalAccessException ie){
             ie.printStackTrace();
         }
      }
      ThreadLocalPage.get().getIncomingSuccessMessages().clear();//only display once
    ]|
    with x_list := <newname> "messagesList"

       
rules //produce messages 

  call-to-java-servlet :
    exp  |[ message(e1) ]| ->   java |[ ThreadLocalPage.get().getOutgoingSuccessMessages().add(e2) ]|
    where <anno-string-type-compatible> e1
    with  e2 := <expression-to-java-servlet> e1
    
rules //store messages

  generate-code-java-servlet-once =
    <emit-java-code; fail> compilation-unit|[
      package utils;
      
      public abstract class PageServlet {
        
        protected void renderIncomingSuccessMessages(){
          if(!this.getIncomingSuccessMessages().isEmpty()) {
            ~*<java-servlet-template-phase-inits>
            ~*<render-success-messages-with-template> <TemplateSuccess>
          }        
        }

      }
      
    ]|
/*    
rules //send along messages in redirects

  add-success-messages-to-url :
    e_list -> <concat> [e_list,[ java:expr|[ ThreadLocalPage.get().produceOutgoingSuccessMessages() ]| ]]
*/