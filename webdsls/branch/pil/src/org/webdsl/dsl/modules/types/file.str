module org/webdsl/dsl/modules/types/file

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  
imports 
  libwebdsl-front
    
rules
  is-simple-sort = ?SimpleSort("File")
  extends-builtin : "File" -> "Object"
  built-in-template-rule-setup = <declare-built-in-template-rules> ("inputFile",[SimpleSort("File")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputFile",[SimpleSort("File")]);fail
    
rules

  type-default-value :
    SimpleSort("File") -> exp|[ null ]|
    
rules // Desugaring
      
  DeriveInput :
    elem|[ input(e)[passign*]{} ]| -> elem|[ inputFile(e)[passign*]{} ]|
    where SimpleSort("File") := <type-of> e
  
  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputFile(e)[passign*]{} ]|
    where SimpleSort("File") := <type-of> e
    
  desugar :
    elem|[ outputFile(e) [passign*]{} ]| -> elem|[ downloadLink(e.fileName(),action{e.download();}) ]|
    
rules //file download

  check-builtin-signature :
    (SimpleSort("File"), "download", []) ->  SimpleSort("Void")
    
  check-builtin-signature :
    (SimpleSort("File"), "fileName", []) ->  SimpleSort("String")

    