module org/webdsl/dsl/modules/types/wikitext

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  is-simple-sort = ?SimpleSort("WikiText")
  extends-builtin : "WikiText" -> "Object"
  built-in-template-rule-setup = <declare-built-in-template-rules> ("inputWikiText",[SimpleSort("WikiText")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputWikiText",[SimpleSort("WikiText")]);fail
  
rules // types

  type-default-value :
    SimpleSort("WikiText") -> exp|[ "" ]|

  type-compatible =
    ?(SimpleSort("WikiText"), SimpleSort("String"))

  type-compatible =
    ?(SimpleSort("String"), SimpleSort("WikiText"))

rules // desugar
  DeriveInput :
      elem|[ input(e)[passign*]{} ]| -> elem|[ inputWikiText(e)[passign*]{} ]|
      where SimpleSort("WikiText") := <type-of> e

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputWikiText(e)[passign*]{} ]|
    where SimpleSort("WikiText") := <type-of> e


