module org/webdsl/dsl/to-pil/entity-main

imports
  libstrategolib

imports
  libwebdsl-front
  org/webdsl/dsl/to-pil/property-events

strategies

   entity-to-pil-entity :
     EntityNoSuper(x_Class, props, functions) ->
     <pil-emit> pil-decl|[

       class data::x_Class extends pil::db::model::Entity {
          new() { }

          //~*idcodes
          
          //~*[<entity-cbds-equals-pil(|x_Class)>]
          //~*cbds_propertyevents

          /*
          public int compareTo(org.webdsl.WebDSLEntity o) {
            return getId().compareTo(((x_Class)o).getId());
          }

          public boolean isInstance(Class<?> c) {
            return c.isInstance(this);
          }
          
          public boolean instanceOf(String s){
            return s.equals("~x_Class")||s.equals("Object");
          }
          */

          ~*cbds
   
       }
     ]|
     with  {| ClassNamePil
      : rules( ClassNamePil := x_Class )
      //; idcodes := <generate-id-code> x_Class
      ; cbds := <properties-to-code-pil(|x_Class)> props
      //; cbds_propertyevents := <entity-cbds-property-events-pil(|x_Class)>
      |}
      
   entity-to-pil-entity :
     SessionEntity(x_ident, props, functions) ->
     <pil-emit> pil-decl|[
       class data::x_Class extends pil::db::model::Entity {
        new() { }

        ~*cbds_propertyevents

        ~*cbds
        ~*cbds1
 
       }
     ]|
     with x_Class := <capitalize-string> x_ident
    ; {| ClassNamePil
        : rules( ClassNamePil := x_Class )
        ; cbds := <properties-to-code-pil(|x_Class)> props
        ; cbds1 := <filter(property-to-session-prop-ids-pil(|x_Class));try(concat)> props
        ; cbds_propertyevents := <entity-cbds-property-events-pil(|x_Class)>
        |}

   entity-to-pil-entity :
     Entity(x_Class, x_super, props, functions) ->
     <pil-emit> pil-decl|[
       class data::x_Class extends data::x_super {

         ~*cbds_propertyevents

         ~*cbds
   
       }
     ]|
     with  {| ClassNamePil
         : rules( ClassNamePil := x_Class )
         ; cbds := <properties-to-code-pil(|x_Class)> props
           |}
         ; cbds_propertyevents := <entity-cbds-property-events-subclass-pil(|x_Class)>
      // @note: don't definne setupForPropertyEvents; it is inherited from superclass (EV)
      // @note: don't define id on an entity with a super class; it
      // is inherited from the super class
      
  /*
  get-superclass-names(|x_classname) : 
    x_Class -> <get-superclass-names(|x_classname)> super 
    where super := <Extends> x_Class 
        ; rules(SuperclassNames :+= bstm|[ instanceOfTypes#x_classname.add("~super"); ]| )
        */

 strategies

   /*
   generate-id-code =
     ?x_Class
     ; if <EntityIdName> x_Class then
         x_idget := <concat-strings> ["get", <capitalize-string> <EntityIdName> x_Class]
         ; x_idset := <concat-strings> ["set", <capitalize-string> <EntityIdName> x_Class]
         ; t := <pil-type> <EntityIdType> x_Class
         ; !class-body-dec* |[
            public t getId() { return x_idget(); }
            public void setId(t val) { x_idset(val); }
          ]|
       else
         !class-body-dec* |[
           @Id @GeneratedValue
           private Long id;

           public Long getId() { return id; }

           public void setId(Long id) { this.id = id; }
         ]|
       end
   */

   properties-to-code-pil(|x_Class) :
     props -> cbds
     where cbds1 := <filter-concat-warn(property-to-property-code-pil(|x_Class)
                                        | "cannot generate code for property: ")> props
         ; cbds2 := <all-properties; name-property-pil(|x_Class) <+ ![]> x_Class
         ; cbd3 := <property-mappings-pil> (x_Class, props)
         ; cbds  := <concat>[cbds1, cbds2, [cbd3]]

  property-mappings-pil :
    (x_class, prop*) -> pil-cbd|[
      pil::db::model::ClassMapping getMapping() {
        var fields = new List<FieldMapping>();
        stat*
        return new pil::db::model::ClassMapping(typeof data::x_class, e_xclass, fields.as<Array<FieldMapping>>);
      }
    ]|    
    with e_xclass := <pil-wrap-string> x_class
       ; stat* := <map(property-to-mapping-pil(|x_class))> prop*

  property-to-mapping-pil(|x_class) :
    Property(x, _, GenericSort("Set", [SimpleSort(x_s)]), anno*) ->
    pil-stat|[ fields.add(new pil::db::model::ManyToManyFieldMapping(e_x, e_inverse, typeof data::x_class, typeof data::x_s, e_managing)); ]|
    with e_x := <pil-wrap-string> x
       ; <filter(?managing@InverseAnno(_, nm) <+ ?InverseSlaveAnno(_, nm))> anno* 
       ; if !managing then
           e_managing := pil-exp|[ true ]|
         else
           e_managing := pil-exp|[ false ]|
         end
       ; e_inverse := <pil-wrap-string> nm

  property-to-mapping-pil(|x_class) :
    Property(x, _, SimpleSort("String"), _) -> pil-stat|[ fields.add(new pil::db::model::ValueFieldMapping(e_x, pil::db::type::stringType, 255)); ]|
    with e_x := <pil-wrap-string> x

  property-to-mapping-pil(|x_class) :
    Property(x, _, SimpleSort("Text"), _) -> pil-stat|[ fields.add(new pil::db::model::ValueFieldMapping(e_x, pil::db::type::stringType, 10000)); ]|
    with e_x := <pil-wrap-string> x

  property-to-mapping-pil(|x_class) :
    Property(x, _, SimpleSort("Secret"), _) -> pil-stat|[ fields.add(new pil::db::model::ValueFieldMapping(e_x, pil::db::type::stringType, 255)); ]|
    with e_x := <pil-wrap-string> x

  property-to-mapping-pil(|x_class) :
    Property(x, _, SimpleSort("Int"), _) -> pil-stat|[ fields.add(new pil::db::model::ValueFieldMapping(e_x, pil::db::type::intType, 0)); ]|
    with e_x := <pil-wrap-string> x

  property-to-mapping-pil(|x_class) :
    Property(x, _, srt@SimpleSort(x_s), _) -> pil-stat|[ fields.add(new pil::db::model::SimpleReferenceFieldMapping(e_x, typeof data::x_s)); ]|
    where <defined-pil-type> srt
    with e_x := <pil-wrap-string> x

 strategies

   name-property-pil(|x_Class) :
     props -> pil-cbd* |[
       String getName() {
         if(x_prop != null) {
           return x_prop.as<String>;
         }
         else {
           return "";
         }
       }
     ]|
     where <not(fetch(    ?Property             ("name",_,_,_  )
                       <+ ?PropertyNoAnno       ("name",_,_    )
                       <+ ?DerivedProperty      ("name",_,_,_,_)
                       <+ ?DerivedPropertyNoAnno("name",_,_,_  )))> props
         ; x_prop := <get-namefield> props

 rules

   property-to-property-code-pil(|x_Class) :
     prop@Property(mx@x, k, s, annos) -> pil-cbd* |[

       SimpleValue<t> x = new SimpleValue<t>(this, e_propname, typeof t, e);

     ]|
    where t      := <builtin-pil-type> s
    with e      := <webdsl-pil-type-default-value> s
       ; e_propname := <pil-wrap-string> mx

   property-to-property-code-pil(|x_Class) :
     prop@Property(mx@x, k, srt@SimpleSort(s), annos) -> pil-cbd* |[

       SimpleReference<t> x = new SimpleReference<t>(typeof t);

     ]|
    where t      := <defined-pil-type> srt
    with e      := <webdsl-pil-type-default-value> s
       ; e_propname := <pil-wrap-string> mx
          
   property-to-property-code-pil(|x_Class) :
     prop@DerivedProperty(x, k, s, annos, e) -> pil-cbd* |[

       t get#x() {
         return e_gettercode;
       }

     ]|
     where t      := <pil-type> s
         ; e_gettercode := <expression-to-pil> e
         
rules // special session entity props

   property-to-session-prop-ids-pil(|x_Class) :
     prop@Property(x, k, s, annos) -> pil-cbd* |[

       String x#ident = "";

     ]|
     where t := <defined-pil-type> s // only succeeds for entity refs

