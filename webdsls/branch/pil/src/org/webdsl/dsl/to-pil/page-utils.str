module org/webdsl/dsl/to-pil/page-utils

imports
  libstratego-lib

imports
  libwebdsl-front

rules // embedding expressions in presentation

  /**
   * Converts an arbitrary WebDSL expression into java statements
   * that produce a string for displaying it on a page.
   *
   * Mainly used to prevent null-pointer exceptions breaking the entire page.
   */
  to-page-string-pil =
    to-page-string-pil(|"none")

  to-page-string-pil(|qid_filter) =
    to-page-string-pil(|qid_filter,"none")
    
  to-page-string-pil(|qid_filter,e3) :
    e -> 
    pil-stat*|[
      String x_tmpstring = null;
      try {
        x_tmpstring = e2;
      }
      catch(NullPointerException npe) {
        x_tmpstring = "";
      }
      out.print(x_tmpstring);
    ]|
    with e1 := <expression-to-pil> e
       ; x_tmpstring := <newname> "tmpstring"
       ; if qid_filter := "none"
         then e2 := pil-exp|[ e1.as<String> ]|
         else
           if e3 := "none" 
           then e2 := pil-exp|[ qid_filter(e1.as<String>) ]| 
           else e2 := pil-exp|[ qid_filter(e1.as<String>,e3) ]|
           end
         end
