module org/webdsl/dsl/to-pil/types/collection

imports 
  libstrategolib 
  libwebdsl-front

rules //types

  builtin-pil-type :
    GenericSort("List", [s]) -> pil-type|[ List<t> ]|
    where t := <pil-type> s

  builtin-pil-type :
    GenericSort("Set", [s]) -> pil-type|[ Set<t> ]|
    where t := <pil-type> s
    
  type-default-value-override-pil :
    GenericSort("List", [s]) -> pil-exp|[ new List<t>() ]|
    where t := <pil-type> s
    
  type-default-value-override-pil :
    GenericSort("Set", [s]) -> pil-exp|[ new Set<t>() ]|
    where t := <pil-type> s

rules //functions

  call-to-pil :
     Call(e1, "addAll", [e2]) -> pil-exp|[ webdsl::util::list::addAll(e3, e4) ]|
     with e3 := <expression-to-pil> e1
     with e4 := <expression-to-pil> e2

  call-to-pil :
    Call(e1, "add", [e2])  -> pil-exp|[ e3.setupForPropertyEvents(dbSession,out,ps).addTo#x_field(e4) ]|
    where GenericSort(_,_) := <type-of> e1
        ; <?FieldAccess(e_entity,x_field)> e1
    with  e3 := <expression-to-pil> e_entity
        ; e4 := <expression-to-pil> e2

  call-to-pil :
    Call(e1, "remove", [e2])  -> pil-exp|[ e3.setupForPropertyEvents(dbSession,out,ps).removeFrom#x_field(e4) ]|
    where GenericSort(_,_) := <type-of> e1
        ; <?FieldAccess(e_entity,x_field)> e1
    with  e3 := <expression-to-pil> e_entity 
        ; e4 := <expression-to-pil> e2
        
  call-to-pil :
    Call(e1, "clear", [])  -> pil-exp|[ e3.setupForPropertyEvents(dbSession,out,ps).removeAllFrom#x_field() ]|
    where GenericSort(_,_) := <type-of> e1
        ; <?FieldAccess(e_entity,x_field)> e1
    with  e3 := <expression-to-pil> e_entity 

  call-to-pil :
    Call(e1, "indexOf", [e2]) -> pil-exp|[ e3.indexOf(e4) ]|
    where GenericSort("List",[s]) := <type-of> e1
    with e3 := <expression-to-pil> e1
    with e4 := <expression-to-pil> e2

  call-to-pil :
    Call(e1, "get", [e2]) -> pil-exp|[ e3.get(e4) ]|
    where GenericSort("List",[s]) := <type-of> e1
    with e3 := <expression-to-pil> e1
    with e4 := <expression-to-pil> e2

  call-to-pil :
    Call(e1, "set", [e2,e3]) -> pil-exp|[ e4.set(e5,e6) ]|
    where GenericSort("List",[s]) := <type-of> e1
    with e4 := <expression-to-pil> e1
    with e5 := <expression-to-pil> e2
    with e6 := <expression-to-pil> e3

  call-to-pil :
     Call(e1, "add", [e2])  -> pil-exp|[ e3.add(e4) ]|
     where GenericSort(_,_) := <type-of> e1
     with  e3 := <expression-to-pil> e1
         ; e4 := <expression-to-pil> e2

  call-to-pil :
     Call(e1, "remove", [e2])  -> pil-exp|[ e3.remove(e4) ]|
     where GenericSort(_,_) := <type-of> e1
     with  e3 := <expression-to-pil> e1
         ; e4 := <expression-to-pil> e2
         
  call-to-pil :
     Call(e1, "clear", [])  -> pil-exp|[ webdsl::util::list::clear(e3) ]|
     where GenericSort(_,_) := <type-of> e1
     with  e3 := <expression-to-pil> e1

rules //typed collection creations

   expression-to-pil : // The version with items (!= []) will be desugared in desugar.str
     TypedListCreation(s, []) -> pil-exp|[ new List<t>() ]|
     with  t := <pil-type> s

   expression-to-pil :
     TypedSetCreation(s, []) -> pil-exp|[ new Set<t>() ]|
     with  t := <pil-type> s

rules //fields

  field-access-to-pil :
    FieldAccess(e1, "length") -> pil-exp|[ e2.size() ]|
    with  e2 := <expression-to-pil> e1
        ; GenericSort(_, _) := <type-of> e1

rules //pages
 
  pil-element-id : 
    tc@Select(x, SimpleSort(s), label, e) -> tc{InputNumberPil(newid),anno*} 
    with  _{anno*} := tc
        ; newid := <newname> "input"

  //SimpleSort property select
  handle-actions-input-to-pil :
    tc@Select(x, srt, label, e)  ->
    <for-loop-counter-wrap-code-pil>
    pil-stat*|[
        ident = ident + e_inputident+uniqueid;
        
        if(ps.getParammap().get(ident)!=null)
        {
            t x_temp = dbSession.get(typeof t, ps.parammap[ident]).as<t>;
            stat 
        }
    ]|
    where SimpleSort(_) := <type-of> e
    with  e_inputident := <pil-wrap-string> <get-inputnumber-pil> tc
        ; x_temp := <newname> "temp"
        ; t       := <pil-type> srt
        ; stat := <input-assignment-to-pil(|x_temp)> e
    
  elem-to-pil : 
    tc@Select(x, srt, label, e)  ->
    <for-loop-counter-wrap-code-pil>
    pil-stat* |[ 
    String x_temp = ident+e_inputident+uniqueid;
    out.print("<select name=\""+x_temp+"\" >");
    for(t x : dbSession.all(typeof t)) {
      out.print("<option value=\""+x.getId()+"\"");
      if(inSubmittedForm)
      {
        if(ps.parammap[x_temp] != null && ps.parammap[x_temp] == x.getId())
        {
          out.print("selected=\"selected\"");
        }
      }
      else
      {
        if(e2 != null && e2.getId() == x.getId())//maybe should use add-not-null checks
        {
          out.print("selected=\"selected\"");
        }
      }
      out.print(">"+x.getName(dbSession)+"</option>");
    }
    out.print("</select>"); 
    ]|
    where SimpleSort(_) := <type-of> e
    with  e_inputident := <pil-wrap-string> <get-inputnumber-pil> tc
        ; x_temp := <newname> "temp"
        ; x_iter := <newname> "iter"
        ; t       := <pil-type> srt
        ; e2 := <expression-to-pil> e

          
  //GenericSort property select
  handle-actions-input-to-pil :
    tc@Select(x, srt, label, e)  ->
    <for-loop-counter-wrap-code-pil>
    pil-stat*|[
        ident = ident + e_inputident+uniqueid;
        if(ps.parammap[ident+"_isinput"]!=null){
          e_clear;
          if(ps.parammapvalues[ident] != null){
            for(String elem : ps.parammapvalues[ident])
            {
              t x_temp = dbSession.load(typeof t, elem).as<t>;
              e_add;  
            }
          }
        }
    ]|
    where GenericSort(_, _) := <type-of> e
    with  e_inputident := <pil-wrap-string> <get-inputnumber-pil> tc
        ; x_temp := <newname> "temp"
        ; t       := <pil-type> srt
        ; stat := <input-assignment-to-pil(|x_temp)> e
        ; e_add := <expression-to-pil> webdsl:e |[ e.add(x_temp) ]| //reuse expression translation for inverse relation add/remove operations
        ; e_clear := <expression-to-pil> webdsl:e |[ e.clear() ]| //reuse expression translation for inverse relation clear operation
    
  elem-to-pil : 
    tc@Select(x, srt, label, e)  ->
    <for-loop-counter-wrap-code-pil>
    pil-stat* |[ 
    String x_temp = ident+e_inputident+uniqueid;
    out.print("<input type=\"hidden\" name=\""+x_temp+"_isinput"+"\" />");
    out.print("<select name=\""+x_temp+"\" multiple=\"multiple\">");
    for(t x : dbSession.all(typeof t)) {
      out.print("<option value=\""+x.getId()+"\"");
      if(inSubmittedForm)
      {
        if(ps.parammapvalues[x_temp] != null){
          for(String elem : ps.parammapvalues[x_temp])
          {
            if(elem == x.getId())
            {
              out.print("selected=\"selected\"");
            }
          }
        }
      }
      else
      {
        if(e2 != null && e2.contains(x))//maybe should use add-not-null checks
        {
          out.print("selected=\"selected\"");
        }
      }
      out.print(">"+x.getName(dbSession)+"</option>");
    }
    out.print("</select>"); 
    ]|
    where GenericSort(_, _) := <type-of> e
    with  e_inputident := <pil-wrap-string> <get-inputnumber-pil> tc
        ; x_temp := <newname> "temp"
        ; x_iter := <newname> "iter"
        ; t       := <pil-type> srt
        ; e2 := <expression-to-pil> e

rules // select from

  pil-element-id : 
    tc@SelectFromList(e1, e7)-> tc{InputNumberPil(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"

  //SimpleSort property select
  handle-actions-input-to-pil :
    tc@SelectFromList(e1, e7)  ->
    <for-loop-counter-wrap-code-pil>
    pil-stat*|[
        ident = ident + e_inputident+uniqueid;
        
        if(ps.parammap[ident]!=null)
        {
            t x_temp = dbSession.get(typeof t, ps.parammap[ident]).as<t>;
            if(e8.contains(x_temp))
            {
              stat
            } 
        }
    ]|
    where SimpleSort(_) := <type-of> e1
    with  e_inputident := <pil-wrap-string> <get-inputnumber-pil> tc
        ; srt := <type-of> e1
        ; x_temp := <newname> "temp"
        ; t       := <pil-type> srt
        ; stat := <input-assignment-to-pil(|x_temp)> e1
        ; e8 := <expression-to-pil> e7
    
  elem-to-pil : 
    tc@SelectFromList(e1, e7)  ->
    <for-loop-counter-wrap-code-pil>
    pil-stat* |[ 
    String x_temp = ident+e_inputident+uniqueid;
    out.print("<select name=\""+x_temp+"\" >");
    for(t x : e8) {
      out.print("<option value=\""+x.getId()+"\"");
      if(inSubmittedForm)
      {
        if(ps.parammap[x_temp] != null && ps.parammap[x_temp] == x.getId())
        {
          out.print("selected=\"selected\"");
        }
      }
      else
      {
        if(e2 != null && e2.getId() == x.getId())//maybe should use add-not-null checks
        {
          out.print("selected=\"selected\"");
        }
      }
      out.print(">"+x.getName(dbSession)+"</option>");
    }
    out.print("</select>"); 
    ]|
    where SimpleSort(_) := <type-of> e1
    with  e_inputident := <pil-wrap-string> <get-inputnumber-pil> tc
        ; srt        := <type-of> e1
        ; x_temp     := <newname> "temp"
        ; x          := <newname> "x"
        ; t          := <pil-type> srt
        ; e2         := <expression-to-pil> e1
        ; e8         := <expression-to-pil> e7

          
  //GenericSort property select
  handle-actions-input-to-pil :
    tc@SelectFromList(e1, e7)  ->
    <for-loop-counter-wrap-code-pil>
    pil-stat*|[
        ident = ident + e_inputident+uniqueid;
        if(ps.parammap[ident+"_isinput"]!=null){
          e_clear;
          if(ps.parammapvalues[ident] != null){
            for(String elem : ps.parammapvalues[ident])
            {
              t x_temp = dbSession.get(typeof t, elem).as<t>;
              if(e8.contains(x_temp)) {
                e_add;  
              } 
            }
          }
        }
    ]|
    where GenericSort(_, [srt]) := <type-of> e1
    with  e_inputident := <pil-wrap-string> <get-inputnumber-pil> tc
        ; x_temp := <newname> "temp"
        ; t       := <pil-type> srt
        ; e_add := <expression-to-pil> webdsl:e |[ e1.add(x_temp) ]| //reuse expression translation for inverse relation add/remove operations
        ; e8 := <expression-to-pil> e7
        ; e_clear := <expression-to-pil> webdsl:e |[ e1.clear() ]| //reuse expression translation for inverse relation clear operation
    
  elem-to-pil : 
    tc@SelectFromList(e1, e7) ->
    <for-loop-counter-wrap-code-pil>
    pil-stat* |[ 
    String x_temp = ident+e_inputident+uniqueid;
    out.print("<input type=\"hidden\" name=\""+x_temp+"_isinput"+"\" />");
    out.print("<select name=\""+x_temp+"\" multiple=\"multiple\">");
    for(t x : e8) {
      out.print("<option value=\""+x.getId()+"\"");
      if(inSubmittedForm)
      {
        if(ps.parammapvalues[x_temp] != null){
          for(String elem : ps.parammapvalues[x_temp])
          {
            if(elem == x.getId())
            {
              out.print("selected=\"selected\"");
            }
          }
        }
      }
      else
      {
        if(e2 != null && e2.contains(x))//maybe should use add-not-null checks
        {
          out.print("selected=\"selected\"");
        }
      }
      out.print(">"+x.getName(dbSession)+"</option>");
    }
    out.print("</select>"); 
    ]|
    where GenericSort(_, [srt]) := <type-of> e1
    with  e_inputident := <pil-wrap-string> <get-inputnumber-pil> tc
        ; x_temp := <newname> "temp"
        ; t       := <pil-type> srt
        ; x := <newname> "x"
        ; e2 := <expression-to-pil> e1
        ; e8 := <expression-to-pil> e7

