module org/webdsl/dsl/to-pil/types/text

imports 
  libstrategolib 
  libwebdsl-front
  
rules

  builtin-pil-type :
    SimpleSort("Text") -> pil-type|[ String ]|

  builtin-persistence-annotations-pil :
    SimpleSort("Text") -> [pil-anno|[ @pil::db::field::text ]|]  
  
rules 
  pil-element-id : 
    tc@elem|[ inputText(e)[passign*]{} ]| -> tc{InputNumberPil(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"

  handle-actions-input-to-pil :
    tc@elem|[ inputText(e)[passign*]{} ]| ->
    <for-loop-counter-wrap-code-pil> 
    pil-stat*|[
          
        ident = ident + e_inputident+uniqueid;
        
        if(ps.parammap[ident] != null)
        {
            ~*<text-length-check-pil(|x_temp,name)>
            stat 
        }
      
    ]|
    with  e_inputident := <get-inputnumber-pil; pil-wrap-string> tc
        ; x_temp := <newname> "temp"
        ; name := <pp-webdsl> e
        ; stat := <input-assignment-to-pil(|x_temp)> e
        
  text-length-check-pil(|x_temp,name) =
    e_name := <pil-wrap-string> name
    ; ! pil-stat* |[
        String x_temp = ps.parammap[ident];
        if(x_temp.length>1000000) {
          ps.conversionErrorMessages[e_name] = x_temp+" is too long";
          x_temp = null;
        }
      ]|
    
  elem-to-pil : 
    tc@elem|[ inputText(e)[passign*]{} ]| ->
    <for-loop-counter-wrap-code-pil>
      pil-stat* |[ 
        String x_temp = ident+e_inputident+uniqueid;
        out.print("<textarea name=\""+x_temp+"\" "+e_attrs1+" class=\"inputTextarea\" >");
        if(inSubmittedForm)
        {
          out.print(ps.parammap[x_temp]);
        }
        else
        {
          stat_exptostring*
        }
        out.print("</textarea>"); 
      ]|
    with  e_inputident := <get-inputnumber-pil; pil-wrap-string> tc
        ; stat_exptostring* := <to-page-string-pil> e
        ; x_temp := <newname> "temp"
        ; e_attrs1 := <to-html-attrs-pil> passign*        
    
  elem-to-pil :
    elem |[ outputText(e)[passign*]{} ]| -> stat_exptostring*
    with stat_exptostring* := <to-page-string-pil(|pil-qid|[ webdsl::encoders::encodeMarkdown ]|)> e
 
