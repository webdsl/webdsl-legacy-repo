module org/webdsl/dsl/to-pil/control-flow

imports
  libstratego-lib

imports
  libwebdsl-front
    
// control flow page elements   : for, if, templatecall 

rules // iteration

  for-loop-counter-wrap-code-pil:
    stat* ->
    pil-stat*|[
        ident = "";
        if(inForLoop) {
          ident = ident + forelementcounter.as<String>;
        }
        stat*
    ]|
    
  wrap-in-for-loop-pil(|x,t,e) :
    bstm ->
    pil-stat*|[
      for(t x : e) {
        stat*
      }
    ]|
    with if <is-list> bstm
          then stat* := bstm
          else stat* := [bstm] end

  handle-actions-input-to-pil  =
    control-flow-for-pil(collect-om(handle-actions-input-to-pil)|)

  handle-actions-to-pil =
    control-flow-for-pil(collect-om(handle-actions-to-pil)|)

  elem-to-pil =
    control-flow-for-pil(elems-to-pil|)

  control-flow-for-pil(s|) :
    For(x, srt, e, Filter(we, obe, l), elems) ->
    pil-stat* |[
      inForLoop = true;
      forLoopCounter++;
      ~*<wrap-in-for-loop-pil(|x,t,e1)>
      pil-stat* |[  
        forelementcounter++;
        if(e2){ 
          stat*
        }
      ]|
      forLoopCounter--;
      if(forLoopCounter == 0) {
        inForLoop = false;
      }
    ]|
    with  stat* := <s;concat-with-sublists> elems
        ; e1 := <expression-to-pil> e
        ; e2 := <expression-to-pil> we
        ; t := <pil-type> srt
        ; x_list := <newname> "list"
        ; GenericSort(x_setorlist,[SimpleSort(_)]) := <type-of> e
    
  control-flow-for-pil(s|) :
    ForSep(x, srt, e, Filter(we, obe, l), elems1, elems2) ->
    pil-stat* |[
      inForLoop = true;
      forLoopCounter++;
      Int separatorindex=0;
      ~*<wrap-in-for-loop-pil(|x,t,e1)>
      pil-stat* |[  
        forelementcounter++;
        if(e2){ 
          if(separatorindex > 0) {
            stat2* // place separator if there was an element before this one
          }
          stat1*
          separatorindex++;
        }
      ]|
      forLoopCounter--;
      if(forLoopCounter == 0) {
        inForLoop = false;
      }
    ]|
    with  stat1* := <s;concat-with-sublists> elems1
        ; stat2* := <s;concat-with-sublists> elems2
        ; e1 := <expression-to-pil> e
        ; e2 := <expression-to-pil> we
        ; t     := <pil-type> srt
        ; GenericSort(x_setorlist,[SimpleSort(_)]) := <type-of> e

  control-flow-for-pil(s|) :
    fa@ForAll(x, srt, fltr@Filter(we, obe, l), elems) ->
    pil-stat* |[
      inForLoop = true;
      forLoopCounter++;
      
      List<Entity> x_list = dbSession.all(typeof data::x_ent);
      ~*<wrap-in-for-loop-pil(|x,t,pil-exp|[ x_list ]|)>
      pil-stat* |[  
        forelementcounter++;
        if(e2){ 
          stat*
        }
      ]|
      forLoopCounter--;
      if(forLoopCounter == 0) {
        inForLoop = false;
      }
    ]|
    with  stat*   := <s;concat-with-sublists> elems
        ; t       := <pil-type> srt
        ; SimpleSort(x_ent) := srt
        ; x_list := <newname> "list"
        ; e2 := <expression-to-pil> we

rules // if

  handle-actions-input-to-pil  =
    control-flow-if-pil(collect-om(handle-actions-input-to-pil))

  handle-actions-to-pil =
    control-flow-if-pil(collect-om(handle-actions-to-pil))

  elem-to-pil =
    control-flow-if-pil(elems-to-pil)

  control-flow-if-pil(s|) :
    IfTempl(e, elems, elems2) ->
    pil-stat|[
    if(e1){ 
      stat1*
    } else {
      stat2*
    }
    ]|
    with  stat1* := <s;concat-with-sublists> elems
        ; stat2* := <s;concat-with-sublists> elems2
        ; e1 := <expression-to-pil> e

rules //templatecall    

  handle-actions-input-to-pil  =
    control-flow-tcall-pil(|"storeInputs")

  handle-actions-to-pil =
    control-flow-tcall-pil(|"handleActions")

  elem-to-pil =
    control-flow-tcall-pil(|"render")

  control-flow-tcall-pil(|mx_methodname) :
    tc@TemplateCall(x_name, args,passign*, _) ->
    <for-loop-counter-wrap-code-pil> 
    pil-stat* |[
    ident = ident + e_tname;
    if(templatecalls[ident] == null) {
      try{
        templatecalls[ident] = templates[e_name].newInstance().as<webdsl::TemplateServlet>;
      }
      catch(Exception iae)
      {
        println("Problem in template servlet template lookup: "+iae.as<String>);
      }
    }
    Array<Object> x_args = new Array<Object>(e*);
    templatecounter++;
    templatecounter = templatecalls[ident].mx_methodname(ps, x_args, templatecounter, templates.clone(), variables.clone(), actionclasses.clone(), e1*);
    ]|
    where t* := <map(type-of)> args 
        ; <TemplateSignature <+ LocalTemplateSignature <+ !<strip-annos>x_name;TemplateWithGenerics> (x_name,t*)
    with  x_tname := <get-templatecallnumber-pil> tc
        ; e* := <map({\e -> <expression-to-pil> e\})> args
        ; e_name := <pil-wrap-string> x_name
        ; e_tname := <pil-wrap-string> x_tname
        ; x_args := <newname> "args"
        ; (    !mx_methodname; (?"render"<+?"handleActions"); e1* := [ pil-exp|[ out ]| ]
            //<+ mx_methodname := "handleActions";e1* := [ e|[ actionclasses ]| ]
            <+ e1* := []
          )
