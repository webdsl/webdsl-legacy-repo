module org/webdsl/dsl/syntax/utils

imports org/webdsl/dsl/syntax/WebDSL

strategies

  get-namefield =
    (oncetd(\ Property(name, k, type, annos) -> name where <fetch(?SimpleAnno("name"))> annos \; ?name ) <+  oncetd(\ DerivedProperty(name, k, type, annos, e) -> name where <fetch(?SimpleAnno("name"))> annos \; ?name )); !name
    <+ (oncetd(?Property("name", _, _, _) <+ ?DerivedProperty("name", _, _, _, _)); !"name")
    <+ !"id"

  is-namefield =
    ?Property(name, k, type, annos)
    ; where( <fetch(?SimpleAnno("name"))> annos)

  is-namefield =
    ?DerivedProperty(name, k, type, annos, e)
    ; where( <fetch(?SimpleAnno("name"))> annos)


rules
  qid-to-list(|xs) :
    Qualified(qid, x) -> <qid-to-list(|[x|xs])> qid

  qid-to-list(|xs) :
    x -> [x | xs]
    where <is-string> x

  qid-to-list =
    qid-to-list(|[])

  qid-to-string =
    qid-to-list
    ; foldr(!"", \ (x,y) -> <concat-strings>[x, ".", y] \ )

  qid-to-package-name :
    qid -> PackageName(xs)
    with xs := <qid-to-list; map(!Id(<id>))> qid
