module org/webdsl/dsl/to-java-servlet/page-utils

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

rules // embedding expressions in presentation

  /**
   * Converts an arbitrary WebDSL expression into java statements
   * that produce a string for displaying it on a page.
   *
   * Mainly used to prevent null-pointer exceptions breaking the entire page.
   */
  to-page-string-java-servlet =
    to-page-string-java-servlet(|"none")

  to-page-string-java-servlet(|x_filter) =
    to-page-string-java-servlet(|x_filter,"none")
    
  to-page-string-java-servlet(|x_filter,e3) :
    e -> 
    bstm* |[
      String x_tmpstring;
      try {
        x_tmpstring = e2;
      }
      catch(NullPointerException npe) {
        x_tmpstring = "";
      }
      out.println(x_tmpstring);
    ]|
    with e1 := <expression-to-java-servlet> e
       ; x_tmpstring := <newname> "tmpstring"
       ; if x_filter := "none"
         then e2 := expr |[ String.valueOf(e1) ]|
         else
           if e3 := "none" 
           then e2 := expr |[ x_filter(String.valueOf(e1)) ]| 
           else e2 := expr |[ x_filter(String.valueOf(e1),e3) ]|
           end
         end