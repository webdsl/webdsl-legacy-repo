module template-calls

imports include/nwl 
imports parameters 
imports expressions 
imports templates
imports utils
imports statements
imports entities

rules // projection

  signature-of : 
    Call(x, e*, elem*) -> (x, <map(type-of)> e*)

  call-of : 
    Call(x, e*, elem*) -> (x, e*)

  declaration-of : 
    Call(x, e*, elem*) -> <signature-of; Template>

  is-primitive-template =  
    ?"input" <+ ?"output" <+ ?"form"

rules // checking 

  check :
    Call("input", [e], []) -> (e, $[Argument of input should be variable or property access])
    where require(<is-lvalue> e)
   
  check :
    Call("output", [e], []) -> (e, $[Navigation to undefined page '[n](x: [n])'])
    where t@SimpleType(n) := <type-of> e
    where <is-entity-type> t
    where not(<Template> ("output", [t]))
    where require(<Template> (<lower-case>n, [t]))

  check :
    Call(x, e*, elem*) -> (x, $[Template '[x]' is not defined])
    where not(<is-primitive-template> x)
    where require(<Template> x)

  check :
    Call(x, e*, elem*) -> (x, $[No definition for template with signature '[x]([<map(type-of);pp> e*])'])
    where not(<is-primitive-template> x)
    where <Template> x
    where require(declaration-of)
  
  constraint-warning :
    Call(x, e*, elem*) -> [(x, $[Page definition is used as template])]
    where def := <declaration-of>
    where require(not(<is-page-def> def))
        
  check :
    Call(x, e*, elem*) -> <check-args>

rules // pretty-print

  pp-sig : (x, t*) -> $[[x]([<pp>t*])]
          
rules // desugaring template calls

  desugar : 
    CallArgs(x, e*) -> Call(x, e*, [])
  
  desugar :
    CallElems(x, elem*) -> Call(x, [], elem*)
  
  desugar : 
    Call(x) -> Call(x, [], [])
 
  check :
    Call("form", [e*], []) -> (e*, $[Form should not have arguments])
    where require(<length> e* => 0)
  
rules 

// element composition validation
// input should occur in form
// listitem should occur in list
