/**

 markdown normalization rules for WebDSL

*/

module org/webdsl/dsl/transformation/normalize-markdown

imports
  libstratego-lib

imports
  libwebdsl-front

rules 

  normalize :
    MDListItem(p, e) -> TemplateCall("listitem",[],p2,[e])
    with  p2 := <md-props-to-props> p

  normalize :
    MDListItemMulti(p, e) -> TemplateCall("listitem",[],p2,e)
    with  p2 := <md-props-to-props> p
    
  normalize :
    MDSpacer(p)  -> TemplateCall("spacer",[],p2,[])
    with p2 := <md-props-to-props> p

  normalize :
    MDHeader(p, e)  -> TemplateCall("header",[],p2,[e])
    with p2 := <md-props-to-props> p
    
  normalize :
    MDBlock(p, e)    -> TemplateCall("block",[], p2, e)
    with p2 := <md-props-to-props> p
    
  normalize :
    MDRowNoCols(p,e)  ->TemplateCall("row",[],p2,e)
    with p2 := <md-props-to-props> p
    
  normalize :
    MDRow(p, cols)   -> TemplateCall("row",[],p2, e2)
    with e2 := <map(normalize-md-col)> cols
        ; p2 := <md-props-to-props> p
        
  normalize-md-col :
    MDCol(p, e)  -> TemplateCall("column",[],p2, e)
    with p2 := <md-props-to-props> p
    
  normalize :
    MDNavigate(p, exp, elem)  -> TemplateCall("navigate",[exp],p2, [elem])
    with p2 := <md-props-to-props> p

//NORMALIZE TEMPLATE CALL SHORTHANDS
  
  normalize :
    TemplateCall(name, args, assigns, elems) -> TemplateCall(name2, args, assigns, elems)
    where <fetch(?(name, name2))> <template-short-names>

  template-short-names = ![
    ("p", "par"), 
    ("s", "section"),
    ("h", "header"), 
    ("t","table"), 
    ("r", "row"),
    ("c", "column"),
    ("cell", "column"),
    ("b", "block"),
    ("sp", "spacer"),
    ("i", "image"),
    ("lbl", "label"),
    ("g", "group"),
    ("gi", "groupitem"),
    ("li", "listitem"),
    ("nav", "navigate"),
    ("link", "navigate"),
    ("btn", "action"),
    ("button", "action"),
    ("out", "output")
  ]

//NORMALIZE PROPERTIES

  md-props-to-props :
    None()  -> []
    
  md-props-to-props :
    Some(MDProps(p))  -> p