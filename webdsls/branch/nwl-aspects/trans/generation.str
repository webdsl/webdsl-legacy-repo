module generation

imports libstratego-aterm libstratego-gpp
imports include/nwl
imports desugar type-analysis
imports check

rules
  
  module-name : Module(x, d*) -> x
  
rules // generation

  generate-java:
    (selected, position, ast, path, project-path) ->
    ($[[<module-name>ast].java], <nwl-to-java> selected)

  generate-aterm:
    (selected, position, ast, path, project-path) -> (filename, result-string)
    with
      filename      := <guarantee-extension(|"aterm")> path;
      result-string := <pp-aterm-box; box2text-string(|120)> selected

rules

  nwl-to-java =
    module-to-toplevel-defs;
    desugar-all;
    get-toplevel-defs;
    debug;
    bottomup(to-java-ignore <+ to-java <+ is-string <+ concat-strings <+ aterm-to-string);
    !$[package [<ModuleName>];
   
[<id>]
      ]
      
  to-java-ignore =
    is-java-ignore; !""
    
  is-java-ignore = fail
  
  to-java:
    Function(name,params*,body) -> 
    $[ public void [name]([params*]) {
          [body]
       }
    ]
    
  to-java:
    Param(ident,sort) -> $[[sort] [ident]]
    
  to-java:
    Block(x) -> $[{
      [x]
    }]
    
  to-java:
    Assign(var, val) -> $[[var] = [val];
    ]
  
  to-java: Var(x) -> x
  to-java: StringLit(x) -> x
  to-java: IntLit(x) -> x
  
  to-java: Plus(x,y) -> $[[x] + [y]] 
  
  to-java:
    Entity(x, p*) ->
    $[ class [x] {
           [p*]
       }
     ]

  to-java:
    Property(x, t) -> $[
      private [t] [x];
    
      public [t] get_[x] {
        return [x];  
      }
    
      public void set_[x] ([t] [x]) {
        this.[x] = [x];    
      }
    ]

  to-java:
    SimpleType(x) -> x
   
  aterm-to-string =
    pp-aterm-box; box2text-string(|120)
    