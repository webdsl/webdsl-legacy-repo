module desugar

imports libstratego-aterm libstratego-gpp
imports include/nwl
imports emit read check
imports separation/track separation/desugar

rules // desugared builder
    
  show-desugared :
    (selected, position, ast, path, project-path) -> (filename, result-string)
    with
      filename      := <guarantee-extension(|"aterm")> path;
      result-string := <desugar-all; pp-aterm-box; box2text-string(|120)> selected

  show-desugared-pp :
    (selected, position, ast, path, project-path) -> (filename, result-string)
    with
      filename      := <guarantee-extension(|"d.nwl")> path;
      result-string := <normalize-all; pp-nwl> selected
      
strategies
  
  is-used-dr = id
  ignore-dr-tracking = ?"DesugarApplied"
  
  DesugarSave = id
  
  desugar-all = {| DesugarRulesCreated, DesugarRulesUsed:
    if <not(file-exists)> ".cache/testdr.aterm" then
      load-cached-files;
      desugar-all-1;
      save-desugar-rules
    else
      rules(IncrementalDesugaring := True());
      load-desugar-rules;
      with-rule-resolving(desugar-all-1);
      desugar-all-1
    end
  |}
  
  desugar-all-1 = 
    repeat(
      {| DesugarApplied:
        transform-defs( {
          track-rules(
            innermost(
               desugar;
               rules(DesugarApplied := True()))
            | <CurrentDefinitionKey>
          )})
      ; where(DesugarApplied)    // quit if nothing done
      ; try(where(IncrementalDesugaring); desugar-check-dependencies)
      ; add-emits
      |}
    )
    ; remove-defs(?[])
      
  track-rules(s) = s
  
  normalize-all = alltd(normalize)  // todo: innermost
  
  desugar = fail
  
rules

  normalize : 
    Call(r) -> Call(r, [])
  
  normalize :
    TemplateRef(x) -> TemplateRef(x, [])
      
strategies // pretty-printing
  
  pp-nwl =
    ast2abox(|[<import-term(include/nwl.pp.af)>, <import-term(syntax/nwl-pretty.pp.af)>]);
    box2text-string(|120) 
    
      