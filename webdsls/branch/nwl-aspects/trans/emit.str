module emit

imports libstratego-aterm libstratego-gpp
imports include/nwl read rename

signature constructors
  
  EmittedBy : Term -> Term  

rules
    
  ignore-dr-tracking = ?"Emit"
    
  emit: def -> def
    with
      key := <CurrentDefinitionKey <+ !"">;
      rules(Emit :+= (key, def))
    
  retrieve-emits =
    bagof-Emit;
    rules(Emit :- _)
  
  add-emits =
    with( retrieve-emits;
          map({
            ?(defkey,def);
            if not("" := defkey) then
              mod := <defkey-to-mod> defkey;
              <store-def(|mod,defkey)> def
            else
              <store-def(|<new-emit-mod-name>)> def
            end
          })
        )
        
  add-anno(|anno): x{anno*} -> x{anno, anno*}
      
  emit-mod-name = !"___EMITTED___"
          
  new-emit-mod-name = 
    emit-mod-name;
    newid
    
  is-emit-mod = where(string-starts-with(|<emit-mod-name>))
  