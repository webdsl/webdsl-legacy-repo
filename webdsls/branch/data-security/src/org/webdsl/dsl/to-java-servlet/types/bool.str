module org/webdsl/dsl/to-java-servlet/types/bool

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front

rules 

  java-servlet-element-id :
    tc@elem|[ inputBool(e)[passign*]{elem*} ]| -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"

  handle-actions-input-to-java-servlet :
    tc@elem|[ inputBool(e)[passign*]{elem*} ]| ->
    <wrap-input-java-code(|passign*)>
    bstm*|[
        ident += "~inputident"+uniqueid;
        if(ps.getParammap().get(ident+"_isinput")!=null){
          ~*<wrap-validation-context-code-store-inputs> bstm*|[
            if(ps.getParammap().get(ident)!=null){
                boolean x_temp = true;
                bstm* 
            }
            else{
                boolean x_temp = false;
                bstm* 
            }
          ]|
        }
    ]|
    with  inputident := <get-inputnumber> tc
        ; x_temp := <newname> "temp"
        ; bstm* := <input-assignment-to-java-servlet(|x_temp)> e

        
  elem-to-java-servlet :
    tc@elem|[ inputBool(e)[passign*]{elem*} ]| ->
    <wrap-input-render-java-code>
    bstm* |[ 
      String x_temp = ident+"~inputident"+uniqueid;
      ~*<render-error-messages-with-error-template(|java:expr|[ x_temp ]|,<ErrorTemplateInput>)> bstm*|[
        out.print("<input type=\"hidden\" name=\""+x_temp+"_isinput"+"\" />");
        out.print("<input type=\"checkbox\"");
        ~*<connect-with-enclosing-label>
        out.print("name=\""+x_temp+"\""+e_attrs1);
        if(inSubmittedForm)
        {
          if(ps.getParammap().get(x_temp)!=null)
          {
            out.print(" checked=\"true\" ");
          }
        }
        else
        {
          if(e1)
          {
            out.print(" checked=\"true\" ");
          }
        }
        out.print(" />"); 
      ]|
    ]|
    with  inputident := <get-inputnumber> tc
        ; e1 := <expression-to-java-servlet> e
        ; x_temp := <newname> "temp"
        ; e_attrs1 := <to-html-attrs(|"inputBool")> passign*

  elem-to-java-servlet :
    elem|[ outputBool(e)[passign*]{} ]| ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<input type=\"checkbox\" disabled=\"true\""+e_attrs1);
    if(e1)
    {
      out.print(" checked=\"true\" ");
    }
    out.print(" />"); 
    ]|
    with  e1 := <expression-to-java-servlet> e
        ; e_attrs1 := <to-html-attrs(|"inputBool")> passign*
  
rules

  builtin-java-type :
    SimpleSort("Bool") -> type|[ Boolean ]|
    
rules // literals
  expression-to-java-servlet :
    True() -> java|[ true ]|

  expression-to-java-servlet :
    False() -> java|[ false ]|