module org/webdsl/dsl/to-java-servlet/uicomponents/fieldset

imports
  libstrategolib

imports
  libwebdsl-front

rules

  elem-to-java-servlet :
    TemplateCall("fieldset", [arg],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<fieldset "+e_attrs1+"><legend>");
    bstm_exptostring*
    out.print("</legend>");
    bstm*
    out.print("</fieldset>");
    ]|
    with  bstm* := <elems-to-java-servlet> elems
       ;  bstm_exptostring* := <to-page-string-java-servlet> arg
       ;  e_attrs1 := <to-html-attrs> passign*

  elem-to-java-servlet :
    TemplateCall("fieldset", [],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<fieldset class=\"fieldset_no_legend_\" "+e_attrs1+">");
    bstm*
    out.print("</fieldset>");
    ]|
    with bstm* := <elems-to-java-servlet> elems
       ; e_attrs1 := <to-html-attrs> passign* 