module org/webdsl/dsl/to-java-servlet/java-template-vars

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

rules // page variables

  /**
   * Generating code for page variables
   */
  GenerateCodeJavaServlet =  where(ThisTemplateName);not(InAction <+ InFunction <+ JavaInEntity); generate-java-servlet-variables-code; fail

  generate-java-servlet-variables-code :
    elem|[ var x : srt ]| ->
    //VarDecl(x, s) ->
    <generate-java-servlet-template-partial> class-body-dec*|[
       private t x = e;
       @Partial private void cleanupTemplateArgAndVar(){ x = null; }
       public t get#x() { return x; }
       public void set#x(t x) { this.x = x; }
       @Partial
       private void initializeLocalVars(){ 
         ps.ignoreset.add("~<OriginalNonUniqueName> x "); //hibernate hack
       } 
    ]|
    with  t     := <java-type> srt
        ; X     := <capitalize-string> x
        ; e     := <java-type-default-value> srt

  generate-java-servlet-variables-code :
    //VarDeclInit(x, s, e1) ->
    elem|[ var x : srt := e1 ]| ->
    <generate-java-servlet-template-partial> class-body-dec*|[
       private t x;
       @Partial private void cleanupTemplateArgAndVar(){ x = null; }
       public t get#x() { return x; }
       public void set#x(t x) { this.x = x; }
       @Partial
       private void initializeLocalVars(){ 
         x = e2; 
         ps.ignoreset.add("~<OriginalNonUniqueName> x"); //hibernate hack
       }
    ]|
    where not(HqlQuery(_) := e1)
    with  t     := <java-type> srt
        ; X     := <capitalize-string> x
        ; e2 := <expression-to-java-servlet> e1

  // page-level variables with query initializers
  generate-java-servlet-variables-code :
    elem|[ var x : srt := e ]| ->
    <generate-java-servlet-template-partial> class-body-dec*|[
       private t x;
       @Partial private void cleanupTemplateArgAndVar(){ x = null; }
       public t get#x() { return x; }
       public void set#x(t x) { this.x = x; }
       public void init#x() { x = e2.list(); }
       @Partial 
       private void initializeLocalVars() { 
         init#x(); 
         ps.ignoreset.add("~<OriginalNonUniqueName> x"); //hibernate hack
       }
    ]|
    where <?HqlQuery(e1)> e
    with  t      := <java-type> srt
        ; X      := <capitalize-string> x
        ; e2     := <query-to-create-query> e1


//store vars in scope
  GenerateCodeJavaServlet =  where(ThisTemplateName);not(InAction <+ InFunction <+ JavaInEntity); store-var-java-servlet; fail
 
  store-var-java-servlet:
    elem|[ var x : srt ]| -> 
    <generate-java-servlet-template-partial> java|[
       @Partial 
       private void initializePassOn() { env.putVariable("~x",x); } // the x is a renamed var name, so local vars do not overlap
    ]|
    with  rules (Variables :+= elem|[ var x : srt ]|)
                
  store-var-java-servlet:
    elem|[ var x : srt := e ]| -> 
    <generate-java-servlet-template-partial> java|[
       @Partial 
       private void initializePassOn() { env.putVariable("~x",x); }
    ]|
    with  rules (Variables :+= elem|[ var x : srt ]|)
    
//retrieve vars in scope

  GenerateCodeJavaServlet = retrieve-stored-vars-java-servlet; fail
 
  retrieve-stored-vars-java-servlet:
    def |[ define mod* x(farg*) req* { elem* } ]| -> []
    where vars := <bagof-Variables>
    with  <map( //\ x -> x where <oncetd(?Arg(x,_))> farg* \;debug(!1) <+ debug(!2);  //check whether arg names shadow the passed vars (check vars in page too?)
    generate-java-servlet-passed-variables-code)> vars
               
                 
  generate-java-servlet-passed-variables-code:
    elem|[ var x : srt ]| ->
    <generate-java-servlet-template-partial> java|[
       private t x;
       public t get#x() { return x; }
       public void set#x(t x) { this.x = x; }
       @Partial
       protected void initialize(){ x = (x_arg) env.getVariable("~x"); }
    ]|
    with  t     := <java-type> srt
        ; x_arg := <java-type-name> srt
