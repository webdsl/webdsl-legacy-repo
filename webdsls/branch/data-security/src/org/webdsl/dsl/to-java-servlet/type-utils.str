module org/webdsl/dsl/to-seam/type-utils

imports 
  libstrategolib  
  Java-15
  libjava-front 
  
imports 
  libwebdsl-front
  
rules

  java-type =
    builtin-java-type <+ defined-java-type <+ with(fail)

  defined-java-type :
    s@SimpleSort(x_class) -> type|[ x ]|
    where <is-entity-type> s
    with  x := <concat-strings> [<pp-java5-to-string> <DomainPackage>, ".", x_class]
    
  java-type-name :
    srt -> x_Class
    where x_Class := <java-type;pp-java5-to-string> srt   

  //override used by list and set, see types/collection.str
  java-type-default-value =
     type-default-value-override-java 
  <+ type-default-value ; expression-to-java-servlet 
  <+ with(fail)

  persistence-annotations =
    builtin-persistence-annotations <+ ![]
   
rules //webdsl arguments to java types

  argument-java-servlet-java-type:
    arg@farg|[ x : srt ]| -> t
    where t := <java-type> srt

  // give just the name of the type, (eg. for use in a cast)
  argument-java-servlet-type:
    arg@farg|[ x : srt ]| -> x_Class
    where x_Class := <java-type-name> srt      
   
rules //webdsl formal argument to java formal argument 
  
  arguments-to-java-servlet :  
    arg@farg|[ x : srt ]| -> param |[ t x ]|
    with  t := <argument-java-servlet-java-type> arg