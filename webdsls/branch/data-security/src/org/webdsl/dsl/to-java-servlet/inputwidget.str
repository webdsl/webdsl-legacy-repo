module org/webdsl/dsl/to-java-servlet/inputwidget

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front

rules 

  is-input-template =
    ?tc@TemplateCall(n,args,p,b)
    ;where ( <IsInputTemplate> n )

  java-servlet-element-id : //Both tcall and input number
    tc@TemplateCall(_,_,_,_){anno*} -> tc{InputNumber(newid),TemplateCallNumber(tn),anno*} 
    where <is-input-template> tc
    with  newid := <newname> "input"
        ; tn := <newname> "tcall"

  handle-actions-to-java-servlet :
    tc@elem|[ x(e)[passign*]{elem*} ]| ->
    <wrap-input-render-java-code>
    bstm* |[ 
      String x_temp = ident+"~inputident"+uniqueid;
//      ~*<render-error-messages-with-error-template(|java:expr|[ x_temp ]|,<ErrorTemplateInput>)> bstm*|[
        bstm_call*
//      ]|
    ]|
    where <is-input-template> tc
    with  inputident := <get-inputnumber> tc
        ; x_temp := <newname> "temp"
        ; bstm_call* := <control-flow-tcall-helper(|"handleActions",  expr|[ x_temp ]|)> tc
    
  handle-actions-input-to-java-servlet :
  //TODO: now only supports string
    tc@TemplateCall(x,e,passign*,elem*) ->
    <wrap-input-java-code(|passign*)>
    bstm*|[
      ident += "~inputident"+uniqueid;
      if (ps.getParammap().get(ident)!=null) {
        ~*<wrap-validation-context-code-store-inputs> bstm*|[
          String x_temp = ps.getParammap().get(ident);
          bstm*           
          bstm_call* 
        ]|
      }
    ]|
    where <is-input-template> tc
    with  inputident := <get-inputnumber> tc
        ; x_temp := <newname> "temp"
        ; bstm* := <Hd;input-assignment-to-java-servlet(|x_temp)> e
        ; bstm_call* := <control-flow-tcall-helper(|"storeInputs", expr |[ x_temp ]|)> tc
        

    
  elem-to-java-servlet : 
    tc@elem|[ x(e)[passign*]{elem*} ]| ->
    <wrap-input-render-java-code>
    bstm* |[ 
      String x_temp = ident+"~inputident"+uniqueid;
      ~*<render-error-messages-with-error-template(|java:expr|[ x_temp ]|,<ErrorTemplateInput>)> bstm*|[
        bstm_call*
      ]|
    ]|
    where <is-input-template> tc
    with  inputident := <get-inputnumber> tc
        ; x_temp := <newname> "temp"
        ; bstm_call* := <control-flow-tcall-helper(|"render",  expr|[ x_temp ]|)> tc
        
