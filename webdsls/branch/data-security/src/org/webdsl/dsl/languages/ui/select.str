module org/webdsl/dsl/languages/ui/select

rules

  rename-ui :
    Select(x, s, lab1, e1, props,elems) -> Select(y, s, lab2, e2, props,elems1) 
    with  (y, s, lab2, e2, elems1) := <rename-select> (x, s, lab1, e1, elems)
 
  rename-ui :
    SelectNoProps(x, s, lab1, e1,elems) -> SelectNoProps(y, s, lab2, e2,elems1) 
    with  (y, s, lab2, e2, elems1) := <rename-select> (x, s, lab1, e1, elems)
  rename-ui :
    SelectNoPropsNoElems(x, s, lab1, e1) -> SelectNoPropsNoElems(y, s, lab2, e2) 
    with  (y, s, lab2, e2, []) := <rename-select> (x, s, lab1, e1, [])
  rename-ui :
    SelectNoElems(x, s, lab1, e1, props) -> SelectNoElems(y, s, lab2, e2, props) 
    with  (y, s, lab2, e2, []) := <rename-select> (x, s, lab1, e1, [])
        
  rename-select :
    (x, s, lab1, e1, elems1) -> (y, s, lab2, e2, elems2) 
    with {| Rename
            : y := <rename-bound(|s)> x
            ; e2 := <rename-all> e1
            ; lab2 := <rename-all> lab1
            ; elems2 := <rename-all> elems1
          |}
         
rules

  normalize-ui : 
    SelectNoProps(a,b,c,d,e) -> Select(a,b,c,d,[],e)
  normalize-ui : 
    SelectNoPropsNoElems(a,b,c,d) -> Select(a,b,c,d,[],[])
  normalize-ui : 
    SelectNoElems(a,b,c,d,e) -> Select(a,b,c,d,e,[])
 
  normalize-ui : 
    SelectFromListNoProps(a,b,c) -> SelectFromList(a,b,[],c)
  normalize-ui : 
    SelectFromListNoPropsNoElems(a,b) -> SelectFromList(a,b,[],[])
  normalize-ui : 
    SelectFromListNoElems(a,b,c) -> SelectFromList(a,b,c,[])
    
rules

  constraint-error-ui :
    sel@SelectFromListNoProps(e1, e2, elems)    -> <constraint-error-ui-selectfrom> (e1,e2,sel)
  constraint-error-ui :
    sel@SelectFromList(e1, e2, passign*, elems) -> <constraint-error-ui-selectfrom> (e1,e2,sel)
  constraint-error-ui :
    sel@SelectFromListNoPropsNoElems(e1, e2)    -> <constraint-error-ui-selectfrom> (e1,e2,sel)
  constraint-error-ui :
    sel@SelectFromListNoElems(e1, e2, passign*) -> <constraint-error-ui-selectfrom> (e1,e2,sel)
    
  constraint-error-ui-selectfrom :
    (e1,e2,sel)  -> <add-error(|[<pp-webdsl> e2, " should be of collection type"])> sel
    where not(GenericSort(_, _) := <type-of> e2)

  constraint-error-ui-selectfrom :
    (e1,e2,sel) -> <add-error(|["type of ",<pp-webdsl> e1, " (",<pp-webdsl> srt2,") should be compatible with type in collection ",<pp-webdsl> e2," (",<pp-webdsl> srt,")"])> sel
    where not(
            srt3 := <type-of> e2
          ; GenericSort(_, [srt]) := srt3
          ; srt2 := <type-of> e1
          ; (srt2 := srt <+ srt2 := srt3)// TODO hard check is too restrictive, but type-compatible seems broken
          )

/**
 *  'allowed=exp' annotation
 */
rules

  desugar-ui :
    tc@Select(x, srt, label, FieldAccess(var,x_prop), passign*, elem*) ->
    result
    where SimpleSort(x_vartypename) := <type-of> var
        ; propannos := <property-annos> (x_vartypename,x_prop) 
        ; <fetch(?AllowedAnno(e1))> propannos
    with  t_typeofcol := <type-of> e1
        ; x_name := <allowed-collection-function-name> x_prop
        ; e_col := Call(var,x_name,[])
        ; result := SelectFromList(FieldAccess(var,x_prop), e_col, passign*, elem*)

// TODO constraint: 'allowed=' collection must be Set or List of property type
// constraint: only 1 'allowed'
// constraint: overlap with generated function below

strategies

  desugar-ui :
    Entity(x_class,super,props,funcs) -> <rename> Entity(x_class,super,props1,funcs1) 
    where <oncetd(not-translated-allowed-anno)> props
    with  (props1,funcs1) := <get-all-function-allowed-annotation(|x_class)> (props,funcs)
    
  desugar-ui : 
    EntityNoSuper(x_class,props,funcs) -> <rename> EntityNoSuper(x_class,props1,funcs1) 
    where <oncetd(not-translated-allowed-anno)> props
    with  (props1,funcs1) := <get-all-function-allowed-annotation(|x_class)> (props,funcs)
    
  desugar-ui :
    SessionEntity(x_class,props,funcs) -> <rename> SessionEntity(x_class,props1,funcs1) 
    where <oncetd(not-translated-allowed-anno)> props
    with  (props1,funcs1) := <get-all-function-allowed-annotation(|x_class)> (props,funcs) 
   
signature
  constructors 
    AllowedAnnotationFunctionGenerated: Term
  
rules 

  not-translated-allowed-anno : 
    prop@Property(x, k, srt, annos) -> prop
    where <fetch(?AllowedAnno(e1))> annos
        ; <not(fetch(AllowedAnnotationFunctionGenerated))> annos

  get-all-function-allowed-annotation(|x_class):
    (props,funcs) -> (props2,funcs2)
    with  tuples := <map(get-function-allowed-annotation(|x_class) <+ !(<id>,[]) )> props
        ; (props2,funcs1) := <unzip> tuples
        ; funcs2 := <flatten-list> funcs1

  get-function-allowed-annotation(|x_class) :  
    prop@Property(x, k, srt, annos) -> 
    (Property(x, k, srt, [AllowedAnnotationFunctionGenerated()|annos])
    ,result)
    where not-translated-allowed-anno
    with  <fetch(?AllowedAnno(e1))> annos
        ; x_name := <allowed-collection-function-name> x
        ; srt_typeofcol := <type-of> e1
        ; result := 
            fun |[ 
              function x_name():srt_typeofcol { return e1; } 
            ]|
     
  allowed-collection-function-name : x -> <concat-strings> ["allowedCollectionForProperty",<capitalize-string> x]   
  

/**
 *  'not null' annotation
 */
 rules
 
  desugar-ui :
    Select(x, srt, label, FieldAccess(var,x_prop), passign*, elem*) ->
    Select(x, srt, label, FieldAccess(var,x_prop), passign1*, elem*)
    where passign1* := <desugar-not-null-anno(|passign*, var, x_prop)>
    
  desugar-ui :
    SelectFromList(FieldAccess(var,x_prop), e, passign*, elem*) ->
    SelectFromList(FieldAccess(var,x_prop), e, passign1*, elem*)
    where passign1* := <desugar-not-null-anno(|passign*, var, x_prop)>
       
  desugar-not-null-anno(|passign*, var, x_prop) :
    _ -> passign1*
    where not(<fetch(?PropertyAssignment("notnull", "true"))> passign*)
        ; SimpleSort(x_vartypename) := <type-of> var
        ; propannos := <property-annos> (x_vartypename,x_prop) 
        ; <fetch(?NotNullAnno())> propannos
    with  passign1* := [PropertyAssignment("notnull", "true") | passign*]
        