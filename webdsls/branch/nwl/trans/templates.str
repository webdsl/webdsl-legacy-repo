module templates

rules // template definitions

  declare-template :
    TemplateDef(mod*, x, params, elems) -> TemplateDef(mod*, x, params, elems)
    with ?def; rules( Template : x -> def )
             
  is-page-def =
    ?TemplateDef([Page()],_,_,_)
    
  param-types :
    TemplateDef(mod*, x, params, elems) -> <map(?Param(_,<id>))> params 
    
  rename :
    TemplateDef(mod*, x, param1*, elem1*) -> TemplateDef(mod*, x, param2*, elem2*)
    with {| RenameId
          : param2* := <rename-all> param1*
          ; elem2*  := <rename-all> elem1*
          |}
 
  rename : 
    Param(x, t) -> Param(y, t)
    with y := <rename-var>(x, t) 
     
rules // template variables

  rename :
    VarDeclElem(x, t, e1) -> VarDeclElem(y, t, e2)
    with e2 := <rename-all> e1; 
         y := <rename-var> (x, t)
         
  check :
    VarDeclElem(x, t, e) -> (e, ["Expression should have type '", <pp>t, "'"])
    where not(<type-of> e => t)
    
rules // template element iteration 
         
  rename :
    ForElem(x, t, e1, elem1*) -> ForElem(y, t, e2, elem2*)
    with e2 := <rename-all> e1;
          {| RenameId 
           : y := <rename-var>(x, t)
           ; elem2* := <rename-all> elem1*
           |}
           
  check :
    ForElem(x, t, e, elem*) -> (e, ["Should have type '", <pp>SetType(t), "'"])
    where not(<type-of> e => SetType(t))
    
rules // navigation

  template-of :
    Navigate(x, args, elems) -> <Template> x
    
  check :
    Navigate(x, args, elems) -> (x, ["Navigation to non-existing page"])
    where not(template-of)
    
  check :
    Navigate(x, args, elems) -> (x, ["Navigation to (non-page) template definition"])
    where not(template-of; is-page-def)
  
  check :
    Navigate(x, e*, elems) -> (e*, ["Template '", x, "' expects ", k, " arguments, not ", l])
    where k := <template-of; param-types; length>;
          l := <length> e*;
          not(<eq>(k,l))
          
  check :
    Navigate(x, e*, elems) -> errors 
    where t* := <template-of; param-types>; 
          errors := <zip(check-arg)> (e*, t*)

  check-arg :
    (e, t) -> (e, ["Type of argument should be '", <pp>t, "'"])
    where not(<type-of> e => t) 

rules

  // check that argument of input is an lvalue (syntactic constraint gives bad error message)
  
rules

  // element composition validation
  // input should occur in form
  // listitem should occur in list
  
rules

  // calling tempaltes in pages ?
  
  // navigation is like a regular template call
  
  // navigate TemplateCall
  
  
    
