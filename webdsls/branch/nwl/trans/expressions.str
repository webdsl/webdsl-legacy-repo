 module expressions 
 
rules

  hover-help :
    e -> <pp> t
    where t := <type-of> e
 
 rules
 
   is-lvalue = 
     ?Var(_) <+ ?PropertyAccess(_, _)
 
 rules // string literals
 
   type-of :
     StringLit(x) -> SimpleType("String")
     


 rules // expressions
 
  type-of :
    PropertyAccess(e, f) -> t
    where t := <type-of; lookup-property-type(|f)> e
    
  type-of :
    PropertyAccess(e, "name") -> t
    where t := <type-of; lookup-name-property; type-of> e
          
  check :
    e1@PropertyAccess(e2, f) -> (f, $[[<pp>t] has no property '[f]])
    where t := <type-of> e2
    where not(<type-of>e1)
    
rules // constructors

  type-of :
    Constructor(x) -> SimpleType(x)
    
  check :
    Constructor(x) -> (x, $[Constructor for undefined type '[x]'])
    where not(type-of; is-entity-type)
    
  check :
    Constructor(x) -> (x, $[Cannot construct objects for non-entity types])
    where type-of; not(is-entity-type)

rules // method calls

  type-of :
    MethodCall(e, f, e*) -> t
    where t := <method-type> (<type-of>e, f, <map(type-of)> e*)
    
  method-type :
    (t, "save", []) -> SimpleType("Void")
    where <is-entity-type> t
    
  method-type :
    (EntityType(x), "all", []) -> SetType(SimpleType(x))
    
  check :
    MethodCall(e, f, e*) -> (f, $[Method not defined])
    where not(type-of)
    
    