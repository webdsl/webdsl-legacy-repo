module entities

signature
  constructors
    EntityType : ID -> Type
  
rules // declaring entities

  declare-def:
    ent@Entity(x, prop*) -> Entity(x, prop*)
    with rules( 
           EntityDeclaration : x -> ent
           TypeOf : x -> EntityType(x)
         )
         
  declaration-of :
    SimpleType(x) -> <EntityDeclaration> x
 
rules // checking entity declarations

  constraint-warning :
    Entity(x, _) -> (x, $[Entity names must start with a capital letter])
    where not(<explode-string; Hd; is-upper> x)
      
  check :
    ent@Entity(x, prop*) -> (x, $[Entity '[x]' defined more than once])
    where not(<EntityDeclaration> x => ent)

rules // primitive types

  carrier-type =
    try(?SetType(<id>))
    
  declare-primitive-types =
    where( primitive-types; map(declare-primitive-type) )
    
  primitive-types =
    !["String", "Int", "Text", "WikiText", "Secret"]
    
  declare-primitive-type =
    ?x; rules( IsPrimitiveType : x -> x )
    
  is-primitive-type =
     SimpleType(IsPrimitiveType)
     
  is-string-type =
    ?SimpleType("String")
   
  is-entity-type =    
    where(SimpleType(EntityDeclaration))

  name-of :
    Entity(x, prop*) -> x
    
  type-of :
    Entity(x, prop*) -> SimpleType(x)
    
  is-simple-type =
    is-primitive-type <+ is-entity-type

rules // pretty-printing types

  pp : SimpleType(x) -> x
  pp : SetType(t) -> $[Set<[<pp> t]>]
  pp : [] -> $[]
  pp : [t] -> <pp>t
  pp : [t1,t2|ts] -> $[[<pp>t1],[<pp>[t2|ts]]]
  
rules // checking types

  check :
    SimpleType(x) -> (x, $[Type '[x]' is not defined])
    where not(is-simple-type)
    
  check :
    t@SetType(type) -> (t, $[Set should have entity type as argument])
    where <is-simple-type> type
    where not(<is-entity-type> type)
    
