module org/webdsl/dsl/transformation/desugar

imports
  libstrategolib
  Java-15
  libjava-front

imports
  libwebdsl-front

signature
  constructors
    RemoveMe : Annotation

strategies

  desugar-top =
    repeat(innermost-rep(desugar))

  innermost-rep(s) =
    {| InnermostNormalForm, InnermostApplied
     : rules(InnermostApplied := False())
     ; innermost-rep-aux(s)
     ; where(InnermostApplied => True())
     |}

  innermost-rep-aux(s) =
    InnermostNormalForm                           // don't normalize terms in normal form
    <+ all-consnil(innermost-rep-aux(s))          // bottom-up traversal
       ; try(
           s                                      // apply rule
           ; rules( InnermostApplied := True() )  // remember that a rule was applied
           ; innermost-rep-aux(s)                 // normalize reduct
         )
       ; {?q; rules( InnermostNormalForm : q )}   // memoize normal forms

  all-consnil(s) = 
    ?[_ | _] < [s | s] + all(s)

    // (standard all(s) behaves as map on lists; does not normalize sub-lists)

  desugar :
    [RemoveMe()|lst] -> lst

rules 
  
  rename-all-vars :
    (t, []) -> (t, [])

  rename-all-vars :
    (t, [(from, to)|rst]) -> <rename-all-vars> (t2, rst)
    where t2 := <rename-var(|from, to)> t


