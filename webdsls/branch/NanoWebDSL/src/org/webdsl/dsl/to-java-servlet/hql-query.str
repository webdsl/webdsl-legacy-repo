module org/webdsl/dsl/to-java-servlet/hql-query

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front
  
rules // query translation
   expression-to-java-servlet :
     HqlQuery(e1) -> expr|[ e2.list() ]|
     with  {| AddQueryParmeter : e2 := <query-to-create-query> e1 |}

  query-to-create-query :
    e -> <repeat(once-AddQueryParameter)> java|[ hibSession.createQuery("~q") ]|
    where q := <alltd(replace-parameter-java-servlet); pp-hql-to-string; trim-chars('\n')> e

  replace-parameter-java-servlet :
    DslExp(e1) -> IdParam(x2)
    where x1  := <newname> "param"
        ; x2 := <concat-strings>[":", x1]
        ; e2 := <expression-to-java-servlet> e1
        ; rules(
            AddQueryParameter :+ e -> java|[ e.setParameter("~x1", e2) ]|
          )