module org/webdsl/dsl/to-java-servlet/request-param-conversion

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

rules 

  java-servlet-conversion-to-object :
    (type|[ java.util.UUID ]|,x_name,x_id) ->
    bstm* |[
    try{ 
      x_id = java.util.UUID.fromString(parammap.get("~<OriginalNonUniqueName> x_name")); 
    }
    catch(IllegalArgumentException nfe){System.out.println(nfe.getMessage());}  
    ]|

  java-servlet-conversion-to-object :
    (type|[ Long ]|,x_name,x_id) ->
    bstm* |[
    try{ 
      x_id = Long.valueOf(parammap.get("~<OriginalNonUniqueName> x_name")); 
    }
    catch(NumberFormatException nfe){System.out.println(nfe.getMessage());}  
    ]|
 
  java-servlet-conversion-to-object :
    (type|[ Integer ]|,x_name,x_id) ->
    bstm* |[
    try{ 
      x_id = Integer.valueOf(parammap.get("~<OriginalNonUniqueName> x_name")); 
    }
    catch(NumberFormatException nfe){System.out.println(nfe.getMessage());}  
    ]|

  java-servlet-conversion-to-object :
    (type|[ String ]|,x_name,x_id) ->
    bstm* |[
      x_id = parammap.get("~<OriginalNonUniqueName> x_name"); 
    ]|

  java-servlet-conversion-to-object :
    (type|[ Double ]|,x_name,x_id) ->
    bstm* |[
    try{ 
      x_id = Double.valueOf(parammap.get("~<OriginalNonUniqueName> x_name")); 
    }
    catch(NumberFormatException nfe){System.out.println(nfe.getMessage());}  
    ]|
    
  java-servlet-conversion-to-object :
    (type|[ Boolean ]|,x_name,x_id) ->
    bstm* |[
      x_id = Boolean.valueOf(parammap.get("~<OriginalNonUniqueName> x_name")); 
    ]|
    
  java-servlet-conversion-to-object :
    (type|[ Float ]|,x_name,x_id) ->
    bstm* |[
    try{ 
      x_id = Float.valueOf(parammap.get("~<OriginalNonUniqueName> x_name")); 
    }
    catch(NumberFormatException nfe){System.out.println(nfe.getMessage());}  
    ]|