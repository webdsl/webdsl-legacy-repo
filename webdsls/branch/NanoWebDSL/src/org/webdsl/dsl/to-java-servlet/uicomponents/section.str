module org/webdsl/dsl/to-java-servlet/uicomponents/section

imports
  libstrategolib

imports
  libwebdsl-front
  
rules 

  elem-to-java-servlet :
    TemplateCall("section", [],passign*, elems) ->
    bstm* |[ 
      ps.enterSectionContext();
      out.print("<span class=\"section section"+ps.getSectionDepth()+"\" "+e_attrs1+">");
      bstm*
      out.print("</span>");
      ps.leaveSectionContext();
    ]|
    with  bstm* := <elems-to-java-servlet> elems 
        ; e_attrs1 := <to-html-attrs> passign*
        
rules

  GenerateCodeJavaServlet = generate-java-servlet-section-context-var; fail

  generate-java-servlet-section-context-var :
    Application(qid, sections)
    ->
    <emit-java-code> compilation-unit|[
      package utils;
      
      public abstract class PageServlet {
        public int sectionDepth = 0;

        public int getSectionDepth() { 
          return sectionDepth;
        }
      
        public void enterSectionContext() { 
          sectionDepth++;
        }
      
        public void leaveSectionContext() { 
          sectionDepth--;
        }
      
        public boolean inSectionContext() { 
          return sectionDepth > 0;
        }
      }
    ]|
