module org/webdsl/dsl/to-java-servlet/uicomponents/navigate

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

rules // links

  // link to a url, maybe constant, or obtained from an entity field
        
  elem-to-java-servlet :
    TemplateCall("navigate", [ThisCall("url",[e])],elems) ->
    bstm* |[ 
    out.print("<a class=\"navigate\" href=\"");
    bstm_exptostring*
    bstm*
    out.print("</a>");
    ]|
    with  bstm_exptostring* := <to-page-string-java-servlet> e
        ; bstm* := <elems-to-java-servlet> elems

  elem-to-java-servlet :
    TemplateCall("navigate",[call@ThisCall(p,args)],elems) ->
    bstm* |[ 
    out.print("<a class=\"navigate\" href=\""+e_resulturl+"\" >");
    bstm*
    out.print("</a>");
    ]|
    where <IsPage> p
    with  e_url := <page-thiscall-to-url-string-java-servlet> call
        ; bstm* := <elems-to-java-servlet> elems
        ; e_resulturl := e_url
      
rules      
       
  page-thiscall-to-url-string-java-servlet :
    ThisCall(p,args) -> e |[ utils.HTMLFilter.filter(e_url) ]|
    with  def |[ define mod* x(farg*) { elem* } ]| := <TemplateSignature> (p, <map(type-of)> args)
        ; params := <zip(bind-param-java-servlet)> (farg*, args)
        ; e_url := <page-call-to-url-java-servlet> (p, params)
        
  bind-param-java-servlet :
    (Arg(x, srt), e) -> (x, e|[ e1.getNaturalId()]|)
    where <is-entity-type> srt
    with  SimpleSort(x_class) := srt
        ; e1 := <expression-to-java-servlet> e
 
  bind-param-java-servlet :
    (Arg(x, srt), e) -> (x, el)
    where <is-simple-sort> srt
    with  el := <expression-to-java-servlet> e

  page-call-to-url-java-servlet :
    (fn, []) -> java:expr |[ "~s" ]| //java:expr |[ e1 + e2 ]|
    with  s := <concat-strings> [<AppRoot>, "/", fn]
        //; [e1,e2] := <add-success-messages-to-url> [e |[ "~s" ]| ]

  page-call-to-url-java-servlet :
    (fn, params) -> e |[ "~s" + e1 ]| 
    where not([] := params)
    with  s  := <concat-strings> [<AppRoot>, "/", fn, "/"]
        ; e1 := <map(param-to-request-param-java-servlet);combine-params-to-exp> params // add-success-messages-to-url; 

  combine-params-to-exp :
    [e] -> e |[ e ]|
  
  combine-params-to-exp :
    [e|e*] -> e |[ e + "/" + e1 ]|
    with  e1 := <combine-params-to-exp> e*

  param-to-request-param-java-servlet :
    (param, value) -> value
    //    (param, value) -> <concat-strings> [param, "=", value]
