module org/webdsl/dsl/to-seam/property-validation-annotation

imports 
  libstrategolib  
  Java-15
  libjava-front 
  
imports 
  libwebdsl-front
  
rules

  validation-annotations-setter :
    (x_old,x_new, annos) -> 
    bstm* |[
      //validate if value changed
      //if(!org.webdsl.tools.Utils.equal(x_old,x_new)){
      bstm*
      //}
    ]|
    with  bstm* := <validation-annotations-entity-helper-no-crud-anno> annos
    
  validation-annotations-collection-operations : //delete annos not checked here, see rules below
    annos -> <validation-annotations-entity-helper-no-crud-anno> annos
 
  validation-annotations-entity-helper-no-crud-anno =
    remove-all(?ValidateDeleteAnno(_,_)<+?ValidateCreateAnno(_,_)<+?ValidateUpdateAnno(_,_))
  ; validation-annotations-entity-helper
    
  validation-annotations-entity-helper :
    annos ->
    bstm* |[
        java.util.List<utils.ValidationException> exceptions = new java.util.LinkedList<utils.ValidationException>();
       
        bstm_validation*
       
        if(exceptions.size() > 0){
          //overwrite relevant objects the place of the validate anno determines this here
          /*
          for(utils.ValidationException ve : exceptions){
            ve.getRelevantObjects().clear();
            ve.getRelevantObjects().add(x_old);
          }*/
          throw new MultipleValidationExceptions(exceptions);
        }
    ]|
    with  bstm_validation* := <collect(validate-to-java-servlet(data-validation-to-java-servlet-collect(|"exceptions")))> annos
  
rules //delete anno

   GenerateCodeJavaServlet = entity-to-java-servlet-validation-annotation; fail
   
   entity-to-java-servlet-validation-annotation :
     EntityNoSuper(x_Class, props, funcs) ->
     <emit-java-code> compilation-unit|[
       package pkgname;
       
       @Partial
       public class x_Class
       {
         ~*cbds*
       }
     ]|
     with  pkgname := <DomainPackage>
         ; cbds* := <entity-to-java-servlet-validation-annotation-helper> props
       
   entity-to-java-servlet-validation-annotation :
    SessionEntity(x_ident, props, funcs) ->
    <emit-java-code> compilation-unit|[
       package pkgname;
       
       @Partial
       public class x_Class
       {
         ~*cbds*
       }
     ]|
     with  x_Class := <capitalize-string> x_ident
         ; pkgname := <DomainPackage>
         ; cbds* := <entity-to-java-servlet-validation-annotation-helper> props
        
   entity-to-java-servlet-validation-annotation :
     Entity(x_Class, x_super, props, funcs) ->
     <emit-java-code> compilation-unit|[
       package pkgname;
       
       @Partial
       public class x_Class
       {
         ~*cbds*
       }
     ]|
     with  pkgname := <DomainPackage>
         ; cbds* := <entity-to-java-servlet-validation-annotation-helper> props
         
  entity-to-java-servlet-validation-annotation-helper :
    props ->
    class-body-dec*|[
      public void validateDelete(){
        bstm*
      }
      public void validateSave(){
        bstm1*
      }
    ]|
    with  annos := <collect(?ValidateDeleteAnno(_,_))> props
        ; bstm* := <validation-annotations-entity-helper> annos
        ; annos1 := <collect(?ValidateAnno(_,_)<+?ValidateCreateAnno(_,_)<+?ValidateUpdateAnno(_,_))> props
        ; bstm1* := <validation-annotations-entity-helper> annos1

        