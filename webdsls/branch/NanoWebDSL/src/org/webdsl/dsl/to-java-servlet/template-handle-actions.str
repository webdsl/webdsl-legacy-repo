module org/webdsl/dsl/to-java-servlet/template-handle-actions

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

rules

  GenerateCodeJavaServlet = log-time(handle-actions-java-servlet-template|"handle-actions-java-servlet-template"); fail
  
  handle-actions-java-servlet-template =
    ?def |[ define mod* x(farg*) { elem* } ]|
    ; where (    
      bstm1* := <  filter-elements-for-java-servlet-handling
                 ; map-handle-actions-to-java-servlet> elem*
      ; not(bstm1* := []))
    ; with(
      <generate-java-servlet-template-partial> 
        java |[ 
            @Partial
            private void handleActionsInternal()
            {
              String ident = "";
              int forelementcounter = 0;
              boolean inForLoop=false;
              int forLoopCounter = 0;
                
              bstm1*          
            }
        ]|)
 
//coming from elements to xhtml
strategies // lists of elements

  map-handle-actions-to-java-servlet= //alltd because only interested in control flow
    collect-om(handle-actions-to-java-servlet)
    ; concat-with-sublists //accept both bstm and bstm*

  actual-action-args-to-state-enc = 
    filter(action-id-param-encoding-java-servlet)
    ; generate-addition-exp
  
  action-id-param-encoding-java-servlet :
    e -> expr |[ e2.getVersion() == 0 ? "_" : e2.getId().toString() ]|
    where <type-of;defined-java-type> e; not(GenericSort(_,_) := <type-of> e)
    with  e2 := <expression-to-java-servlet> e

  action-id-param-encoding-java-servlet :
    e -> el
    where GenericSort(_,[x]) := <type-of> e; <defined-java-type> x
    with //e2 := <to-page-string-java-servlet> e 
        e2 := <expression-to-java-servlet> e
      ; el := java|[ org.webdsl.tools.Utils.encodeIdList(e2) ]| 
    
   // go through ast if form found, check formid , if button pressed , storinputs ,apply action
   // see control-flow-to-java-servlet and elements-to-java-servlet (form) and types/* (handle-action(-input) strategies)