module org/webdsl/dsl/to-java-servlet/types/text

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules

  builtin-java-type :
    SimpleSort("Text") -> type|[ String ]|

  builtin-persistence-annotations :
    SimpleSort("Text") -> [anno|[ @Column(length = 1000000) ]|]  
  
rules 
  java-servlet-element-id : 
    tc@elem|[ inputText(e){elem*} ]| -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"

  handle-actions-input-to-java-servlet :
    tc@elem|[ inputText(e){elem*} ]| ->
    <wrap-input-java-code(|[])>
    bstm*|[
      ident += "~inputident"+uniqueid;
      if(ps.getParammap().get(ident)!=null)
      {
        ~*<wrap-validation-context-code-store-inputs> bstm*|[
          ~*<text-length-check-java-servlet(|x_temp,e)>
          bstm* 
        ]|
      }
    ]|
    with  inputident := <get-inputnumber> tc
        ; x_temp := <newname> "temp"
        ; bstm* := <input-assignment-to-java-servlet(|x_temp)> e
        
  text-length-check-java-servlet(|x_temp,e) =
    ! bstm* |[
        String x_temp = ps.getParammap().get(ident);
    ]|
    
  elem-to-java-servlet : 
    tc@elem|[ inputText(e){elem*} ]| ->
    <wrap-input-render-java-code>
    bstm* |[ 
      String x_temp = ident+"~inputident"+uniqueid;
        out.print("<textarea name=\""+x_temp+"\" class=\"inputTextarea\"");
        out.print(">");
        if(inSubmittedForm)
        {
          out.print(utils.HTMLFilter.filter(ps.getParammap().get(x_temp)));
        }
        else
        {
          bstm_exptostring*
        }
        out.print("</textarea>"); 
    ]|
    with  inputident := <get-inputnumber> tc
        ; bstm_exptostring* := <to-page-string-java-servlet> e
        ; x_temp := <newname> "temp"
    
  elem-to-java-servlet :
    elem |[ outputText(e){} ]| ->
    <wrap-common-context-code>
    bstm* |[ 
      com.petebevin.markdown.MarkdownProcessor x_temp = new com.petebevin.markdown.MarkdownProcessor();
      bstm_exptostring*
    ]|
    with  x_temp := <newname> "temp"
        ; bstm_exptostring* := <to-page-string-java-servlet(|<concat-strings>[x_temp,".markdown"])> e
 
