module org/webdsl/dsl/to-java-servlet/uicomponents/group

imports
  libstrategolib

imports
  libwebdsl-front

rules // groups

  elem-to-java-servlet :
    TemplateCall("group", [arg], elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<fieldset ><legend>");
    bstm_exptostring*
    out.print("</legend>");
    bstm*
    out.print("</fieldset>");
    ]|
    with  bstm* := <elems-to-java-servlet> elems
       ;  bstm_exptostring* := <to-page-string-java-servlet> arg

  elem-to-java-servlet :
    TemplateCall("group", [], elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<fieldset class=\"fieldset_no_legend_\" >");
    bstm*
    out.print("</fieldset>");
    ]|
    with bstm* := <elems-to-java-servlet> elems

  elem-to-java-servlet :
    TemplateCall("groupitem", [],elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<tr >");
    bstm*
    out.print("</tr>");
    ]|
    with bstm* := <elems-to-java-servlet> elems
  