module org/webdsl/dsl/languages/ui/navigate
       
overlays

  NavigateErrorMessage =
    "Not a valid navigate link, page does not exist: "   
        
rules // typechecking page elements

  constraint-error-tc = ?tc@TemplateCallNoBody(p,arg*)       ;where(<constraint-error-navigate(|tc)> (p,arg*))
  constraint-error-tc = ?tc@TemplateCallNoArgs(p)            ;where(<constraint-error-navigate(|tc)> (p,[]  ))
  constraint-error-tc = ?tc@TemplateCall(p,arg*,_)  ;where(<constraint-error-navigate(|tc)> (p,arg*))
  constraint-error-tc = ?tc@TemplateCallBody(p,_)            ;where(<constraint-error-navigate(|tc)> (p,[]  ))

  constraint-error-navigate(|tc) :
    ("navigate",[e]) -> <add-error(|[NavigateErrorMessage()])> tc
    where <check-link> e

  constraint-error-navigate(|tc) :
    ("navigate",[e1,e]) -> <add-error(|[NavigateErrorMessage()])> tc
    where <check-link> e
    
  constraint-error-navigate(|tc) :
    ("navigatebutton", [e,e1]) -> <add-error(|[NavigateErrorMessage()])> tc
    where <check-link> e
    
  check-link :
    tc@ThisCall("url", [e]) -> tc
    where not(<type-of> e)

  check-link :
    tc@ThisCall(p, args) -> tc
    where pstripped := <strip-annos> p
        ; not("url" := pstripped)
        ; not(<IsPage> pstripped)

rules // desugar

  desugar-ui: 
    TemplateCall("navigate", [e1,e2], elem*) -> TemplateCall("navigatebutton", [e2,e1], elem*)
