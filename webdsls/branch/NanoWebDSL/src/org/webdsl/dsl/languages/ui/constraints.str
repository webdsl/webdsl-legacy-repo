module org/webdsl/dsl/languages/ui/constraints

imports
  libstrategolib

imports
  libwebdsl-front

rules // constraints

  //duplicate page names
  constraint-error-ui :
    Application(nm, sections) -> <add-error(|["Pages ", <separate-by(|",")> page*, " are defined multiple times"])> 
    where pages := <collect(?Define([Page()], _, _, _)); map(\ Define([Page()], nm, _, _) -> nm \)> sections
        ; page* := <find-doubles> pages
        ; not([] := page*)

  constraint-error-ui :
    IfTempl(e, _, _) -> <add-error(|["Expression ", <pp-webdsl> e, " should be of type Bool"])>
    where not(SimpleSort("Bool") := <type-of> e)

  constraint-error-ui :
    IfNoElseTempl(e, _) -> <add-error(|["Expression ", <pp-webdsl> e, " should be of type Bool"])>
    where not(SimpleSort("Bool") := <type-of> e)



  constraint-error-ui :
    TemplateCase(e, talt*) -> <add-error(|["case expression should have value type instead of ", t])>
    where t := <type-of> e
  ; not(<is-constant-type> t)

  constraint-error-ui :
    s@TemplateCase(e, talt*) -> s
    where t := <type-of> e
  ; not(<filter(constraint-error-case-alt(|t))> talt* => [])

  constraint-error-ui(|t1) :
    s@TemplateCaseAlt(e, elem*) -> <add-error(|["constant in case alternative ", e, 
                         " should have same type as case expression ", t1])>
    where t2 := <type-of> e
        ; not(<type-compatible>(t1,t2))
        