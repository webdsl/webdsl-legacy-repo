module org/webdsl/dsl/modules/types/patch

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  is-simple-sort = ?SimpleSort("Patch")  
  extends-builtin : "Patch" -> "Object"
  //built-in-template-rule-setup = <declare-built-in-template-rules> ("inputPatch",[SimpleSort("Patch")]);fail
  //built-in-template-rule-setup = <declare-built-in-template-rules> ("outputPatch",[SimpleSort("Patch")]);fail  
  
rules // types

  type-default-value :
    SimpleSort("Patch") -> exp|[ "" ]|

  check-builtin-signature :
    (SimpleSort("Patch"), "applyPatch", [s]) -> s
    where <type-compatible> (s, SimpleSort("String"))

  check-builtin-signature :
    (s1, "makePatch", [s2]) -> SimpleSort("Patch")
    where <type-compatible> (s1, SimpleSort("String"))
        ; <type-compatible> (s2, SimpleSort("String"))
        
  check-builtin-signature :
    (s1, "diff", [s2]) -> GenericSort("List",[SimpleSort("String")])
    where <type-compatible> (s1, SimpleSort("String"))
        ; <type-compatible> (s2, SimpleSort("String"))
        
rules // desugar
  DeriveInput :
    elem|[ input(e)[passign*]{elem*} ]| -> elem|[ inputText(e)[passign*]{elem*} ]|
    where SimpleSort("Patch") := <type-of> e

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputText(e)[passign*]{} ]|
    where SimpleSort("Patch") := <type-of> e
