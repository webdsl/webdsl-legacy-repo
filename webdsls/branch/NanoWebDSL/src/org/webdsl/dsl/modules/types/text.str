module org/webdsl/dsl/modules/types/text

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
 
rules

  is-simple-sort = ?SimpleSort("Text") 
  extends-builtin : "Text" -> "Object" 
  built-in-template-rule-setup = <declare-built-in-template-rules> ("inputText",[SimpleSort("Text")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputText",[SimpleSort("Text")]);fail
  
rules

  type-default-value :
    SimpleSort("Text") -> exp|[ "" ]|

  type-compatible =
    ?(SimpleSort("Text"), SimpleSort("String"))
  
  type-compatible =
    ?(SimpleSort("String"), SimpleSort("Text"))

rules // desugar

  DeriveInput :
    elem|[ input(e){elem*} ]| -> elem|[ inputText(e){elem*} ]|
    where SimpleSort("Text") := <type-of> e

  DeriveOutput :
    elem|[ output(e){} ]| -> elem|[ outputText(e){} ]|
    where SimpleSort("Text") := <type-of> e

        
