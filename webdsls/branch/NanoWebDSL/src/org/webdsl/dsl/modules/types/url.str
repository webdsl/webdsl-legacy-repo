module org/webdsl/dsl/modules/types/url

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  is-simple-sort = ?SimpleSort("URL")  
  extends-builtin : "URL" -> "Object"  
  built-in-template-rule-setup = <declare-built-in-template-rules> ("inputURL",[SimpleSort("URL")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputURL",[SimpleSort("URL")]);fail
  
rules // types

  type-default-value :
    SimpleSort("URL") -> exp|[ "" ]|

  type-compatible =
    ?(SimpleSort("URL"), SimpleSort("String"))

rules // desugar

  DeriveInput :
    elem|[ input(e){elem*} ]| -> elem|[ inputURL(e){elem*} ]|
    where SimpleSort("URL") := <type-of> e

  DeriveOutput :
    elem|[ output(e){} ]| -> elem|[ navigate(url(e)){url(e)} ]|
    where SimpleSort("URL") := <type-of> e

rules

 check-builtin-signature :
   (None(), "url", [s])  ->  SimpleSort("URL")
   where <type-compatible> (s, SimpleSort("String"))
