module stratego-interface

imports
  libstrategolib
  libwebdsl-front
  aterm-interface

strategies

  call-to-java-servlet :
    exp  |[ Stratego(e) ]| ->
    java |[ org.webdsl.tools.strategoxt.StrategoProgram.get(e2, utils.AppInfo.class) ]|
    with e2 := <expression-to-java-servlet> e
 
  call-to-java :
    exp  |[ Stratego(e) ]| ->
    java |[ org.webdsl.tools.strategoxt.StrategoProgram.get(e2, getClass()) ]|
    with e2 := <expression-to-java> e
  
  call-to-java-any(rec) :
    exp  |[ e.invoke(e_strategy, e_input) ]| ->
    java |[ e2.invoke(e_strategy2, e_input2) ]|
    where SimpleSort("Stratego") := <type-of> e
    with e2          := <rec> e
       ; e_input2    := <rec> e_input
       ; e_strategy2 := <rec> e_strategy
  
  check-builtin-signature :
    (None(), "Stratego", [SimpleSort("String")]) -> SimpleSort("Stratego")
  
  check-builtin-signature :
    (SimpleSort("Stratego"), "invoke", [SimpleSort("String"), arg]) -> SimpleSort("ATerm")
    where
      SimpleSort("ATerm") := arg + SimpleSort("String") := arg
  
  builtin-java-type :
    SimpleSort("Stratego") -> type |[ org.webdsl.tools.strategoxt.StrategoProgram ]|

  builtin-java-type-default-value :
    SimpleSort("Stratego") -> java |[ null ]|
  
  sort-to-java-type-string :
    SimpleSort("Stratego") -> "org.webdsl.tools.strategoxt.StrategoProgram"
  
  