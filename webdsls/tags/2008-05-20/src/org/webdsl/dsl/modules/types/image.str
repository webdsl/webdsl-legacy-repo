module org/webdsl/dsl/modules/types/image

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules // types
  sort-to-java-type-string : SimpleSort("Image") -> "java.lang.Byte[]"

  builtin-java-type :
    SimpleSort("Image") -> type|[ byte[] ]|

  builtin-java-type-default-value :
    SimpleSort("Image") -> java|[ null ]|

  builtin-persistence-annotations :
    SimpleSort("Image") -> [anno|[ @Basic(fetch=FetchType.LAZY) ]|,
                            anno|[ @Column(columnDefinition="LONGBLOB") ]|]

rules // desugar
  DeriveInput :
    elem|[ input(e){} ]| -> elem|[ inputImage(e){} ]|
    where SimpleSort("Image") := <type-of> e

  DeriveOutput :
    elem|[ output(e){} ]| -> elem|[ outputImage(e){} ]|
    where SimpleSort("Image") := <type-of> e

rules
  elem-to-xhtml :
    elem|[ inputImage(e){} ]| ->
    %>
      <s:decorate>
        <s:fileUpload id="<%= label%>"
      data="<%= <arg-to-value-string> e %>" required="<%= req %>"
      accept="images/*" />
      </s:decorate>
    <%
    where estring := <arg-to-el> e
        ; label := <newname> "inputImage"
        ; req := <input-of-required-property(|e)>
        ; <debug> <arg-to-value-string> e

  elem-to-xhtml :
    elem|[ outputImage(e){} ]| ->
    %>
      <s:decorate>
        <s:graphicImage value="#{<%= <arg-to-el> e %>}" />
      </s:decorate>
    <%

