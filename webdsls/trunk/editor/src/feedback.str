module feedback

imports
  libstrategolib
  libwebdsl-front
  libwebdsl-generator

strategies // editor service interface

  editor-analyze:
    (ast, path, fullpath) -> results
    with
      <debug(!"editor-analyze called ")> path;
      
      editor-init;
      
      // <search-main-file> ".";
      results := <webdsl-collect-all-errors; debug(!"collected errors: ")> ast;

      <analyze-definitions> ast

  reference-resolve:
    (source, filename, reference, path) -> target
    where
      <debug(!"reference-resolve invoked ")> reference; 
      reference' := <desugar-top> reference;
      target := <find-definition> reference';
      debug(!"reference-resolve resolved ")
  
  reference-info:
    (source, filename, target, path) -> info
    where
      <debug(!"reference-info invoked ")> target;
      info := <pp-webdsl-to-string> target

strategies // helpers

  editor-init =
    with(
      <set-verbosity> 3;
      set-default-config;
      try(dr-scope-all-end);
      dr-scope-all-start
    )

  search-main-file:
    dir -> app-file
    where
      <readdir> dir;
      getfirst(
        if isdir then
         search-main-file
        else
          string-ends-with(|".app")
        end
     );
     app-file := <conc-strings> (dir, "/", <id>)

strategies // "library" strategies

  term-at-path(|path) =
    if [] := path then
      id
    else
      with(
        [index | path'] := path
      );
      subterm-at-index(|index);
      term-at-path(|path')
    end
  
  apply-at-path(s|path) =
    if [] := path then
      s
    else
      with(
        [index | path'] := path
      );
      apply-at-index(apply-at-path(s|path') |index)
    end
  
  subterm-at-index(|index) =
    prim("SSL_EXT_subterm", <id>, index)
  <+
    get-arguments;
    index(|index)
  
  apply-at-index(s|index):
    ctor#(args) -> ctor#(args')
    with
      args' := <at-index(s|index)> args

  dr-scope-all-start =
    where(
      table := Hashtable(<dr-rule-sets-hashtable>);
      names := ["DrScopedRules" | <hashtable-keys> table];
      
      dr-begin-scopes(|names);
      rules(DrScopedRules := names)
    )

  dr-scope-all-end =
    where(
      table := Hashtable(<dr-rule-sets-hashtable>);
      names := <DrScopedRules>;
      
      dr-end-scopes(|names);
  
      // Remove other, newly defined rules
      <diff> (<hashtable-keys> table, names);
      list-loop(
        { key: ?key;
          <hashtable-remove(|key)> table
        }
      )
    )

  dr-scope-all(s) =
    finally(
      dr-scope-all-start; s
    , dr-scope-all-end
    )
  
    /*
    with(
      table := Hashtable(<dr-rule-sets-hashtable>);
      keys  := <hashtable-keys> table
    );
    
    dr-scopes(
      s
    | keys
    );

    // Remove other, newly defined rules    
    where(
      <diff> (<hashtable-keys> table, keys);
      list-loop(
        { key: ?key;
          <hashtable-remove(|key)> table
        }
      )
    )
    */
