module feedback

imports
  libstrategolib
  libwebdsl-front
  libwebdsl-generator

strategies // editor service interface

  editor-analyze:
    (ast, path, fullpath) -> results
    where
      debug(!"editor-analyze called");
      editor-init;
      // <search-main-file> ".";
      results := <webdsl-collect-all-errors; debug(!"collected errors: ")> ast

  reference-resolve:
    (source, filename, reference, path) -> target
    where
      debug(!"reference-resolve invoked");      
      <oncetd(?Description(_); ?target)> source
  
  reference-info:
    (source, filename, target, path) -> info
    where
      debug(!"reference-info invoked");
      info := <pp-webdsl-to-string> target

strategies // type-checker interface

  editor-init =
    with(
      <set-verbosity> 3;
      set-default-config
    )

strategies // helpers

  search-main-file:
    dir -> app-file
    where
      <readdir> dir;
      getfirst(
        if isdir then
         search-main-file
        else
          string-ends-with(|".app")
        end
     );
     app-file := <conc-strings> (dir, "/", <id>)

strategies // "library" strategies

  term-at-path(|path) =
    if [] := path then
      id
    else
      where(
        [index | path'] := path
      );
      subterm-at-index(|index);
      term-at-path(|path')
    end
  
  apply-at-path(s|path) =
    if [] := path then
      s
    else
      where(
        [index | path'] := path
      );
      apply-at-index(apply-at-path(s|path') |index)
    end
  
  subterm-at-index(|index) =
    prim("SSL_EXT_subterm", <id>, index)
  <+
    get-arguments;
    index(|index)
  
  apply-at-index(s|index):
    ctor#(args) -> ctor#(args')
    where
      args' := <at-index(s|index)> args

  dr-scope-all(s) =
    where(
      table := Hashtable(<dr-rule-sets-hashtable>);
      keys  := <hashtable-keys> table
    );
    
    dr-scopes(
      s
    | keys
    );

    // Remove newly defined rules    
    where(
      <diff> (<hashtable-keys> table, keys);
      list-loop(
        { key: ?key;
          <hashtable-remove(|key)> table
        }
      )
    )