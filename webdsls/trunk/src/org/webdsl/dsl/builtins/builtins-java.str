/**

  Builtin types for WebDSL
  
*/

module org/webdsl/dsl/builtins/builtins-java

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  
imports 
  libwebdsl-syntax

// Default formats:
//      Date: dd/MM/yyyy
//      Time: H:mm
//  DateTime: dd/MM/yyyy H:mm

rules // Persistence

  builtin-persistence-annotations :
    SimpleSort("Time") -> [anno|[ @Temporal(TemporalType.TIME) ]|]
    
  builtin-persistence-annotations :
    SimpleSort("Date") -> [anno|[ @Temporal(TemporalType.DATE) ]|]
    
  builtin-persistence-annotations :
    SimpleSort("DateTime") -> [anno|[ @Temporal(TemporalType.TIMESTAMP) ]|]
    
  builtin-call-to-java :
    ThisCall("now", []) -> |[ new java.util.Date() ]|

  builtin-call-to-java :
    ThisCall("Date", [d, f]) -> |[ org.webdsl.tools.Utils.parseDate(e1, e2) ]|
    where e1 := <expression-to-java> d
        ; e2 := <expression-to-java> f

  builtin-call-to-java :
    ThisCall("Date", [d]) -> |[ org.webdsl.tools.Utils.parseDate(e1, "dd/MM/yyyy") ]|
    where e1 := <expression-to-java> d

  builtin-call-to-java :
    ThisCall("Time", [d, f]) -> |[ org.webdsl.tools.Utils.parseDate(e1, e2) ]|
    where e1 := <expression-to-java> d
        ; e2 := <expression-to-java> f

  builtin-call-to-java :
    ThisCall("Time", [d]) -> |[ org.webdsl.tools.Utils.parseDate(e1, "H:mm") ]|
    where e1 := <expression-to-java> d

  builtin-call-to-java :
    ThisCall("DateTime", [d, f]) -> |[ org.webdsl.tools.Utils.parseDate(e1, e2) ]|
    where e1 := <expression-to-java> d
        ; e2 := <expression-to-java> f

  builtin-call-to-java :
    ThisCall("DateTime", [d]) -> |[ org.webdsl.tools.Utils.parseDate(e1, "dd/MM/yyyy H:mm") ]|
    where e1 := <expression-to-java> d

rules // File

  builtin-persistence-annotations :
    SimpleSort("Binary") ->
      [anno|[ @Basic(fetch=FetchType.LAZY) ]|,
       anno|[ @Column(columnDefinition="LONGBLOB") ]|]

  