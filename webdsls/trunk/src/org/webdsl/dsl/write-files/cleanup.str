module org/webdsl/dsl/write-files/cleanup

imports
  libstratego-lib
  libwebdsl-front
  org/webdsl/dsl/write-files/-

rules

  touch-generated-file: filename -> filename
    with 
        (GeneratedFiles <+ rules(GeneratedFiles := <new-hashtable>); GeneratedFiles)
      ; hashtable-put(|filename, 1)
      
rules

  cleanup-generated-files =
    if not(is-custom-compile-units-enabled) then // for now just skip pruning when using custom compile units, would be better to take changed compile units into account here as well
      GeneratedOutputDir
    ; where(traverse-files(try(prune-generated-file)))
    ; where(traverse-dirs-innermost(try(prune-empty-dir)))
    end
  
rules			// remove unused files
      
  prune-generated-file =
      ?path
    ; fullpath := <concat-strings> [<GeneratedOutputDir>, "/", path] 
    ; where(<not(hashtable-get(|fullpath))> (<GeneratedFiles>))
    ; <verbose-msg> ["Pruning generated file: ", <id>]
    ; <remove-file> path
    
rules			// remove empty directories

  prune-empty-dir =
      ?path
    ; where([] := <readdir-filtered> path)
    ; <verbose-msg> ["Pruning empty directory: ", <id>]
    ; <remove-file> path
