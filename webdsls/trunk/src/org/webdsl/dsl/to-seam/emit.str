module org/webdsl/dsl/generation/emit

imports
  libstrategolib
  libjava-front

imports
  libwebdsl-front
  org/webdsl/dsl/generation/-

rules

  /* Takes a java compilation unit and stores it to be emitted later */

  emit-java-code =
    ?cu@CompilationUnit(_, _, _)
    ; rules(
        JavaClassesToEmit :+= JavaFile(["src"], cu)
      )

  emit-facelets =
    ?do@Document(_, _, _)
    ; rules(
        FaceletsToEmit :+= XmlFile(["view"], <concat-strings> [<ThisPage>, ".xhtml"], do)
      )

  emit-css =
    ?st@Stylesheet(_)
    ; rules(
        StylesheetsToEmit :+= CssFile(["view/stylesheets"], <concat-strings> [<ThisPage>, ".css"], st)
      )

  emit-seam-config =
    ?do@Document(_, _, _)
    ; rules(
        SeamConfigToEmit :+= XmlFile(["resources/WEB-INF"], <concat-strings> [<ThisPage>, ".xml"], do)
      )
      
  add-bean-interfaces =
    <concat> [<filter(add-bean-interface)>, <id>]

  add-bean-interface :
    jf@JavaFile(_, CompilationUnit(_, _, [ClassDec(ClassDecHead(_, Id(classname), _, _, _), _)]))
    ->
    <create-local-interface(|<BeanPackage>, <DomainPackage>, <concat-strings> [classname, "Interface"])> jf
    where <string-ends-with(|"Bean")> classname
          <+ "ApplicationContext" := classname

  get-java-artifacts =
    bagof-JavaClassesToEmit
    ; java-concat-ids
    ; merge-classes
    ; add-bean-interfaces

  get-facelets =
    bagof-FaceletsToEmit

  get-css-artifacts =
    bagof-StylesheetsToEmit

  get-seam-config-artifacts =
    rewrite-rules-to-xml
    ; generate-page-actions-file
    ; bagof-SeamConfigToEmit
