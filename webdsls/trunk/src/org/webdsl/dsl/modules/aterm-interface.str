module aterm-interface

imports
  libstrategolib
  libwebdsl-front

strategies

  call-to-java :
    exp  |[ e.subterms() ]| ->
    java |[ e2.getAllSubterms() ]|
    where SimpleSort("ATerm") := <type-of> e
    with e2 := <expression-to-java> e

  call-to-java :
    exp  |[ e.get(e_index) ]| ->
    java |[ e2.getSubterm(e_index2) ]|
    where SimpleSort("ATerm") := <type-of> e
    with e2       := <expression-to-java> e
       ; e_index2 := <expression-to-java> e_index

  call-to-java :
    exp  |[ e.length() ]| ->
    java |[ e2.getSubtermCount() ]|
    where SimpleSort("ATerm") := <type-of> e
    with e2 := <expression-to-java> e

  call-to-java :
    exp  |[ e.toString() ]| ->
    java |[ e2.toString() ]|
    where SimpleSort("ATerm") := <type-of> e
    with e2 := <expression-to-java> e

  call-to-java :
    exp  |[ e.toInt() ]| ->
    java |[ Integer.parseInt(e2.toString()) ]|
    where SimpleSort("ATerm") := <type-of> e
    with e2 := <expression-to-java> e
  
  check-builtin-signature :
    (SimpleSort("ATerm"), "subterms", []) -> GenericSort("List", [SimpleSort("ATerm")])
  
  check-builtin-signature :
    (SimpleSort("ATerm"), "get", [SimpleSort("Int")]) -> SimpleSort("ATerm")
  
  check-builtin-signature :
    (SimpleSort("ATerm"), "length", []) -> SimpleSort("Int")

  check-builtin-signature :
    (SimpleSort("ATerm"), "toString", []) -> SimpleSort("String")

  check-builtin-signature :
    (SimpleSort("ATerm"), "toInt", []) -> SimpleSort("Int")
    
  builtin-java-type :
    SimpleSort("ATerm") -> type |[ org.spoofax.interpreter.terms.IStrategoTerm ]|

  builtin-java-type-default-value :
    SimpleSort("ATerm") -> java |[ "" ]|
      
  sort-to-java-type-string :
    SimpleSort("ATerm") -> "org.spoofax.interpreter.terms.IStrategoTerm"
    