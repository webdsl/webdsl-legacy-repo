module org/webdsl/dsl/modules/types/wikitext

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  elem-to-xhtml :
    elem|[ inputWikiText(e){} ]| ->
    %>
      <s:decorate>
        <h:inputTextarea styleClass="inputTextarea" value="<%= estring %>" required="<%= req %>"/>
      </s:decorate>
    <%
    with estring := <arg-to-value-string> e
       ; req := <input-of-required-property(|e)>

  elem-to-xhtml :
    elem|[ outputWikiText(e){} ]| ->
    %>
      <h:outputText styleClass="outputWikiText" value="#{wdsl:wikiFormat(<%= estring %>, '<%= <AppRoot> %>')}" escape="false" />
    <%
    with estring := <arg-to-el> e

rules // types

  builtin-java-type :
    SimpleSort("WikiText") -> type|[ String ]|

  builtin-java-type-default-value :
    SimpleSort("WikiText") -> java|[ "" ]|

  builtin-persistence-annotations :
    SimpleSort("WikiText") -> [anno|[ @Column(length = 1000000) ]|]

  type-compatible =
    ?(SimpleSort("WikiText"), SimpleSort("String"))

rules // desugar
  DeriveInput :
      elem|[ input(e){} ]| -> elem|[ inputWikiText(e){} ]|
      where SimpleSort("WikiText") := <type-of> e

  DeriveOutput :
    elem|[ output(e){} ]| -> elem|[ outputWikiText(e) ]|
    where SimpleSort("WikiText") := <type-of> e


