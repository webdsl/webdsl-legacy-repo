module org/webdsl/dsl/modules/types/float

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  is-simple-sort = ?SimpleSort("Float")  
  extends-builtin : "Float" -> "Object"
  built-in-template-rule-setup = <declare-built-in-template-rules> ("inputFloat",[SimpleSort("Float")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputFloat",[SimpleSort("Float")]);fail
  eq-type-of : Float(x) -> SimpleSort("Float")

rules // desugar

  DeriveInput :
    elem|[ input(e)[passign*]{elem*} ]| -> elem|[ inputFloat(e)[passign*]{elem*} ]|
    where SimpleSort("Float") := <type-of> e

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputFloat(e)[passign*]{} ]|
    where SimpleSort("Float") := <type-of> e

rules
  type-default-value :
    SimpleSort("Float") -> exp|[ 0f ]| //webdsl exp, not java exp

rules // conversion

 check-builtin-signature :
    (SimpleSort("Float"), "round", []) ->  SimpleSort("Int")
 
 check-builtin-signature :
    (SimpleSort("Float"), "floor", []) ->  SimpleSort("Int")

 check-builtin-signature :
    (SimpleSort("Float"), "ceil", []) ->  SimpleSort("Int")
    
rules 

 check-builtin-signature :
    (None(), "random", []) ->  SimpleSort("Float")
