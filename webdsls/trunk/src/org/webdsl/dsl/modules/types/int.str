module org/webdsl/dsl/modules/types/int

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  elem-to-xhtml :
    elem|[ inputInt(e){} ]| ->
    %>
      <s:decorate>
        <h:inputText styleClass="inputInt" value="<%= estring %>" required="<%= req %>"/>
      </s:decorate>
    <%
    with estring := <arg-to-value-string> e
        ; req := <input-of-required-property(|e)>

  elem-to-xhtml :
    elem|[ outputInt(e){} ]| ->
    %>
      <h:outputText styleClass="outputInt" value="<%= estring %>"/>
    <%
    with estring := <arg-to-value-string> e

rules // type

  sort-to-java-type-string : SimpleSort("Int") -> "java.lang.Integer"

  builtin-java-type :
    SimpleSort("Int") -> type|[ Integer ]|

  builtin-wrapper-java-type :
    SimpleSort("Int") -> type|[ Integer ]|

  builtin-java-type-default-value :
    SimpleSort("Int") -> java|[ 0 ]|

  type-default-value :
    SimpleSort("Int") -> exp|[ 0 ]|

  eq-type-of :
    Int(x) -> SimpleSort("Int")

rules // desugar
  DeriveInput :
    elem|[ input(e){} ]| -> elem|[ inputInt(e){} ]|
    where SimpleSort("Int") := <type-of> e


  DeriveOutput :
    elem|[ output(e){} ]| -> elem|[ text(e){} ]|
    where SimpleSort("Int") := <type-of> e

rules // java
   expression-to-java :
     Int(x) -> Lit(Deci(x))

