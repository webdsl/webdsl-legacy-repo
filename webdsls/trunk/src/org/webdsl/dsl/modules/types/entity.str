module org/webdsl/dsl/modules/types/entity

imports
  libstrategolib
  libwebdsl-front
  

rules //built-in Entity type

  is-entity-type = ?SimpleSort("Entity") 
  reserved-entity-name = ?"Entity"
  extends-builtin : "Entity" -> "Object"  
  
  overlays

  AllEntityProperties =
    [VersionProperty(), IdProperty(), NameProperty(), CreatedProperty(), ModifiedProperty()]  

  IdProperty =
    Property("id", Simple(), SimpleSort("UUID"), []){IsGeneratedProperty()}

  VersionProperty =
    Property("version", Simple(), SimpleSort("Int"), []){IsGeneratedProperty()}

  NameProperty =
    DerivedProperty("name", Simple(), SimpleSort("String"), [], []){IsGeneratedProperty()}
  
  CreatedProperty =
    Property("created", Simple(), SimpleSort("DateTime"), []){IsGeneratedProperty()}  
    
  ModifiedProperty =
    Property("modified", Simple(), SimpleSort("DateTime"), []){IsGeneratedProperty()}  
  
rules
  
  built-in-template-rule-setup =
    <declare-function(|"Entity");fail> def|[ function getTypeString():String {return "";} ]|
    
  built-in-template-rule-setup =
    <declare-function(|"Entity");fail> def|[ function all():List<Entity> {return List<Entity>();} ]| //overridden for each defined entity
  
  built-in-template-rule-setup =
    <map(declare-property(|"Entity"));fail> AllEntityProperties()


rules //defined entities and built-in Entity type

  type-default-value :
    s@SimpleSort(y) -> exp |[ null ]|
    where <is-entity-type> s

rules

  check-builtin-signature :
    (s@SimpleSort(_), "delete", []) -> s
    where <is-entity-type> s
        ; <not(IsSessionEntityType)> s

  check-builtin-signature :
    (s@SimpleSort(_), "save", []) -> s
    where <is-entity-type> s
        ; <not(IsSessionEntityType)> s
  
  desugar-data :
    Entity(x_class, sup, entbodydecs1){anno*} -> <rename> Entity(x_class, sup, entbodydecs2){anno*}
    where entbodydecs2 := <oncetd(desugar-save-delete-call);alltd(desugar-save-delete-call)> entbodydecs1
  desugar-data :
    EntityNoSuper(x_class, entbodydecs1){anno*} -> <rename> EntityNoSuper(x_class, entbodydecs2){anno*}
    where entbodydecs2 := <oncetd(desugar-save-delete-call);alltd(desugar-save-delete-call)> entbodydecs1
    
  desugar-save-delete-call : ThisCall("save",[])   -> Call(Var("this"),"save",[])  
  desugar-save-delete-call : ThisCall("delete",[]) -> Call(Var("this"),"delete",[]) 
    
  constraint-error : e@Entity(x, _, entbodydecs)    -> <filter(constraint-error-ent-fun-name);fail> entbodydecs
  constraint-error : e@EntityNoSuper(x,entbodydecs) -> <filter(constraint-error-ent-fun-name);fail> entbodydecs

  constraint-error-ent-fun-name :  
    Function(name,_,_,_) -> <constraint-error-ent-fun-name-error(|name)>
    where <not-allowed-ent-fun-name> name
    
  constraint-error-ent-fun-name-error(|name) = 
    add-error(|["Entity function with name '", name, "' collides with a built-in function that you are not allowed to overwrite."])

  not-allowed-ent-fun-name = ?"save"
  not-allowed-ent-fun-name = ?"delete"

rules

  desugar-types :
    TemplateCall(x,arg*,passign*,elem*) -> result
    where <?"output"> x //"output" string not in lhs because the x with annotations is needed to check whether it is a call to built-in output
        ; <CallToBuiltInTemplate> x
        ; [e] := arg*
        ; SimpleSort(x_ent) := <type-of> e
        ; <IsEntity> x_ent
    with  x_view := <decapitalize-string> x_ent
        // if there is a view page with lowercase entityname and 1 argument of that entitytype, 
        // then show navigate, otherwise just show name
        ; if <CheckPageSignature> (Page(), <decapitalize-string; strip-annos> x_ent, [<strip-annos>SimpleSort(<strip-annos>x_ent)])
          then result := <rename-all> elem|[ navigate(x_view(e))[passign*]{ if(e!=null){ output(e.name) } } ]|
          else result := <rename-all> elem|[ if(e!=null){ output(e.name) } ]|
          end

  desugar-in-define-context : // desugar-in-define-context instead of desugar-types, because the rename afterwards is required by query analysis, because the rename adds template name/args annos on for-loops
    TemplateCall(x,arg*,passign*,elem*) -> result
    where <?"output"> x
        ; <CallToBuiltInTemplate> x
        ; [e] := arg*
        ; GenericSort(<?"List" <+ ?"Set">,[SimpleSort(x_ent)]) := <type-of> e
    with  x1 := <toplevel-rename> "outputlist"
        ; e2 := Var(x1)
        ; result := <rename-all> elem|[ list { for(x1 : x_ent in e){ listitem{ output(e2)[passign*] } } } ]|
    
  is-call-to-output-generic-sort : // Just the where clause of the above rule
    tc@TemplateCall(x,arg*,passign*,elem*) -> tc
    where <?"output"> x
        ; <CallToBuiltInTemplate> x
        ; [e] := arg*
        ; GenericSort(<?"List" <+ ?"Set">,[SimpleSort(x_ent)]) := <type-of> e
