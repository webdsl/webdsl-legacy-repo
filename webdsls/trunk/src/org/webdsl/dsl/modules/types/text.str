module org/webdsl/dsl/modules/types/text

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
 
rules
  is-simple-sort = ?SimpleSort("Text") 
  extends-builtin : "Text" -> "Object" 
  built-in-template-rule-setup = <declare-built-in-template-rules> ("inputText",[SimpleSort("Text")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputText",[SimpleSort("Text")]);fail
  
rules // types
  sort-to-java-type-string : SimpleSort("Text") -> "java.lang.String"

  builtin-java-type :
    SimpleSort("Text") -> type|[ String ]|

  builtin-java-type-default-value :
    SimpleSort("Text") -> java|[ ""  ]|

  builtin-persistence-annotations :
    SimpleSort("Text") -> [anno|[ @Column(length = 1000000) ]|]

  type-compatible =
    ?(SimpleSort("Text"), SimpleSort("String"))
  
  type-compatible =
    ?(SimpleSort("String"), SimpleSort("Text"))

rules // desugar
  DeriveInput :
    elem|[ input(e)[passign*]{} ]| -> elem|[ inputText(e)[passign*]{} ]|
    where SimpleSort("Text") := <type-of> e

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputText(e)[passign*]{} ]|
    where SimpleSort("Text") := <type-of> e

        
