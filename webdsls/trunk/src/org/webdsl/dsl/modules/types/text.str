module org/webdsl/dsl/modules/types/text

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
 
rules

  is-simple-sort = ?SimpleSort("Text") 
  extends-builtin : "Text" -> "Object" 
  built-in-template-rule-setup = <declare-built-in-template-rules> ("inputText",[SimpleSort("Text")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputText",[SimpleSort("Text")]);fail
  
rules

  type-default-value :
    SimpleSort("Text") -> exp|[ "" ]|

rules //compatibility with String

  string-compatible = ?SimpleSort("Text")

rules // desugar

  DeriveInput :
    elem|[ input(e)[passign*]{elem*} ]| -> elem|[ inputText(e)[passign*]{elem*} ]|
    where SimpleSort("Text") := <type-of> e

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputText(e)[passign*]{} ]|
    where SimpleSort("Text") := <type-of> e

        
