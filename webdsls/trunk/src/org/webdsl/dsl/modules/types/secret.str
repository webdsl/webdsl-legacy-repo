module org/webdsl/dsl/modules/types/secret

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  elem-to-xhtml :
    TemplateCall("outputSecret", [e], []) ->
      [%>*******<%]

  elem-to-xhtml :
    TemplateCall("inputSecret",[e],[]) ->
    %>
      <s:decorate>
        <h:inputSecret styleClass="inputSecret" value="<%= estring %>" required="<%= req %>"/>
      </s:decorate>
    <%
    where estring := <arg-to-value-string> e
        ; req := <input-of-required-property(|e)>

  sort-to-java-type-string : SimpleSort("Secret") -> "java.lang.String"

rules // types

  builtin-java-type :
    SimpleSort("Secret") -> type|[ String ]|

  builtin-java-type-default-value :
    SimpleSort("Secret") -> java|[ "" ]|

  check-builtin-signature :
    (s@SimpleSort(_), "digest", []) -> SimpleSort("Secret")
    where SimpleSort("Secret") := s

  check-builtin-signature :
    (s, "check", [s]) -> SimpleSort("Bool")
    where SimpleSort("Secret") := s

  type-compatible =
    ?(SimpleSort("Secret"), SimpleSort("String"))

rules // desugar

  DeriveInput :
    elem|[ input(e){} ]| -> elem|[ inputSecret(e){} ]|
    where SimpleSort("Secret") := <type-of> e

  DeriveOutput :
    elem|[ output(e){} ]| -> elem|[ outputSecret(e){} ]|
    where SimpleSort("Secret") := <type-of> e

rules // java
   call-to-java :
     Call(e1, "digest", []) ->
     java|[ {|
       org.jasypt.util.password.StrongPasswordEncryptor x_var2 =
         new org.jasypt.util.password.StrongPasswordEncryptor();
       String x_var = x_var2.encryptPassword(e2);
       | x_var
     |} ]|
     where e2 := <expression-to-java> e1
         ; x_var := <newname> "_string"
         ; x_var2 := <newname> "_passwordEncryptor"

   call-to-java :
     Call(e1, "check", [e2]) ->
     java|[ {|
       org.jasypt.util.password.StrongPasswordEncryptor x_var2 =
         new org.jasypt.util.password.StrongPasswordEncryptor();
       boolean x_var = x_var2.checkPassword(e4,e3);
       | x_var
     |} ]|
     where e3 := <expression-to-java> e1
         ; e4 := <expression-to-java> e2
         ; x_var := <newname> "_string"
         ; x_var2 := <newname> "_passwordEncryptor"

