module org/webdsl/dsl/modules/types/secret

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  is-simple-sort = ?SimpleSort("Secret") 
  extends-builtin : "Secret" -> "Object" 
  built-in-template-rule-setup = <declare-built-in-template-rules> ("inputSecret",[SimpleSort("Secret")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputSecret",[SimpleSort("Secret")]);fail
  sort-to-java-type-string : SimpleSort("Secret") -> "java.lang.String"

rules // types

  builtin-java-type :
    SimpleSort("Secret") -> type|[ String ]|

  builtin-java-type-default-value :
    SimpleSort("Secret") -> java|[ "" ]|

  type-default-value :
    SimpleSort("Secret") -> exp|[ "" ]|

  check-builtin-signature :
    (s@SimpleSort(_), "digest", []) -> SimpleSort("Secret")
    where SimpleSort("Secret") := s

  check-builtin-signature :
    (s, "check", [s]) -> SimpleSort("Bool")
    where SimpleSort("Secret") := s

  type-compatible =
    ?(SimpleSort("Secret"), SimpleSort("String"))

rules // desugar

  DeriveInput :
    elem|[ input(e)[passign*]{} ]| -> elem|[ inputSecret(e)[passign*]{} ]|
    where SimpleSort("Secret") := <type-of> e

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputSecret(e)[passign*]{} ]|
    where SimpleSort("Secret") := <type-of> e

rules // java
   call-to-java :
     exp|[ e1.digest() ]| ->
     java|[ {|
       org.jasypt.util.password.StrongPasswordEncryptor x_var2 =
         new org.jasypt.util.password.StrongPasswordEncryptor();
       String x_var = x_var2.encryptPassword(e2);
       | x_var
     |} ]|
     with e2 := <expression-to-java> e1
        ; x_var := <newname> "_string"
        ; x_var2 := <newname> "_passwordEncryptor"

   call-to-java :
     exp|[ e1.check(e2) ]| ->
     java|[ {|
       org.jasypt.util.password.StrongPasswordEncryptor x_var2 =
         new org.jasypt.util.password.StrongPasswordEncryptor();
       boolean x_var = x_var2.checkPassword(e4,e3);
       | x_var
     |} ]|
     with e3 := <expression-to-java> e1
        ; e4 := <expression-to-java> e2
        ; x_var := <newname> "_string"
        ; x_var2 := <newname> "_passwordEncryptor"

