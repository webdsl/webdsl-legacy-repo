module org/webdsl/dsl/modules/types/secret

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  is-simple-sort = ?SimpleSort("Secret") 
  extends-builtin : "Secret" -> "Object" 
  built-in-template-rule-setup = <declare-built-in-template-rules> ("inputSecret",[SimpleSort("Secret")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputSecret",[SimpleSort("Secret")]);fail

rules // types

  type-default-value :
    SimpleSort("Secret") -> exp|[ "" ]|

  after-tc-declare-hook = 
    <declare-type-def> 
    def |[ 
      type Secret {
       org.webdsl.tools.Utils.secretDigest  as digest():Secret
       org.webdsl.tools.Utils.secretCheck   as check(Secret):Bool
      }
    ]|
  ; fail

rules //compatibility with String

  built-in-template-rule-setup = <make-string-compatible> "Secret"; fail

rules // desugar

  DeriveInput :
    elem|[ input(e)[passign*]{elem*} ]| -> elem|[ inputSecret(e)[passign*]{elem*} ]|
    where SimpleSort("Secret") := <type-of> e

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputSecret(e)[passign*]{} ]|
    where SimpleSort("Secret") := <type-of> e
