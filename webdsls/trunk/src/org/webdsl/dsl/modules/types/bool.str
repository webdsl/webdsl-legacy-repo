module org/webdsl/dsl/modules/types/bool

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front

rules
  is-simple-sort = ?SimpleSort("Bool") 
  
rules
  elem-to-xhtml :
    elem|[ inputBool(e)[]{} ]| ->
    %>
      <s:decorate>
        <h:selectBooleanCheckbox styleClass="inputBool" value="<%= estring %>" required="<%= req %>"/>
      </s:decorate>
    <%
    where estring := <arg-to-value-string> e
        ; req := <input-of-required-property(|e)>

        
  elem-to-xhtml :
    elem|[ outputBool(e)[]{} ]| ->
    %>
      <h:outputText styleClass="outputBool" value="<%= estring %>"/>
    <%
    where estring := <arg-to-value-string> e

rules // type
  sort-to-java-type-string : SimpleSort("Bool") -> "java.lang.Boolean"

  builtin-java-type :
    SimpleSort("Bool") -> type|[ Boolean ]|

  builtin-wrapper-java-type :
    SimpleSort("Bool") -> type|[ Boolean ]|

  builtin-java-type-default-value :
    SimpleSort("Bool") -> java|[ false ]|

  type-default-value :
    SimpleSort("Bool") -> exp|[ false ]|

  eq-type-of :
    True() -> SimpleSort("Bool")

  eq-type-of :
    False() -> SimpleSort("Bool")

rules // desugar
  DeriveInput :
    elem|[ input(e)[passign*]{} ]| -> elem|[ inputBool(e)[passign*]{} ]|
    where SimpleSort("Bool") := <type-of> e

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputBool(e)[passign*]{} ]|
    where SimpleSort("Bool") := <type-of> e

rules // java
   expression-to-java :
     True() -> java|[ true ]|

   expression-to-java :
     False() -> java|[ false ]|

