module org/webdsl/dsl/modules/types/email

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  is-simple-sort = ?SimpleSort("Email")  
  
rules
  elem-to-xhtml :
    elem|[ inputEmail(e)[]{} ]| ->
    %>
      <s:decorate>
        <h:inputText styleClass="inputEmail" value="<%= estring %>" required="<%= req %>"/>
      </s:decorate>
    <%
    with estring := <arg-to-value-string> e
        ; req := <input-of-required-property(|e)>
    // @todo validate email address

rules // types
  sort-to-java-type-string : SimpleSort("Email") -> "java.lang.String"

  builtin-java-type :
    SimpleSort("Email") -> type|[ String ]|

  builtin-java-type-default-value :
    SimpleSort("Email") -> java|[ "" ]|

  type-default-value :
    SimpleSort("Email") -> exp|[ "" ]|

  type-compatible =
    ?(SimpleSort("Email"), SimpleSort("String"))

rules // desugar
  DeriveInput :
    elem|[ input(e)[passign*]{} ]| -> elem|[ inputEmail(e)[passign*]{} ]|
    where SimpleSort("Email") := <type-of> e

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ text(e)[passign*]{} ]|
    where SimpleSort("Email") := <type-of> e

  
