module org/webdsl/dsl/modules/types/image

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  is-simple-sort = ?SimpleSort("Image")
  extends-builtin : "Image" -> "Object"
  built-in-template-rule-setup = <declare-built-in-template-rules> ("inputImage",[SimpleSort("Image")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputImage",[SimpleSort("String"),SimpleSort("Void")]);fail

rules

  type-default-value :
    SimpleSort("Image") -> exp|[ null ]|

rules // desugar
  DeriveInput :
    elem|[ input(e)[passign*]{elem*} ]| -> elem|[ inputImage(e)[passign*]{elem*} ]|
    where SimpleSort("Image") := <type-of> e

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputImage(e.fileName(),action{e.download();})[passign*]{} ]|
    where SimpleSort("Image") := <type-of> e
    
  //desugar-types :
  //  elem|[ outputImage(e) [passign*]{} ]| -> elem|[ form{ actionLink(e.fileName(),{e.download();}) } ]|

rules //file download

 check-builtin-signature :
    (SimpleSort("Image"), "download", []) ->  SimpleSort("Void")
    
  check-builtin-signature :
    (SimpleSort("Image"), "fileName", []) ->  SimpleSort("String")

 check-builtin-signature :
    (SimpleSort("Image"), "resize", [SimpleSort("Int"), SimpleSort("Int")]) ->  SimpleSort("Void")

 check-builtin-signature :
    (SimpleSort("Image"), "crop", [SimpleSort("Int"), SimpleSort("Int"), SimpleSort("Int"), SimpleSort("Int")]) ->  SimpleSort("Void")

 check-builtin-signature :
    (SimpleSort("Image"), "getWidth", []) ->  SimpleSort("Int")

 check-builtin-signature :
    (SimpleSort("Image"), "getHeight", []) ->  SimpleSort("Int")
