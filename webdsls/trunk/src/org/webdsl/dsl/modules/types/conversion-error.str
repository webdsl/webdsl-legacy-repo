module org/webdsl/dsl/modules/types/conversion-error

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front

//conversion from request param string to actual object is implicit in WebDSL, 
// but when it fails (the property equals null) you can get the specific error using this function

rules

  check-builtin-signature :
    (None(), "conversionError", [s]) ->  SimpleSort("String")
    where <type-compatible> (SimpleSort("Object"),s)
    
  check-builtin-signature :
    (None(), "conversionFailed", [s]) ->  SimpleSort("Bool")
    where <type-compatible> (SimpleSort("Object"),s)
    
rules //reserve these functions names + arg count

  constraint-error-ui :
    fun@Function(_,_,_,_) -> <reserved-function-constraint> (fun,"conversionError",1,"WebDSL data validation")
  
  constraint-error-ui :
    fun@Function(_,_,_,_) -> <reserved-function-constraint> (fun,"conversionFailed",1,"WebDSL data validation")
  
  reserved-function-constraint :
    (fun,name,len,mess) -> <add-error(|["Function with name ",name," and ",<int-to-string> len," argument(s) is reserved for ",mess])> fun
    where Function(name,args,_,_) := fun //checks name and binds args
        ; <length;?len> args
