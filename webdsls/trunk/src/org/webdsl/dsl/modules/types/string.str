module org/webdsl/dsl/modules/types/string

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules
  sort-to-java-type-string : SimpleSort("String") -> "java.lang.String"
  
  builtin-java-type :
    SimpleSort("String") -> type|[ String ]|

  builtin-java-type-default-value :
    SimpleSort("String") -> java|[ "" ]|

  type-default-value :
    SimpleSort("String") -> exp|[ "" ]|
  
  eq-type-of :
    String(x) -> SimpleSort("String")

  check-builtin-signature :
    (_, "toString", []) -> SimpleSort("String")

rules
   expression-to-java :
     String(x) -> expr|[ "~x" ]|


rules // page parameters 

  arg-to-java-exp :
    String(x) -> expr|[ "~x" ]|

rules // JSF
  elem-to-xhtml :
    tc@elem|[ inputString(e){} ]| ->
    %>
        <h:inputText styleClass="inputString" value="<%= estring %>" required="<%= req %>">
          <a4j:support event="onblur" att0 att1/>
        </h:inputText>
    <%
    with estring := <arg-to-value-string> e
        ; req := <input-of-required-property(|e)>
        ; if <get-combined-validation-function> tc
          then att0 := @> reRender="<%= <get-message-ids;separate-by(|", ");concat-strings> tc %>" <@
             ; att1 := @> action="<%= <concat-strings> ["#{",<Component>,".",<get-combined-validation-function> tc,"}"] %>" <@
          else att0:= []; att1:=[] end
          
  elem-to-xhtml :
    elem|[ outputString(e){} ]| ->
    %>
      <h:outputText styleClass="outputString" value="<%= estring %>"/>
    <%
    with estring := <arg-to-value-string> e

rules // Desugar

  DeriveInput :
    elem|[ input(e){} ]| -> elem|[ inputString(e){} ]|
    where SimpleSort("String") := <type-of> e

  DeriveOutput :
    elem|[ output(e){} ]| -> elem|[ text(e){} ]|
    where SimpleSort("String") := <type-of> e

