module org/webdsl/dsl/modules/types/string

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front

rules
  is-simple-sort = ?SimpleSort("String")  
  extends-builtin : "String" -> "Object"
  
rules
  sort-to-java-type-string : SimpleSort("String") -> "java.lang.String"
  
  builtin-java-type :
    SimpleSort("String") -> type|[ String ]|

  builtin-java-type-default-value :
    SimpleSort("String") -> java|[ "" ]|

  type-default-value :
    SimpleSort("String") -> exp|[ "" ]|
  
  eq-type-of :
    String(x) -> SimpleSort("String")

  check-builtin-signature :
    (_, "toString", []) -> SimpleSort("String")

rules
   expression-to-java :
     String(x) -> expr|[ "~x" ]|


rules // page parameters 

  arg-to-java-exp :
    String(x) -> expr|[ "~x" ]|
/*
rules // JSF
  elem-to-xhtml :
    tc@elem|[ inputString(e)[]{} ]| ->
    %>
        <h:inputText styleClass="inputString" value="<%= estring %>" required="<%= req %>">
          <a4j:support event="onblur" att0 att1/>
        </h:inputText>
    <%
    with estring := <arg-to-value-string> e
        ; req := <input-of-required-property(|e)>
        ; if <get-combined-validation-function> tc
          then att0 := @> reRender="<%= <get-message-ids;separate-by(|", ");concat-strings> tc %>" <@
             ; att1 := @> action="<%= <concat-strings> ["#{",<Component>,".",<get-combined-validation-function> tc,"}"] %>" <@
          else att0:= []; att1:=[] end
          
  elem-to-xhtml :
    elem|[ outputString(e)[]{} ]| ->
    %>
      <h:outputText styleClass="outputString" value="<%= estring %>"/>
    <%
    with estring := <arg-to-value-string> e
*/
rules // Desugar

  DeriveInput :
    elem|[ input(e)[passign*]{} ]| -> elem|[ inputString(e)[passign*]{} ]|
    where SimpleSort("String") := <type-of> e
        ; not(<get-select-anno> e)

  DeriveInput : // for an inputSelectString, the property has an annotation 'select' that points to the values to select from
    elem|[ input(e)[passign*]{} ]| -> elem|[ inputSelectString(e)[passign*]{} ]|
    where SimpleSort("String") := <type-of> e
        ; SelectAnno(srt) := <get-select-anno> e

  get-select-anno = // gets the select anno or fails if there is none
    ?exp|[ e_object.x_property ]|
    ; SimpleSort(srt) := <type-of> e_object
    ; <PropertyAnnos> (srt, x_property)
    ; fetch-elem(?SelectAnno(_))

  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ text(e)[passign*]{} ]|
    where SimpleSort("String") := <type-of> e


rules //native string operations

 check-builtin-signature :
    (SimpleSort("String"), "contains", [s]) ->  SimpleSort("Bool")
    where <type-compatible> (s, SimpleSort("String"))

 check-builtin-signature :
    (None(), "log", [s]) ->  SimpleSort("Void")
    where <type-compatible> (s, SimpleSort("String"))
