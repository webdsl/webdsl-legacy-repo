module org/webdsl/dsl/modules/styling/to-css/layout-to-css

imports 
  libstrategolib  
  libjava-front 
  libwebdsl-front

rules
  /**
   * Generate css for layout definition
   */
  GenerateCode = generate-layout-css; fail
  
  generate-layout-css =
    ?LayoutDefinition(type, MatchDefinition(n, arg*), stat*)
    ; {| ThisPage
       : name := <concat-strings> [n, "Layout"]
       ; rules ( ThisPage := name )
       ; <emit-css> Stylesheet(<mapconcat(layout-to-css)> stat*)
       |}


  layout-to-css :
    LayoutStatement(expr) -> <layout-to-css> expr

  layout-to-css :
    NamedLayoutExpression(name, expr*) -> []

  layout-to-css :
    LayoutExpression(MatchDefinition(name, arg*)) ->
      rule* |(
        sel0 {
          clear: both;
          float: left;
        }
      )|
    where sel0 := <name-to-css-class> name

  layout-to-css :
    FloatStatement(def*) -> <map(float-to-css)> def*
  
  float-to-css:
    MatchDefinition(ident, args) ->
      rule* |(
        sel0 {
          float: left;
        }
      )|
    where sel0 := <match-definition-to-class> MatchDefinition(ident, args)

