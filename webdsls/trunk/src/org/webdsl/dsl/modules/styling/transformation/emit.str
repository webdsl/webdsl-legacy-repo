module org/webdsl/dsl/modules/styling/transformation/emit

imports
  libstrategolib

imports
  org/webdsl/dsl/modules/styling/-

strategies

  emit-style-section =
    ?sec@StyleSection(_,_)
    ; rules(
        AddStyleSection :+ Application(qid, sections) -> Application(qid, [sec|sections])
      )
  
  desugar = once-AddStyleSection

  emit-style-definition =
    ?def@StyleDefinition(_,_,_)
    ; where ( x := <ThisPage> )
    ; rules(
        AddStyleDef :+ StyleSection(nm, defs) -> StyleSection(nm, [def|defs]) where <?x> nm
      )
  
  desugar = once-AddStyleDef
  
  emit-style-declaration =
    ?stat@StyleDeclaration(_,_)
    ; where (
        x := <ThisPage> 
        ; f* := <ThisArgumentTypes>
      )
    ; rules(
        AddStyleDecl :+ StyleDefinition(type, MatchDefinition(nm, farg*), stats) ->
                            StyleDefinition(type, MatchDefinition(nm, farg*), [stat|stats])
                          where <?x> nm ; <?f*> <map(?Arg(_,<id>))> farg*
      )
  
  desugar = once-AddStyleDecl
