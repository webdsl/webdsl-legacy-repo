module org/webdsl/dsl/modules/styling/declare

imports
  libstrategolib

imports
  libwebdsl-front

strategies

  declare-style-definition =
    ?def@StyleDefinition(type, MatchDefinition(name, arg*), decl*)
    ; where (
	    t* := <map(?Arg(_,<id>))> arg*
	    ; x := <InSection>
	    ; rules (
	        StyleSignature : (name, t*) -> def
	        StylesheetIncludes :+ name -> x
	      )
	  )

  declare-style-declaration =
    ?decl@StyleDeclaration(prop, val)
    ; where (
        (name, t*) := <InDef>
        ; p := <remove-position-anno> prop
        ; v := <remove-position-anno> val
        ; rules (
            StyleDeclarations :+ (name, t*) -> (p, v)
            StyleProperties :+ (name, t*) -> p
            StyleValue : (name, t*, p) -> v
          )
      )

        
  declare-style-var-decl =
    ?StyleVarDeclInit(x, srt, e)
    ; where (
        e' := <strip-annos> e
        ; rules ( StyleVariable : x -> e' )
      )

  declare-layout-definition =
    ?LayoutDefinition(matchdef, decl*)
    ; where (
        MatchDefinition(n, arg*) := matchdef
        ; name := <concat-strings> [n, "Layout"]
        ; rules ( StylesheetIncludes :+ n -> name )
      )
      