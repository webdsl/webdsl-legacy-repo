module org/webdsl/dsl/modules/styling/types

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules

  type-of =
    type-of-style-property <+ type-of-style-value <+ style-error

  style-error =
    ( ?StyleProperty(_) <+ ?StyleValue(_) <+ ?StyleValue(_, _) )
    ; !StyleSort("Error")
  
  style-types-compatible =
    ?(StyleSort(t, u), StyleSort(t, u))
    
  style-types-compatible =
    ?(StyleSort(t), StyleSort(t))
  
  style-types-compatible =
    ?(sort*, StyleSort(t, u))
    ; where ( <elem> (StyleSort(t, u), sort*) )
   
  style-types-compatible =
    ?(sort*, StyleSort(t))
    ; where ( <elem> (StyleSort(t), sort*) ) 
    
    
//  style-types-compatible =
//    ?(StyleProperty(prop1), StyleProperty(prop2))
    
  type-of-style-property :
    StyleProperty("width") -> [StyleSort("Length", "px"),
                               StyleSort("Length", "pt"),
                               StyleSort("Length", "mm"),
                               StyleSort("Length", "cm"),
                               StyleSort("Ems"),
                               StyleSort("Exs"),
                               StyleSort("Percentage")]

  type-of-style-property :
    StyleProperty("background-color") -> StyleSort("Color")
  
  type-of-style-value :
    StylePropertyValue(matchdef, prop) -> <type-of-style-property> prop
  
  type-of-style-value :
    StyleValue(value, unit) -> <type-of-style-unit> unit

  type-of-style-value :
    StyleValue(color) -> StyleSort("Color")
    where <is-color> color
    
  type-of-style-value :
    StyleValue(Hash(color)) -> StyleSort("Color")
  
  type-of-style-value :
    StyleValue(Uri(address)) -> StyleSort("Uri")
  
  type-of-style-unit :
    "%" -> StyleSort("Percentage")
  
  type-of-style-unit :
    "px" -> StyleSort("Length")
  
  type-of-style-unit :
    "pt" -> StyleSort("Length")
  
  type-of-style-unit :
    "mm" -> StyleSort("Length")
  
  type-of-style-unit :
    "cm" -> StyleSort("Length")
  
  type-of-style-unit :
    "em" -> StyleSort("Ems")
  
  type-of-style-unit :
    "ex" -> StyleSort("Exs")
  

  is-color =
    ?"red" <+ ?"green" <+ ?"blue" <+ ?"yellow" <+
    ?"orange" <+ ?"brown" <+ ?"black" <+ ?"white" <+
    ?"cyan" <+ ?"magenta" <+ ?"purple" <+ ?"gray"
