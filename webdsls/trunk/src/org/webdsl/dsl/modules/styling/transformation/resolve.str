module org/webdsl/dsl/modules/styling/transformation/resolve

imports
  libstrategolib
  libwebdsl-front

strategies

  resolve-style-properties =
    alltd(resolve-style)
  
  translate-style-properties =
    alltd(translate-style)
    
rules

  resolve-style :
    StyleDefinition(selector, stat1*) -> StyleDefinition(selector, stat2*)
    where {| ThisSelector
          : rules ( ThisSelector := selector )
//          ; <debug> <pp-webdsl> selector
//          ; <debug> stat1*
//          ; stat2* := <bottomup-l(repeat(resolve))> stat1*
          ; stat2* := <reduce(resolve)> stat1*
//          ; <debug> stat2*
          ; <declare-style-definition> StyleDefinition(selector, stat2*)
//          ; <debug> ""
          |}

  translate-style :
    StyleSection(name, def1*) -> StyleSection(name, def2*)
    where {| ThisPage
           : rules ( ThisPage := name )
           ; def2* := <alltd(translate-style)> def1*
           |}

  translate-style :
    StyleDefinition(selector, stat1*) -> StyleDefinition(selector, stat2*)
    with {| ThisSelector
          : rules ( ThisSelector := selector )
          ; stat2* := <filter(not(translate-style-decl))> stat1*
          ; <declare-style-definition> StyleDefinition(selector, stat2*)
          |}

rules

//  desugar = resolve

rules

  resolve :
    SimpleSelector(MatchDefinition("form", _)) -> ElemSimpleSelector("form")
    where AfterProcessStyle
  
  resolve :
    SimpleSelector(MatchDefinition("fieldset", _)) -> ElemSimpleSelector("fieldset")
    where AfterProcessStyle
  
  resolve :
    SimpleSelector(MatchDefinition("group", _)) -> ElemSimpleSelector("fieldset")
    where AfterProcessStyle
  
  resolve :
    SimpleSelector(MatchDefinition("action", _)) -> ElemSimpleSelector("input")
    where AfterProcessStyle
  
  resolve :
    SimpleSelector(MatchDefinition("inputText", _)) -> ClassSimpleSelector("inputText")
    where AfterProcessStyle
    
  resolve :
    SimpleSelector(MatchDefinition("menubar", _)) -> ClassSimpleSelector("menubar")
    where AfterProcessStyle
      
  resolve :
    SimpleSelector(MatchDefinition("menu", _)) -> ElemSimpleSelector("ul")
    where AfterProcessStyle

  resolve :
    SimpleSelector(MatchDefinition("menuheader", _)) -> ElemSimpleSelector("li")
    where AfterProcessStyle
  
  resolve :
    SimpleSelector(MatchDefinition("list", _)) -> ElemSimpleSelector("ul")
    where AfterProcessStyle
  
  resolve :
    SimpleSelector(MatchDefinition("listitem", _)) -> ElemSimpleSelector("li")
    where AfterProcessStyle
  
  resolve :
    SimpleSelector(MatchDefinition("navigate", _)) -> ElemSimpleSelector("a")
    where AfterProcessStyle
    
  resolve :
    SimpleSelector(MatchDefinition("table", _)) -> ElemSimpleSelector("table")
    where AfterProcessStyle
  
  resolve :
    SimpleSelector(MatchDefinition("section", _)) -> ClassSimpleSelector("section")
    where AfterProcessStyle

  resolve :
    SimpleSelector(MatchDefinition("block", _)) -> ClassSimpleSelector("block")
    where AfterProcessStyle

  resolve :
    SimpleSelector(MatchDefinition("text", _)) -> ClassSimpleSelector("text")
    where AfterProcessStyle

  resolve :
    Selector(SimpleSelector(MatchDefinition("groupitem", _)), s*) -> Selector(ElemSimpleSelector("ol"), [(ChildCombinator, ElemSimpleSelector("li"), None)|s*])
    where AfterProcessStyle

  resolve :
    Selector(SimpleSelector(MatchDefinition("header", _)), l1) -> Selector(ElemSimpleSelector("h1"), l1)
    where AfterProcessStyle

  resolve :
    Selector(s, l1) -> Selector(s, l2)
    where AfterProcessStyle
        ; not( <?SimpleSelector(MatchDefinition("table", _))> s <+ <?ElemSimpleSelector("table")> s )
        ; l2 := <sometd(
                  {\ (c, SimpleSelector(MatchDefinition("header", _)), p)
                      -> (c, ElemSimpleSelector("h1"), p) \})> l1
      
  resolve :
    Selector(s, l1) -> Selector(s, l2)
    where AfterProcessStyle
        ; <sometd(?(_, SimpleSelector(MatchDefinition("table", _))))> l1
        ; l2 := <sometd(
                  {\ (c, SimpleSelector(MatchDefinition("header", _)), p)
                      -> [(DescendantCombinator, ElemSimpleSelector("tr"), None),
                          (ChildCombinator, ElemSimpleSelector("th"), p)]
                   \}); flatten-list> l1
  
  resolve :
    Selector(s, l1) -> Selector(s, l2)
    where AfterProcessStyle
        ; ( <?SimpleSelector(MatchDefinition("table", _))> s <+ <?ElemSimpleSelector("table")> s )
        ; l2 := <sometd(
                  {\ (c, SimpleSelector(MatchDefinition("header", _)), p)
                      -> [(DescendantCombinator, ElemSimpleSelector("tr"), None),
                          (ChildCombinator, ElemSimpleSelector("th"), p)]
                   \}); flatten-list> l1

  resolve :
    Selector(s, l1) -> Selector(s, l2)
    where AfterProcessStyle
        ; l2 := <sometd(
                  {\ (c, s', p)
                      -> [(DescendantCombinator, ElemSimpleSelector("tr"), None),
                          (ChildCombinator, ElemSimpleSelector("td"), p)]
                      where <oncetd(?"row")> s'
                   \}); flatten-list> l1
  
  resolve :
    Selector(s, l1) -> Selector(s, l2)
    where AfterProcessStyle
        ; l2 := <sometd(
                  {\ (c, SimpleSelector(MatchDefinition("groupitem", _)), p)
                      -> [(c, ElemSimpleSelector("ol"), None),
                          (ChildCombinator, ElemSimpleSelector("li"), p)]
                   \}); flatten-list> l1
/*    
  resolve :
    SimpleSelector(MatchDefinition("groupitem", _)) -> ElemSimpleSelector("li")
    where AfterProcessStyle
*/  
  resolve :
    SimpleSelector(MatchDefinition("label", _)) -> ElemSimpleSelector("label")
    where AfterProcessStyle

  resolve :
    SimpleSelector(MatchDefinition(ident, args)) -> SimpleSelector("template", MatchDefinition(ident, args))
    where AfterProcessStyle
        ; <matches-template-definition> MatchDefinition(ident, args)

rules

  resolve :
    StylePropertyValue(MatchDefinition(name, farg*), prop) -> val
    where ThisSelector
        ; t* := <map(?Arg(_,<id>))> farg*
	    ; s := <construct-selector> ("template", name, t*)
	    ; val := <get-style-value> (s, prop)
//	    ; <debug(!"___")> (<pp-webdsl> prop, <pp-webdsl> val)
//        ; <declare-style-value> (prop, val)
  
  resolve :
    StyleValueExpression(selector, prop) -> val
    where ThisSelector
/*        ; if <sometd(?"createOwner")> <ThisSelector> then
            <debug(!"__valex: ")> StyleValueExpression(selector, prop)
            ; <debug(!"ThisSelector1: ")> <ThisSelector>
          end */
        ; val := <get-style-value> (selector, prop)
//        ; <debug(!"___")> (<pp-webdsl> prop, <pp-webdsl> val)
//        ; <declare-style-value> (prop, val)

  resolve :
    StyleAdd(StyleValue(v1, u1), StyleValue(v2, u2)) -> value
    where ThisSelector
     with value := <add-values> (StyleValue(v1, u1), StyleValue(v2, u2))

  resolve :
    StyleSub(StyleValue(v1, u1), StyleValue(v2, u2)) -> value
    where ThisSelector
     with value := <subtract-values> (StyleValue(v1, u1), StyleValue(v2, u2))
     
  resolve :
    StyleMul(StyleValue(v1, u1), StyleValue(v2, u2)) -> value
    where ThisSelector
     with value := <multiply-values> (StyleValue(v1, u1), StyleValue(v2, u2))
   
  resolve :
    StyleDiv(StyleValue(v1, u1), StyleValue(v2, u2)) -> value
    where ThisSelector
     with value := <divide-values> (StyleValue(v1, u1), StyleValue(v2, u2)) 
      
  resolve :
    StyleDeclaration(prop, Var(x)) -> <declare-style-declaration> StyleDeclaration(prop, val)
    where ThisSelector
        ; val := <StyleVariable> x

  resolve :
    Var(x) -> <StyleVariable> x
    where ThisSelector

rules
/*
  translate-style-decl =
    ?StyleDeclaration(StyleProperty("font"), StyleValue(sort, val*))
    ; stval0 := StyleValue(<construct-font-names> val*)
    ; <emit-style-declaration> |[ font-family := stval0; ]|
    
  construct-font-names =
    ?[s|s*]
    ; !<concat-strings> ["'", <construct-font-name> s, " ", <construct-font-names> s*, "'"]

  construct-font-names :
    [s] -> <construct-font-name> s

  construct-font-name :
    StyleValueExtension(stval0) -> stval0

  */    

    
  resolve :
    ststat |[ image-repeat := Repeat.horizontal; ]| -> ststat |[ background-repeat := repeat-x; ]|
    where ThisSelector
  
  resolve :
    ststat |[ image-repeat := Repeat.vertical; ]| -> ststat |[ background-repeat := repeat-y; ]|
    where ThisSelector
  
  resolve :
    ststat |[ image-repeat := Repeat.both; ]| -> ststat |[ background-repeat := repeat; ]|
    where ThisSelector
  
  resolve :
    ststat |[ font-color := stval0; ]| -> ststat |[ color := stval0; ]|
    where ThisSelector

  resolve :
    ststat |[ font-line := Line.none; ]| -> ststat |[ text-decoration := none; ]|
    where ThisSelector

  resolve :
    ststat |[ font-line := Line.under; ]| -> ststat |[ text-decoration := underline; ]|
    where ThisSelector
  
  resolve :
    ststat |[ font-line := Line.over; ]| -> ststat |[ text-decoration := overline; ]|
    where ThisSelector
    
  resolve :
    ststat |[ font-line := Line.through; ]| -> ststat |[ text-decoration := line-through; ]|
    where ThisSelector
  
  resolve :
    ststat |[ image-repeat := Repeat.none; ]| -> ststat |[ background-repeat := no-repeat; ]|
    where ThisSelector 

  resolve : // @todo: padding-bottom is not valid for all applications of spacing!
    ststat |[ spacing := stval0; ]| -> ststat |[ padding-bottom := stval0; ]|
    where ThisSelector

  resolve :
    ststat |[ align := Align.left; ]| -> ststat |[ margin-right := auto; ]|
    where ThisSelector ; is-for-named-layout-identifier
  
  resolve :
    ststat |[ align := Align.right; ]| -> ststat |[ margin-left := auto; ]|
    where ThisSelector ; is-for-named-layout-identifier

  resolve :
    ststat |[ align := Align.left; ]| -> ststat |[ float := left; ]|
    where ThisSelector ; not( <selector-contains> "label" )

  resolve :
    ststat |[ align := Align.right; ]| -> ststat |[ float := right; ]|
    where ThisSelector ; not( <selector-contains> "label" )

  resolve :
    ststat |[ spacing-right := stval0; ]| -> ststat |[ margin-right := stval0; ]|
    where ThisSelector ; not( <is-defined> StyleProperty("separator") )

  resolve :
    ststat |[ spacing-left := stval0; ]| -> ststat |[ margin-left := stval0; ]|
    where ThisSelector ; not( <is-defined> StyleProperty("separator") )
    
rules

  translate-style-decl =
    ?ststat |[ align := Align.center; ]|
    ; where ( is-for-named-layout-identifier <+ <has-property> StyleProperty("width") )
    ; with (
        <declare-style-declarations; emit-style-declarations> |[
          margin-left := auto;
          margin-right := auto;
        ]| 
      )

  translate-style-decl =
    ?ststat |[ border-color := stval0; ]|
    ; with (
        <declare-style-declarations; emit-style-declarations> |[
            border-top-color := stval0;
            border-right-color := stval0;
            border-bottom-color := stval0;
            border-left-color := stval0;
          ]| 
      )

  translate-style-decl =
    ?ststat |[ border-width := stval0; ]|
    ; with (
        <declare-style-declarations; emit-style-declarations> |[
            border-top-width := stval0;
            border-right-width := stval0;
            border-bottom-width := stval0;
            border-left-width := stval0;
          ]| 
      )

  translate-style-decl =
    ?ststat |[ border-style := stval0; ]|
    ; with (
        <declare-style-declarations; emit-style-declarations> |[
            border-top-style := stval0;
            border-right-style := stval0;
            border-bottom-style := stval0;
            border-left-style := stval0;
          ]| 
      )
  
  translate-style-decl =
    ?ststat |[ align := Align.right; ]|
    ; where ( <selector-contains> "label" )
    ; with (
        <declare-style-declarations; emit-style-declarations> |[
          float := left;
          text-align := right;
        ]| 
      )

  translate-style-decl =
    ?ststat |[ align := Align.left; ]|
    ; where ( <selector-contains> "label" )
    ; with (
        <declare-style-declaration; emit-style-declaration> |[
          float := left;
        ]|
      )

  // @todo: these separators only make sense when the orientation is set to horizontal
  translate-style-decl =
    ?ststat |[ separator := Separator.pipe; ]|
    ; with (
        stsel0 := <add-simple-selector> (<ThisSelector>, |[ > li.before ]| )
        ; <declare-style-definition; emit-style-definition> stdef |[
          stsel0 {
            content := '|';
          }
        ]|
      )
    ; fail // fail, so the current term will not disappear

  // @todo: these separators only make sense when the orientation is set to horizontal
  translate-style-decl =
    ?ststat |[ separator := Separator.comma; ]|
    ; with (
        stsel0 := <add-simple-selector> (<ThisSelector>, |[ > li.before ]| )
        ; <declare-style-definition; emit-style-definition> stdef |[
          stsel0 {
            content := ',';
          }
        ]|
      )
    ; fail // fail, so the current term will not disappear

  translate-style-decl =
    ?ststat |[ separator := Separator.none; ]|
    ; with (
        stsel0 := <add-simple-selector> (<ThisSelector>, |[ > li ]| )
        ; <declare-style-definition; emit-style-definition> stdef |[
          stsel0 {
            list-style-type := none;
          }
        ]|
      )

  translate-style-decl =
    ?ststat |[ orientation := Orientation.horizontal; ]|
    ; with (
        <declare-style-declarations; emit-style-declarations> |[
          list-style := none;
          padding-left := 0;
        ]|
        ; stsel0 := <add-simple-selector> (<ThisSelector>, |[ > li ]| )
        ; <declare-style-definition; emit-style-definition> stdef |[
          stsel0 {
            float := left;
          }
        ]|
      )
  
  translate-style-decl =
    ?ststat |[ orientation := Orientation.vertical; ]|
    ; with (
        <declare-style-declaration; emit-style-declaration> |[
          padding-left := 0;
        ]|
      )
 
  translate-style-decl =
    ?ststat |[ spacing := stval0; ]|
    ; where ( <is-defined> StyleProperty("separator") )
    ; with (
        stsel0 := <add-simple-selector> (<ThisSelector>, |[ > li.before ]| )
        ; StyleValue(value, unit) := stval0
        ; stval1 := StyleValue(<div> (<string-to-real> value, 2), unit)
        ; <declare-style-definition; emit-style-definition> stdef |[
          stsel0 {
            margin-right := stval1;
		    margin-left := stval1;
          }
        ]|
      )  

  translate-style-decl =
    ?ststat |[ spacing-right := stval0; ]|
    ; where ( <is-defined> StyleProperty("separator") )
    ; with (
        stsel0 := <add-simple-selector> (<ThisSelector>, |[ > li.before ]| )
        ; <declare-style-definition; emit-style-definition> stdef |[
          stsel0 {
            margin-right := stval0;
          }
        ]|
      )

   translate-style-decl =
    ?ststat |[ spacing-left := stval0; ]|
    ; where (  <is-defined> StyleProperty("separator") )
    ; with (
        stsel0 := <add-simple-selector> (<ThisSelector>, |[ > li.before ]| )
        ; <declare-style-definition; emit-style-definition> stdef |[
          stsel0 {
            margin-left := stval0;
          }
        ]|
      )
