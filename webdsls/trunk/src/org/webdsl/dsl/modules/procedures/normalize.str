module org/webdsl/dsl/modules/procedures/normalize

imports
  libstrategolib
  libwebdsl-front

rules // Normalization

  desugar = normalize
  
rules // Normalize: add missing blocks

  // add missing enabled block
  normalize :
    def|[ auto procedure x_id (x_o : srt) { procelem* }]| -> def|[ procedure x_id(x_o : srt) { procelem2* } ]| 
    where [] := <filter(?procelem|[ enabled { stat* } ]|)> procelem* 
        ; procelem2* := [procelem|[ enabled { x_o.x_id.do(); } ]| | procelem*]
    
  // ... enabled
  normalize : 
    def|[ auto procedure x_id (x_o : srt) { procelem* }]| -> def|[ procedure x_id(x_o : srt) { procelem2* } ]|
    where [el] := <filter(?procelem|[ enabled { stat* } ]|)> procelem*
        ; procelem2* := [procelem|[ enabled { stat* x_o.x_id.do(); } ]| | procelem*]

  // ... who
  normalize :
    def|[ procedure x_id (x_o : srt) { procelem* }]| -> def|[ procedure x_id(x_o : srt) { procelem2* } ]|
    where [] := <filter(?procelem|[ who { e } ]|)> procelem*
        ; procelem2* := [procelem|[ who { true } ]| | procelem*]

  // ... when
  normalize :
    def|[ procedure x_id (x_o : srt) { procelem* }]| -> def|[ procedure x_id(x_o : srt) { procelem2* } ]|
    where [] := <filter(?procelem|[ when { e } ]|)> procelem*
        ; procelem2* := [procelem|[ when { true } ]| | procelem*]

  // ... do
  normalize :
    def|[ procedure x_id (x_o : srt) { procelem* }]| -> def|[ procedure x_id(x_o : srt) { procelem2* } ]|
    where [] := <filter(?procelem|[ do { stat* } ]|)> procelem*
        ; procelem2* := [procelem|[ do {} ]| | procelem*]

rules // Further normalizing

  normalize :
    def|[ procedure x_id (x_o : srt) { procelem* }]| -> def|[ procedure x_id(x_o : srt) { procelem2* } ]|
    where {| OpArg
           : rules ( OpArg := x_o )
           ; procelem2* := <manytd(normalize-status)> procelem*
           |}

  normalize-status :
    Var("status") -> Var(oparg)
    where oparg := <OpArg>

  normalize-status :
    Qualified("status", f) -> Qualified(oparg, f)
    where oparg := <OpArg>

  normalize :
    def|[ status x_id { prop* fun* } ]| -> def|[ extend entity x_id { prop* fun* } ]|

  normalize : // make the doAction() callable as do()
    exp|[ do() ]| -> exp|[ doAction() ]|

  normalize :
    exp|[ e.start(e*) ]| -> exp|[ x(e1, e*) ]|
    where SimpleSort("WorkflowStatus") := <type-of> e
        ; FieldAccess(e1, wf) := e
        ; x := <concat-strings> ["new", <capitalize-string> wf]

  /*
  normalize :
    exp|[ e.enable() ]| -> exp|[ x(e1) ]|
    where SimpleSort("ProcedureStatus") := <type-of> e
        ; FieldAccess(e1, ps) := e
        ; x := <concat-strings> [ps, "Enabled"]

  normalize :
    exp|[ e.disable() ]| -> exp|[ x(e1) ]|
    where SimpleSort("ProcedureStatus") := <type-of> e
        ; FieldAccess(e1, ps) := e
        ; x := <concat-strings> [ps, "Disabled"]

  normalize :
    exp|[ e.do() ]| -> exp|[ x(e1) ]|
    where SimpleSort("ProcedureStatus") := <type-of> e
        ; FieldAccess(e1, ps) := e
        ; x := <concat-strings> [ps, "Do"]
        */
