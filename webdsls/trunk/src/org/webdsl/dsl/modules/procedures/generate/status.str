module org/webdsl/dsl/modules/procedures/generate/status

imports
  libstrategolib
  libwebdsl-front

rules // Status pages

  procedure-generate-status-pages = 
    <map(procedure-emit-entity-status-page)> <bagof-ProcedureEntities; make-set>

  procedure-emit-entity-status-page =
    ?SimpleSort(ent)
    ; procedures := <bagof-Procedures; make-set> SimpleSort(ent)
    ; x_page := <concat-strings> [<decapitalize-string> ent, "Status"]
    ; x_ent := <decapitalize-string> ent
    ; srt := SimpleSort(ent)
    ; str := <make-human-readable> ent
    ; e := Var(x_ent)
    ; x2 := <concat-strings> [<decapitalize-string> x_ent, "StatusList"]
    ; elem* := <map(procedure-to-status-listitem(|x_ent))> procedures
    ; <emit-webdsl-code> def|[
        define x2() {
          header{ str }
          list {
            for(x_ent : srt) {
              listitem {
                output(e)
                list {
                  elem*
                }
              }
            }
          }
        }
      ]|
    ; <emit-webdsl-code> def|[
        define page x_page() {
          title{ str }
          main()
          define body() {
            section {
              header{"Status"}
              section {
                x2()
              }
            }
          }
        }
      ]|

  procedure-to-status-listitem(|x_arg) :
    x_op -> webdsl|[
            if(e) {
              if(e1) {
                listitem {
                  navigate(x_op(x_arg)) { text(e2) str }
                }
              }
              if(!e1) {
                listitem {
                  text(e2)
                }
              }
            }
          ]|
    where if not(<IsDoProcedure> x_op) then
            str := ""
          else
            str := " (X)"
          end
        ; e := <procedure-rename-var(|<ProcedureArg> x_op, x_arg)> <ProcedureWhen> x_op
        ; e1 := <procedure-rename-var(|<ProcedureArg> x_op, x_arg)> <ProcedureWho> x_op
        ; e2 := String(<make-human-readable> x_op)

