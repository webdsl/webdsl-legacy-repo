module org/webdsl/dsl/modules/procedures/rename

imports
  libstrategolib
  libwebdsl-front

rules // Renaming

  rename :
    def@def|[ procedure x_id (x_o1 : srt) { procelem1* }  ]| -> 
      <declare-procedure> def|[ procedure x_id (x_o2 : srt) { procelem2* }  ]| 
    with {| Rename, InDef
          : rules ( InDef := True() )
          ; x_o2 := <rename-bound(|srt)> x_o1
          ; procelem2* := <rename-all> procelem1*
          |}
  
rules // Declaring

  declare-procedure =
    ?def@def|[ procedure x_id (x_o : srt) { procelem* }]|
    ; where (id
      ; who-exp := <procedure-who-exp> def
      ; when-exp := <procedure-when-exp> def
      ; SimpleSort(s) := srt
      ; hasopfun := <concat-strings> [<decapitalize-string> s, "HasProcedures"]
      ; procstatus := x_id
      ; rules ( 
          IsProcedure      : x_id 
          ProcedureWho     : x_id -> who-exp
          ProcedureWhen    : x_id -> And(FieldAccess(FieldAccess(Var(x_o), procstatus), "enabled"), when-exp)
          ProcedureArg     : x_id -> x_o
          ProcedureArgSort : x_id -> srt
          Procedures       :+ srt -> x_id 
          CheckGeneratedGlobalFunctionSignature : 
            (None(), hasopfun, [srt]) -> SimpleSort("Bool")
          ProcedureEntities :+= srt 
        )
      ; if [] := <procedure-view-elems> def then
          rules ( IsDoProcedure : x_id )
        end
      // for typechecking, these declares are executed early (and overwritten later)
      ; <temp-declare-entity-template> def
      ; <temp-declare-entity-task-page> def
      ; temp-declare-all-tasks-page
      ; <temp-declare-entity-status-page> def
    )
  
  temp-declare-entity-template =
    ?def@def|[ procedure x_id (x_o : srt) { procelem* }]|
    ; SimpleSort(ent) := srt
    ; x1     := <concat-strings> [<decapitalize-string> ent, "Procedures"]
    ; x2     := <concat-strings> [<decapitalize-string> ent, "ProceduresList"]
    ; x_arg  := <decapitalize-string> ent
    ; srt    := SimpleSort(ent)
    ; <declare-template-definition> def|[ define x2(x_arg : srt) {} ]|
    ; <declare-template-definition> def|[ define x1(x_arg : srt) {} ]|
  
  temp-declare-entity-task-page =
    ?def@def|[ procedure x_id (x_o : srt) { procelem* }]|
    ; SimpleSort(ent) := srt
    ; procedures := <bagof-Procedures; make-set> SimpleSort(ent)
    ; x_page := <concat-strings> [<decapitalize-string> ent, "Tasks"]
    ; x_ent := <decapitalize-string> ent
    ; e := Var(x_ent)
    ; x2 := <concat-strings> [<decapitalize-string> x_ent, "TaskList"]
    ; x3 := <concat-strings> [<decapitalize-string> x_ent, "HasProcedures"]
    ; <declare-global-func> def|[
        globals {
          function x3(x_ent : srt) : Bool {
            return true;
          }
        }
      ]|
    ; <declare-template-definition> def|[ define x2() {} ]|
    ; <declare-page-definition> def|[ define page x_page() {} ]|
  
  temp-declare-all-tasks-page =
    <declare-page-definition> def|[ define page allTasks() {} ]|
      
  temp-declare-entity-status-page =
    ?def@def|[ procedure x_id (x_o : srt) { procelem* }]|
    ; SimpleSort(ent) := srt
    ; x_page := <concat-strings> [<decapitalize-string> ent, "Status"]
    ; x_ent := <decapitalize-string> ent
    ; x2 := <concat-strings> [<decapitalize-string> x_ent, "StatusList"]
    ; <declare-template-definition> def|[ define x2() {} ]|
    ; <declare-page-definition> def|[ define page x_page() {} ]|
  