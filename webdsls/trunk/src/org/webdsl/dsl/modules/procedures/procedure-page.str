module org/webdsl/dsl/modules/procedures/procedure-page

imports
  libstrategolib
  libwebdsl-front

rules // Translate to pages and stuff

  procedures-to-pages :
    def@def|[ procedure x_id (x_o : srt) { procelem* } ]| -> <rename-all> def|[ define page x_id(x_o : srt) { elem* } ]|
    where <IsProcedure> x_id
    with  stat* := <procedure-do-statements; add-procedure-done(|x_o, x_id)> def
        ; SimpleSort(s) := srt
        ; x_view := <decapitalize-string> s
        ; if <IsDoProcedure> x_id then
              stat2* := <add-view-page-goto(|x_view, Var(x_o))> stat*
            ; elem*  := elem*|[ init { stat2* } ]|
          else
              stat2*      := <add-view-page-return(|x_view, Var(x_o))> stat*
            ; viewelem*   := <procedure-view-elems> def
            ; actionelem* := webdsl*|[ action doAction() { stat2* } ]|
            ; elem*       := [viewelem*, actionelem*]
          end
        ; e2 := <procedure-get-access-exp> procelem*
        ; <emit-webdsl-section> |[
            access control rules {
              rules page x_id(x_o : srt) {
                e2
                rules action doAction() {
                  e2
                }
              }
            }
          ]|
          
        // create procedure status including viewPage
        ; x_Status := <concat-strings> [<capitalize-string> x_id, "ProcedureStatus"]
        ; x_status := <decapitalize-string> x_Status
        ; x_ent := s
        ; stat3* := <filter(procedure-enabled-statements); concat> procelem*
        ; stat4* := <filter(procedure-processed-statements); concat> procelem*
        ; stat5* := <filter(procedure-done-statements); concat> procelem*
        ; stat6* := <filter(not({e: ?webdsl|[ return e; ]| <+ ?webdsl|[ goto e; ]|}))> stat2*
        ; stat7* := <filter(procedure-disabled-statements); concat> procelem*
        ; <emit-webdsl-code> def|[
            entity x_Status : ProcedureStatus {
              x_o -> srt
              function enabled() {
                this.isEnabled := true;
                stat3*
              }
              function processed() {
                stat4*
              }
              function done() {
                this.isEnabled := false;
                stat5*
              }
              function do() {
                stat6*
              }
              function disabled() {
                this.isEnabled := false;
                stat7*
              }
            }
          ]|
        ; <emit-webdsl-code> def|[
            define page x_status(s : x_Status) {
              derive viewPage from s
            }
          ]|
        ; <emit-webdsl-code> def|[
            extend entity x_ent {
              x_id <> x_Status ()
            }
          ]|

  add-procedure-done(|x_o, x_id) :
    stat* -> webdsl|[
          stat*
          x_o.x_id.done();
        ]|

  procedure-to-check-performed(|arg) :
    nm -> webdsl|[ x_check(e); ]|
    where x_check := <concat-strings> ["check", <capitalize-string> nm, "Performed"]
        ; e := Var(arg)



