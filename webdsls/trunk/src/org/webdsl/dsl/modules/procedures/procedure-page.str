module org/webdsl/dsl/modules/procedures/procedure-page

imports
  libstrategolib
  libwebdsl-front

rules // Translate to pages and stuff

  Desugar :
    def@def|[ procedure x_id (x_o : srt) { procelem* } ]| -> def|[ define page x_id(x_o : srt) { elem* } ]|
    where <IsProcedure> x_id
        ; stat* := <procedure-action-statements; add-procedure-done(|x_id, x_o, srt)> def
        ; SimpleSort(s) := srt
        ; x_view := <decapitalize-string> s
        ; if <IsDoProcedure> x_id then
              stat2* := <add-view-page-goto(|x_view, Var(x_o))> stat*
            ; elem*  := elem*|[ init { stat2* } ]|
          else
              stat2*      := <add-view-page-return(|x_view, Var(x_o))> stat*
            ; viewelem*   := <procedure-view-elems> def
            ; actionelem* := webdsl*|[ action doAction() { stat2* } ]|
            ; elem*       := [viewelem*, actionelem*]
          end
        ; e2 := <procedure-get-access-exp> procelem*
        ; <emit-webdsl-section> |[
            access control rules {
              rules page x_id(x_o : srt) {
                e2
                rules action doAction() {
                  e2
                }
              }
            }
          ]|
        ; x_ent := s
        ; <emit-webdsl-code> def|[
            extend entity x_ent {
              x_id <> ProcedureStatus ()
            }
          ]|
        // Generate functions
        ; x_enabled := <concat-strings> [x_id, "Enabled"]
        ; stat3* := <filter(procedure-enabled-statements); concat> procelem*
        ; <emit-webdsl-code> def|[
            globals {
              function x_enabled(x_o : srt) {
                x_o.x_id.enabled := true;
                stat3*
              }
            }
          ]|
        ; x_processed := <concat-strings> [x_id, "Processed"]
        ; stat4* := <filter(procedure-processed-statements); concat> procelem*
        ; <emit-webdsl-code> def|[
            globals {
              function x_processed(x_o : srt) {
                stat4*
              }
            }
          ]|
        ; x_done := <concat-strings> [x_id, "Done"]
        ; stat5* := <filter(procedure-done-statements); concat> procelem*
        ; <emit-webdsl-code> def|[
            globals {
              function x_done(x_o : srt) {
                x_o.x_id.enabled := false;
                stat5*
              }
            }
          ]|
        ; x_do := <concat-strings> [x_id, "Do"]
        ; stat6* := <filter(not({e: ?webdsl|[ return e; ]| <+ ?webdsl|[ goto e; ]|}))> stat2*
        ; <emit-webdsl-code> def|[
            globals {
              function x_do(x_o : srt) {
                stat6*
              }
            }
          ]|
        ; x_disabled := <concat-strings> [x_id, "Disabled"]
        ; stat7* := <filter(procedure-disabled-statements); concat> procelem*
        ; <emit-webdsl-code> def|[
            globals {
              function x_disabled(x_o : srt) {
                x_o.x_id.enabled := false;
                stat7*
              }
            }
          ]|

  add-procedure-done(|x_op, x_obj, srt) :
    stat* -> webdsl|[
          stat*
          x_done(x_obj);
        ]|
    where x_done := <concat-strings> [x_op, "Done"]

  procedure-to-check-performed(|arg) :
    nm -> webdsl|[ x_check(e); ]|
    where x_check := <concat-strings> ["check", <capitalize-string> nm, "Performed"]
        ; e := Var(arg)



