module org/webdsl/dsl/modules/procedures/procedure-page

imports
  libstrategolib
  libwebdsl-front

rules // Translate to pages and stuff

  procedures-to-pages :
    def@def|[ procedure x_id (x_o : srt) { procelem2* } ]| -> <rename-all> def|[ define page x_id(x_o : srt) { elem* } ]|
    where <IsProcedure> x_id
    with  SimpleSort(s) := srt
        ; x_view := <decapitalize-string> s
        ; if "startWf" := x_id then
            info(|["Procelems ", Var(x_o), ": ", procelem2*])
          end
        ; procelem* := <alltd(translate-object-param(|x_o))> procelem2*
        ; if "startWf" := x_id then
            info(|["Procelem after ", Var(x_o), ": ", procelem*])
          end
        ; stat* := <filter(procedure-do-statements); concat; add-procedure-done> procelem*
        ; if <IsDoProcedure> x_id then
              stat2* := <add-view-page-goto(|x_view, Var(x_o))> stat*
            ; elem*  := elem*|[ init { stat2* } ]|
          else
              stat2*      := <add-view-page-return(|x_view, Var(x_o))> stat*
            ; viewelem*   := <procedure-view-elems> def
            ; elem2       := webdsl*|[ action doAction() { stat2* } ]|
            ; elem*       := <try(oncetd({elem, e: \ elem@elem|[ action(e, doAction()){} ]| -> elem|[ dummy { elem elem2 } ]| \}))> viewelem*
          end
        ; e2 := <procedure-get-access-exp> procelem*
        ; <emit-webdsl-section> |[
            access control rules {
              rules page x_id(x_o : srt) {
                e2
                rules action doAction() {
                  e2
                }
              }
            }
          ]|
          
        // create procedure status including viewPage
        ; x_Status := <concat-strings> [<capitalize-string> x_id, "ProcedureStatus"]
        ; x_status := <decapitalize-string> x_Status
        ; x_ent := s
        ; stat3* := <filter(procedure-enabled-statements); concat> procelem*
        ; stat4* := <filter(procedure-processed-statements); concat> procelem*
        ; stat5* := <filter(procedure-done-statements); concat> procelem*
        ; stat6* := <filter(not({e: ?webdsl|[ return e; ]| <+ ?webdsl|[ goto e; ]|}))> stat2*
        ; stat7* := <filter(procedure-disabled-statements); concat> procelem*
        ; <emit-webdsl-code> def|[
            entity x_Status : ProcedureStatus {
              x_o -> srt
              function enabled() {
                this.isEnabled := true;
                stat3*
              }
              function processed() {
                stat4*
              }
              function done() {
                this.isEnabled := false;
                stat5*
              }
              function do() {
                stat6*
              }
              function disabled() {
                this.isEnabled := false;
                stat7*
              }
            }
          ]|
        ; <emit-webdsl-code> def|[
            define page x_status(s : x_Status) {
              derive viewPage from s
            }
          ]|
        ; <emit-webdsl-code> def|[
            extend entity x_ent {
              x_id <> x_Status ()
            }
          ]|

  translate-object-param(|x_o) =
    ?ProcedureWho(_) <+ ?ProcedureWhen(_) <+ ?ProcedureView(_)

  translate-object-param(|x_o) :
    Var(x_o) -> FieldAccess(Var("this"), <strip-annos> x_o)

  translate-object-param(|x_o) :
    Qualified(x_o, f) -> Qualified(Qualified("this", <strip-annos> x_o), <strip-annos> f)

  add-procedure-done :
    stat* -> webdsl|[
          stat*
          this.done();
        ]|

  /*
  procedure-to-check-performed(|arg) :
    nm -> webdsl|[ x_check(e); ]|
    where x_check := <concat-strings> ["check", <capitalize-string> nm, "Performed"]
        ; e := Var(arg)
  */

