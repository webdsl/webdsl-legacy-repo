module org/webdsl/dsl/modules/procedures/procedure-page

imports
  libstrategolib
  libwebdsl-front

rules // Translate to pages and stuff

  Desugar :
    def@def|[ procedure x_id (x_o : srt) { procelem* } ]| -> def|[ define page x_id(x_o : srt) { elem* } ]|
    where <IsProcedure> x_id
        ; stat* := <procedure-action-statements; add-procedure-performed(|x_id, x_o, srt)> def
        ; SimpleSort(s) := srt
        ; x_view := <decapitalize-string> s
        ; if <IsDoProcedure> x_id then
              stat2* := <add-view-page-goto(|x_view, Var(x_o))> stat*
            ; elem*  := elem*|[ init { stat2* } ]|
          else
              stat2*      := <add-view-page-return(|x_view, Var(x_o))> stat*
            ; viewelem*   := <procedure-view-elems> def
            ; actionelem* := webdsl*|[ action doAction() { stat2* } ]|
            ; elem*       := [viewelem*, actionelem*]
          end
        ; e2 := <procedure-get-access-exp> procelem*
        ; <emit-webdsl-section> |[
            access control rules {
              rules page x_id(x_o : srt) {
                e2
                rules action doAction() {
                  e2
                }
              }
            }
          ]|
        ; x_ent := s
        ; <emit-webdsl-code> def|[
            extend entity x_ent {
              x_id <> ProcedureStatus
            }
          ]|
        // Generate functions
        ; x_enabled := <concat-strings> [x_id, "Enabled"]
        ; stat3* := <filter(procedure-enabled-statements); concat> procelem*
        ; <emit-webdsl-code> def|[
            globals {
              function x_enabled(x_o : srt) {
                x_o.x_id.enabled := true;
                stat3*
              }
            }
          ]|

  add-procedure-performed(|x_op, x_obj, srt) :
    stat*
    //; e := exp|[ status ]|
    //; qid1 := Qualified(Qualified(arg, op), "performed")
    //; qid2 := Qualified(Qualified(arg, op), "date")
//    ; stat* := <map(procedure-to-check-performed(|arg))> <bagof-Workflows;make-set> srt
//    ; <concat> [
     ->
        webdsl|[
          stat*
          x_perf(x_obj, srt);
        ]|
    where x_perf := <concat-strings> [x_op, "Performed"]
//        , stat*]

  procedure-to-check-performed(|arg) :
    nm -> webdsl|[ x_check(e); ]|
    where x_check := <concat-strings> ["check", <capitalize-string> nm, "Performed"]
        ; e := Var(arg)



