module org/webdsl/dsl/modules/procedures/procedure-page

imports
  libstrategolib
  libwebdsl-front

rules // Translate to pages and stuff

  procedures-to-pages :
    def@def|[ procedure x_id (x_o : srt) { procelem* } ]| -> <rename-all> def|[ define page x_id(x_o : srt) { elem* } ]|
    where <IsProcedure> x_id
    with  stat* := <procedure-do-statements; add-procedure-done(|x_id, x_o, srt)> def
        ; SimpleSort(s) := srt
        ; x_view := <decapitalize-string> s
        ; if <IsDoProcedure> x_id then
              stat2* := <add-view-page-goto(|x_view, Var(x_o))> stat*
            ; elem*  := elem*|[ init { stat2* } ]|
          else
              stat2*      := <add-view-page-return(|x_view, Var(x_o))> stat*
            ; viewelem*   := <procedure-view-elems> def
            ; actionelem* := webdsl*|[ action doAction() { stat2* } ]|
            ; elem*       := [viewelem*, actionelem*]
          end
        ; e2 := <procedure-get-access-exp> procelem*
        ; <emit-webdsl-section> |[
            access control rules {
              rules page x_id(x_o : srt) {
                e2
                rules action doAction() {
                  e2
                }
              }
            }
          ]|
          
        // create procedure status including viewPage
        ; x_Status := <concat-strings> [<capitalize-string> x_id, "ProcedureStatus"]
        ; x_status := <decapitalize-string> x_Status
        ; x_ent := s
        ; <emit-webdsl-code> def|[
            entity x_Status : ProcedureStatus {
              function enabled() { }
              function disabled() { }
              function do() { }
              function done() { }
              function processed() { }
            }
          ]|
        ; <emit-webdsl-code> def|[
            define page x_status(s : x_Status) {
              derive viewPage from s
            }
          ]|
        // also establish link from entity to status
        ; <emit-webdsl-code> def|[
            extend entity x_ent {
              x_id <> x_Status ()
            }
          ]|
          
        // Generate functions
        ; stat3* := <filter(procedure-enabled-statements); concat> procelem*
        ; <emit-webdsl-code> def|[
            extend function enabled(x_o : srt) {
              x_o.enabled := true;
              stat3*
            }
          ]|
        ; stat4* := <filter(procedure-processed-statements); concat> procelem*
        ; <emit-webdsl-code> def|[
            extend function processed(x_o : srt) {
              stat4*
            }
          ]|
        ; stat5* := <filter(procedure-done-statements); concat> procelem*
        ; <emit-webdsl-code> def|[
            extend function done(x_o : srt) {
              x_o.enabled := false;
              stat5*
            }
          ]|
        ; <debug(!"stat5*: ")> stat5*
        ; stat6* := <filter(not({e: ?webdsl|[ return e; ]| <+ ?webdsl|[ goto e; ]|}))> stat2*
        ; <emit-webdsl-code> def|[
            extend function do(x_o : srt) {
              stat6*
            }
          ]|
        ; stat7* := <filter(procedure-disabled-statements); concat> procelem*
        ; <emit-webdsl-code> def|[
            extend function disabled(x_o : srt) {
              x_o.enabled := false;
              stat7*
            }
          ]|

  add-procedure-done(|x_op, x_obj, srt) :
    stat* -> webdsl|[
          stat*
          x_obj.x_op.done();
        ]|
    where info(|["x_op: ", x_op, "; x_obj: ", x_obj])

  procedure-to-check-performed(|arg) :
    nm -> webdsl|[ x_check(e); ]|
    where x_check := <concat-strings> ["check", <capitalize-string> nm, "Performed"]
        ; e := Var(arg)



