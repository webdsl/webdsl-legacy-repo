module org/webdsl/dsl/modules/operations/typechecker

imports
  libstrategolib
  libwebdsl-front

rules // Declaring

  declare-definition =
    declare-operation
    <+ declare-workflow

  declare-operation =
    ?def|[ operation x_id (x_o : srt) { opelem* }]|
    ; who-exp := <filter(operation-who-exp); Hd> opelem*
    ; when-exp := <filter(operation-when-exp); Hd> opelem*
    ; SimpleSort(s) := srt
    ; hasopfun := <concat-strings> [s, "HasOperations"]
    ; rules ( IsOperation            : x_id 
              OperationWho           : x_id -> who-exp
              OperationWhen          : x_id -> when-exp
              OperationArg           : x_id -> x_o
              Operations             :+ srt -> x_id 
              CheckGeneratedGlobalFunctionSignature : (None(), hasopfun, [srt]) -> SimpleSort("Bool")
              OperationEntities :+= srt )
    ; viewelems* := <filter(\ opelem|[ view { elem* } ]| -> elem* \);concat> opelem*
    ; if [] := viewelems* then
        rules ( IsDoOperation : x_id )
      end

  declare-workflow =
    ?def|[ workflow x_id (x_o : srt) { opelem* }]|
    ; done-exp := <filter(workflow-done-exp); Hd> opelem*
    ; who-exp := <filter(operation-who-exp); Hd> opelem*
    ; when-exp := <filter(operation-when-exp); Hd> opelem*
    ; rules ( IsWorkflow       : x_id 
              WorkflowDone     : x_id -> done-exp
              WorkflowWho      : x_id -> who-exp
              WorkflowWhen     : x_id -> when-exp
              WorkflowArg      : x_id -> x_o
              Workflows        :+ srt -> x_id 
              WorkflowEntities :+= srt )

rules // Typechecking

  typecheck-section :
    Operations(nm, defs) -> Operations(nm, defs2)
    where defs2 := <typecheck-definitions> defs
    
  typecheck-specific-definition :
    Operation(nm, Arg(o, srt), elems) -> Operation(nm, Arg(o, srt), elems2)
    where {| TypeOf
           : rules ( TypeOf : "status" -> srt ) 
           ; rules ( TypeOf : o -> srt )
           ; <map(declare-typeof-arg)> <collect(\ VarDecl(x, srt) -> Arg(x, srt)\ <+ \ VarDeclInit(x, srt, e) -> Arg(x, srt) \)> elems // @TODO clean this shit up
           ; elems2 := <map(typecheck-operation-element <+ typecheck-error(|["Could not typecheck: ", <id>]))> elems
           |}

  typecheck-specific-definition :
    Workflow(nm, Arg(o, srt), elems) -> Workflow(nm, Arg(o, srt), elems2)
    where {| TypeOf
           : rules ( TypeOf : "status" -> srt ) 
           ; rules ( TypeOf : o -> srt )
           ; elems2 := <map(typecheck-operation-element <+ typecheck-error(|["Could not typecheck: ", <id>]))> elems
           |}
 
  typecheck-operation-element :
    OperationWho(e) -> OperationWho(e2)
    where e2 := <typecheck-expression> e
        ; if not(SimpleSort("Bool") := <type-of> e2) then
            typecheck-error(|["Expression in who should return a boolean: ", e2])
          end

  typecheck-operation-element :
    OperationWhen(e) -> OperationWhen(e2)
    where e2 := <typecheck-expression> e
        ; if not(SimpleSort("Bool") := <type-of> e2) then
            typecheck-error(|["Expression in when should return a boolean: ", e2])
          end

  typecheck-operation-element :
    WorkflowDone(e) -> WorkflowDone(e2)
    where e2 := <typecheck-expression> e
        ; if not(SimpleSort("Bool") := <type-of> e2) then
            typecheck-error(|["Expression in done should return a boolean: ", e2])
          end

  typecheck-operation-element :
    OperationDo(b) -> OperationDo(b2)
    where b2 := <typecheck-block> b

  typecheck-operation-element :
    WorkflowInit(args, b) -> WorkflowInit(args, b2)
    where {| TypeOf
           : <map(declare-typeof-arg)> args
           ; b2 := <typecheck-block> b
           |}

  typecheck-operation-element :
    OperationView(el*) -> OperationView(el2*)
    where el2* := <typecheck-page-elements> el*

  check-builtin-signature :
    (SimpleSort("WorkflowStatus"), "start", args) -> SimpleSort("Bool")
