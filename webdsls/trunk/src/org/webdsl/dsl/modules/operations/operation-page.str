module org/webdsl/dsl/modules/operations/operation-page

imports
  libstrategolib
  libwebdsl-front

rules // Translate to pages and stuff

  Desugar :
    def|[ operation x_id (x_o : srt) { opelem* }]| -> def|[ define page x_id(x_o : srt) { elem* } ]|
    where <IsOperation> x_id
        ; stat* := <filter(operation-get-action-statements);concat;add-operation-performed(|x_id, x_o, srt)> opelem*
        ; SimpleSort(s) := srt
        ; x_view := <decapitalize-string> s
        ; if <IsDoOperation> x_id then
            stat2* := <add-view-page-goto(|x_view, Var(x_o))> stat*
            ; elem* := elem*|[
                init {
                  stat2*
                }
               ]|
          else
            stat2* := <add-view-page-return(|x_view, Var(x_o))> stat*
            ; viewelem* := <filter(operation-get-view-elements);concat> opelem*
            ; actionelem* := webdsl*|[
                             action doAction() {
                               stat2*
                             }
                           ]|
            ; elem* := [viewelem*, actionelem*]
          end
        ; e2 := <operation-get-access-exp> opelem*
        ; <emit-webdsl-code> def|[
            access control rules {
              rules page x_id(x_o : srt) {
                e2
                rules action doAction() {
                  e2
                }
              }
            }
          ]|
        ; x_ent := s
        ; <emit-webdsl-code> def|[
            extend entity x_ent {
              x_id <> OperationStatus
            }
          ]|

  Desugar :
    def|[ workflow x_id (x_o : srt) { opelem* }]| -> def|[ define page x_id(x_o : srt) { elem* } ]|
    where <IsWorkflow> x_id
        // Generate newWorkflow function
        ; x_new := <concat-strings> ["new", <capitalize-string> x_id]
        ; x_check := <concat-strings> ["check", <capitalize-string> x_id, "Performed"]
        ; (farg*, stat*) := <filter(workflow-get-init-fargs-statements);Hd> opelem*
        ; qid1 := Qualified(Qualified(x_o, x_id), "done")
        ; qid2 := Qualified(Qualified(x_o, x_id), "date")
        ; e := <WorkflowDone> x_id
        ; <emit-webdsl-code> def|[
            globals {
              function x_new(x_o : srt, farg*) : srt {
                stat*
                x_o.x_id.started := true;
                return x_o;
              }

              function x_check(x_o : srt) : Bool {
                if(e) {
                  qid1 := true;
                  qid2 := now();
                  return true;
                } else {
                  return false;
                }
              }
            }
          ]|
        ; SimpleSort(s) := srt
        ; x_ent := s
        ; <emit-webdsl-code> def|[
            extend entity x_ent {
              x_id <> WorkflowStatus
            }
          ]|
        ; elem* := []


  add-operation-performed(|op, arg, srt) =
    ?stats
    //; e := exp|[ status ]|
    ; qid1 := Qualified(Qualified(arg, op), "performed")
    ; qid2 := Qualified(Qualified(arg, op), "date")
    ; stat* := <map(workflow-to-check-performed(|arg))> <bagof-Workflows;make-set> srt
    ; <concat> [
        webdsl|[
          qid1 := true;
          qid2 := now();
          stat*
        ]|
        , stats]

  workflow-to-check-performed(|arg) :
    nm -> webdsl|[ x_check(e); ]|
    where x_check := <concat-strings> ["check", <capitalize-string> nm, "Performed"]
        ; e := Var(arg)

  operation-get-view-elements :
    opelem|[ view { elem* } ]| -> elem*

  operation-get-action-statements :
    opelem|[ do { stat* } ]| -> stat*

  workflow-get-init-fargs-statements :
    opelem|[ init(farg*) { stat* } ]| -> (farg*, stat*)

  operation-get-access-exp =
    filter(operation-who-when-exp)
    ; foldr(!True(), exp-and)

  operation-who-exp :
   opelem|[ who { e } ]| -> e 

  operation-when-exp :
   opelem|[ when { e } ]| -> e 

  workflow-done-exp :
   opelem|[ done { e } ]| -> e 

  operation-who-when-exp =
    operation-who-exp <+ operation-when-exp

