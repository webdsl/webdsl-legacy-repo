module org/webdsl/dsl/modules/operations/normalize

imports
  libstrategolib
  libwebdsl-front

rules // Normalization

  Desugar :
    def|[ operation x_id (x_o : srt) { opelem* }]| -> def|[ operation x_id(x_o : srt) { opelem2* } ]|
    where [] := <filter(?opelem|[ who { e } ]|)> opelem*
        ; opelem2* := [opelem|[ who { true } ]| | opelem*]

  Desugar :
    def|[ operation x_id (x_o : srt) { opelem* }]| -> def|[ operation x_id(x_o : srt) { opelem2* } ]|
    where [] := <filter(?opelem|[ when { e } ]|)> opelem*
        ; opelem2* := [opelem|[ when { true } ]| | opelem*]

  Desugar :
    def|[ operation x_id (x_o : srt) { opelem* }]| -> def|[ operation x_id(x_o : srt) { opelem2* } ]|
    where [] := <filter(?opelem|[ do { stat* } ]|)> opelem*
        ; opelem2* := [opelem|[ do {} ]| | opelem*]

  Desugar :
    def|[ workflow x_id (x_o : srt) { opelem* }]| -> def|[ workflow x_id(x_o : srt) { opelem2* } ]|
    where [] := <filter(?opelem|[ who { e } ]|)> opelem*
        ; opelem2* := [opelem|[ who { true } ]| | opelem*]

  Desugar :
    def|[ workflow x_id (x_o : srt) { opelem* }]| -> def|[ workflow x_id(x_o : srt) { opelem2* } ]|
    where [] := <filter(?opelem|[ when { e } ]|)> opelem*
        ; opelem2* := [opelem|[ when { true } ]| | opelem*]

  Desugar :
    def|[ workflow x_id (x_o : srt) { opelem* }]| -> def|[ workflow x_id(x_o : srt) { opelem2* } ]|
    where [] := <filter(?opelem|[ init { stat* } ]| <+ ?opelem|[ init(farg*) { stat* } ]|)> opelem*
        ; opelem2* := [opelem|[ init {} ]| | opelem*]

  Desugar :
    opelem|[ init { stat* } ]| -> opelem|[ init() { stat* } ]|

rules // Further desugarings

  Desugar :
    def|[ operation x_id (x_o : srt) { opelem* }]| -> def|[ operation x_id(x_o : srt) { opelem2* } ]|
    where {| OpArg
           : rules ( OpArg := x_o )
           ; opelem2* := <manytd(desugar-status)> opelem*
           |}

  desugar-status :
    Var("status") -> Var(oparg)
    where oparg := <OpArg>

  desugar-status :
    Qualified("status", f) -> Qualified(oparg, f)
    where oparg := <OpArg>

  Desugar :
    def|[ status x_id { prop* fun* } ]| -> def|[ extend entity x_id { prop* fun* } ]|

  Desugar : // make the doAction() callable as do()
    exp|[ do() ]| -> exp|[ doAction() ]|

  Desugar :
    exp|[ e.start(e*) ]| -> exp|[ x(e1, e*) ]|
    where SimpleSort("WorkflowStatus") := <type-of> e
        ; FieldAccess(e1, wf) := e
        ; x := <concat-strings> ["new", <capitalize-string> wf]
