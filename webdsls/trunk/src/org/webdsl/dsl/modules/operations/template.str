module org/webdsl/dsl/modules/operations/template

imports
  libstrategolib
  libwebdsl-front

rules // Generate templates and status and todo pages

  operation-generate-templates = 
    <map(operation-emit-entity-template)> <bagof-OperationEntities; make-set>

  operation-emit-entity-template =
    ?SimpleSort(ent)
    ; operations := <bagof-Operations; make-set> SimpleSort(ent)
    ; x1 := <concat-strings> [<decapitalize-string> ent, "Operations"]
    ; x2 := <concat-strings> [<decapitalize-string> ent, "OperationsList"]
    ; x_arg  := <decapitalize-string> ent
    ; srt    := SimpleSort(ent)
    ; elem* := <map(operation-to-listitem(|x_arg))> operations
    ; <emit-webdsl-code> def|[
        define x2(x_arg : srt) {
          list {
            elem*
          }
        }
      ]|
    ; if not(<TemplateDefinition> x1) then // don't overwrite an existing definition
        <emit-webdsl-code> def|[
          define x1(x_arg : srt) {
            x2(x_arg)
          }
        ]|
      end

  operation-to-listitem(|x_arg) :
    x_op -> webdsl|[
            if(e) {
              listitem {
                navigate(x_op(x_arg)) { text(e2) str }
              }
            }
          ]|
    where if not(<IsDoOperation> x_op) then
            str := ""
          else
            str := " (X)"
          end
        ; e := <operation-rename-var(|<OperationArg> x_op, x_arg)> And(<OperationWho> x_op, <OperationWhen> x_op)
        ; e2 := String(<make-human-readable> x_op)
