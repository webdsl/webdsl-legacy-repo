module org/webdsl/dsl/modules/uicomponents/menu-style

imports
  libstrategolib

imports
  libwebdsl-front

rules

  default-style =
    ?call |[ menubar(e) { elem* } ]|
    ; where (
      stsel00 := <ThisSelector <+ ![]> ;
      {| ThisSelector
       : stsel0 := stsel |[ .menubar ]|
       
       ; rules ( ThisSelector := stsel0 )
       
       ; <alltd(default-style)> elem*
       ; not( <elem> ("menubar", <bagof-ProcessedElements> ) )
       
       ; if <?[]> stsel00 then
           ststat01 := ststat |[ border-color := Color.black; ]| ;
           ststat02 := ststat |[ border-width := 1px; ]|
         else
           ststat01 := ststat |[ border-color := (stsel00).border-color; ]| ;
           ststat02 := ststat |[ border-width := (stsel00).border-width; ]|
         end
       
       ; ststat0* := <map(set-style-property); flatten-list>
                     [ ststat |[ background-color := Color.white; ]|,
                       ststat |[ width := 100%; ]|, ststat01, ststat02,
                       ststat |[ border-style := BorderStyle.none; ]| ]
       ; <declare-style-definition; emit-style-definition> stdef |[ stsel0 { ststat0* } ]|
       
       ; stsel1 := <add-simple-selector> (stsel0, |[ >> li ]| )
       ; stsel2 := <add-simple-selector> (stsel1, |[ >> ul ]| )
       ; rules ( ThisSelector := stsel2 )
       ; ststat1* := <flatten-list>
                     [ <set-style-property> ststat |[ border-color := (stsel0).border-color; ]|,
                       <set-style-property> ststat |[ border-width := (stsel0).border-width; ]|,
                       <set-style-property> ststat |[ border-style := BorderStyle.solid; ]| ]
       ; <declare-style-definition; emit-style-definition> stdef |[ stsel2 { ststat1* } ]|
       
       ; rules ( ProcessedElements :+= "menubar" )
       |}
      )

  default-style =
    ?call |[ menu() { elem* } ]|
    ; where (
      {| ThisSelector
       : stsel00 := <ThisSelector>
       ; stsel0 := <add-simple-selector> (stsel00, |[ > menu() ]| )
       ; rules ( ThisSelector := stsel0 )
       
       ; <alltd(default-style)> elem*
       ; not( <elem> ("menu", <bagof-ProcessedElements> ) )
       
       ; ststat0* := <map(set-style-property); flatten-list>
                     [ ststat |[ border-color := (stsel00).border-color; ]|,
                       ststat |[ border-width := (stsel00).border-width; ]|,
                       ststat |[ border-style := BorderStyle.none; ]| ]
       ; <declare-style-definition; emit-style-definition> stdef |[ stsel0 { ststat0* } ]|

       ; rules ( ProcessedElements :+= "menu" )
       |}
      )

  default-style =
    ?call |[ menuheader() { elem* } ]|
    ; where (
      {| ThisSelector
       : stsel00 := <ThisSelector>
       ; stsel0 := <add-simple-selector> (stsel00, |[ > menuheader() ]| )
       ; rules ( ThisSelector := stsel0 )

       ; <alltd(default-style)> elem*
       ; not( <elem> ("menuheader", <bagof-ProcessedElements> ) )

       ; ststat0* := <flatten-list>
                     [ <set-style-property> ststat |[ border-right-color := (stsel00).border-color; ]|,
                       <set-style-property> ststat |[ border-right-width := (stsel00).border-width; ]|,
                       <set-style-property> ststat |[ border-right-style := BorderStyle.solid; ]| ]
       ; <declare-style-definition; emit-style-definition> stdef |[ stsel0 { ststat0* } ]|
       
       ; stsel2 := <add-simple-selector> (stsel00, |[ > menuheader().first-child ]| )
       ; rules ( ThisSelector := stsel2 )
       ; ststat1* := <flatten-list>
                     [ <set-style-property> ststat |[ border-left-color := (stsel0).border-right-color; ]|,
                       <set-style-property> ststat |[ border-left-width := (stsel0).border-right-width; ]|,
                       <set-style-property> ststat |[ border-left-style := (stsel0).border-right-style; ]| ]
       ; <declare-style-definition; emit-style-definition> stdef |[ stsel2 { ststat1* } ]|
       
       ; rules ( ProcessedElements :+= "menuheader" )
       |}
      )

  default-style =
    ?call |[ menuspacer() { elem* } ]|
    ; where (
      {| ThisSelector
       : stsel0 := <add-simple-selector> (<ThisSelector>, |[ >> .menuspacer ]| )
       ; stsel1 := <add-simple-selector> (<ThisSelector>, |[ > menuheader() ]| )
       ; rules ( ThisSelector := stsel0 )

       ; <alltd(default-style)> elem*       
       ; not( <elem> ("menuspacer", <bagof-ProcessedElements> ) )
       
       ; ststat0* := <flatten-list>
                     [ <set-style-property> ststat |[ border-top-style := (stsel1).border-right-style; ]|,
                       <set-style-property> ststat |[ border-top-width := (stsel1).border-right-width; ]|,
                       <set-style-property> ststat |[ border-top-color := (stsel1).border-right-color; ]| ]
       ; <declare-style-definition; emit-style-definition> stdef |[ stsel0 { ststat0* } ]|

       ; rules ( ProcessedElements :+= "menuspacer" )
       |}
      )

