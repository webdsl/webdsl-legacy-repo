module sdf-interface

imports
  libstrategolib
  libwebdsl-front

strategies // typechecking

  is-generic-sort = ?GenericSort("SDFInput",_)

  type-exists = ?GenericSort("SDFInput",_)
      
  language-of :
    e -> language
    where GenericSort("SDFInput", [language]) := <type-of> e

  check-builtin-signature :
    (GenericSort("SDFInput", [_]), "parse", []) -> SimpleSort("ATerm")
  
  check-builtin-signature :
    (GenericSort("SDFInput", [_]), "isValid", []) -> SimpleSort("Bool")
  
strategies

  DeriveInput :
    elem |[ input(e)[passign*] {} ]| -> elem |[ inputSDF(e)[passign*] {} ]|
    where GenericSort("SDFInput", _) := <type-of> e

strategies // Basic type definitions

  type-compatible =
    ?(GenericSort("SDFInput", _), SimpleSort("String"))

  type-compatible =
    ?(SimpleSort("String"), GenericSort("SDFInput", _))

  type-compatible =
    ?(SimpleSort("Object"), GenericSort("SDFInput", _)) // SDFInput is subclass of Object for typechecker 
