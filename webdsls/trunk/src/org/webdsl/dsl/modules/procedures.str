module org/webdsl/dsl/modules/procedures

imports
  libstrategolib
  libwebdsl-front

imports
  org/webdsl/dsl/modules/procedures/-
  org/webdsl/dsl/modules/procedures/generate/-
  
rules

  desugar = procedures-to-pages
  desugar = procedure-extend-procelems
  desugar = derive-procedure-page

rules // Generation of templates and pages 

  // is only done once, registered by a dynamic rule ProcedureGeneratedBase
  desugar = 
    not(ProceduresProcessed) // @TODO: only do this if procedures are used
    ; try(procedure-generate-base) // try may be unnecessary
    ; rules ( ProceduresProcessed := True() )

  
  procedure-generate-base = 
    where(procedure-generate-templates)
    ; where(procedure-generate-task-pages)
    ; where(procedure-generate-status-pages)
    ; where(procedure-emit-status-entity)
    ; where(procedure-emit-status-view-page)

  procedure-emit-status-entity =
    <emit-webdsl-code> def|[
      entity ProcedureStatus {
        name      :: String := "Procedure status"
        enabled   :: Bool ()
        date      :: DateTime ()
      }
    ]|

  procedure-emit-status-view-page =
    <emit-webdsl-code> def|[
      define page procedureStatus(s : ProcedureStatus) {
        derive viewPage from s
      }
    ]|
