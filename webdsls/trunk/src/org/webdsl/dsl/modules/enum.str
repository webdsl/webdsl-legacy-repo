module org/webdsl/dsl/modules/enum

imports
  libstrategolib
  libwebdsl-front

strategies

  declare = declare-enum
  desugar = desugar-enum

rules

  enum-generate-vardecl(|x) :
    EnumValue(y, str) -> |[ var y : srt := x { name := str }; ]|
    where srt := SimpleSort(x)

  desugar-enum :
    Enum(x, value*) -> <rename-all> def|[
      entity x {
        name :: String
      }
    ]|
    with <emit-webdsl-code> GlobalsDefinition(<map(enum-generate-vardecl(|x))> value*)

  rename :
    Enum(x, value*) -> Enum(x, value*)
    with declare-enum

  declare-enum :
    Enum(x, value*) -> <id>
    with <declare> def|[
            entity x {
              name :: String
            }
          ]|
          ; <map(declare-enum-value(|SimpleSort(x)))> value*

  declare-enum-value(|srt) :
    EnumValue(y, str) -> <id>
    with rules ( TypeOf : y -> srt )
