module org/webdsl/dsl/modules/enum

imports
  libstrategolib
  libwebdsl-front
  org/webdsl/dsl/typechecker/-
  org/webdsl/dsl/transformation/-

strategies

  declare = declare-enum

rules

  enum-generate-vardecl(|x) :
    EnumValue(y, e) -> GlobalVarDeclInit(y, srt, exp|[ x { name := e } ]|)
    where srt := SimpleSort(x)

  normalize-declare: Enum(x, value*) -> def |[
      entity x {
        cache
        name :: String
      }
   ]|

  declare-enum :
    def@Enum(x, value*) -> <id>
    with  entdef := <with-origin(normalize-declare)> def
        ; rules(EnumDecl :+ x -> def)
        ; valdef* := <map(enum-generate-vardecl(|x))> value*
        ; nameanno := TopLevelName(<concat-strings> ["enum_", x])
        ; <emit-webdsl-code> GlobalsDefinition([entdef, valdef*]){nameanno}
        