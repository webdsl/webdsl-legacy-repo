module stratego-interface

imports
  libstrategolib
  libwebdsl-front
  aterm-interface

strategies

  call-to-java-servlet :
    exp  |[ stratego(e) ]| ->
    java |[ org.webdsl.tools.strategoxt.StrategoProgram.get(e2, utils.AppInfo.class) ]|
    with e2 := <expression-to-java-servlet> e
 
  call-to-java :
    exp  |[ stratego(e) ]| ->
    java |[ org.webdsl.tools.strategoxt.StrategoProgram.get(e2, getClass()) ]|
    with e2 := <expression-to-java> e
  
  call-to-java-any(rec) :
    exp  |[ e.x(y) ]| ->
    java |[ e2.invoke("~x", y2) ]|
    where SimpleSort("Stratego") := <type-of> e
    with e2 := <rec> e
       ; y2 := <rec> e
  
  check-builtin-signature :
    (None(), "stratego", [SimpleSort("String")]) -> SimpleSort("Stratego")
  
  check-builtin-signature :
    (SimpleSort("Stratego"), _, [SimpleSort("ATerm")]) -> SimpleSort("ATerm")
  
  builtin-java-type :
    SimpleSort("Stratego") -> type |[ org.webdsl.tools.strategoxt.StrategoProgram ]|

  builtin-java-type-default-value :
    SimpleSort("Stratego") -> java |[ null ]|
  
  sort-to-java-type-string :
    SimpleSort("Stratego") -> "org.webdsl.tools.strategoxt.StrategoProgram"
  
  