module rename-template

imports 
  libstrategolib
  
imports 
  libwebdsl-front

rules

  rename-templates =
    where (remove-declared-templates)
    ; rename-all-templates
  
  rename-all-templates =
    alltd(rename-template)

  remove-declared-templates =
    alltd(remove-declared-template)
  
  remove-declared-template =
    ?def |[ define template x(farg*) { elem* } ]|
    ; repeat(<once-TemplateArguments> x)
    ; repeat(<once-TopLevelTemplateDefinitions> x)

  
  rename-template :
    def0@def |[ define page x(farg*) { elem* } ]| -> <declare-page-definition> def1
    where {| RenameVar
           : def1@def |[ define page x(farg1*) { elem1* } ]| := <rename-bound-variables> def0
           ; <alltd(rename-template)> elem1*
           |}

  rename-template :
    def0@def |[ define template x(farg*) { elem* } ]| -> <declare-template-definition> def1
    where {| InTemplate
           : rules ( InTemplate := x )
           ; def1 := <rename-bound-variables> def0
           |}

  rename-template :
    def0@def |[ define local x(farg*) { elem* } ]| -> <declare-local-template-definition> def0

