/**

 Desugaring rules for WebDSL
 
*/

module org/webdsl/dsl/transformation/desugar

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  
imports 
  org/webdsl/dsl/syntax/WebDSL

strategies

  desugar = 
    topdown-l(repeat(Desugar))

rules

  Desugar :
    TemplateCall("navigate", [ThisCall("url",[e])], []) ->
    TemplateCall("navigate", [e, ThisCall("url",[e])], [])

  Desugar :
    String(x) -> String(y)
    where y := <un-double-quote> x
    
 Desugar :
    Select(x, s, label1, e) -> Select(x, s, label2, e)
    where label2 := <un-double-quote> label1
    
  Desugar : 
    PropertyNoAnno(x, k, s) -> Property(x, k, s, [])
    
  Desugar :
    TemplateCallNoArgs(x) -> TemplateCall(x, [], [])
    
  Desugar :
    TemplateCallNoBody(x, args) -> TemplateCall(x, args, [])
    
  Desugar :
    TemplateCallBody(x, body) -> TemplateCall(x, [], body)
    
  Desugar :
    Text(x) -> Text(y)
    where y := <un-double-quote> x
    
  Desugar :
    [TemplateCall("text", [arg1, arg2 | args], []) | elems]->
    [TemplateCall("text", [arg1], [])
    ,TemplateCall("text", [arg2 | args], []) | elems]
    
    
    
   