/**
 * Transformation of a WebDSL application to a collection of XML and Java files
 *
 * @author Eelco Visser
 */

module org/webdsl/dsl/transformation/dsl-to-core

imports
  libstrategolib
  libjava-front

imports
  libwebdsl-front
  org/webdsl/dsl/transformation/-
  org/webdsl/dsl/typechecker/-

strategies
  info(|msg) = log(|Info(), msg)

strategies

  dsl-to-core = id
    ; stop-before-importing
    ; info(|"importing modules")
    ; import-modules

    ; stop-before-desugaring
    ; try(before-first-desugar)
    ; info(|"desugaring")
    ; desugar
      ; stop-after

    ; declare-definitions
      ; stop-after

    //; add-view-edit-pages-to-app
    //  ; stop-after

    ; aggregate-subclass-info

    ; desugar
      ; stop-after
    ; declare-definitions
      ; stop-after
    ; desugar // Ruben: added for test

    ; stop-before-typechecking
    ; info(|"typechecking")
    ; typecheck
      ; stop-after
    ; desugar
      ; stop-after
      
    ; info(|"access control before template expansion")
    ; process-access-control
    ; desugar
    ; declare-definitions
      ; stop-after
      
    ; try(before-template-expansion)
    ; merge-webdsl-declarations
    ; desugar
      ; stop-after
      
    ; stop-before-template-expansion
    ; info(|"template expansion")
    ; expand-page-templates
      ; stop-after
    ; desugar
      ; stop-after
    ; typecheck
      ; stop-after

    ; info(|"access control after template expansion")
    ; derive
    ;if AccessControlEnabled then
       process-access-control-checks
     end

    ; derive
      ; stop-after
    ; desugar
      ; stop-after
    ; typecheck
      ; stop-after
    ; aggregate-and-combine-functions
    ; propagate-component-names
      ; stop-after

strategies

  derive = fail

  before-template-expansion = fail

  before-declare-definitions = fail

  declare-definitions =
    where(
      alltd(declare-definition)
      ; ?Application(qid, sections)
      ; ents := <collect(?SessionEntity(_, _, _))> sections
      ; rules ( SessionEntities := ents )
    )

  declare-definition =
    declare-entity

  declare-definition =
    declare-template-definition

  declare-definition =
    declare-action

  declare-definition =
    declare-globals


   // Creates <AllSubClasses> rule to be used in desugar.str (DeriveOutputSimpleRefAssociation)
   aggregate-subclass-info =
     where(
        allsubclasses := <repeat(once-SubClasses)> []
        ; rules( AllSubClasses := allsubclasses ))
