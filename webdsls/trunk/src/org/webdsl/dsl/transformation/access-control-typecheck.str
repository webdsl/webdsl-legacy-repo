/**

 Security rules for WebDSL
 
*/

module org/webdsl/dsl/transformation/access-control-typecheck

imports 
  libstrategolib 
  
imports 
  org/webdsl/dsl/syntax/WebDSL
  org/webdsl/dsl/transformation/- 
rules
  typecheck-security=
  	?Application(qid, sections)
  	; where(<collect(?AccessControlDefinition(_));map(typecheck-security)> sections)
  	; if TypecheckErrorSecurity then
  	    security-typecheck-error(|"Typechecking error in security definitions")
       ; fail
      end
  	
  typecheck-security:
	AccessControlDefinition(defs) -> AccessControlDefinition(newdefs)
	where newdefs :=
		<map(collect(typecheck-security-subject
					<+ typecheck-create-rule-classes-top
					<+ typecheck-security-checks
					<+ ?x; security-typecheck-error(|["Couldn't typecheck definition ",x]) 
				))> defs
	
  typecheck-security-subject:
    AccessControlPrincipal(ident,props) -> AccessControlPrincipal(ident,props)
    where <IsEntity <+ security-typecheck-error(|[ident," is not a declared type"])> ident

  typecheck-create-rule-classes-top:
    AccessControlRule(checktype,matchstring,checks,acrules) -> AccessControlRule(checktype,matchstring,<filter(typecheck-security-checks)> checks,<filter(typecheck-create-rule-classes(|[checktype]))> acrules)
    where <elem <+ security-typecheck-error(|[checktype," is not a valid security check type"])> (checktype,<Security-Check-Type>)

  typecheck-create-rule-classes(|ct):
    AccessControlRule(checktype,matchstring,checks,acrules) -> AccessControlRule(checktype,matchstring,<filter(typecheck-security-checks)> checks,<filter(typecheck-create-rule-classes(|newtype))> acrules)
    where newtype := <concat>[ct , [checktype]]
        ; <elem <+ security-typecheck-error(|[newtype ," is not a valid security check type"])> (newtype,<Security-Check-Type>)
   
  typecheck-security-checks:
    AccessControlCheckExpression(expr) -> AccessControlCheckExpression(expr)
     
  security-typecheck-error(|msg) =
    err-msg(|msg)
    ; rules( TypecheckErrorSecurity := True() )