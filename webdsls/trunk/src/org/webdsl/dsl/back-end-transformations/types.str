module org/webdsl/dsl/back-end-transformations/types

rules  // retrieve type information passed from front-end by annotations

  get-type-anno:
    _{anno*} -> type
    where TypeAnno(type) := <collect(?TypeAnno(_));Hd> anno*
    
rules    // native classes
  
  is-native-class = get-type-anno; get-anno(?NativeClassAnno(_,_))
  native-class-name = get-type-anno; get-anno(?NativeClassAnno(name,class)); !class
  is-native-function = ?Function(f, args, s, None())
 
rules    // entity related
  
  // given a type annotation, return the entity id name
  get-entity-id-name = get-anno(?EntityAnno(idname,_,_)); !idname
  // given a type-annotation, return natural id (if present) or natural id of superclass
  get-entity-id-name-with-super = get-anno(?EntityAnno(_,superid,_)); !superid; not(?None())
  // given a type-annotation, succeed if type is entity and has natural id 
  has-natural-id = where(get-anno(?EntityAnno(_,_,True())))
  // given a type-annotation, succeed if type is an entity
  has-entity-anno = where(get-anno(?EntityAnno(_,_,_)))
  
rules    // stratego interface related

  get-stratego-language = get-type-anno; ?GenericSort("SDFInput", [<id>])
  