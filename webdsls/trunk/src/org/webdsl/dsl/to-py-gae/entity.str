module org/webdsl/dsl/to-py-gae/entity

imports
  libstrategolib
  libwebdsl-front

imports 
  org/webdsl/dsl/to-py-gae/-
  
strategies
  PyGenerateCode = py-entity-to-code; fail

  py-entity-to-code =
    ?EntityNoSuper(x, props, funcs)
    ; x_proxy := <concat-strings> [x, "Proxy"]
    ; if not(<SubClass> x) then
        e_super := py-exp|[ webdsl.db.Model ]|
      else
        e_super := py-exp|[ webdsl.polymodel.PolyModel ]|
      end
    ; <emit-python(|"data")> python*|[
        @partial class x(e_super): {
          pass;
        };
        x_proxy = webdsl.db.create_proxy_model(x);
      ]|
    ; <map(py-property-to-code(|x) <+ debug(!"Could not generate for property: "))> props
    ; <map(py-entity-function-to-code(|x) <+ debug(!"Could not generate for entity function: "))> funcs

  py-entity-to-code =
    ?Entity(x, super, props, funcs)
    ; with(id
      ; x_proxy := <concat-strings> [x, "Proxy"]
      ; e_super := <python-type> SimpleSort(super)
      ; <emit-python(|"data")> python*|[
          @partial class x(e_super): {
            pass;
          };
          x_proxy = webdsl.db.create_proxy_model(x);
        ]|
      ; <map(py-property-to-code(|x) <+ debug(!"Could not generate for property: "))> props
      ; <map(py-entity-function-to-code(|x) <+ debug(!"Could not generate for entity function: "))> funcs
      )

  py-entity-to-code =
    ?SessionEntity(x, props, funcs)
    ; X := <capitalize-string> x
    ; <py-entity-to-code> EntityNoSuper(X, props, funcs)

  py-property-to-code(|x_class) = // Simple property
    ?Property(x_prop, knd, srt@SimpleSort(_), annos)
    ; not(<fetch(?InverseAnno(_, _) <+ ?InverseSlaveAnno(_, _))> annos)
    ; not(<defined-java-type> srt)
    ; with(id
      ; e_prop := <concat-strings> ["'", x_prop, "'"]
      ; if <elem> (SimpleAnno("id"), annos) then
          e_idprop := <concat-strings> ["'", x_prop, "'"]
          ; <emit-python(|"data")> python*|[ @partial class x_class(): { id_property = e_idprop; }; ]|
        end
      ; if <elem> (SimpleAnno("name"), annos); not("name" := x_prop) then
          <emit-python(|"data")> python*|[ 
            @partial
            class x_class(): { 
              @property
              def name(self): {
                return self.x_prop;
              };
            };
          ]|
        end
      ; e := <python-property-type(|e_prop)> srt
      ; <emit-python(|"data")> python*|[ @partial class x_class(): { x_prop = e; }; ]|
      )

  python-entity-property-type(|e_name, annos) :
    SimpleSort(s) -> py-exp|[ webdsl.db.PartiallyInlinedReferenceProperty(e_type, e, name=e_name) ]|
    where defined-java-type
    with e_type := <concat-strings> ["'", s, "'"]
       ; if <fetch(?InlineAnno(inlineprop*))> annos then
           e := ListLiteral([py-exp|[ 'id']| | <map(\ str -> <concat-strings> ["'", str, "'"] \)> inlineprop*])
         else
           e := py-exp|[ ['id'] ]|
         end

  python-entity-property-type(|e_name, annos) =
    ?srt
    ; defined-java-type
    ; <python-property-type(|e_name)> srt


  py-property-to-code(|x_class) = // Simple reference property
    ?prop@Property(x_prop, knd, srt@SimpleSort(s), annos)
    ; not(<fetch(?InverseAnno(_, _) <+ ?InverseSlaveAnno(_, _))> annos)
    ; <defined-java-type> srt
    ; with(id
      ; e_type := <concat-strings> ["'", s, "'"]
      ; x_type := s
      ; x_propid := <concat-strings> [x_prop, "_id"]
      ; e_propid := <concat-strings> ["'", x_prop, "_id'"]
      ; e_prop := <concat-strings> ["'", x_prop, "'"]
      ; if <fetch(?InlineAnno(inlineprop*))> annos then
          e_propinline := <concat-strings> ["'", x_prop, "_inline'"]
          ; e := ListLiteral(<map(\ str -> <concat-strings> ["'", str, "'"] \)> inlineprop*)
          ; stm* := python*|[ x_prop = webdsl.db.PartiallyOneInlinedReferenceProperty(e_type ,  e, name=e_propinline); ]|
        else
          x_get := <concat-strings> ["get_", x_prop]
          ; x_set := <concat-strings> ["set_", x_prop]
          ; e_internalprop := <concat-strings> ["'__", x_prop, "'"]
          ; stm* := python*|[
                      def x_get(self): {
                        if not hasattr(self, e_internalprop): {
                          setattr(self, e_internalprop, x_type.get(self.x_propid));
                        };
                        return getattr(self, e_internalprop);
                      };
                      def x_set(self, value): {
                        setattr(self, e_propid, value);
                      };
                      x_prop = property(x_get, x_set);
                    ]|
        end
      //; e_propcol := <concat-strings> ["'", x_prop, "_id_set'"]
      ; <emit-python(|"data")> python*|[ 
          @partial class x_class(): {
            stm*
            x_propid = webdsl.db.SimpleReferenceProperty(name=e_prop);
          };
        ]|
      )

  py-property-to-code(|x_class) = // many-to-one back reference
    ?prop@Property(x_prop, knd, srt@SimpleSort(s), annos)
    ; <fetch(?InverseAnno(inverseent, inversefld) <+ ?InverseSlaveAnno(inverseent, inversefld))> annos
    ; with(id
      ; e_prop := <concat-strings> ["'", x_prop, "'"]
      ; e := <python-entity-property-type(|e_prop, annos)> srt
      ; stm* := <py-generate-backreference-property(|s, x_prop, annos, inverseent, inversefld)>
      ; <emit-python(|"data")> python*|[ @partial class x_class(): { stm* }; ]|
      )

  py-property-to-code(|x_class) = // One-to-many with inverse
    ?prop@Property(x_prop, knd, GenericSort("Set", [SimpleSort(s)]), annos)
    ; <fetch(?InverseAnno(inverseent, inversefld) <+ ?InverseSlaveAnno(inverseent, inversefld))> annos
    ; SimpleSort(inverse_sort) := <TypeOfProperty> (inverseent, inversefld)
    ; with(id
      ; <emit-python(|"data")> <py-generate-many-property(|x_class, x_prop, s, "OneToManyDbQuerySet", inversefld)>
      ; <py-generate-inverse-prop(|s, "")> prop
      )

  py-property-to-code(|x_class) = // Many-to-many no inverse
    ?prop@Property(x_prop, knd, GenericSort("Set", [SimpleSort(s)]), annos)
    ; not(<fetch(?InverseAnno(_, _) <+ ?InverseSlaveAnno(_, _))> annos)
    ; with(id
        ; <emit-python(|"data")> <py-generate-many-property(|x_class, x_prop, s, "ManyToManyDbQuerySet", "")>
        ; <py-generate-inverse-prop(|s, "Set")> prop
      )

  py-property-to-code(|x_class) = // Many-to-many with inverse
    ?prop@Property(x_prop, knd, GenericSort("Set", [SimpleSort(s)]), annos)
    ; <fetch(?InverseAnno(inverseent, inversefld) <+ ?InverseSlaveAnno(inverseent, inversefld))> annos
    ; GenericSort(_, _) := <TypeOfProperty> (inverseent, inversefld)
    ; with(id
        ; <emit-python(|"data")> <py-generate-many-property(|x_class, x_prop, s, "ManyToManyDbQuerySet", inversefld)>
        ; <py-generate-inverse-prop(|s, "Set")> prop
      )

  py-property-to-code(|x_class) =
    ?DerivedProperty(x_prop, knd, srt, annos, e)
    ; with(id
        ; e2 := <expression-to-python> e
        ; <emit-python(|"data")> python*|[
            @partial 
            class x_class(): { 
                @property
                def x_prop(self): {
                  return e2;
                };
            };
          ]|
      )

  py-generate-backreference-property(|x_class, x_prop, annos, x_inverseent, x_inversefld) =
    with(
      x_inversepropinl := <concat-strings> [x_prop, "_inline"]
      ; x_inverseprop := <concat-strings> ["inverse__", x_inverseent, "_", x_inversefld]
      ; x_get := <concat-strings> ["get_", x_prop]
      ; x_set := <concat-strings> ["set_", x_prop]
      ; e_inlinepropname := <concat-strings> ["'", x_inversepropinl, "'"]
      ; e_inlinetype := <python-entity-property-type(|e_inlinepropname, annos)> SimpleSort(x_class)
     )
    ; !python*|[
        x_inversepropinl = e_inlinetype;
        def x_get(self): {
            return self.x_inversepropinl;
        };
        def x_set(self, value): {
            self.x_inversepropinl = value;
            self.x_inverseprop = value.id;
        };
        x_prop = property(x_get, x_set);
      ]|

  py-generate-many-property(|x_class, x_prop, sort, x_manyclass, x_declinvprop) =
    x_inverseprop := <concat-strings> ["'inverse__", x_class, "_", x_prop, "'"]
    ; x_internalprop := <concat-strings> ["_", x_prop]
    ; x_countprop := <concat-strings> [x_prop, "_count"]
    ; x_get := <concat-strings> ["get_", x_prop]
    ; x_lst := <concat-strings> [x_prop, "List"]
    ; x_set := <concat-strings> ["set_", x_prop]
    ; e_propstr := <concat-strings> ["'", x_prop, "'"]
    ; e_typestr := <concat-strings> ["'", sort, "'"]
    ; if "" := x_declinvprop then
        e_declinvprop := py-exp|[ None ]|
      else
        e_declinvprop := String(<concat-strings> ["'", x_declinvprop, "'"])
      end
    ; !python*|[ @partial class x_class(): {
        x_internalprop = None;

        def x_get(self): {
            if self.x_internalprop == None: {
                self._post_process_props.append(e_propstr);
                self.x_internalprop = webdsl.querylist.x_manyclass(self, e_typestr, x_inverseprop, self.id, e_declinvprop);
            };
            return self.x_internalprop;
        };
        def x_set(self, value): {
            for item in self.x_prop: {
                self.x_prop.remove(item);
            };
            for item in value: {
                self.x_prop.append(item);
            };
        };
        x_prop = property(x_get, x_set);
        x_lst = x_prop;
      }; ]|

  py-generate-inverse-prop(|x_ent, lst) =
    ?Property(x, knd, GenericSort("Set", [SimpleSort(invers_ent)]), annos)
    ; with(id
        ; x_prop := <concat-strings> ["inverse__", <ThisEntity>, "_", x]
        ; if <EntityIdType> <ThisEntity> then
            type2 := <EntityIdType> <ThisEntity>
          else
            type2 := SimpleSort("Int")
          end
         ; if <fetch(?InverseAnno(inverseent, inversefld) <+ ?InverseSlaveAnno(inverseent, inversefld))> annos then
            e_propname := <concat-strings> ["'", inversefld, "_'"]
            ; e_inlinepropname := <concat-strings> ["'", inversefld, "_inline'"]
          else
            e_propname := <concat-strings> ["'", x_prop, "'"]
            ; e_inlinepropname := <concat-strings> ["'", x_prop, "_inline'"]
          end
        ; if "Set" := lst then
            type := GenericSort("Set", [type2])
          else // Not a collection, also add an inline property
            type := type2
            ; x_propinline := <concat-strings> [x_prop, "_inline"]
            //; e_inlinetype := <python-property-type(|e_inlinepropname)> SimpleSort(<ThisEntity>)
            //; <emit-python(|"data")> python*|[ @partial class x_ent(): { x_propinline = e_inlinetype; }; ]|
          end
        ; e_type := <python-property-type(|e_propname)> type
        ; <emit-python(|"data")> python*|[ @partial class x_ent(): { x_prop = e_type; }; ]|
        )

  py-entity-function-to-code(|x_ent) =
    ?Function(x_fun, farg*, _, Block(stat*)) 
    ; e* := [py-exp|[ self ]| | <map(\ Arg(x, _) -> py-exp|[ x ]| \)> farg*]
    ; {| InFunction
       : rules ( InFunction := True() )
       ; stm* := <statements-to-python> stat*
       |}
    ; <emit-python(|"data")> python*|[
        @partial 
        class x_ent(): { 
          def x_fun(e*): {
            import global_variable;
            stm*
          };
        }; ]|

