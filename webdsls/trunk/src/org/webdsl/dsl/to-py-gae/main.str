module org/webdsl/dsl/to-py-gae/main

imports
  libstrategolib
  libwebdsl-front

imports 
  org/webdsl/dsl/to-py-gae/-
  
strategies

  core-to-python = 
    info(|"Generating Python code")
    ; py-generate-code
    ; <map({
        ?type
        ; info(|["Going to write code for ", type])
        ; get-python-stats
        ; info(|"Merging...")
        ; py-merge-classes
        ; !Application(<concat> [<py-header> type, <id>])
        ; alltd(py-remove-pass)
        ; info(|"Writing to file...")
        ; output-python-to-file(|<concat-strings> [type, ".py"])
        })> ["data", "template"]

  py-header =
    ?type
    ; !python*|[
      from google.appengine.ext import db;
      import logging;
      import webdsl.querylist;
      import webdsl.db;
      from webdsl.utils import new_line;
    ]|
    ; if "template" := type then
        <concat> [<id>, python*|[
          import data;
          import cgi;
          import md5;
        ]| ]
      end

strategies
   py-generate-code =
     if ?Define(mods, name, args, elems); is-page-or-template then
       {| Parameter, ThisPage
        : rules ( ThisPage := name )
        ; not(PyGenerateCode)
        ; all(py-generate-code)
        |}
     else
       if ?Action(_, _, _) then
         {| InAction
          : rules ( InAction := True() )
          ; not(PyGenerateCode)
	        ; all(py-generate-code)
	        |}
       else
         if ?Entity(nm, _, _, _) <+ ?EntityNoSuper(nm, _, _) then
           {| ThisEntity
            : rules ( ThisEntity := nm )
            ; not(PyGenerateCode)
            ; all(py-generate-code)
            |}
         else
           not(PyGenerateCode)
           ; all(py-generate-code)
         end
       end
     end

