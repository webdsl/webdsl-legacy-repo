module org/webdsl/dsl/to-py-gae/page

imports
  libstrategolib
  libwebdsl-front

imports 
  org/webdsl/dsl/to-py-gae/-
  
strategies

  PyGenerateCode = py-page-to-code; fail
  PyGenerateCode = py-template-to-code; fail

  py-page-to-code =
    ?def@Define([Page()], x, args, elem*)
    ; with(id
        ; x_cls := <capitalize-string> x
        ; if "home" := x then
            path := "'/"
          else
            path := <concat-strings> ["'/", x]
          end
        ; e_pattern := String(<concat-strings> [path, <map(\ _ -> "/([^/]+)" \); concat-strings> args, "'"])
        ; e_lst := ListLiteral(<map(py-argument-to-register-tuple)> args)
        ; <emit-python(|"template")> python*|[ webdsl.utils.register(e_pattern, x_cls, e_lst); ]|
        // Find all local templates
        ; localtemplates := <collect(\ Define([Local()], x, _, _) -> x \)> elem*
        ; stm1* := <map({e_ltmp, x_up: 
                         \ x -> python|[ self.template_bindings[e_ltmp] = x_up; ]| 
                           with e_ltmp := <concat-strings> ["'", x, "'"]
                              ; x_up := <concat-strings> [x_cls, "_", x] \})> localtemplates
        ; allstylesheets := <concat; make-set> [<bagof-AllStylesheets>, <bagof-StyleSheetsAllPages>]
        ; e* := <map(\ sheet -> String(<concat-strings> ["'", sheet, "'"]) \)> allstylesheets
        ; stm3* := <map(py-session-var-to-code); concat> <bagof-SessionVariable; make-set>
        ; stm4* := <map({e_var: \ x -> python|[ (self.scope[e_var]).put(); ]| where e_var := <concat-strings> ["'", x, "'"]\})> <bagof-SessionVariable; make-set>
        ; <emit-python(|"template")> python*|[
            @partial
            class x_cls(webdsl.utils.RequestHandler): {
              def prepare_templates(self): {
                stm1*
                self.stylesheets = [e*];
                pass;
              };

              def load_session(self): {
                  session = webdsl.session.Session();
                  stm3*
              };

              def store_session(self): {
                  stm4*
                  pass;
              };
            };
          ]|
      )

  py-session-var-to-code :
    x -> python*|[ 
      if not e_str in session: {
          session_var = data.x_captype();
          session_var.put();
          session[e_str] = session_var.id;
      };
      self.scope[e_str] = data.x_captype.fetch_by_id(session[e_str]);
    ]|
    with e_str := String(<concat-strings> ["'", x, "'"])
       ; x_captype := <capitalize-string> x

  py-argument-to-register-tuple :
    Arg(nm, srt@SimpleSort(x_s)) -> 
    TupleLiteral(<concat-strings> ["'", nm, "'"], [<EntityIdType; python-type <+ !"long"> x_s, py-exp|[ data.x_s ]| ])
    where <defined-java-type> srt

  py-argument-to-register-tuple :
    Arg(nm, srt) -> TupleLiteral(<concat-strings> ["'", nm, "'"], ["''", <python-type> srt ])
    where <builtin-java-type> srt

  py-template-to-code =
    ?Define(mods, x, args, elem*)
    ; not(<fetch(?Local())> mods)
    ; with(id
        ; x_cls := <capitalize-string> x
        ; if "home" := x then
            e_path := String("'/'")
          else
            e_path := String(<concat-strings> ["'/", x, "'"])
          end
        ; {| ThisTemplate, IsScoped
           : rules ( ThisTemplate := x )
           ; <map(\ Arg(x, _) -> <rules( IsScoped : x )> \)> args
           ; stm* := <collect(py-page-elem-to-init); concat> elem*
           ; {| CurrentActionId
              : stm1* := <py-page-elems-to-databind> elem*
              |}
           ; {| CurrentActionId
              : stm2* := <py-page-elems-to-render> elem*
              |}
           ; stm3* := <py-page-elems-to-template-init> elem*
           |}
        ; e_tmplid := String(<concat-strings> ["'", x, "'"])
        ; <emit-python(|"template")> python*|[
            @partial
            class x_cls(webdsl.utils.RequestHandler): {
              def init_templates(self): {
                stm*
                stm3*
                pass;
              };
              def databind(self): {
                template_call_counter = -1;
                stm1*
                pass;
              };
              def render(self, out): {
                template_call_counter = -1;
                out.write('<div id="' + e_tmplid + '">');
                stm2*
                out.write('</div>');
              };
            };
          ]|
      )

  py-page-elems-to-databind =
    map( py-page-elem-to-databind <+ py-page-elem-to-databind-generic <+ ![])
    ; flatten-list

  py-page-elems-to-template-init =
    map( py-page-elem-to-template-init <+ py-page-elem-to-template-init-generic <+ ![])
    ; flatten-list

  py-page-elems-to-render =
    map( py-page-elem-to-render; ![<id>|python*|[ out.write(new_line()); ]| ]
      <+ py-skipped-element)
    ; flatten-list

  py-page-elems-to-render(wrap) =
    map( py-page-elem-to-render; wrap; ![<id>|python*|[ out.write(new_line()); ]| ]
      <+ py-skipped-element)
    ; flatten-list

rules
  py-page-elem-to-databind-generic :
    TemplateCall(_, _, elems*) -> <py-page-elems-to-databind> elems*

  py-page-elem-to-template-init-generic :
    TemplateCall(_, _, elems*) -> <py-page-elems-to-template-init> elems*
//////////////////////////////////////////
  py-page-elem-to-template-init :
    TemplateCall(name, args, _) ->
    python*|[ 
        t = e_tmplobj;
        t.init();
        self.templates.append(t);
        ]|
    where t* := <map(type-of)> args
        ; Define(mods, n, fargs, body) := <TemplateSignature> (name, t*)
    with e_namestr := <concat-strings> ["'", name, "'"]
       ; e_tmplclass := py-exp|[ self.template_bindings[e_namestr] ]|
       ; scopedparams := <zip(\ (Arg(x, _), e) -> NamedParam(x, <expression-to-python> e) \)> (fargs, args)
       ; e_tmplobj := Call(e_tmplclass, <concat> [[Param(py-exp|[ self ]|), Param(py-exp|[ self.rh ]|)], scopedparams])

rules
  py-skipped-element =
   debug(!"Skipped element: ")
   ; e := <concat-strings> ["'", <write-to-string> <id>, "'"]
   ; !python |[ out.write('Skipped element: ' + e); ]|
   ; <exit> 1

rules // Local templates

  py-page-elem-to-render :
    Define([Local()], x, args, elem*) -> []
    with name := <concat-strings> [<ThisTemplate>, "_", x]
       ; x_cls := <capitalize-string> name
       ; e_path := String(<concat-strings> ["'/", x, "'"])
       ; {| ThisTemplate, IsScoped, CurrentActionId
          : rules ( ThisTemplate := name )
          ; <map(\ Arg(x, _) -> <rules( IsScoped : x )> \)> args
          ; stm* := <collect(py-page-elem-to-init); concat> elem*
          ; stm2* := <py-page-elems-to-render> elem*
          ; stm3* := <py-page-elems-to-template-init> elem*
          |}
       ; x_pagecls := <ThisTemplate; capitalize-string>
        ; e_tmplid := String(<concat-strings> ["'", x, "'"])
       ; <emit-python(|"template")> python*|[
           @partial
           class x_cls(webdsl.utils.RequestHandler): {
              def init_templates(self): {
                stm*
                stm3*
                pass;
              };
              def render(self, out): {
                template_call_counter = -1;
                out.write('<div id="' + e_tmplid + '">');
                stm2*
                out.write('</div>');
              };
           };
        ]|

  py-page-elem-to-databind :
    Define([Local()], x, args, elem*) -> []
    with name := <concat-strings> [<ThisTemplate>, "_", x]
       ; x_cls := <capitalize-string> name
       ; e_path := String(<concat-strings> ["'/", x, "'"])
       ; {| ThisTemplate, IsScoped, CurrentActionId
          : rules ( ThisTemplate := name )
          ; <map(\ Arg(x, _) -> <rules( IsScoped : x )> \)> args
          ; stm* := <py-page-elems-to-databind> elem*
          |}
       ; x_pagecls := <ThisTemplate; capitalize-string>
       ; <emit-python(|"template")> python*|[
           @partial
           class x_cls(webdsl.utils.RequestHandler): {
             def databind(self): {
                template_call_counter = -1;
                stm*
             };
           };
        ]|
      
rules // Init block

  py-page-elem-to-databind :
    InitAction(Block(stat*)) -> []
    with x_cls := <capitalize-string> <ThisTemplate>
       ; stm* := <statements-to-python> stat*
       ; <emit-python(|"template")> python*|[
           @partial
           class x_cls(webdsl.utils.RequestHandler): {
             def initialize(self): {
                stm*
                pass;
             };
           };
        ]|

  py-page-elem-to-render :
    InitAction(Block(stat*)) -> []
rules

  py-page-elem-to-render :
    Text(e) -> python*|[ out.write(cgi.escape(unicode(e2))); ]|
    where e2 := <expression-to-python> e

  py-page-elem-to-render :
    TemplateCall("text", [e], []) -> python*|[ out.write('<span class="text">' + cgi.escape(unicode(e2)) + '</span>'); ]|
    where e2 := <expression-to-python> e

  py-page-elem-to-render :
    TemplateCall("title", [], elems) -> 
    python*|[ 
      from StringIO import StringIO;
      old_out = out;
      out = StringIO();
      stm*
      self.title = out.getvalue();
      out = old_out;
    ]|
    where stm* := <py-page-elems-to-render> elems

  py-page-elem-to-render :
    TemplateCall("title", [e], elems) -> <py-page-elem-to-render> TemplateCall("title", [], [Text(e)])

  py-page-elem-to-render :
    elem|[ form() { elem* }]| -> python*|[
      out.write('<form method="POST">');
      form_id = webdsl.utils.generateFormHash(self.scope, self);
      out.write('<input type="hidden" name="form_id" value="%s"/>' % form_id);
      stm*
      out.write('</form>');
    ]|
    where stm* := <map(py-page-elem-to-render <+ py-skipped-element)> elem*

  py-page-elem-to-databind :
    elem|[ form() { elem* }]| -> python*|[
      form_id = webdsl.utils.generateFormHash(self.scope, self);
      is_submitted_form = self.rh.request.get('form_id') == form_id;
      stm*
    ]|
    where stm* := <py-page-elems-to-databind> elem*

  py-get-data-object =
    ?e
    ; if FieldAccess(obj, _) := e then
        !<expression-to-python> obj
      else
        !<expression-to-python> e
      end

rules 
  py-page-elem-to-render :
    elem|[ inputHidden(e){} ]| -> python*|[
          field_id = webdsl.utils.generateUniqueFieldName(e_data, e_fld, self);
          out.write('<input type="hidden" name="' + field_id + '" value="');
          out.write(cgi.escape(unicode(e_target), True));
          out.write('"/>');
    
    ]|
    where <type-of; is-simple-sort> e
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]
       ; e_data := <py-get-data-object> e
       ; e_convert := <python-type> <type-of> e

  py-page-elem-to-databind :
    elem|[ inputHidden(e){} ]| -> python*|[
          field_id = webdsl.utils.generateUniqueFieldName(e_data, e_fld, self);
          if is_submitted_form: {
              e_target = e_convert(self.rh.request.get(field_id));
          };
    ]|
    where <type-of; is-simple-sort> e
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]
       ; e_data := <py-get-data-object> e
       ; e_convert := <python-type> <type-of> e

rules

  py-page-elem-to-render :
    elem|[ action(e1, e2){} ]| -> python*|[
          out.write('<input type="submit" name="action-' + e_actionr + '" value="' + cgi.escape(unicode(e_txt), True) + '"/>');
    ]|
    with actionr := <CurrentActionId <+ !1>
       ; rules ( CurrentActionId := <inc> actionr )
       ; e_actionr := <concat-strings> ["'", <int-to-string> actionr, "'"]
       ; e_txt := <expression-to-python> e1
       ; ThisCall(x, arg*) := e2
       ; x_action := <concat-strings> ["do_", x]
       ; e_call := py-exp|[ self.x_action ]|
       ; e* := <map(expression-to-python)> arg*

  py-page-elem-to-databind :
    elem|[ action(e1, e2){} ]| -> python*|[
          if self.rh.request.get('action-' + e_actionr) and is_submitted_form: {
              self.queue_action(e_call, [e*]);
          };
    ]|
    with actionr := <CurrentActionId <+ !1>
       ; rules ( CurrentActionId := <inc> actionr )
       ; e_actionr := <concat-strings> ["'", <int-to-string> actionr, "'"]
       ; ThisCall(x, arg*) := e2
       ; x_action := <concat-strings> ["do_", x]
       ; e_call := py-exp|[ self.x_action ]|
       ; e* := <map(expression-to-python)> arg*

  py-page-elem-to-render :
    elem|[ actionLink(e1, e2){} ]| -> python*|[
          out.write('<input type="hidden" name="action-' + e_actionr + '" value=""/>');
          out.write('<a href="#" onclick="document.getElementsByName(\'action-' + e_actionr + '\')[0].value=\'1\';submit();">' + cgi.escape(unicode(e_txt), True) + '</a>');
    ]|
    with actionr := <CurrentActionId <+ !1>
       ; rules ( CurrentActionId := <inc> actionr )
       ; e_actionr := <concat-strings> ["'", <int-to-string> actionr, "'"]
       ; e_txt := <expression-to-python> e1
       ; ThisCall(x, arg*) := e2
       ; x_action := <concat-strings> ["do_", x]
       ; e_call := py-exp|[ self.x_action ]|
       ; e* := <map(expression-to-python)> arg*

  py-page-elem-to-databind :
    elem|[ actionLink(e1, e2){} ]| -> <py-page-elem-to-databind> elem|[ action(e1, e2) ]|

  py-page-elem-to-databind :
    a@elem|[ action x_action(farg*) { stat* } ]| ->
    []
    with x_class := <ThisTemplate; capitalize-string>
       ; stm_action := <action-to-python> a
       ; <emit-python(|"template")> python*|[ @partial class x_class(webdsl.utils.RequestHandler): { stm_action }; ]|

  py-page-elem-to-render :
    a@elem|[ action x_action(farg*) { stat* } ]| ->
    []

  action-to-python :
    elem|[ action x_action(farg*) { stat* } ]| ->
    python*|[ def x_action2(self, e*): { stm* }; ]|
    with x_action2 := <concat-strings> ["do_", x_action]
       ; e* := <map(\ Arg(x, _) -> x \)> farg*
       ; {| InAction
          : rules ( InAction := True() )
          ; stm* := <statements-to-python> stat*
          |}

rules
  py-page-elem-to-render :
    TemplateCall("dummy", [], elems) -> <py-page-elems-to-render> elems

rules // block

  // Legacy
  py-page-elem-to-render :
    TemplateCall("div", [String(str)], elems) ->
    python*|[ 
      out.write('<div class="' + e_str + '">');
      stm*
      out.write('</div>');
      ]|
    with stm* := <py-page-elems-to-render> elems
       ; e_str := <concat-strings> ["'", str, "'"]

  py-page-elem-to-render :
    TemplateCall("block", [], elems) ->
    python*|[ 
      out.write('<div class="block">');
      stm*
      out.write('</div>');
      ]|
    where stm* := <py-page-elems-to-render> elems

  py-page-elem-to-render :
    TemplateCall("block", [String(str)], elems) ->
    python*|[ 
      out.write('<div id="' + e_str + '" class="block">');
      stm*
      out.write('</div>');
      ]|
    with stm* := <py-page-elems-to-render> elems
       ; e_str := <concat-strings> ["'", str, "'"]

  py-page-elem-to-render :
    TemplateCall("block", [String(str1), String(str2)], elems) ->
    python*|[ 
      out.write('<div id="' + e_str1 + '" class="' + e_str2 + ' block">');
      stm*
      out.write('</div>');
      ]|
    with stm* := <py-page-elems-to-render> elems
       ; e_str1 := <concat-strings> ["'", str1, "'"]
       ; e_str2 := <concat-strings> ["'", str2, "'"]

  py-page-elem-to-render =
    ?ForAll(_, _, _, _)
    ; py-page-elem-to-generic(py-page-elems-to-render)

  py-page-elem-to-template-init =
    ?ForAll(_, _, _, _)
    ; py-page-elem-to-generic(py-page-elems-to-template-init)

  py-page-elem-to-databind =
    ?ForAll(_, _, _, _)
    ; py-page-elem-to-generic(py-page-elems-to-databind)
    
  py-page-elem-to-generic(s) :
    ForAll(x, s, Filter(we, obe, Limit(limit, offset)), elems) ->
    python*|[
      for x in e_coll: {
        stm*
        pass;
      };
    ]|
    with e_type := <python-type> s
       ; e_start := py-exp|[ webdsl.querylist.AllDbQuerySet(e_type) ]|
       ; filters := <py-exp-to-filters(|x) <+ debug(!"Cannot translate to filters: ")> we 
       ; e2 := <foldr(!e_start, \ (tuple, e) -> <py-add-filter(|tuple)> e \)> filters
       ; e_ob := <py-exp-to-order-by(|x) <+ debug(!"Cannot translate order by: ")> obe
       ; if not(None() := e_ob) then
           e := py-exp|[ e2.order_by(e_ob) ]|
         else
           e := e2
         end
       ; e_limit := <expression-to-python> limit
       ; e_offset := <expression-to-python> offset
       ; e_coll := py-exp|[ e.limit(e_limit, e_offset) ]|
       ; stm* := <s> elems

  py-page-elem-to-render =
    ?For(_, _, _, _, _)
    ; py-page-elem-to-generic(py-page-elems-to-render)

  py-page-elem-to-databind =
    ?For(_, _, _, _, _)
    ; py-page-elem-to-generic(py-page-elems-to-databind)

  py-page-elem-to-template-init =
    ?For(_, _, _, _, _)
    ; py-page-elem-to-generic(py-page-elems-to-template-init)

  py-page-elem-to-generic(s) :
    For(x, s, ine, Filter(we, obe, Limit(limit, offset)), elems) ->
    python*|[
      for x in e_coll: {
        stm*
        pass;
      };
    ]|
    with e_type := <python-type> s
       ; filters := <py-exp-to-filters(|x) <+ debug(!"Cannot translate to filters: ")> we 
       ; e_start := <expression-to-python> ine
       ; e2 := <foldr(!e_start, \ (tuple, e) -> <py-add-filter(|tuple)> e \)> filters
       ; e_ob := <py-exp-to-order-by(|x) <+ debug(!"Cannot translate order by: ")> obe
       ; if not(None() := e_ob) then
           e := py-exp|[ e2.order_by(e_ob) ]|
         else
           e := e2
         end
       ; e_limit := <expression-to-python> limit
       ; e_offset := <expression-to-python> offset
       ; e_coll := py-exp|[ e.limit(e_limit, e_offset) ]|
       ; stm* := <s> elems

  py-exp-to-filters(|obj) :
    exp|[ true ]| -> []

  py-exp-to-filters(|obj) :
    exp|[ x1.x2 = e ]| -> [(x2, "eq", e)]
    where x1 := obj

  py-exp-to-filters(|obj) :
    exp|[ x1.x2 > e ]| -> [(x2, "gt", e)]
    where x1 := obj

  py-exp-to-filters(|obj) :
    exp|[ x1.x2 >= e ]| -> [(x2, "geq", e)]
    where x1 := obj

  py-exp-to-filters(|obj) :
    exp|[ x1.x2 < e ]| -> [(x2, "lt", e)]
    where x1 := obj

  py-exp-to-filters(|obj) :
    exp|[ x1.x2 <= e ]| -> [(x2, "leq", e)]
    where x1 := obj

  py-exp-to-filters(|obj) :
    exp|[ e1 && e2 ]| -> <concat> [<py-exp-to-filters(|obj)> e1, <py-exp-to-filters(|obj)> e2]


  py-add-filter(|filter) :
    e -> py-exp|[ e.x(e_prop, e_value) ]|
    with (prop, type, value) := filter
       ; x := <concat-strings> ["filter_", type]
       ; e_value := <expression-to-python> value
       ; e_prop := <concat-strings> ["'", prop, "'"]

  py-exp-to-order-by(|obj) :
    OrderNonSpecific(_) -> None()

  py-exp-to-order-by(|obj) :
    OrderAscending(Null()) -> None()

  py-exp-to-order-by(|obj) :
    OrderDescending(Null()) -> None()

  py-exp-to-order-by(|obj) :
    OrderDescending(e@FieldAccess(Var(v), x)) -> <concat-strings> ["'-", x, "'"]
    where v := obj

  py-exp-to-order-by(|obj) :
    OrderDescending(e@Var(_)) -> result
    where SimpleSort(s) := <type-of> e
        ; if <EntityIdName> s then
            result := <concat-strings> ["'-", <EntityIdName>, "'"]
          else
            result := None()
          end
  py-exp-to-order-by(|obj) :
    OrderAscending(e@FieldAccess(Var(v), x)) -> <concat-strings> ["'", x, "'"]
    where v := obj

  py-exp-to-order-by(|obj) :
    OrderAscending(e@Var(_)) -> result
    where SimpleSort(s) := <type-of> e
        ; if <EntityIdName> s then
            result := <concat-strings> ["'", <EntityIdName>, "'"]
          else
            result := None()
          end

rules // Template call
  py-page-elem-to-render :
    TemplateCall(name, args, _) ->
    python*|[ 
        template_call_counter = template_call_counter + 1;
        (self.templates[template_call_counter]).render(out);
        ]|
    where t* := <map(type-of)> args
        ; Define(mods, n, fargs, body) := <TemplateSignature <+ LocalTemplateSignature> (name, t*)
    with e_namestr := <concat-strings> ["'", name, "'"]
       ; e_tmplclass := py-exp|[ self.template_bindings[e_namestr] ]|
       ; scopedparams := <zip(\ (Arg(x, _), e) -> NamedParam(x, <expression-to-python> e) \)> (fargs, args)
       ; e_tmplobj := Call(e_tmplclass, <concat> [[Param(py-exp|[ self ]|), Param(py-exp|[ self.rh ]|)], scopedparams])

  py-page-elem-to-databind :
    TemplateCall(name, args, _) ->
    python*|[ 
        template_call_counter = template_call_counter + 1;
        (self.templates[template_call_counter]).databind();
        ]|
    where t* := <map(type-of)> args
        ; Define(mods, n, fargs, body) := <TemplateSignature <+ LocalTemplateSignature> (name, t*)
    with e_namestr := <concat-strings> ["'", name, "'"]
       ; e_tmplclass := py-exp|[ self.template_bindings[e_namestr] ]|
       ; scopedparams := <zip(\ (Arg(x, _), e) -> NamedParam(x, <expression-to-python> e) \)> (fargs, args)
       ; e_tmplobj := Call(e_tmplclass, <concat> [[Param(py-exp|[ self ]|), Param(py-exp|[ self.rh ]|)], scopedparams])

rules // spacers

  /*
  py-page-elem-to-render :
    TemplateCall("spacer", [], []) ->
    python*|[ out.write('<hr id="' + e_id + "/>'); ]|
    %> <rich:spacer id="<%= spacer-id %>" /> <%
    where e_id := <concat-strings> ["'", <newname> "spacer", "'"]
    */

  py-page-elem-to-render :
    TemplateCall("horizontalspacer", [], []) ->
    python*|[ out.write('<hr/>'); ]|

rules // images

  py-page-elem-to-render :
    TemplateCall("image",[e],[]) -> 
    python*|[ out.write('<img src="' + e2 + '"/>'); ]|
    where e2 := <expression-to-python> e

rules // lists

  py-page-elem-to-render :
    TemplateCall("list", [], elems) ->
    python*|[
      out.write('<ul>');
      stm*
      out.write('</ul>');
    ]|
    where stm* := <py-page-elems-to-render> elems

  py-page-elem-to-render :
    TemplateCall("listitem", [], elems) ->
    python*|[
      out.write('<li>');
      stm*
      out.write('</li>');
    ]|
    where stm* := <py-page-elems-to-render> elems

rules // sections and headers

  py-page-elem-to-render :
    TemplateCall("header", [], elems) ->
    python*|[
      out.write('<' + e_h + '>');
      stm*
      out.write('</' + e_h + '>');
    ]|
    where not(DataTable) ; not(TableScope)
    with n := <SectionDepth <+ !1>
       ; e_h := <concat-strings> ["'h", <int-to-string> n, "'"]
       ; stm* := <py-page-elems-to-render> elems

  py-page-elem-to-render :
    TemplateCall("section", [], elems) ->
    python*|[
      out.write('<div class="' + e_level + ' section">');
      stm*
      out.write('</div>');
    ]|
    with {| SectionDepth
           : rules( SectionDepth := <(SectionDepth <+ !0); inc> )
           ; e_level := <concat-strings> ["'section", <SectionDepth; int-to-string>, "'"]
           ; stm* := <py-page-elems-to-render> elems
           |}

  py-page-elem-to-render :
    TemplateCall("par", [], elems) ->
    python*|[
      out.write('<p>');
      stm*
      out.write('</p>');
    ]|
    where stm* := <py-page-elems-to-render> elems

  py-page-elem-to-render :
    TemplateCall("container", [], elems) ->
    python*|[
      out.write('<span>');
      stm*
      out.write('</span>');
    ]|
    where stm* := <py-page-elems-to-render> elems

rules // tables

  py-page-elem-to-render :
    TemplateCall("table", [], elems) ->
    python*|[
      out.write('<table>');
      stm*
      out.write('</table>');
    ]|
    where {| TableScope
           : rules ( TableScope := 1 )
           ; stm* := <py-page-elems-to-render> elems
           |}

  py-page-elem-to-render :
    TemplateCall("header", [], elems) ->
    python*|[
      out.write('<tr>');
      stm*
      out.write('</tr>');
    ]|
    where TableScope
    with stm* := <py-page-elems-to-render(py-table-header-wrap)> elems

  py-page-elem-to-render :
    TemplateCall("row", [], elems) ->
    python*|[
      out.write('<tr>');
      stm*
      out.write('</tr>');
    ]|
    with stm* := <py-page-elems-to-render(py-table-cell-wrap)> elems

  py-table-cell-wrap :
    stm* -> python*|[
      out.write('<td>');
      stm*
      out.write('</td>');
    ]|

  py-table-header-wrap :
    stm* -> python*|[
      out.write('<th>');
      stm*
      out.write('</th>');
    ]|

rules // if

  py-page-elem-to-generic(s) :
    IfTempl(e, elems) ->
    python*|[
      if(e2): {
        stm*
        pass;
      };
    ]|
    with stm* := <s> elems
       ; e2 := <expression-to-python> e

  py-page-elem-to-render =
    ?IfTempl(e, elems)
    ; py-page-elem-to-generic(py-page-elems-to-render)

  py-page-elem-to-databind =
    ?IfTempl(e, elems)
    ; py-page-elem-to-generic(py-page-elems-to-databind)

  py-page-elem-to-template-init =
    ?IfTempl(e, elems)
    ; py-page-elem-to-generic(py-page-elems-to-template-init)
    
rules // links

  // link to a url, maybe constant, or obtained from an entity field

  py-page-elem-to-render :
    TemplateCall("navigate", [ThisCall("url",[e])], elems) ->
    python*|[
      out.write('<a href="' + e_link + '" class="navigate">');
      stm*
      out.write('</a>');
    ]|
    where not(ThisCall(p,args) := e)
    with stm* := <py-page-elems-to-render> elems
       ; e_link := <expression-to-python> e

  py-page-elem-to-render :
    TemplateCall("navigate",[call@ThisCall(p,args)],elems) ->
    python*|[
      out.write('<a href="' + e_link + '" class="navigate">');
      stm*
      out.write('</a>');
    ]|
    where <IsPage> p
        ; stm* := <py-page-elems-to-render> elems
        ; e_link := <py-call-to-view-string> call

  py-page-elem-to-render :
    TemplateCall("navigatebutton", [ThisCall("url",[e]), txt], []) ->
    python*|[
      out.write('<input type="button" onclick="location=\'' + e_link + '\';" value="' + e_txt + '"/>');
    ]|
    where not(ThisCall(p,args) := e)
    with e_txt := <expression-to-python> txt
       ; e_link := <expression-to-python> e

  py-page-elem-to-render :
    TemplateCall("navigatebutton",[call@ThisCall(p,args), txt],[]) ->
    python*|[
      out.write('<input type="button" onclick="location=\'' + e_link + '\';" value="' + e_txt + '"/>');
    ]|
    where <IsPage> p
    with e_txt := <expression-to-python> txt
       ; e_link := <py-call-to-view-string> call

  /*
  py-page-elem-to-render :
    TemplateCall("url",[e],[]) ->
    %> <h:outputText value="<%= url %>" /> <%
    where not(ThisCall(p,args) := e)
        ; url := <arg-to-value-string> e

  py-page-elem-to-render :
    TemplateCall("url",[ThisCall(p,args)],[]) ->
//    %> <h:outputText value="http://#{<%= <ThisPage> %>.facesContext.externalContext.getRequestHeaderMap("host")}<%= url %>" /> <%
    %> <h:outputText value="#{<%= <ThisPage> %>.hostName}<%= url %>" /> <%
    where <IsPage> p
        ; fargs := <TemplateArguments> p
        ; params := <zip(bind-param)> (fargs, args)
        ; url := <page-call-to-url> (p, params)

*/

rules // vardecl

  py-page-elem-to-render :
    VarDeclInit(x, s, expr) -> []

  py-page-elem-to-render :
    VarDecl(x, s) -> []

  py-page-elem-to-init : // Don't look inside actions
    Action(_, _, _) -> []

  py-page-elem-to-init :
    VarDeclInit(x, s, expr) ->
    python*|[ 
        self.scope[e_x] = e;
        ]|
    with rules ( IsScoped : x )
       ; e_x := <concat-strings> ["'", x, "'"]
       ; e := <expression-to-python> expr

  py-page-elem-to-init :
    VarDecl(x, s) ->
    python*|[ 
        self.scope[e_x] = e;
        ]|
    with rules ( IsScoped : x )
       ; e_x := <concat-strings> ["'", x, "'"]
       ; e := <python-type-default-value> s

rules

  py-page-elem-to-render :
    TemplateCall("output",[e],[]) ->
    python*|[
        out.write(cgi.escape(unicode(e_target), True));
    ]|
    with e_target := <expression-to-python> e

rules // groups

  py-page-elem-to-render :
    TemplateCall("group", [arg], elems) ->
    python*|[
        out.write('<fieldset><legend>');
        out.write(cgi.escape(unicode(e_text)));
        out.write('</legend><ol>');
        stm*
        out.write('</ol></fieldset>');
    ]|
    with stm* := <py-page-elems-to-render> elems
       ; e_text := <expression-to-python> arg

  py-page-elem-to-render :
    TemplateCall("group", [], elems) ->
    python*|[
        out.write('<fieldset class="fieldset_no_legend_">');
        stm*
        out.write('</fieldset>');
    ]|
    with stm* := <py-page-elems-to-render> elems
    
  py-page-elem-to-render :
    TemplateCall("groupitem", [], elems) ->
    python*|[
        out.write('<li>');
        stm*
        out.write('</li>');
    ]|
    with stm* := <py-page-elems-to-render> elems

rules // label

  py-page-elem-to-render :
    TemplateCall("label", [arg], elems) ->
    python*|[
        out.write('<label for="">');
        out.write(cgi.escape(unicode(e_text)));
        out.write('</label>');
        stm*
    ]|
    with stm* := <py-page-elems-to-render> elems
       ; e_text := <expression-to-python> arg

/*
  py-page-elem-to-render :
    SelectFromList(e1, e2) ->
    python*|[
        field_id = webdsl.utils.generateUniqueFieldName( e_data,  e_fld ,  self );
        out.write('<select multiple="multiple" name="' + field_id + '">');
        for item in e_coll: {
            if item in e_target: {
                out.write('<option selected="selected" ');
            } else: {
                out.write('<option ');
            };
            out.write('value="' + cgi.escape(unicode(item.id), True) + '">');
            out.write(cgi.escape(unicode(item.name), True));
            out.write('</option>');
        };
        out.write('</select>');
    ]|    
    where GenericSort(_, _) := <type-of> e1
    with e_target := <expression-to-python> e1
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e1, "'"]
       ; e_data := <py-get-data-object> e1
       ; e_coll := <expression-to-python> e2

  py-page-elem-to-databind :
    SelectFromList(e1, e2) ->
    python*|[
        field_id = webdsl.utils.generateUniqueFieldName( e_data,  e_fld ,  self );
        if is_submitted_form: {
            e_target = [];
            for item in self.rh.request.params.getall(field_id): {
              e_target.append( 
            e_target = data.x_type.fetch_by_id(self.rh.request.get( field_id ));
        };
    ]|    
    where GenericSort(_, [SimpleSort(s)]) := <type-of> e1
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]
       ; e_data := <py-get-data-object> e
       */
 /*
  py-page-elem-to-render :
    SelectFromList(e1, e2) ->
    %>
       <h:selectManyListbox value="#{<%= x_prop %>}">
          <s:selectItems value="#{<%= x_List %>}"
                         var="<%= x %>" label="#{<%= x %>.name}" />
          <s:convertEntity />
        </h:selectManyListbox>
    <%
    where GenericSort(_, _) := <type-of> e1
        ; x_prop    := <concat-strings> [<arg-to-el> e1, "List"]
        ; x_List    := <concat-strings> [<arg-to-el> e2, "List"]
        ; x         := <newname> "selectvar"

  py-page-elem-to-render :
    SelectFromList(e1, e2) ->
    %>
       <h:selectOneMenu value="#{<%= x_prop %>}">
          <s:selectItems value="#{<%= x_List %>}"
                         var="<%= x %>" label="#{<%= x %>.name}" noSelectionLabel="" />
          <s:convertEntity />
        </h:selectOneMenu>
    <%
    where SimpleSort(_) := <type-of> e1
        ; x_prop    := <concat-strings> [<arg-to-el> e1]
        ; x_List    := <concat-strings> [<arg-to-el> e2, "List"]
        ; x         := <newname> "selectvar"

rules // select

  py-page-elem-to-render :
    Select(x, SimpleSort(s), label, e) ->
    %>
       <h:selectManyListbox value="#{<%= x_prop %>}">
          <s:selectItems value="#{<%= <ThisPage> %>.<%= x_List %>}"
                         var="<%= x %>" label="#{<%= x %>.name}" />
          <s:convertEntity />
        </h:selectManyListbox>
    <%
    where GenericSort(_, _) := <type-of> e
        ; X         := <capitalize-string> x
        ; x_List    := <concat-strings>[x, "List"]
        ; x_prop    := <concat-strings> [<arg-to-el> e, "List"]

  py-page-elem-to-render :
    Select(x, SimpleSort(s), label, e) ->
    %>
       <h:selectOneMenu value="#{<%= x_prop %>}">
          <s:selectItems value="#{<%= <ThisPage> %>.<%= x_List %>}"
                         var="<%= x %>" label="#{<%= x %>.name}" noSelectionLabel="" />
          <s:convertEntity />
        </h:selectOneMenu>
    <%
    where SimpleSort(_) := <type-of> e
        ; X         := <capitalize-string> x
        ; x_List    := <concat-strings>[x, "List"]
        ; x_prop    := <arg-to-el> e

rules // template

  py-page-elem-to-render :
    def |[ define local x(farg*) { elem* } ]| ->
    %>
    
    <ui:define name="<%= x %>">
	  <div id="<%= x %>">
	    <%= elems ::* %>
	  </div>
	</ui:define>
    <%
    with {| InLocalTemplate
          : rules ( InLocalTemplate := x )
          ; elems := <elems-to-xhtml> elem*
          |}
          
rules // template call

  py-page-elem-to-render :
    TemplateCall(name, args, _) ->
    %>
      <ui:insert name="<%= name %>">
         <%= include::* %>
	  </ui:insert>
    <%
    where ( not(<IsPage> <ThisPage>) <+ InLocalTemplate )
//        ; <debug> [">>>>", <IsPage> <ThisPage>, <InLocalTemplate>]
//        ; <debug> ["+++++++++tc: ", name, args]
        ; t* := <map(type-of)> args
//        ; <debug> ["________t*", t*]
        ; if Define(mods, n, fargs, body) := <TemplateSignature> (name, t*) then
            target := <concat-strings> [name, ".xhtml"]
//            ; <debug> ["________", n, fargs]
            ; params := <try(zip(templatecall-args-to-facelets); concat)> (args, fargs)
            ; include := 
              %>
	            <ui:include src="<%= target %>">
    	            <%= params::* %>
		        </ui:include>
		      <%
		  else
		    if Define(mods, n, fargs, body) := <LocalTemplateSignature> (name, t*) then
   		      include := []
   		    end
          end
//        ; <debug> ["_____", n, fargs]

  py-page-elem-to-render :
    TemplateCall(name, args, _) ->
    %>
      <ui:include src="<%= target %>">
        <%= params::* %>
      </ui:include>
    <%
    where is-call-to-toplevel-template ; <IsPage> <ThisPage>
    with target := <concat-strings> [name, ".xhtml"]
       ; Define([Template()], n, fargs, body) := <TemplateSignature> (name, <map(type-of)> args)
       ; params := <try(zip(templatecall-args-to-facelets); concat)> (args, fargs)
    
rules // forms

  py-page-elem-to-render :  // the <f:facet> puts validation errors behind their inputs
    tc@TemplateCall("form", [], elems) ->
    %>
      <h:form enctype="multipart/form-data" att0>
        <%= hidden-ids ::*%>
        <f:facet name="afterInvalidField"><s:message/></f:facet>
        <%= <elems-to-xhtml> elems ::*%>
      </h:form>
    <%
    where if InToplevelTemplate then
            hidden-ids := <mapconcat(arg-to-xhtml)> <bagof-TemplateArguments> <ThisPage>
          else
            hidden-ids := <mapconcat(arg-to-xhtml)> <TemplateArguments> <ThisPage>
          end
        ; if <get-jsf-component-id-1> tc then 
            att0 := @>  id="<%= <get-jsf-component-id-1> tc %>" <@
          else att0:= [] end

  arg-to-xhtml :
    Arg(e, s) -> %> <input type="hidden" name="<%= e %>" value="#{<%= <ThisPage>%>.<%= e%>}" /> <%
    where <builtin-java-type> s

  arg-to-xhtml :
    Arg(i, s) -> %> <input type="hidden" name="<%= e1 %>" value="#{<%= <ThisPage>%>.<%= i%>.id}" /> <%
    where <defined-java-type> s
        ; e1 := <concat-strings>[i, "_id"]

  py-page-elem-to-render :
    TemplateCall("action",[e1,e2],[]) ->
    %>
       <h:commandButton type="submit" value="<%= e1string %>" action="<%= e2string %>"/>
    <%
    where <not(is-cancel-action)> e2
        ; e1string := <arg-to-value-string> e1
        ; e2string := <arg-to-value-string> e2

  py-page-elem-to-render :
    TemplateCall("action",[e1,e2,e3],[]) ->
    %>
       <h:commandButton type="submit" value="<%= e1string %>" action="<%= e2string %>"/>
    <%
    where <not(is-cancel-action)> e2
        ; e1string := <arg-to-value-string> e1
        ; e2string := <arg-to-value-string> e2

  py-page-elem-to-render :
    TemplateCall("action",[e1,e2,e3],[]) ->
    %>
       <s:button type="submit" value="<%= e1string %>" action="<%= e2string %>"/>
    <%
    where <is-cancel-action> e2
        ; e1string := <arg-to-value-string> e1
        ; e2string := <arg-to-value-string> e2

    // @todo this is a big hack! the s:button component invokes the
    // corresponding action, but does not send the form information, does
    // canceling the form edits; however, this should NOT be expressed in the
    // name of the action method

  py-page-elem-to-render :
    TemplateCall("actionLink",[e1,e2],[]) ->
    %>
       <h:commandLink type="submit" value="<%= e1string %>" action="<%= e2string %>"/>
    <%
    where <not(is-cancel-action)> e2
        ; e1string := <arg-to-value-string> e1
        ; e2string := <arg-to-value-string> e2

   is-cancel-action =
     ?Call(Var(component), "cancel", [])

   py-page-elem-to-render :
    TemplateCall("action",[e1,e2],[]) ->
    %>
       <h:commandButton value="<%= e1string %>" action="<%= e2string %>"/>
    <%
    where e1string := <arg-to-value-string> e1
        ; e2string := <arg-to-value-string> e2

   py-page-elem-to-render :
     Action(name, args, body) -> []

rules // captcha

  py-page-elem-to-render :
    TemplateCall("captcha", [], []) ->
    %>
      <div class="captcha">
        <div>
          <h:graphicImage value="/seam/resource/captcha?#{captcha.id}"/>
        </div>
        <div>
          <h:outputLabel for="verifyCaptcha">Enter the above letters</h:outputLabel>
          <h:inputText id="verifyCaptcha" value="#{captcha.response}" required="true">
            <s:validate />
          </h:inputText>
          <div class="validationError"><h:message for="verifyCaptcha"/></div>
        </div>
      </div>
    <%

rules // helper rules for input

  input-of-required-property(|e)=
    if <page-var-access-to-class-prop-tuple;PropertyIsRequired> e
    then
      !"true"
    else
      !"false"
    end

  get-type-of-right-side-field-access:
    fa@FieldAccess(e, f) -> thetype
    where SimpleSort(thetype) := <type-of> fa

  page-var-access-to-class-prop-tuple :
    FieldAccess(e, f) -> (<get-type-of-right-side-field-access> e,f)


rules // output


*/         
