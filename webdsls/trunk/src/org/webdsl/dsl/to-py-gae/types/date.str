module org/webdsl/dsl/to-py-gae/types/date

imports 
  libwebdsl-front

rules

  python-property-type :
    SimpleSort("DateTime") -> py-exp|[ db.DateTimeProperty(auto_now_add=True) ]|

  python-type :
    SimpleSort("DateTime") -> py-exp|[ datetime.datetime ]|

  python-type-default-value :
    SimpleSort("DateTime") -> py-exp|[ datetime.datetime.now() ]|

  python-property-type :
    SimpleSort("Date") -> py-exp|[ db.DateProperty(auto_now_add=True) ]|

  python-type :
    SimpleSort("Date") -> py-exp|[ datetime.date ]|

  python-type-default-value :
    SimpleSort("Date") -> py-exp|[ (datetime.datetime.today()).date() ]|

  python-property-type :
    SimpleSort("Time") -> py-exp|[ db.TimeProperty(auto_now_add=True) ]|

  python-type :
    SimpleSort("Time") -> py-exp|[ datetime.time ]|

  python-type-default-value :
    SimpleSort("Time") -> py-exp|[ (datetime.datetime.now()).time() ]|

rules

  py-page-elem-to-render :
    elem|[ outputDateTime(e){} ]| -> python*|[
      out.write(e_target.strftime('%a, %d %b %Y %H:%M:%S'));
    ]|
    with e_target := <expression-to-python> e

  py-page-elem-to-render :
    elem|[ outputDate(e){} ]| -> python*|[
      out.write(e_target.strftime('%a, %d %b %Y'));
    ]|
    with e_target := <expression-to-python> e

  py-page-elem-to-render :
    elem|[ inputDate(e){} ]| -> python*|[
      field_id = webdsl.utils.generateUniqueFieldName(e_data, e_fld, self);
      out.write('<input type="text" readonly="readonly" name="' + field_id + '" onclick="displayDatePicker(\'' + field_id + '\', this, \'dmy\', \'/\');"/>');
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]
       ; e_data := <py-get-data-object> e

  py-page-elem-to-databind :
    elem|[ inputDate(e){} ]| -> python*|[
          field_id = webdsl.utils.generateUniqueFieldName(e_data, e_fld, self);
          if is_submitted_form: {
              e_target = (datetime.datetime.strptime(self.rh.request.get(field_id), '%d/%m/%Y')).date();
          };
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]
       ; e_data := <py-get-data-object> e

  py-page-elem-to-render :
    elem|[ outputTime(e){} ]| -> python*|[
      out.write(e_target.strftime('%H:%M:%S'));
    ]|
    with e_target := <expression-to-python> e

  call-to-python :
    ThisCall("Date", [e]) ->
    py-exp|[ (datetime.datetime.strptime(e2, '%d/%m/%Y')).date() ]|
    with e2 := <expression-to-python> e

  call-to-python :
    ThisCall("Date", [e, f]) ->
    py-exp|[ (datetime.datetime.strptime(e2, e3)).date() ]|
    with e2 := <expression-to-python> e
       ; e3 := <concat-strings> ["'", <expression-to-python> f, "'"]

  call-to-python :
    ThisCall("DateTime", [e]) ->
    py-exp|[ datetime.datetime.strptime(e2, '%d/%m/%Y %H:%M:%S') ]|
    with e2 := <expression-to-python> e

  call-to-python :
    ThisCall("DateTime", [e, f]) ->
    py-exp|[ datetime.datetime.strptime(e2, e3) ]|
    with e2 := <expression-to-python> e
       ; e3 := <concat-strings> ["'", <expression-to-python> f, "'"]

  call-to-python :
    exp|[ now() ]| -> py-exp|[ datetime.datetime.now() ]|

  call-to-python :
    exp|[ today() ]| -> py-exp|[ datetime.datetime.today() ]|
