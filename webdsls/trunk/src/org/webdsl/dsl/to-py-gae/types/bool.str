module org/webdsl/dsl/to-py-gae/types/bool

imports 
  libwebdsl-front
  
rules // typechecking
  python-property-type :
    SimpleSort("Bool") -> py-exp|[ db.BooleanProperty(default=False) ]|

  python-type :
    SimpleSort("Bool") -> py-exp|[ bool ]|

  python-type-default-value :
    SimpleSort("Bool") -> py-exp|[ False ]|

rules

  py-page-elem-to-render :
    elem|[ inputBool(e){} ]| -> python*|[
          out.write('<input type="checkbox" name="' + e_fld + '" ');
          if e_target: {
            out.write('checked="checked"');
          };
          out.write('/>');
    
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]

  py-page-elem-to-databind :
    elem|[ inputBool(e){} ]| -> python*|[
          if is_submitted_form: {
              e_target = bool(self.rh.request.get(e_fld));
          };
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]

  py-page-elem-to-render :
    elem|[ outputBool(e){} ]| -> python*|[
          if e_target: {
            out.write('Yes');
          }
          else: {
            out.write('No');
          };
    ]|
    with e_target := <expression-to-python> e
