module org/webdsl/dsl/to-py-gae/types/int

imports 
  libwebdsl-front
  
rules

  py-page-elem-to-render :
    elem|[ outputInt(e){} ]| -> python*|[
          out.write(unicode(e_target));
    ]|
    with e_target := <expression-to-python> e

  py-page-elem-to-render :
    elem|[ inputInt(e){} ]| -> python*|[
          field_id = webdsl.utils.generateUniqueFieldName(e_data, e_fld, self);
          out.write('<input type="text" name="' + field_id + '" class="inputInt" value="');
          out.write(unicode(e_target), True);
          out.write('"/>');
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]
       ; e_data := <py-get-data-object> e

  py-page-elem-to-databind :
    elem|[ inputInt(e){} ]| -> python*|[
          field_id = webdsl.utils.generateUniqueFieldName(e_data, e_fld, self);
          if is_submitted_form: {
              e_target = self.rh.request.get(field_id);
          };
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]
       ; e_data := <py-get-data-object> e
