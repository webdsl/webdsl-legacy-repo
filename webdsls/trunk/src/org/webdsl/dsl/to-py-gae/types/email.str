module org/webdsl/dsl/to-py-gae/types/email

imports 
  libwebdsl-front
  
rules
  py-page-elem-to-render :
    elem|[ inputEmail(e){} ]| -> python*|[
          out.write('<input type="text" name="' + e_fld + '" value="');
          out.write(cgi.escape(unicode(e_target), True));
          out.write('"/>');
    
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]

  py-page-elem-to-databind :
    elem|[ inputEmail(e){} ]| -> python*|[
          if is_submitted_form: {
              e_target = self.rh.request.get(e_fld);
          };
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]

  py-page-elem-to-render :
    elem|[ outputEmail(e){} ]| -> python*|[
          out.write('<a href="mailto:' + cgi.escape(unicode(e_target), True) + '">' + cgi.escape(unicode(e_target), True) + '</a>');
    ]|
    with e_target := <expression-to-python> e

