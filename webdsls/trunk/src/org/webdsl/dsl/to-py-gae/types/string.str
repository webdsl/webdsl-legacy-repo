module org/webdsl/dsl/to-py-gae/types/string

imports 
  libwebdsl-front
  
rules
  py-page-elem-to-render :
    elem|[ inputString(e){} ]| -> python*|[
          field_id = webdsl.utils.generateUniqueFieldName(e_data, e_fld, self);
          out.write('<input type="text" name="' + field_id + '" class="inputString" value="');
          out.write(cgi.escape(unicode(e_target), True));
          out.write('"/>');
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]
       ; e_data := <py-get-data-object> e

  py-page-elem-to-databind :
    elem|[ inputString(e){} ]| -> python*|[
          field_id = webdsl.utils.generateUniqueFieldName(e_data, e_fld, self);
          if is_submitted_form: {
              e_target = self.rh.request.get(field_id);
          };
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]
       ; e_data := <py-get-data-object> e

  py-page-elem-to-render :
    elem|[ outputString(e){} ]| -> python*|[
          out.write(cgi.escape(unicode(e_target), True));
    ]|
    with e_target := <expression-to-python> e

