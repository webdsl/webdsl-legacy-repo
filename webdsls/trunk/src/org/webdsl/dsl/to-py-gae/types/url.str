module org/webdsl/dsl/modules/types/url

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules

  python-property-type(|e_name) :
    SimpleSort("URL") -> py-exp|[ db.LinkProperty(default='', name=e_name) ]|

  python-type :
    SimpleSort("URL") -> py-exp|[ unicode ]|

  python-type-default-value :
    SimpleSort("URL") -> py-exp|[ '' ]|

rules
  py-page-elem-to-render :
    elem|[ inputURL(e)[]{} ]| -> python*|[
          out.write('<input type="text" class="inputURL" name="' + e_fld + '" value="');
          out.write(cgi.escape(unicode(e_target), True));
          out.write('"/>');
    
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]

  py-page-elem-to-databind :
    elem|[ inputURL(e)[]{} ]| -> python*|[
          if is_submitted_form: {
              e_target = self.rh.request.get(e_fld);
          };
    ]|
    with e_target := <expression-to-python> e
       ; e_fld := <concat-strings> ["'", <pp-webdsl> e, "'"]

  py-page-elem-to-render :
    elem|[ outputURL(e)[]{} ]| -> python*|[
          out.write('<a href="' + cgi.escape(unicode(e_target), True) + '">' + cgi.escape(unicode(e_target), True) + '</a>');
    ]|
    with e_target := <expression-to-python> e

  /*
  py-page-elem-to-render :
    elem|[ url(e)[]{} ]| -> python*|[
          out.write('<a href="' + cgi.escape(unicode(e_target), True) + '">' + cgi.escape(unicode(e_target), True) + '</a>');
    ]|
    with e_target := <expression-to-python> e
    */

  py-page-elem-to-render :
    TemplateCall("url",[call@ThisCall(p, args)],[], []) ->
    python*|[
      out.write(self.rh.request.host_url + e_link);
    ]|
    where e_link := <py-call-to-view-string> call
