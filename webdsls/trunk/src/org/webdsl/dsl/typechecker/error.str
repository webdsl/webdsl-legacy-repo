module org/webdsl/dsl/typechecker/error

imports
  libstrategolib
  Java-15 
  libjava-front

imports
  libwebdsl-front

strategies
  add-error(|msgs) =
    !msgs
    ; concat-strings
    ; ?msg
    ; errors := <inc> <FoundErrors <+ !0>
    ; info(|["Error: ", msg])
    ; rules ( FoundErrors := errors )


  pp-webdsl = pp-webdsl-to-string
/*
  add-error =
    not(?[m|rest])
    ; ?msg
    ; rules ( Errors :+= msg )
    */

rules
  constraint-error :
    VarDeclInit(_, srt, e) -> <add-error(|[<pp-webdsl> e, " should be of type ", <pp-webdsl> srt])>
    where not(srt := <type-of> e)

  // will change with additions proposed by Jippe, 
  // also currently no overloading of templates so just matching names
  // these are the built-ins in the generator, types can specify their own constraints for template defs   
  constraint-error : 
    d@webdsl|[ define x(farg*) { elem* } ]| -> <add-error(|["redefinition of this built-in template is not allowed: ",<pp-webdsl> d])>
    where <elem> (x,[ "menu"
                     ,"menuheader"
                     ,"menuitem"
                     ,"div"
                     ,"block"
                     ,"text"
                     ,"spacer"
                     ,"image"
                     ,"menubar"
                     ,"navigate"
                     ,"menuspacer"
                     ,"table"
                     ,"header"
                     ,"list"
                     ,"listitem"
                     ,"section"
                     ,"par"
                     ,"container"
                     ,"row"
                     ,"navigateButton"
                     ,"url"
                     ,"form"
                     ,"action"
                     ,"actionLink"
                     ,"captcha"
                     ,"input"
                     ,"output"
                    ])
