module org/webdsl/dsl/typechecker/rename

/**
 * Renaming
 * --
 * At the start of the rewriting process, all variables are renamed by annotating them, to make them unique.
 * Declaring is part of the renaming process
 */

imports
  libstrategolib

imports
  libwebdsl-front

rules 

  rename-all = alltd(rename)

  rename = Rename

  /**
   * rename-bound does the actual renaming. The other rules guide the application of rename-bound
   * 
   * NOTE: Qualified and Assignment are anomalies: because of the non-expression nature of left-hand sides of assignments 
   *   (which Qualified is used for), variables like a are parsed as "a", instead of Var("a"). Therefore, 
   *   the renaming step must take this into account, so that the leftmost item in a Qualified is rewritten
   *   as Vars are.
   */
  rename-bound(|srt) :
    x -> y
    with  y := x{<newname> x}
        ; rename-bound-rules(|x,y,srt) 

  rename-bound-rules(|x,y,srt) =
     rules ( Rename : Var(x) -> Var(y)
             Rename : Qualified(x, z) -> Qualified(y, z)
             Rename : Stat(Assignment(x, e)) -> Stat(Assignment(y, <rename-all> e))
             TypeOf : y -> srt
             )

  rename :
    VarDecl(x, srt) -> VarDecl(y, srt)
    with y := <rename-bound(|srt)> x

  rename :
    VarDeclInit(x, srt, e) -> VarDeclInit(y, srt, e')
    where InDef
       ; e' := <rename-all> e
       ; y := <rename-bound(|srt)> x

  rename :
    VarDeclInit(x, srt, e) -> VarDeclInit(x, srt, e')
    where not(InDef)
    with e' := <rename-all> e
       ; rules ( TypeOf : x -> srt )
       ; rules ( IsGlobal : x )

  rename :
    def |[ define page x(farg1*) { elem1* } ]| -> <declare-page-definition> def |[ define page x(farg2*) { elem2* } ]|
    with {| Rename, InDef, InPage, RenameActionCall
          : rules ( InDef := True() )
          ; rules ( InPage := x )
          ; farg2* := <map(rename)> farg1*
          ; elem2* := <alltd(rename-action); rename-all> elem1*
          ; <try(alltd(rename))> elem1*
          ; rules ( TypeOf : x -> "Component" )
          |}

  rename :
    def |[ define mod* x(farg1*) { elem1* } ]| -> <declare-toplevel-template-definition> def |[ define mod* y(farg2*) { elem2* } ]|
    where not(InPage)
        ; not(<elem> (Page(), mod*))
        ; not(<elem> (Email(), mod*))
    with {| Rename, InDef, InTemplate, RenameActionCall
          : rules ( InDef := True() )
          ; rules ( InTemplate := x )
          ; y := x{<newname> x}
          ; farg2* := <map(rename; declare-template-arg)> farg1*
          ; elem2* := <alltd(rename-action); rename-all> elem1*
          |}
        
  rename :
    def |[ define mod* x(farg1*) { elem1* } ]| -> <declare-local-template-definition> def |[ define mod* y(farg2*) { elem2* } ]|
    where InPage
        ; not(<elem> (Page(), mod*))
        ; not(<elem> (Email(), mod*))
        ; not(InProcedure)
    with {| Rename, RenameActionCall
         : y := x{<newname> x}
         ; farg2* := <map(rename)> farg1*
         ; elem2* := <alltd(rename-action); rename-all> elem1*
         |}

  rename :
    def |[ define mod* x(farg1*) { elem1* } ]| -> <declare-local-template-definition> def |[ define mod* y(farg2*) { elem2* } ]|
    where InPage; InProcedure
        ; not(<elem> (Page(), mod*))
        ; not(<elem> (Email(), mod*))
    with {| RenameActionCall
         : y := x{<newname> x}
         ; farg2* := <map(rename)> farg1*
         ; elem2* := <alltd(rename-action); rename-all> elem1*
         |}

  rename :
    Function(x, args1, srt, b1) -> <declare-global-func <+ debug> Function(x, args2, srt, b2)
    where not(InEntity)
    with {| Rename, InDef
          : rules ( InDef := True() )
          ; args2 := <map(rename)> args1
          ; b2 := <rename-all> b1
          |}

  rename :
    FunctionNoReturn(x, args1, b1) -> <declare-global-func <+ debug> FunctionNoReturn(x, args2, b2)
    where not(InEntity)
    with {| Rename, InDef
          : rules ( InDef := True() )
          ; args2 := <map(rename)> args1
          ; b2 := <rename-all> b1
          |}

  rename :
    Function(x, args1, srt, b1) -> Function(x, args2, srt, b2)
    where InEntity
    with {| Rename, InDef
          : rules ( InDef := True() )
          ; args2 := <map(rename)> args1
          ; b2 := <rename-all> b1
          |}

  rename :
    FunctionNoReturn(x, args1, b1) -> FunctionNoReturn(x, args2, b2)
    where InEntity
    with {| Rename, InDef
          : rules ( InDef := True() )
          ; args2 := <map(rename)> args1
          ; b2 := <rename-all> b1
          |}

  rename :
    Entity(x_class, s, props1, functions1) -> <declare-entity> Entity(x_class, s, props2, functions2)
    with {| Rename, InEntity
          : rules ( InEntity := x_class )
          ; y := <rename-bound(|SimpleSort(x_class))> "this"
          ; props2 := <rename-all> props1
          ; functions2 := <rename-all> functions1
          |}

  rename :
    EntityNoSuper(x_class, props1, functions1) -> <declare-entity> EntityNoSuper(x_class, props2, functions2)
    with {| Rename, InEntity
          : rules ( InEntity := x_class )
          ; y := <rename-bound(|SimpleSort(x_class))> "this"
          ; props2 := <rename-all> props1
          ; functions2 := <rename-all> functions1
          |}

  rename :
    SessionEntity(nm, props1, functions1) -> <declare-entity> SessionEntity(nm, props2, functions2)
    with {| Rename, InEntity
          : rules ( InEntity := nm )
          ; x_class := <capitalize-string> nm
          ; y := <rename-bound(|SimpleSort(x_class))> "this"
          ; props2 := <rename-all> props1
          ; functions2 := <rename-all> functions1
          |}
           
  rename :         
    ExtendSessionEntity(x_class, props1, funcs1) -> <declare-extend-entity> ExtendSessionEntity(x_class, props2, funcs2) 
    with {| Rename, InEntity
          : rules ( InEntity := x_class )
          // removed because extend session matches on original name: x_class := <capitalize-string> x_class1 
          ; y := <rename-bound(|SimpleSort(<capitalize-string> x_class))> "this"
          ; props2 := <rename-all> props1
          ; funcs2 := <rename-all> funcs1
          |}

  rename :
    ExtendEntity(x_class, props1, funcs1) ->  <declare-extend-entity> ExtendEntity(x_class, props2, funcs2) 
    with {| Rename, InEntity
          : rules ( InEntity := x_class )
          ; y := <rename-bound(|SimpleSort(x_class))> "this"
          ; props2 := <rename-all> props1
          ; funcs2 := <rename-all> funcs1
          |}

  rename-action = ?Define(_, _, _, _) // End of scope

  rename-action :
    Action(nm1, args1, b1) -> Action(nm2, args2, b2)
    with {| Rename
          : args2 := <map(rename)> args1
          ; b2 := <rename-all> b1
          |}
       ; nm2 := nm1{<newname> nm1}
       ; rules ( RenameActionCall :
                  ThisCall(nm1, a1*){anno*} -> ThisCall(nm2, a2*){anno*}
                  with  a2* := <rename-all> a1* )
       ; <declare-action> Action(nm2, args2, b1)

  rename = RenameActionCall

  rename :
    For(x, srt, e1, f1, elems1) -> For(y, srt, e2, f2, elems2)
    with {| Rename
          : y := <rename-bound(|srt)> x
          ; e2 := <rename-all> e1
          ; f2 := <rename-all> f1
          ; elems2 := <rename-all> elems1
          |}

  rename :
    ForNoFilter(x, srt, e1, elems1) -> ForNoFilter(y, srt, e2, elems2)
    with {| Rename
          : y := <rename-bound(|srt)> x
          ; e2 := <rename-all> e1
          ; elems2 := <rename-all> elems1
          |}

  rename :
    ForSep(x, srt, e1, f1, elems1, elems2) -> ForSep(y, srt, e2, f2, elems3, elems4)
    with {| Rename
          : y := <rename-bound(|srt)> x
          ; e2 := <rename-all> e1
          ; f2 := <rename-all> f1
          ; elems3 := <rename-all> elems1
          |}
          ; elems4 := <rename-all> elems2 // separator cannot refer to loop index

  rename :
    ForSepNoFilter(x, srt, e1, elems1, elems2) -> ForSepNoFilter(y, srt, e2, elems3, elems4)
    with {| Rename
          : y := <rename-bound(|srt)> x
          ; e2 := <rename-all> e1
          ; elems3 := <rename-all> elems1
          |}
          ; elems4 := <rename-all> elems2 // separator cannot refer to loop index

  rename :
    ForAll(x, srt, f1, elems1) -> ForAll(y, srt, f2, elems2)
    with {| Rename
          : y := <rename-bound(|srt)> x
          ; f2 := <rename-all> f1
          ; elems2 := <rename-all> elems1
          |}


  rename :
    ForAllNoFilter(x, srt, elems1) -> ForAllNoFilter(y, srt, elems2)
    with {| Rename
          : y := <rename-bound(|srt)> x
          ; elems2 := <rename-all> elems1
          |}


  rename :
    ForStmt(x, srt, e1, f1, b1) -> ForStmt(y, srt, e2, f2, b2)
    with {| Rename
          : y := <rename-bound(|srt)> x
          ; e2 := <rename-all> e1
          ; f2 := <rename-all> f1
          ; b2 := <rename-all> b1
          |}

  rename :
    ForStmtNoFilter(x, srt, e1, b1) -> ForStmtNoFilter(y, srt, e2, b2)
    with {| Rename
          : y := <rename-bound(|srt)> x
          ; e2 := <rename-all> e1
          ; b2 := <rename-all> b1
          |}

  rename :
    ForAllStmt(x, srt, f1, b1) -> ForAllStmt(y, srt, f2, b2)
    with {| Rename
          : y := <rename-bound(|srt)> x
          ; f2 := <rename-all> f1
          ; b2 := <rename-all> b1
          |}

  rename :
    ForAllStmtNoFilter(x, srt, b1) -> ForAllStmtNoFilter(y, srt, b2)
    with {| Rename
          : y := <rename-bound(|srt)> x
          ; b2 := <rename-all> b1
          |}

  rename :
    ForExp(oe1, x, s, ie1, f1) -> ForExp(oe2, y, s, ie2, f2)
    with  {| Rename
           : y := <rename-bound(|s)> x
           ; oe2 := <rename-all> oe1
           ; f2 := <rename-all> f1
           |}
          ; ie2 := <rename-all> ie1

  rename :
    ForExpNoFilter(oe1, x, s, ie1) -> ForExpNoFilter(oe2, y, s, ie2)
    with {| Rename
          : y := <rename-bound(|s)> x
          ; oe2 := <rename-all> oe1
          |}
         ; ie2 := <rename-all> ie1

  rename :
    Select(x, s, lab1, e1) -> Select(y, s, lab2, e2) 
    with {| Rename
            : y := <rename-bound(|s)> x
            ; e2 := <rename-all> e1
            ; lab2 := <rename-all> lab1
          |} 

  rename :
    Arg(x, srt) -> Arg(y, srt)
    with y := <rename-bound(|srt)> x
  
  rename :
    InitAction(bl) -> InitAction(bl2)
    where not(InDef)
    with {|InDef :
          rules (InDef := True())
          ; bl2 := <rename-all> bl
        |}
  
  //template call id renaming
   
  rename :
    tc@TemplateCallPropsNoBody(x1, e1*,passign*) -> TemplateCallPropsNoBody(x1, e2*,passign2*)
    with  e2* := <rename-all> e1*
        ; passign2* := <rename-all> passign*
        ; <declare-template-html-id(|tc)> passign*

  rename :
    tc@TemplateCallProps(x1, passign*) -> TemplateCallProps(x1, passign2*)
    with passign2* := <rename-all> passign*
        ; <declare-template-html-id(|tc)> passign*

  rename :
    tc@TemplateCallPropsNoArgs(x1, passign*, elem1*) -> TemplateCallPropsNoArgs(x1,passign2*, elem2*)
    with  elem2* := <rename-all> elem1*
        ; passign2* := <rename-all> passign*
        ; <declare-template-html-id(|tc)> passign*
    
  rename :
    tc@TemplateCall(x1, e1*,passign*, elem1*) -> TemplateCall(x1, e2*,passign2*, elem2*)
    with  e2* := <rename-all> e1*
        ; elem2* := <rename-all> elem1*
        ; passign2* := <rename-all> passign*        
        ; <declare-template-html-id(|tc)> passign*
