module org/webdsl/dsl/typechecker/collection

imports
  libstrategolib

imports
  libwebdsl-front

rules

   type-compatible =
    ?(GenericSort(a,types1), GenericSort(b, types2))
    ; where (  a2 := <strip-annos> a
             ; a2 := <strip-annos> b
             ; <zip; map(type-compatible)>(types1, types2)
            )

   eq-type-of :
     SetCreation(e*) -> GenericSort("Set", [t])
     where t := <Hd; type-of> e*
         
   eq-type-of :
     TypedSetCreation(s, e*) -> GenericSort("Set", [s])
     where <map(<type-compatible>(s, <type-of>))> e*
 
   eq-type-of :
     TypedListCreation(s, e*) -> GenericSort("List", [s])
     where <map(<type-compatible>(s, <type-of>))> e*
         
   eq-type-of :
     ListCreation(e*) -> GenericSort("List", [t])
     where t := <Hd; type-of> e*

rules // fields, operations

   eq-type-of :
     FieldAccess(e1, "length") -> SimpleSort("Int")
     where GenericSort(t,_) := <type-of> e1
         ; ("List" := t <+ "Set" := t)
     
   check-builtin-signature :
     (GenericSort("List", [s]), "add", [s]) -> SimpleSort("void")

   check-builtin-signature :
     (GenericSort("List", [s]), "remove", [s]) -> SimpleSort("void")

   check-builtin-signature :
     (GenericSort("List", [s]), name, [s]) -> SimpleSort("void")
     where <is-substring("remove")> name

   check-builtin-signature :
     (GenericSort("Set", [s]), "add", [s]) -> SimpleSort("void")

   check-builtin-signature :
     (GenericSort("Set", [s]), "addAll", [GenericSort("Set", [s])]) -> SimpleSort("void")

   check-builtin-signature :
     (GenericSort("Set", [s]), "remove", [s]) -> SimpleSort("void")

   check-builtin-signature :
     (GenericSort("Set", [s]), "list", []) -> GenericSort("List", [s])

   desugar :
     |[ e.list() ]| -> |[ List<srt>().addAll(e) ]|
     where GenericSort("Set", [srt]) := <type-of> e

   desugar :
     |[ e.list() ]| -> |[ Set<srt>().addAll(e) ]|
     where GenericSort("List", [srt]) := <type-of> e

   check-builtin-signature :
     (GenericSort("List", [s]), "addAll", [GenericSort("List", [s])]) -> SimpleSort("void")

   check-builtin-signature :
     (GenericSort("List", [s]), "set", []) -> GenericSort("Set", [s])

   check-builtin-signature :
     (_, name, [s]) -> SimpleSort("void")
     where <is-substring("remove")> name

   check-builtin-signature :
     (_, name, [s]) -> SimpleSort("void")
     where <is-substring("add")> name
         
