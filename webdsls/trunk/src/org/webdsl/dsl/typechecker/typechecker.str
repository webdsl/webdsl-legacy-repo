/**

  Type rules for WebDSL

*/

module org/webdsl/dsl/typechecker/typechecker

imports
  libstrategolib
  Java-15 
  libjava-front

imports
  libwebdsl-front

signature
  constructors
    Type    : Sort         -> Annotation
    Error   : List(String) -> Error
    Warning : List(String) -> Warning
 
strategies

  typecheck =
      not(built-in-template-rule-setup
          ; <debug> "error in typechecker.str, failure should not occur here, make sure built-in-template-setup rules end with ';fail'"
          ; <exit> 1) // setup dyn rules for checks
    ; declare-all //declares globally visible webdsl elements
    ; rename-all
    ; where(bottomup(try(constraint-error)))
    ; (not(FoundErrors) <+ (<exit> 1))

  constraint-error-helper =
    ?t
  ; constraint-error
  ; try(
      ?Error(m); <add-error(|m)> t
    + ?Warning(m); <add-warning(|m)> t
    )
  
rules

  type-of-query :
    QueryRule(
      SelectFrom(Some(Select(_,[AliasedExpression(Path([x]),_)])), FromClause(ranges)),
      _, _, _) -> GenericSort("List", [t])
    where t := <fetch-elem(get-type-from-range(|x))> ranges

  get-type-from-range(|x) :
    FromRangeJoin(FromClass(Path([x_Class]),Some(AsAlias(Alias(x))),_),_) -> SimpleSort(x_Class)
