module org/webdsl/dsl/typechecker/object-creation

imports
  libstrategolib
  Java-15 
  libjava-front

imports
  libwebdsl-front
  org/webdsl/dsl/typechecker/utils
  org/webdsl/dsl/typechecker/declare

rules

  constraint-error :
    oc@ObjectCreation(SimpleSort(x_class),[propassign*]) -> oc //<add-error(|["Error in object creation of class '",x_class,"'"])> 
    where <fetch(check-object-creation-property-assignment-1(|x_class))> propassign*
       <+ <fetch(check-object-creation-property-assignment-2(|x_class))> propassign*

  check-object-creation-property-assignment-1(|x_class) :
    ObjectPropertyAssignment(ident,exp) -> <add-error(|["Entity '",x_class,"' does not contain a property '",ident,"'"])> 
    where not(<type-of-property> (x_class,ident))

  check-object-creation-property-assignment-2(|x_class) :
    ObjectPropertyAssignment(ident,exp) -> <add-error(|["Type of expression '",srt_exp,"' is incompatible with type of entity property '",srt_prop,"'"])> 
    where srt_prop := <type-of-property> (x_class,ident)
        ; srt_exp := <type-of> exp
        ; not(<type-compatible> (srt_prop,srt_exp))

rules        

  eq-type-of :
    ObjectPropertyAssignment(y, e) -> SimpleSort("Void") //<type-of> y