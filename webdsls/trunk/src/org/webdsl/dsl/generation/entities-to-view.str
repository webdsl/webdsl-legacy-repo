module org/webdsl/dsl/generation/entities-to-view

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  
imports 
  org/webdsl/dsl/syntax/WebDSL
  org/webdsl/dsl/generation/utils
  org/webdsl/dsl/generation/entity-to-java
  
strategies

  entities-to-view :
    Application(qid, sections) -> 
      [<collect(entity-to-edit-view <+ (entity-to-view-fails; fail))> sections]
    where domainpkg := <concat-strings> [ qid, ".domain"]
        ; rules( 
            Package : _ -> qid 
            DomainPackage : _ -> domainpkg
          )

strategies
          
  entity-to-view-fails =
    (?EntityNoSuper(_, _) <+ ?Entity(_,_,_))
    ; err(|"cannot generate view for entity")

strategies
    
  entity-to-edit-view : 
    EntityNoSuper(x_class, props) -> XmlFile("view", x_class, <xml-composition-wrap> 
    %><ui:define name="body">
        <h1>Edit <%= Text(Literal(x_class)) %></h1>
        <h:messages globalOnly="true" styleClass="message"/>
        <h:form id="edit<%= x_class %>">
          <div class="dialog">
            <s:validateAll><%= flds %></s:validateAll>
          </div>
          <div class="actionButtons">
            <h:commandButton id="register" value="register" 
                action="#{edit_x_class.save}"/>     			  
          </div>
        </h:form>
      </ui:define><% )
    where flds := <filter(property-to-edit-component(|"edit x_class") 
                          <+ debug(!"cannot generate code for property: "); fail)> props
	    
  property-to-edit-component(|component) :
    Property(x_prop, SimpleSort(y), []) -> 
    %><div class="prop">
         <span class="x_prop">x_prop</span>
            <span class="value">
               <s:decorate>
                  <h:inputText id="x_prop" required="true"
                       value="#{component.x_prop}"/>
               </s:decorate>
            </span>
       </div><%
       
  xml-composition-wrap : 
    content -> 
    %><!DOCTYPE composition PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" 
                             "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
        <ui:composition xmlns="http://www.w3.org/1999/xhtml"
                xmlns:s="http://jboss.com/products/seam/taglib"
                xmlns:ui="http://java.sun.com/jsf/facelets"
                xmlns:f="http://java.sun.com/jsf/core"
                xmlns:h="http://java.sun.com/jsf/html"
                template="layout/template.xhtml"><%= content %></ui:composition><%

   