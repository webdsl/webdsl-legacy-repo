/*

  @todo input-wrap or xtc-input-wrap

  @todo use sglr library

  @todo derive filename from compilation unit
 
*/

module org/webdsl/dsl/generation/dsl-to-seam

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  
imports 
  org/webdsl/dsl/syntax/WebDSL
  org/webdsl/dsl/generation/utils
  org/webdsl/dsl/generation/entity-to-java
  org/webdsl/dsl/generation/entities-to-view

strategies 

  main = 
   io-wrap(
     desugar
     ; where(alltd(declare-entity))
     ; !(<entities-to-java>, <entities-to-view>)
     ; topdown(try(java-file-to-file <+ xml-file-to-file))
   )
strategies // desugaring

  desugar = 
    topdown(try(Desugar))

  Desugar : 
    PropertyNoAnno(x, s) -> Property(x, s, [])
    
strategies // register all declared entities
        
  declare-entity =
    (?EntityNoSuper(x_class, _) <+ ?Entity(x_class, _, _))
    ; rules( defined-java-type : SimpleSort(x_class) -> type|[ x_class ]| )
    
strategies

  java-type =
    defined-java-type <+ builtin-java-type <+ err(|"undefined entity: "); fail

  builtin-java-type : 
    SimpleSort("Int") -> type|[ int ]|
   
  builtin-java-type : 
    SimpleSort("Bool") -> type|[ boolean ]|

  builtin-java-type : 
    SimpleSort("String") -> type|[ String ]|

  builtin-java-type : 
    SimpleSort("Secret") -> type|[ String ]|

  builtin-java-type : 
    SimpleSort("Text") -> type|[ String ]|

  builtin-java-type : 
    SimpleSort("Date") -> type|[ Date ]|
    
  builtin-java-type :
    GenericSort("List", [s]) -> type|[ List<t> ]|
    where t := <java-type> s
    
  builtin-java-type :
    GenericSort("Set", [s]) -> type|[ Set<t> ]|
    where t := <java-type> s

  builtin-java-type :
    GenericSort("Map", [s1, s2]) -> type|[ List<t1, t2> ]|
    where t1 := <java-type> s1
        ; t2 := <java-type> s2