module org/webdsl/dsl/generation/xhtml-email

imports 
  libstrategolib 
  
imports 
  org/webdsl/dsl/syntax/WebDSL
  org/webdsl/dsl/utils/-
    
rules
  
  emails-to-xml =
    ?def@Define([Email()], x, args, elems1)
    ; email_calls := <EmailCallList> x
    ; <map(construct-email(|def))> email_calls
  
  construct-email(|def) =
    call_from_page := <id>
    ; <email-to-xml(|call_from_page)> def
  
  email-to-xml(|call_from_page) : 
    Define([Email()], x, fargs, elems1) ->
      XmlFile(["view"], <concat-strings>[template_name, ".xhtml"], 
		%>
		<?xml version="1.0" encoding="UTF-8"?>
		
		<m:message xmlns="http://www.w3.org/1999/xhtml"
		           xmlns:m="http://jboss.com/products/seam/mail"
		           xmlns:h="http://java.sun.com/jsf/html">
		    <%= elems3 ::*%>
		</m:message>
		<%)
  where (template_name, page) := <EmailCallToTemplateName> call_from_page
      ; ThisCall("email", [ThisCall(name2, aargs)]) := call_from_page
      ; {|
          ArrayIndex : bargs := <map(parameters-to-array-calls(|page))> aargs
        |}
      ; arg_list := <zip> (fargs, bargs)
      ; elems2 := <alltd(transform-variables(|arg_list))> elems1
      ; elems3 := <email-elems-to-xhtml> elems2

  transform-variables(|arg_list) :
    Var(name) -> exp
    where <collect(?(Arg(name, _), exp))> arg_list

  parameters-to-array-calls(|x_page) =
    rules( ArrayIndex := <(ArrayIndex; inc) <+ !0> )
    ; x_parm := <concat-strings>[<GetEmailArgumentsList> x_page, "[", <ArrayIndex; int-to-string>, "]"] 
    ; !FieldAccess(Var(x_page){Type("Component")}, x_parm)

  email-elems-to-xhtml =
    filter-concat-warn(email-elem-to-xhtml <+ undefined-template-call | "email-elem-to-xhtml undefined: ")
  
  email-elem-to-xhtml :
    TemplateCall("to", [elem], []) ->
    %>
      <m:to address="<%= email_address %>" />
    <%
    where email_address := <arg-to-value-string> elem
  
  email-elem-to-xhtml :
    TemplateCall("from", [String(name), String(email)], []) ->
    %>
      <m:from name="<%= name %>" address="<%= email %>" />
    <%
    
  email-elem-to-xhtml :
    TemplateCall("subject", [String(subject)], []) ->
    %>
      <m:subject>
        <%= subject::cdata %>
      </m:subject>
    <%
  
  email-elem-to-xhtml :
    TemplateCall("body", [], elems) ->
    %>
      <m:body>
        <%= <elems-to-xhtml> elems::* %>
      </m:body>
    <%
