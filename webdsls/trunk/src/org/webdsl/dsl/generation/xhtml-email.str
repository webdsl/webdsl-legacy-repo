module org/webdsl/dsl/generation/xhtml-email

imports 
  libstrategolib 
  
imports 
  org/webdsl/dsl/syntax/WebDSL
  org/webdsl/dsl/utils/-
    
rules
  
  emails-to-xml =
    ?def@Define([Email()], x, args, elems1)
    ; email_calls := <EmailCallList> x
    ; <map(construct-email(|def))> email_calls
  
  construct-email(|def) =
    call_from_page := <id>
    ; <email-to-xml(|call_from_page)> def
  
  email-to-xml(|call_from_page) : 
    Define([Email()], x, fargs, elems1) ->
      XmlFile(["view"], <concat-strings>[template_name, ".xhtml"], 
		%>
		<?xml version="1.0" encoding="UTF-8"?>
		
		<m:message xmlns="http://www.w3.org/1999/xhtml"
		           xmlns:m="http://jboss.com/products/seam/mail"
		           xmlns:h="http://java.sun.com/jsf/html">
		    <%= elems3 ::*%>
		</m:message>
		<%)
  where (template_name, page) := <EmailCallToTemplateName> call_from_page
      ; ThisCall("email", [ThisCall(name2, aargs)]) := call_from_page
      ; arg_list := <zip> (fargs, aargs)
      ; elems2 := <alltd(transform-variables(|arg_list))> elems1
      ; elems3 := <email-elems-to-xhtml(|page)> elems2

  transform-variables(|arg_list) :
    Var(name) -> exp
    where <collect(?(Arg(name, _), exp))> arg_list

  email-elems-to-xhtml(|page) =
    filter-concat-warn(email-elem-to-xhtml(|page) <+ undefined-template-call | "email-elem-to-xhtml undefined: ")
  
  email-elem-to-xhtml(|page) :
    TemplateCall("to", [elem], []) ->
    %>
      <m:to address="#{<%= page %>._email_to_address}" />
    <%
 //   where email_address := <arg-to-value-string> elem
  
  email-elem-to-xhtml(|page) :
    TemplateCall("from", [String(name), String(email)], []) ->
    %>
      <m:from name="<%= name %>" address="<%= email %>" />
    <%
    
  email-elem-to-xhtml(|page) :
    TemplateCall("subject", [String(subject)], []) ->
    %>
      <m:subject>
        <%= subject::cdata %>
      </m:subject>
    <%
  
  email-elem-to-xhtml(|page) :
    TemplateCall("body", [], elems) ->
    %>
      <m:body>
        <%= <elems-to-xhtml> elems::* %>
      </m:body>
    <%
