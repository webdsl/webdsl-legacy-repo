module org/webdsl/dsl/utils/stage

imports
  libstrategolib

strategies

  stage(s|msg) = {| StageNewLinePrinted:
    rules( Stage := <Stage; inc> )
  ; where(stagemsg := <concat-strings>["stage ", <Stage;int-to-string>, ": ", <try(is-list;concat-strings)> msg])
  ; print-stage(|stagemsg)
  ; log-timed(s|stagemsg)
  ; if <geq>(<Stage>, <OptionStage>) then output-webdsl end
  |}
  
  stage-newline-once =
    where(try(not(StageNewLinePrinted); <debug> ""; rules(StageNewLinePrinted := True())))
  
  print-stage(|msg) =
    where(
      verbose-1(<concat-strings ; log-puts> ["[ ", <log-src>, " | ",  <severity-string> Info(), " ] ", msg ])
    )
  
  print-stage-time(|msg) =
    where(verbose-1(
      <log-puts> msg
    ; <log-puts> "\n"
    ))
  
  /**
   *  from stratego compiler source /strc-core/lib/stratego/strc/strc/strc.str
   */
  log-timed(s|msg)=//|msg,level) =
    //if <geq>(<get-config> "--statistics", level) then
      where(times => starttime)
    ; s
    ; where(
        <diff-times>(<times>,starttime)
      ; <concat-strings>[ //<align-left>(' ', msg, 27)
                          <align-helper(|' ', 40)> msg
                        , "[ "
                        , <self-children-user-time; ticks-to-seconds ; real-to-string(|2)>
                        , "s ]"
                        ]
      ; print-stage-time(|<id>)
      )
    //else
    //  s
    //end
   
  /**
   *  Helper strategy for finding performance bottlenecks
   */  
  log-time(s|msg)=
    if-statistics(log-time-helper(s|msg),s)
  
  log-time-helper(s|msg)=
    {| LogTimeSection :
      where(enclosingsection := <LogTimeSection <+ !None()>)
    ; rules(LogTimeSection := msg )
    ; where(times => starttime)
    ; s
    ; where(
        <diff-times>(<times>,starttime)
      ; ?time
      ; oldtime := <LogTime <+ !(0,0,0,0)> msg
      ; newtime := <add-times> (time, oldtime)
      ; rules( LogTime : msg -> newtime )
        // Be careful not to create a new rule for each log-time call
      ; where(
          None() := enclosingsection; try(not(<LogTimesKey> msg); rules( LogTimesKey : msg ))
        <+ !(enclosingsection, msg); try(not(LogTimesKeySection); ?pair; rules(LogTimesKeySection : pair)))
      )
    |}
      
  // After clearing the rules with :-, all-keys-RuleName still returns the old keys (which then have no values).
  // The filter prevents these unused keys from being printed.
  print-total-log-time =
    where(all-keys-LogTimesKey; filter(LogTimesKey); map(print-total-log-time-helper(|"")))
      // clear times
    ; all-keys-LogTimesKey; map(\key -> <rules(LogTimesKey :- key)>\)
    ; all-keys-LogTimesKeySection; map(\key -> <rules(LogTimesKeySection :- key)>\)
    ; all-keys-LogTime; map(\key -> <rules(LogTime :- key)>\) 
    
  print-total-log-time-helper(|prefix) :
    x -> <concat-strings;debug;<print-total-log-time-subsections(|prefix)> x> [prefix,res]
    where total := <LogTime> x    // where: after calling print-total-log-time, LogTime is undefined
    with  t1 := <self-children-user-time; ticks-to-seconds; real-to-string(|2)> total
        ; t2 := <self-children-sys-time; ticks-to-seconds; real-to-string(|2)> total
        ; msg := <concat-strings> ["total time ",x]
        ; res := <concat-strings> [msg,<align-helper(|' ', 40)> msg,": [",t1,"s]"]
        
  print-total-log-time-subsections(|prefix): x -> x
    with  all-keys-LogTimesKeySection; filter(LogTimesKeySection); filter(?(x,<id>))
        ; map(print-total-log-time-helper(|<concat-strings> ["  ",prefix]))
            