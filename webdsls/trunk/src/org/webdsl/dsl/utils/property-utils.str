module org/webdsl/dsl/languages/data-model/property-utils

  /**
   *  generic property rules/strategies (work on all property constructor variants)
   */
  rules

    get-property-type : Property(x, k, srt, annos) -> srt
    get-property-type : PropertyNoAnno(x, k, srt) -> srt
    get-property-type : DerivedProperty(x, k, srt, annos,_) -> srt
    get-property-type : DerivedPropertyNoAnno(x, k, srt, _) -> srt

    get-property-name : Property(x, k, srt, annos) -> x
    get-property-name : PropertyNoAnno(x, k, srt) -> x
    get-property-name : DerivedProperty(x, k, srt, annos,_) -> x
    get-property-name : DerivedPropertyNoAnno(x, k, srt, _) -> x

    get-property-annos : Property(x, k, srt, annos) -> annos
    get-property-annos : PropertyNoAnno(x, k, srt) -> []
    get-property-annos : DerivedProperty(x, k, srt, annos,_) -> annos
    get-property-annos : DerivedPropertyNoAnno(x, k, srt, _) -> []

    set-property-annos(|newannos) : Property(x, k, srt, annos){anno*} -> Property(x, k, srt, newannos){anno*}
    set-property-annos(|newannos) = ?PropertyNoAnno(_,_,_)
    set-property-annos(|newannos) : DerivedProperty(x, k, srt, annos,e){anno*} -> DerivedProperty(x, k, srt, newannos,e){anno*}
    set-property-annos(|newannos) = ?DerivedPropertyNoAnno(_,_,_,_)

    get-property-kind : Property(x, k, srt, annos) -> k
    get-property-kind : PropertyNoAnno(x, k, srt) -> k
    get-property-kind : DerivedProperty(x, k, srt, annos,_) -> k
    get-property-kind : DerivedPropertyNoAnno(x, k, srt, _) -> k

    get-non-derived-prop-name : Property(x, k, srt, annos) -> x
    get-non-derived-prop-name : PropertyNoAnno(x, k, srt) -> x
    get-non-derived-prop-name : DerivedProperty(x, k, srt, annos,_) -> None()
    get-non-derived-prop-name : DerivedPropertyNoAnno(x, k, srt, _) -> None()

    get-derived-prop-exp : DerivedProperty(x, k, srt, annos,e) -> e
    get-derived-prop-exp : DerivedPropertyNoAnno(x, k, srt, e) -> e

    set-derived-prop-exp(|e1) : DerivedProperty(x, k, srt, annos,e){anno*} -> DerivedProperty(x, k, srt, annos,e1){anno*}
    set-derived-prop-exp(|e1) : DerivedPropertyNoAnno(x, k, srt, e){anno*} -> DerivedPropertyNoAnno(x, k, srt, e1){anno*}

    is-derived-prop-cons = ?DerivedProperty(_,_,_,_,_)
    is-derived-prop-cons = ?DerivedPropertyNoAnno(_,_,_,_)

    is-ref-or-comp-prop = get-property-kind; (?Ref() <+ ?Comp())
