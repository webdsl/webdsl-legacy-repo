module org/webdsl/dsl/generation/java-concat-ids

imports 
  libstrategolib  
  libjava-front
  //Java-15

signature constructors
  ConcatIds : ID -> ID

strategies

  java-concat-ids =
    topdown(try(
       concat-id-java-pascal-case
    <+ concat-id-underscore-camel-case
    <+ concat-id-camel-case
    ))

/** @internal */
rules

  concat-id-java-pascal-case =
      ClassDecHead(id, Id(concat-id-pascal-case), id, id, id)
    + TypeName(id, alltd(concat-id-pascal-case))
    + TypeName(alltd(concat-id-pascal-case))
  
  // PascalCase
  concat-id-pascal-case:
    ConcatIds(xs) -> <concat-strings> xs'
    where
      xs' := <map(give-capital)> xs
  
  // _camelCase
  concat-id-underscore-camel-case:
    ConcatIds(["_", x | xs]) -> <concat-strings> ["_", x | xs']
    where
      xs' := <map(give-capital)> xs
  
  // camelCase
  concat-id-camel-case:
    ConcatIds([x | xs]) -> <concat-strings> [x | xs']
    where
      xs' := <map(give-capital)> xs
  
  give-capital =
    string-as-chars([to-upper | id])
