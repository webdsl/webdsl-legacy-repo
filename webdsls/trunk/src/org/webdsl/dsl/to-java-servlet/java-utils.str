module org/webdsl/dsl/to-seam/java-utils

imports 
  libstrategolib  
  Java-15
  libjava-front 
  
imports 
  libwebdsl-front
  
rules

  property-getter =
    <concat-strings>["get", <capitalize-string>] 
    
  property-setter =
    <concat-strings>["set", <capitalize-string>] 

strategies

  // @note this extracts also methods from inner classes
  extract-method-signatures =
    collect(method-dec-to-abstract-method-dec)
    
  method-dec-to-abstract-method-dec :
    MethodDec(MethodDecHead(mods1, x , t, x_method, args, y), body) ->
    AbstractMethodDec(mods2, x, t, x_method, args, y)
    where <fetch(?Public())> mods1
        ; mods2 := <filter(not(?MarkerAnno(_) <+ ?Anno(_,_) <+ ?SingleElemAnno(_,_)))> mods1
        
  create-local-interface(|pkgname, pkgname2, x_Interface) : 
    JavaFile(prefix, cu) -> 
    (JavaFile(prefix, cu), 
     JavaFile(prefix, 
       compilation-unit|[
         package pkgname;
      
         import javax.ejb.Local;
         import javax.faces.event.ValueChangeEvent;
         import javax.faces.context.FacesContext;
         import java.util.*;
         import pkgname2.*;
            
         @Local
         public interface x_Interface  {
           ~*methodsdecs
         }
       ]|))
    where methodsdecs   := <extract-method-signatures> cu
    
rules

  sort-to-java-type-string = builtin-java-type;pp-java5-to-string
  sort-to-java-type-string : s@SimpleSort(x_class) -> <concat-strings> [<pp-java5-to-string> <DomainPackage>, ".", x_class] where <is-entity-type> s
  
strategies

  java-type =
    defined-java-type <+ builtin-java-type

  defined-java-type :
    SimpleSort(x_class) -> type|[ x_class ]| where <IsEntity> x_class

  persistence-annotations =
    builtin-persistence-annotations <+ ![]

  java-type-default-value =
    defined-java-type-default-value
    <+ builtin-java-type-default-value

  defined-java-type-default-value :
//s@SimpleSort(y) -> java |[ null ]| //|[ new y() ]|
    s@SimpleSort(y) -> java |[ new y() ]|
    where <defined-java-type> s

rules

  builtin-java-type :
    SimpleSort("Object") -> type|[ Object ]|

  builtin-java-type :
    GenericSort("List", [s]) -> type|[ java.util.List<t> ]|
    where t := <java-type> s

  builtin-java-type-default-value :
    GenericSort("List", [s]) -> java |[ new java.util.ArrayList<t>() ]|
    where t := <java-type> s

  builtin-java-type :
    GenericSort("Set", [s]) -> type|[ java.util.Set<t> ]|
    where t := <java-type> s

  builtin-java-type-default-value :
    GenericSort("Set", [s]) -> java |[ new java.util.LinkedHashSet<t>() ]|
    where t := <java-type> s

  builtin-java-type :
    GenericSort("Map", [s1, s2]) -> type|[ java.util.Map<t1, t2> ]|
    where t1 := <java-type> s1
        ; t2 := <java-type> s2

  builtin-java-type-default-value :
    GenericSort("Map", [s1,s2]) -> java |[ new java.util.HashMap<t1,t2>() ]|
    where t1 := <java-type> s1
        ; t2 := <java-type> s2