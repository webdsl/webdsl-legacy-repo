module org/webdsl/dsl/to-java-servlet/control-flow-to-java-servlet

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front
    
// control flow page elements   : for, if, templatecall 

rules // iteration

  for-loop-counter-wrap-code:
    bstm* -> result
    with  if <?[]> bstm* 
          then result :=  []
          else result :=  bstm*|[
                            ident = "";
                            if(inForLoop) {
                              ident += forelementcounter;
                            }
                            bstm*
                          ]| end
    
  wrap-in-for-loop(|x,t,e) :
    bstm -> res
    with  x_i := <newname> "i"
        ; res := <wrap-in-for-loop(|x_i,x,t,e)> bstm
  
  wrap-in-for-loop(|x_i,x,t,e) :
    bstm ->
    bstm* |[
      Object[] x_array = e.toArray(); // need to iterate set too without iterator, this copied array also makes sure a remove on the collection doesn't cause skipped elements
      for(int x_i = 0; x_i < x_array.length; x_i++)
      { 
        t x = (x_argtype) x_array[x_i];
        bstm*
      }
    ]|
    with  x_array := <newname> "array"
        ; x_argtype := <pp-java5-to-string> t
        ; if <is-list> bstm
          then bstm* := bstm
          else bstm* := [bstm] end

  handle-actions-input-to-java-servlet  =
    control-flow-for(collect-om(handle-actions-input-to-java-servlet)|)

  handle-actions-to-java-servlet =
    control-flow-for(collect-om(handle-actions-to-java-servlet)|)

  elem-to-java-servlet =
    control-flow-for(elems-to-java-servlet|)

  control-flow-for(s|) :
     ForCount(x, e1, e2, elems, sep) -> result
     with  x_bound := <newname> "stop"
         ; e3 := <expression-to-java-servlet> e1
         ; e4 := <expression-to-java-servlet> e2
         ; bstm* := <s;concat-with-sublists> elems
         ; x_separatorindex := <newname> "separatorindex"
         ; if <?[]> bstm* 
           then result :=  []
           else result :=  bstm*|[
                             inForLoop = true;
                             forLoopCounter++;
                             int x = e3; //x has been renamed already
                             int x_bound = e4;
                             int x_separatorindex = 0;
                             while(x < x_bound) {
                               forelementcounter++;
                               ~*<control-flow-for-separator(s|sep, x_separatorindex)> bstm*|[
                                 bstm*
                               ]|
                               x = x + 1;
                             }
                             forLoopCounter--;
                             if(forLoopCounter == 0){
                               inForLoop = false;
                               forelementcounter = 0;
                             }
                           ]| end

  control-flow-for(s|) :
    For(x, srt, e1, fltr, elems, sep) -> result
    with  bstm* := <s;concat-with-sublists> elems
        ; e2 := <expression-to-java-servlet> e1
        ; t := <java-type> srt
        ; x_list := <newname> "list"
        ; GenericSort(x_setorlist,[SimpleSort(_)]) := <type-of> e1
        ; if <?[]> bstm*
          then result :=  [] 
          else result :=  bstm* |[
                            java.util.ArrayList<t> x_list = new java.util.ArrayList<t>(e2);
                            ~*<control-flow-for-common(s | x_list, x, srt, t, fltr, sep)> bstm* |[    
                              bstm*
                            ]|
                          ]| end

  control-flow-for(s|) :
    fa@ForAll(x, srt, fltr, elems, sep) -> result
    with  bstm*   := <s;concat-with-sublists> elems
        ; t       := <java-type> srt
        ; x_list := <newname> "list"
        ; e_criteria := <hibernate-forall-criteria> (fa, t, x, srt, fltr)
        ; if <?[]> bstm*
          then result :=  [] 
          else result :=  bstm* |[ 
                            java.util.List<t> x_list = e_criteria.list();
                            ~*<control-flow-for-common(s | x_list, x, srt, t, fltr, sep)> bstm*|[
                              bstm*
                            ]|
                          ]| end
        
  control-flow-for-common(s | x_list, x, srt, t, fltr, sep) :
    bstm* ->
    bstm* |[
      inForLoop = true;
      forLoopCounter++;
      bstm_order*
      int x_separatorindex = 0;
      ~*<  control-flow-for-separator(s|sep, x_separatorindex)
         ; wrap-in-for-loop(|x,t,expr|[ x_list ]|)>
      bstm* |[  
        forelementcounter++;
        bstm*
      ]|
      forLoopCounter--;
      if(forLoopCounter == 0){
        inForLoop = false;
        forelementcounter = 0;
      }
    ]|
    with  bstm_order* := <for-filter-by-code(|x, srt, t, x_list)> fltr
        ; x_separatorindex := <newname> "separatorindex"
                          
  control-flow-for-separator(s|sep, x_separatorindex) :
    bstm* -> bstm*
    where None() := sep
    
  control-flow-for-separator(s|sep, x_separatorindex) :
    bstm* -> result
    where ForSeparator(elems) := sep
    with  bstm2* := <s;concat-with-sublists> elems
        ; if <?[]> bstm*
          then result :=  [] 
          else result :=  bstm* |[
                              if(x_separatorindex > 0) {
                                bstm2* // place separator if there was an element before this one
                              }
                              bstm*
                              x_separatorindex++;
                          ]| end

rules // if

  handle-actions-input-to-java-servlet  =
    control-flow-if(collect-om(handle-actions-input-to-java-servlet)|)

  handle-actions-to-java-servlet =
    control-flow-if(collect-om(handle-actions-to-java-servlet)|)

  elem-to-java-servlet =
    control-flow-if(elems-to-java-servlet|)

  control-flow-if(s|) :
    IfTempl(e, elems, elems2) -> result
    with  bstm* := <s;concat-with-sublists> elems
        ; bstm2* := <s;concat-with-sublists> elems2
        ; e1 := <expression-to-java-servlet> e
        ; if <?([],[])> (bstm*,bstm2*)
          then result :=  [] 
          else result :=  bstm |[
                            if(e1){ 
                              bstm*
                            } else {
                              bstm2*
                            }
                          ]| end

rules //templatecall    

  handle-actions-input-to-java-servlet  =
    control-flow-tcall(|"storeInputs")

  handle-actions-to-java-servlet =
    control-flow-tcall(|"handleActions")

  elem-to-java-servlet =
    control-flow-tcall(|"render")

  control-flow-tcall(|x_methodname) :
    tc@TemplateCall(x_name, args, passign*, _) -> result
    where <CallToDefinedTemplateAfterIdRename> x_name
          ; not(<is-input-template> tc)
    with  e* := <map({\e -> <expression-to-java-servlet> e\})> args
        ; x_tname := <get-templatecallnumber> tc
        ; arg_dump := <alltd(OriginalNonUniqueName);map(pp-webdsl);separate-by(|", ");concat-strings;explode-string;escape-chars;implode-string> args
        ; bstm_npe* :=
            <![];if-debug(!bstm*|[
              System.out.println("WebDSL warning: null reference in template arguments: "+"~x_name"+"("+"~arg_dump"+")"); // TODO proper location information; some way to disable these warnings
              npe.printStackTrace(); // display java exception for now so it can be traced back to the template
            ]|)>
        // implicit template argument
        ; if <fetch(?TemplateCall(templname,vars,_,_))> passign*
          then e_templ* := <map({\e -> <expression-to-java-servlet> e\})> vars
          else templname := "none"; e_templ* := [] end
        //templatecall with 'with' arguments
        ; withcallsmap := <collect(control-flow-emit-with-calls);concat> passign* 
        ; attrsmap := <filter(control-flow-emit-attrs-calls);concat> passign* 
        ; result := <generate-template-call(|x_methodname)> (expr|["~x_name"]|, expr|["~x_tname"]|, var-init|[ { e* } ]|, expr|["~templname"]|, var-init|[ { e_templ* } ]|, bstm_npe*, withcallsmap, attrsmap)
 
  /**
   *  'TemplateCall' for element in with { } arguments (derived from ArgDef constructor), 
   *  stored in TemplateCall with only part of the needed arguments, the closure at the call
   */
  control-flow-emit-with-calls :
   TemplateCall(templname,vars,passigns,_) -> bstm* |[ Object[] x_array = { e_templ* }; withcallsmapout.put("~origname", new utils.TemplateCall("~templname", x_array)); ]|
   where <fetch(?ResolvedRequireKey(origname))> passigns //also matches on elements calls
   with e_templ* := <map({\e -> <expression-to-java-servlet> e\})> vars
   //      ; origname := <OriginalNonUniqueName>  templname
      ; x_array := <newname> "array"
      
  control-flow-emit-attrs-calls :
    PropertyAssignment(name, val) -> bstm* |[
      attrsmapout.put("~name",e_val);
    ]|
    with e_val := <control-flow-emit-attrs-value <+ expression-to-java-servlet> val
  
  //translate ids
  control-flow-emit-attrs-value :
    val -> expr |[ "~val" ]|
    where <is-string> val
 
  generate-template-call(|x_methodname):
    (e_name, e_tname, e, e_templname, e_templ, bstm_npe*, bstm_withcallsmap*, bstm_attrs*) ->
    <for-loop-counter-wrap-code> 
    bstm* |[
    ident += e_tname;
      try{
        Object[] x_args = e;
        Object[] x_templargs = e_templ;
        PageServlet context = ps;
        Environment env2 = new Environment(env);
        int tc2 = ++templatecounter; 
        if(templatecalls.get(ident)==null){
          templatecalls.put(ident,(TemplateServlet)env.getTemplate(e_name).newInstance());
        }
        if (ps.isServingStandAloneTemplate) {
          context = (PageServlet) templatecalls.get("#Page"+ident);
          if (context == null) {
            context = (PageServlet) DispatchServlet.pages.get(e_name).newInstance();
            context.initializeBasics(ps, x_args, new Environment(env));
            templatecalls.put("#Page"+ident, context); 
          }
          env2 = context.env;
          tc2 = 1; 
        }
        try {
          withcallsmapout = new HashMap<String, utils.TemplateCall>();
          bstm_withcallsmap*
          Map<String,String> attrsmapout = new HashMap<String, String>();
          bstm_attrs*
          templatecounter = ((TemplateServlet) templatecalls.get(ident)).x_methodname(context, x_args, tc2, env2, new utils.TemplateCall(e_templname, x_templargs), withcallsmapout, attrsmapout, e1*);
        }
        catch(NullPointerException npe) {
          bstm_npe*
        }
      }
      catch(IllegalAccessException iae)
      {
        System.out.println("Problem in template servlet template lookup: "+iae.getMessage());
      }
      catch(InstantiationException ie)
      {
        System.out.println("Problem in template servlet template lookup: "+ie.getMessage());
      }
    ]|
    with  x_args := <newname> "args"
        ; x_templargs := <newname> "args"
        ; (    !x_methodname; (?"render"<+?"handleActions")
               ; e1* := [ e|[ out ]| ]
            <+ e1* := []
          )
      
rules //elements

  handle-actions-input-to-java-servlet  =
    control-flow-elemcall(|"storeInputs")

  handle-actions-to-java-servlet =
    control-flow-elemcall(|"handleActions")

  elem-to-java-servlet =
    control-flow-elemcall(|"render")

  control-flow-elemcall(|x_methodname) :
    tc@TemplateCall("elements", _, _, _) -> result 
    with  bstm_npe* := bstm*|[ System.out.println("WebDSL warning: null reference in environment of 'elements' call"); ]|
//        ; x_tname := <get-templatecallnumber> tc
        ; result := <generate-template-call(|x_methodname)> 
            (expr|[ templateArg.name ]|, expr|["elements"]|, expr|[ templateArg.args ]|, expr|["none"]|, var-init|[ {} ]|, bstm_npe*, [],[])


/**
 *  Call to template passed as part of required templates
 *  Since the passed TemplateCall object only has the first part of the arguments, it is concatted with the 
 *  actual arguments of the required template definition
 *
 */
rules //with element calls 

  handle-actions-input-to-java-servlet  =
    control-flow-elemwithcall(|"storeInputs")

  handle-actions-to-java-servlet =
    control-flow-elemwithcall(|"handleActions")

  elem-to-java-servlet =
    control-flow-elemwithcall(|"render")

  control-flow-elemwithcall(|x_methodname) :
  tc@TemplateCall(x,args,passigns,_) -> result
  where <CallToRequiredTemplateAfterIdRename> x 
  with  bstm_npe* := bstm*|[ System.out.println("WebDSL warning: null reference in environment of 'with' clause of call"); ]|
        ; x_tname := <get-templatecallnumber> tc
        ; e_origargs* := <map(expression-to-java-servlet)> args
        ; originalname := <OriginalNonUniqueName> x
        ; attrsmap := <filter(control-flow-emit-attrs-calls);concat> passigns 
//        ; e_origargs := var-init |[ { e_origargs* } ]|
        ; result := <generate-template-call(|x_methodname)> 
            (expr|[ withcallsmap.get("~originalname").name ]|, 
             expr|["~x_tname"]|, 
             expr|[ Utils.concatArrays(new Object[] { e_origargs* } ,withcallsmap.get("~originalname").args) ]|, 
             expr|["none"]|, 
             var-init|[ {} ]|, 
             bstm_npe*,
             [],
             attrsmap)
