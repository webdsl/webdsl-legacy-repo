module org/webdsl/dsl/to-java-servlet/control-flow-to-java-servlet

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front
    
// control flow page elements   : for, if, templatecall 

rules // iteration

  for-loop-counter-wrap-code:
    bstm* -> result
    with  if <?[]> bstm* 
          then result :=  []
          else result :=  bstm*|[
                            ident = "";
                            if(inForLoop) {
                              ident += forelementcounter;
                            }
                            bstm*
                          ]| end
                        
  //wraps expression that starts with ident                        
  for-loop-counter-wrap-code-expr:
    e1 -> e |[((inForLoop?forelementcounter:"") + e1 ) ]|
                            
    
  wrap-in-for-loop(|x,t,e) :
    bstm -> res
    with  x_i := <newname> "i"
        ; res := <wrap-in-for-loop(|x_i,x,t,e)> bstm
  
  wrap-in-for-loop(|x_i,x,t,e) :
    bstm ->
    bstm* |[
      Object[] x_array = e.toArray(); // need to iterate set too without iterator, this copied array also makes sure a remove on the collection doesn't cause skipped elements
      for(int x_i = 0; x_i < x_array.length; x_i++)
      { 
        t x = (x_argtype) x_array[x_i];
        bstm*
      }
    ]|
    with  x_array := <newname> "array"
        ; x_argtype := <pp-java5-to-string> t
        ; if <is-list> bstm
          then bstm* := bstm
          else bstm* := [bstm] end

  handle-actions-input-to-java-servlet  =
    control-flow-for(collect-om(handle-actions-input-to-java-servlet)|)

  handle-actions-to-java-servlet =
    control-flow-for(collect-om(handle-actions-to-java-servlet)|)

  elem-to-java-servlet =
    control-flow-for(elems-to-java-servlet|)
    
  data-validation-to-java-servlet  =
    control-flow-for(collect-om(data-validation-to-java-servlet)|)

  control-flow-for(s|) :
     ForCount(x, e1, e2, elems, sep) -> result
     with  x_bound := <newname> "stop"
         ; e3 := <expression-to-java-servlet> e1
         ; e4 := <expression-to-java-servlet> e2
         ; bstm* := <s;concat-with-sublists> elems
         ; x_separatorindex := <newname> "separatorindex"
         ; x_delta := <newname> "delta"
         ; if <?[]> bstm* 
           then result :=  []
           else result :=  bstm*|[
                             inForLoop = true;
                             forLoopCounter++;
                             int x = e3; //x has been renamed already
                             int x_bound = e4;
                             int x_separatorindex = 0;
                             int x_delta = (x < x_bound? 1 : -1);
                             while(x != x_bound) {
                               forelementcounter = Integer.toString(++fallbackcounter);
                               ~*<control-flow-for-separator(s|sep, x_separatorindex)> bstm*|[
                                 ThreadLocalPage.get().enterTemplateContext(forelementcounter);
                                 bstm*
                                 ThreadLocalPage.get().leaveTemplateContextChecked(forelementcounter);
                               ]|
                               x += x_delta;
                             }
                             forLoopCounter--;
                             if(forLoopCounter == 0){
                               inForLoop = false;
                               forelementcounter = "0";
                               fallbackcounter = 0;
                             }
                           ]| end

  control-flow-for(s|) :
    For(x, srt, e1, fltr, elems, sep) -> result
    with  bstm* := <s;concat-with-sublists> elems
        ; e2 := <expression-to-java-servlet> e1
        ; t := <java-type> srt
        ; x_list := <newname> "list"
        ; GenericSort(x_setorlist,[SimpleSort(_)]) := <get-type-anno> e1
        ; if <?[]> bstm*
          then result :=  [] 
          else result :=  bstm* |[
                            java.util.ArrayList<t> x_list = new java.util.ArrayList<t>(e2);
                            ~*<control-flow-for-common(s | x_list, x, srt, t, fltr, sep)> bstm* |[    
                              bstm*
                            ]|
                          ]| end

  control-flow-for(s|) :
    fa@ForAll(x, srt, fltr, elems, sep) -> result
    with  bstm*   := <s;concat-with-sublists> elems
        ; t       := <java-type> srt
        ; x_list := <newname> "list"
        ; e_criteria := <hibernate-forall-criteria> (fa, t, x, srt, fltr)
        ; if <?[]> bstm*
          then result :=  [] 
          else result :=  bstm* |[ 
                            java.util.List<t> x_list = e_criteria.list();
                            ~*<control-flow-for-common(s | x_list, x, srt, t, fltr, sep)> bstm*|[
                              bstm*
                            ]|
                          ]| end
        
  control-flow-for-common(s | x_list, x, srt, t, fltr, sep) :
    bstm* ->
    bstm* |[
      inForLoop = true;
      forLoopCounter++;
      bstm_order*
      int x_separatorindex = 0;
      ~*<  control-flow-for-separator(s|sep, x_separatorindex)
         ; wrap-in-for-loop(|x,t,expr|[ x_list ]|)>
      bstm* |[  
        fallbackcounter += 1; 
        if(WebDSLEntity.class.isInstance(x)) {
           WebDSLEntity x_ent = ((WebDSLEntity) ((Object) x));
           forelementcounter = (x_ent.getVersion() == 0 
             ? Integer.toString(fallbackcounter) 
             : x_ent.getId().toString());
        }
        else
          forelementcounter = Integer.toString(fallbackcounter);
        ThreadLocalPage.get().enterTemplateContext(forelementcounter);
        bstm*
        ThreadLocalPage.get().leaveTemplateContextChecked(forelementcounter);
      ]|
      forLoopCounter--;
      if(forLoopCounter == 0){
        inForLoop = false;
        forelementcounter = "0";
        fallbackcounter = 0;
      }
    ]|
    with  bstm_order* := <for-filter-by-code(|x, srt, t, x_list)> fltr
        ; x_separatorindex := <newname> "separatorindex"
        ; x_ent := <newname> "temp"
                          
  control-flow-for-separator(s|sep, x_separatorindex) :
    bstm* -> bstm*
    where None() := sep
    
  control-flow-for-separator(s|sep, x_separatorindex) :
    bstm* -> result
    where ForSeparator(elems) := sep
    with  bstm2* := <s;concat-with-sublists> elems
        ; if <?[]> bstm*
          then result :=  [] 
          else result :=  bstm* |[
                              if(x_separatorindex > 0) {
                                bstm2* // place separator if there was an element before this one
                              }
                              bstm*
                              x_separatorindex++;
                          ]| end

rules // if

  handle-actions-input-to-java-servlet  =
    control-flow-if(collect-om(handle-actions-input-to-java-servlet)|)

  handle-actions-to-java-servlet =
    control-flow-if(collect-om(handle-actions-to-java-servlet)|)

  elem-to-java-servlet =
    control-flow-if(elems-to-java-servlet|)
    
  data-validation-to-java-servlet =
    control-flow-if(collect-om(data-validation-to-java-servlet)|)

  control-flow-if(s|) :
    IfTempl(e, elems, elems2) -> result
    with  bstm* := <s;concat-with-sublists> elems
        ; bstm2* := <s;concat-with-sublists> elems2
        ; e1 := <expression-to-java-servlet> e
        ; if <?([],[])> (bstm*,bstm2*)
          then result :=  [] 
          else result :=  bstm |[
                            if(e1){ 
                              bstm*
                            } else {
                              bstm2*
                            }
                          ]| end

rules //templatecall    

  handle-actions-input-to-java-servlet  =
    control-flow-tcall(|"storeInputs")

  handle-actions-to-java-servlet =
    control-flow-tcall(|"handleActions")

  elem-to-java-servlet =
    control-flow-tcall(|"render")
    
  data-validation-to-java-servlet =
    control-flow-tcall(|"validateInputs")

  control-flow-tcall(|x_methodname) :
    tc@TemplateCall(x_name, args, passign*, _) -> bstm* 
    where <get-anno(?CallToDefinedAnno())> tc
          ; not(<is-input-template> tc)
    with  bstm* := <control-flow-tcall-helper(|x_methodname,[])> tc
    
  template-call-to-java-string: 
    (x_name,args) -> java:expr|[ "~x_name"+"("+"~arg_dump"+")" ]|
    with  arg_dump := <alltd(get-original-name);map(pp-webdsl-to-string);separate-by(|", ");concat-strings;explode-string;escape-chars;implode-string> args  
    
  control-flow-tcall-helper(|x_methodname, e_extrainputarg) :
    tc@TemplateCall(x_name, args, passign*, _){anno*} -> bstm* |[  bstm_result* ]|
    with  e1* := <map({\e -> <expression-to-java-servlet> e\})> args
        ; if ([] := e_extrainputarg) 
          then e* := e1*
          else e* := <conc>(e1*, [ e_extrainputarg ] ) end
        ; x_tname := <get-templatecallnumber> tc
        ; e_tc := <template-call-to-java-string> (x_name,args)
        ; bstm_npe* :=
            <![];if-debug(!bstm*|[
              System.out.println("WebDSL warning: null reference in template arguments: "+e_tc); // TODO proper location information; some way to disable these warnings
              npe.printStackTrace(); // display java exception for now so it can be traced back to the template
            ]|)>
        // implicit template argument
        ; if <fetch(?TemplateCall(templname,vars,_,_))> passign*
          then e_templ* := <map({\e -> <expression-to-java-servlet> e\})> vars
          else templname := "none"; e_templ* := [] end
        ; x_templargs := <newname> "args"
        //templatecall with 'with' arguments, if with, the template argument is the templateArg of this template. With is desugared away, so test the number of statements
        ; withcallsmap := <control-flow-emit-with-calls-map> passign* 
        ; if 1 := <length> withcallsmap 
          then e_templarg := expr|[new utils.TemplateCall("~templname", x_templargs) ]|
          else e_templarg := expr|[ this.templateArg ]| end
        ; attrsmap := <control-flow-emit-attrs-calls-map(|x_methodname)> passign* 
        // ref args
        ; calledTemplateArgs := <fetch-elem(?CalledTemplateArgs(<id>))>anno*
        ; javaexpr_refargassign* := <create-assigns-for-ref-arg> (calledTemplateArgs, args)
        ; e_argswithref* := <zip(\(e,None()) -> e\ <+ \(e,e_new) -> e_new\)> (e*,javaexpr_refargassign*)
        // create template call
        ; bstm_result* := <generate-template-call(|x_methodname)> (
            expr|["~x_name"]|, 
            expr|["~x_tname"]|, 
            var-init|[ { e_argswithref* } ]|, 
            e_templarg,
            var-init|[ { e_templ* } ]|, 
            bstm_npe*, 
            withcallsmap, 
            attrsmap,
            x_templargs
            )
     
  create-assigns-for-ref-arg=
    zip(create-assigns-for-ref-arg-helper <+ !None())
           
  create-assigns-for-ref-arg-helper :
    (Arg(_,RefSort(_)), e_webdsl) -> e|[ x ]| //for passing on refarg don't use regular expression translation, since it will add a call to `get`
    where <has-ref-type> e_webdsl
    with  Var(x) := e_webdsl //always a Var(1), because it is an argument of the template
    
  create-assigns-for-ref-arg-helper :
    (Arg(_,RefSort(_)), e_webdsl) -> e|[ ((new x_refstorageclass(this)).set(e_java)) ]|
    where not(<has-ref-type> e_webdsl)
    with  x_refstorageclass := <generate-ref-arg-storage-class> e_webdsl
        ; e_java := <expression-to-java-servlet> e_webdsl
  
  create-webdsl-dummy-var : //construct the var with annotations so it can be translated by exp/stm-to-java
    x -> Var(x{OriginalNameAnno(x{x})}){TypeAnno(SimpleSort("Object"))}
  
  wrap-exp-in-fieldaccess(|name):
    Var(x){anno*} -> FieldAccess(<create-webdsl-dummy-var> name,x){anno*}
  wrap-exp-in-fieldaccess(|name):
    FieldAccess(exp,ident) -> FieldAccess(<wrap-exp-in-fieldaccess(|name)> exp, ident) 
  wrap-exp-in-fieldaccess(|name):
    CollectionIndex(exp1,exp2) -> CollectionIndex(<wrap-exp-in-fieldaccess(|name)> exp1, exp2) 

  generate-ref-arg-storage-class :
    e_webdsl -> x_refstorageclass
    where key := (<ThisTemplateName>,e_webdsl)
        ; x_refstorageclass := <GeneratedRefArgStorageClass> key
          
  generate-ref-arg-storage-class :
    e_webdsl -> x_refstorageclass
    where key := (<ThisTemplateName>,e_webdsl)
        ; not(<GeneratedRefArgStorageClass> key)
    with srt_webdsltype := <get-type-anno> e_webdsl
        ; x_type := <java-type-name> srt_webdsltype 
        ; e_webdsl2 := <wrap-exp-in-fieldaccess(|"template")> e_webdsl
        ; e_java := <expression-to-java-servlet> e_webdsl2
        ; bstm := <statement-to-java-servlet> Stat(Assignment(e_webdsl2,Cast(<create-webdsl-dummy-var> "obj",srt_webdsltype))){DirectCollectionReplace()}
        ; pkgname2 := <DomainPackage>
        ; pkgname3 := <BeanPackage>
        ; x_class := <ThisTemplateClassName>
        ; x_refstorageclass := <!["RefArg_",<ThisTemplateName>];concat-strings;newname>
        ; <emit-java-code-local>
            compilation-unit|[
              package pkgname3;
              import pkgname2.*;
              public class x_refstorageclass implements utils.RefArg{
                public x_class template;
                public x_refstorageclass(x_class templ){
                  this.template = templ;
                }
                public Object set(Object obj){
                  bstm
                  return this;
                }
                public Object get(){
                  return e_java;
                }
              }
            ]|
        ; rules (GeneratedRefArgStorageClass : key -> x_refstorageclass)
    
    
  
  /**
   *  'TemplateCall' for element in with { } arguments (derived from ArgDef constructor), 
   *  stored in TemplateCall with only part of the needed arguments, the closure at the call
   */
  
  control-flow-emit-with-calls-map : //optimization when withcallsmap is empty
  passign* -> bstm_1* 
  with
      bstm* := <collect(control-flow-emit-with-calls);concat> passign*
    ; if (0 := <length> bstm*) then
      bstm_1* := <control-flow-emit-with-calls-map-def>
      else 
      bstm_1* := bstm* |[
          withcallsmapout = new HashMap<String, utils.TemplateCall>();
          withcallsmapout.putAll(withcallsmap);
          bstm*
      ]|
      end

  control-flow-emit-with-calls-map-def =
    !bstm* |[ withcallsmapout = withcallsmap; ]|
      
  control-flow-emit-with-calls :
//   TemplateCall(templname,vars,passigns,_) -> bstm* |[ Object[] x_array = { e_templ* }; withcallsmapout.put("~origname", new utils.TemplateCall("~templname", x_array)); ]|
   TemplateCall(templname,vars,passigns,_) -> bstm* |[ withcallsmapout.put("~origname", new utils.TemplateCall("~templname", new Object[] { e_templ* } )); ]|
   where <fetch(?ResolvedRequireKey(origname))> passigns //also matches on elements calls
   with e_templ* := <map({\e -> <expression-to-java-servlet> e\})> vars
   //      ; origname := <get-original-name>  templname
//      ; x_array := <newname> "array"
   
  /**
   * e_name : unique template name for lookup in hashmaps - Java String
   * e_tname : static template call number - Java String
   * e : regular template arguments - Java Object[]
   * e_templarg : elements template arguments - Java Object[]
   * bstm_npe* : null pointer exception handling statements - Java bstm*
   * bstm_withcallsmap* : 'with' arguments, references to lifted templates - Java bstm*
   * bstm_attrs* : attributes, arguments between [] - Java bstm*
   * x_templargs : name of 'elements' call arguments collection - Java ID
   */    
  generate-template-call(|x_methodname):
    (e_name, e_tname, e, e_templarg, e_templ, bstm_npe*, bstm_withcallsmap*, bstm_attrs*,x_templargs) ->
    <for-loop-counter-wrap-code> 
    bstm* |[
      ident += e_tname;
      ThreadLocalPage.get().enterTemplateContext(e_tname);
      try{
        Object[] x_args = e;
        Object[] x_templargs = e_templ;
        if(templatecalls.get(ident)==null){
          templatecalls.put(ident,(TemplateServlet)env.getTemplate(e_name).newInstance());
        }
        try {
          bstm_withcallsmap*
          bstm_attrs*
          ((TemplateServlet) templatecalls.get(ident)).x_methodname(x_args, new Environment(env), e_templarg, withcallsmapout, attrsmapout);
          ~*<![];try(<?"handleActions"> x_methodname;!bstm*|[
            if (ThreadLocalPage.get().hasExecutedAction)
              return;
          ]|)>
        }
        catch(NullPointerException npe) {
          bstm_npe*
        }
      }
      catch(IllegalAccessException iae)
      {
        System.out.println("Problem in template servlet template lookup: "+iae.getMessage());
      }
      catch(InstantiationException ie)
      {
        System.out.println("Problem in template servlet template lookup: "+ie.getMessage());
      }
      catch(NullPointerException npe) {
        System.out.println("Problem in template lookup for "+e_name+": "+npe.getMessage());
        npe.printStackTrace();
      }
      ident = "";
      ThreadLocalPage.get().leaveTemplateContextChecked(e_tname);
    ]|
    with  x_args := <newname> "args"
      
rules //elements

  handle-actions-input-to-java-servlet  =
    control-flow-elemcall(|"storeInputs")

  handle-actions-to-java-servlet =
    control-flow-elemcall(|"handleActions")

  elem-to-java-servlet =
    control-flow-elemcall(|"render")
    
  data-validation-to-java-servlet =
    control-flow-elemcall(|"validateInputs")

  control-flow-elemcall(|x_methodname) :
    tc@TemplateCall("elements", _, _, _) -> result 
    with  bstm_npe* := bstm*|[ System.out.println("WebDSL warning: null reference in environment of 'elements' call"); ]|
//        ; x_tname := <get-templatecallnumber> tc
        ; x_templargs := <newname> "args"
        ; result := <generate-template-call(|x_methodname)> 
            (expr|[ templateArg.name ]|, 
             expr|["elements"]|, 
             expr|[ templateArg.args ]|, 
             expr|[new utils.TemplateCall("none", x_templargs) ]|, 
             var-init|[ {} ]|, 
             bstm_npe*, 
             <control-flow-emit-with-calls-map-def>,
             <control-flow-emit-attrs-calls-map-def>,
             x_templargs)


/**
 *  Call to template passed as part of required templates
 *  Since the passed TemplateCall object only has the first part of the arguments, it is concatted with the 
 *  actual arguments of the required template definition
 *
 */
rules //with element calls 

  handle-actions-input-to-java-servlet  =
    control-flow-elemwithcall(|"storeInputs")

  handle-actions-to-java-servlet =
    control-flow-elemwithcall(|"handleActions")

  elem-to-java-servlet =
    control-flow-elemwithcall(|"render")
    
  data-validation-to-java-servlet =
    control-flow-elemwithcall(|"validateInputs")

  control-flow-elemwithcall(|x_methodname) :
  tc@TemplateCall(x,args,passigns,_) -> bstm* |[ bstm_result* ]|
  where <get-anno(?CallToRequiredAnno())> tc 
  with  e_tc := <template-call-to-java-string> (x,args)
        ; bstm_npe* :=
            <![];if-debug(!bstm*|[
              System.out.println("WebDSL warning: null reference in environment of 'with' clause of call: "+e_tc);
              npe.printStackTrace(); // display java exception for now so it can be traced back to the template
            ]|)>
        ; x_tname := <get-templatecallnumber> tc
        ; e_origargs* := <map(expression-to-java-servlet)> args
        ; originalname := <get-original-name> x
        ; attrsmap := <control-flow-emit-attrs-calls-map(|x_methodname)> passigns 
        ; x_templargs := <newname> "args"
//        ; e_origargs := var-init |[ { e_origargs* } ]|
        ; bstm_result* := <generate-template-call(|x_methodname)> 
            (expr|[ withcallsmap.get("~originalname").name ]|, 
             expr|["~x_tname"]|, 
             expr|[ Utils.concatArrays(new Object[] { e_origargs* } ,withcallsmap.get("~originalname").args) ]|, 
             //expr|["none"]|, 
             expr|[ this.templateArg ]|,
             var-init|[ {} ]|, 
             bstm_npe*,
             <control-flow-emit-with-calls-map-def>,
             attrsmap,
             x_templargs)

