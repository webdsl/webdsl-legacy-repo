module org/webdsl/dsl/to-java-servlet/regex

rules

  call-to-java-servlet :
    exp|[ e1.find(e2) ]| -> java|[ java.util.regex.Pattern.compile("~r").matcher(e3).find() ]|
    where <type-compatible> (<type-of> e1, SimpleSort("Regex"))
        ; <type-compatible> (<type-of> e2, SimpleSort("String")) 
    with  e3 := <expression-to-java-servlet> e2
        ; r := <convert-regex-pattern> e1
        
  call-to-java-servlet :
    exp|[ e1.match(e2) ]| -> java|[ java.util.regex.Pattern.compile("~r").matcher(e3).matches() ]|
    where <type-compatible> (<type-of> e1, SimpleSort("Regex"))
        ; <type-compatible> (<type-of> e2, SimpleSort("String")) 
    with  e3 := <expression-to-java-servlet> e2
        ; r := <convert-regex-pattern> e1
        
  call-to-java-servlet :
    exp|[ e1.split(e2) ]| -> java|[ java.util.Arrays.asList(e3.split("~r")) ]|
    where <type-compatible> (<type-of> e1, SimpleSort("Regex"))
        ; <type-compatible> (<type-of> e2, SimpleSort("String")) 
    with  e3 := <expression-to-java-servlet> e2
        ; r := <convert-regex-pattern> e1
        
  call-to-java-servlet :
    exp|[ e1.replaceAll(e2,e3) ]| -> java|[ e5.replaceAll("~r",e4) ]|
    where <type-compatible> (<type-of> e1, SimpleSort("Regex"))
        ; <type-compatible> (<type-of> e2, SimpleSort("String")) 
    with  e4 := <expression-to-java-servlet> e2
        ; e5 := <expression-to-java-servlet> e3
        ; r := <convert-regex-pattern> e1
        
  call-to-java-servlet :
    exp|[ e1.replaceFirst(e2,e3) ]| -> java|[ e5.replaceFirst("~r",e4) ]|
    where <type-compatible> (<type-of> e1, SimpleSort("Regex"))
        ; <type-compatible> (<type-of> e2, SimpleSort("String")) 
    with  e4 := <expression-to-java-servlet> e2
        ; e5 := <expression-to-java-servlet> e3
        ; r := <convert-regex-pattern> e1

  convert-regex-pattern = ?RegexPattern(<id>); pp-webdsl-to-string; explode-string; escape-chars(Escape-backslash); implode-string