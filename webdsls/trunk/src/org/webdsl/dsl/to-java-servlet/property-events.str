module org/webdsl/dsl/to-java-servlet/property-events

imports
  libstratego-lib
imports
  libwebdsl-front

rules

  entity-cbds-property-events(|x_Class) = !
    java:class-body-dec* |[
      @Transient
      public boolean propertyEventsEnabled = false;
      
      @Transient
      public org.hibernate.Session hibSession = null;
      
      @Transient
      public HashMap<String, Object> variables = null;
      
      @Transient
      public PageServlet ps = null;
      
      @Transient
      public java.io.PrintWriter out = null;
      
      public x_Class setupForPropertyEvents(org.hibernate.Session hibSession, java.io.PrintWriter out, PageServlet ps) {
        if(!propertyEventsEnabled) {
          this.hibSession = hibSession;
          this.out = out;
          this.ps = ps;
          this.variables = ps.variablesGlobalAndSession;
          propertyEventsEnabled = true;
        }
        return this;
      }
    ]|         
   
  entity-cbds-property-events-subclass(|x_Class) = !
    java:class-body-dec* |[
      public x_Class setupForPropertyEvents(org.hibernate.Session hibSession, java.io.PrintWriter out, PageServlet ps) {
        if(!propertyEventsEnabled) {
          this.hibSession = hibSession;
          this.out = out;
          this.ps = ps;
          this.variables = ps.variablesGlobalAndSession;
          propertyEventsEnabled = true;
        }
        return this;
      }
    ]|         
   
  entity-bstm-property-events :
    (x_Class,propname,propsrt,methodnamestart,x_var) -> bstm_extension
    with  oldnameset := <concat-strings> [methodnamestart,<capitalize-string> propname]
        ; if <elem> ((<find-entity-root>x_Class,oldnameset),<RenamedEntityFunctions>) // check whether it was renamed due to overloading
          then x_setfunname := <new-function-name> (oldnameset,[propsrt])
          else x_setfunname := oldnameset end 
        ; if <HasPropertyEvent> (x_Class,propname,methodnamestart)
          then bstm_extension := 
            java:bstm*|[ 
              if(propertyEventsEnabled){
                this.x_setfunname#_(hibSession, variables, x_var, ps, out);
                this.hibSession = null;
                this.out = null;
                this.ps = null;
                this.variables = null;
                propertyEventsEnabled = false;
              }  
            ]|
          else bstm_extension := []
          end 