module org/webdsl/dsl/to-java-servlet/global-variables

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

rules //global var use

  expression-to-java-servlet :
    gv@GlobalVar(x) -> expr|[ (x_class) env.getVariable("~x") ]|
    where x_class := <get-type-anno; java-type-name> gv

rules

  generate-code-java-servlet-once: _ ->
    <emit-java-code; fail> compilation-unit|[
      package utils;
  
      import java.io.*;
      import java.util.HashMap;
  
      import javax.servlet.*;
      import javax.servlet.http.*;
      import java.util.*;
      import utils.*;
      import pkgname.*;
      import pkgname2.*;
      
      import org.webdsl.lang.Environment;
  
      public class GlobalVariables {
      
        private static org.hibernate.Session hibSession;
        
        private static Environment env; 
        
        private static java.io.PrintWriter out = null;
          
        ~*cbds*
        ~*cbds1*
    
        static boolean applicationContextPropertyExists(String name, List<ApplicationContextProperty> res){
          for(ApplicationContextProperty a : res){
            if(a.getName().equals(name))	
              return true;
          }
          return false;
        } 
        
        public static boolean globalvarsChecked = false;
        
        public static void forceLoad(){
          globalvarsChecked = false;
          load();
        }
        
        public static void load(){
          if(!globalvarsChecked){
            try{
              out = new java.io.PrintWriter(System.out);
              ThreadLocalOut.push(out);
              
              bstm_initobjects*
            
              //start hibsession
              hibSession = HibernateUtilConfigured.getSessionFactory().getCurrentSession();
              hibSession.beginTransaction();
              
              env = PageServlet.staticEnv;
        
              boolean pageServletIsNotSet = ThreadLocalPage.get()==null;
              if(pageServletIsNotSet){
                ThreadLocalPage.set(new GlobalsPageServlet(env));
              }
              
              org.hibernate.Query q = hibSession.createQuery("from ApplicationContextProperty");
              List<ApplicationContextProperty> res = q.list();
      
              bstm3*
              bstm1*
              bstm2*
  
              org.hibernate.Query q2 = hibSession.createQuery("from ApplicationContextProperty");
              List<ApplicationContextProperty> results = q2.list();
              for(ApplicationContextProperty a : results)
              {
                bstm4*
              }
              
              hibSession = null;
              env = null;
              if(pageServletIsNotSet){
                ThreadLocalPage.set(null);
              }
              ThreadLocalOut.popChecked(out);
              out = null;
              globalvarsChecked = true;
            }
            catch(Exception se){
              System.out.println("Exception occured while instantiating global variables.");
              se.printStackTrace();
            }
          }
        }
      }
    ]|
    //where not([] := <bagof-AllGlobalVars>) // fails if there are no global vars, wont generate anything then
    with  pkgname    := <BeanPackage>
        ; pkgname2   := <DomainPackage>
        ; cbds* := <map({\ 
                           elem |[ var x : srt := e ]| -> 
                           class-body-dec*|[ 
                             //private static t x = null;
                             private static UUID x#id = null;
                              
                             public static x_class get#x(org.hibernate.Session hibSession)
                             {
                                 t x = (x_class) hibSession.get(x_class.class, x#id);
                                 return x;
                             }
                              
                           ]| 
                           with  t := <java-type> srt
                               ; SimpleSort(typename) := srt
                               ; x_class := <java-type-name> srt
                               //; x_type := "java.util.UUID"
                               //; x_conversionmethod := "fromString"                                   
                       \});concat> 
                       <bagof-AllGlobalVars <+ ![]>
        ; vardeclinits := <bagof-AllGlobalVars <+ ![]>
        // persist right away to get an id
        ; bstm3* := <map(vardeclinit-to-persist-statement-java-servlet <+ pp-aterm(!"vardeclinit-to-persist-statement-java-servlet: ");fail);concat-with-sublists> vardeclinits
        //update with assignments
        ; bstm1* := <map(vardeclinit-to-method-call-java-servlet <+ pp-aterm(!"vardeclinit-to-method-call-java-servlet: ");fail)> vardeclinits
        //persist updates
        ; bstm2* := <map(vardeclinit-to-method-persist-call-java-servlet <+ pp-aterm(!"vardeclinit-to-method-persist-call-java-servlet: ");fail)> vardeclinits
        ; bstm4* := <map(vardeclinit-to-id-load <+ pp-aterm(!"vardeclinit-to-id-load: ");fail)> vardeclinits
        ; cbds1* := <map(vardeclinit-to-method-java-servlet <+ pp-aterm(!"vardeclinit-to-method-java-servlet: ");fail); concat-with-sublists> vardeclinits
        ; bstm_initobjects* := <map(vardeclinit-to-init-object-java)> vardeclinits
 
  vardeclinit-to-init-object-java :
    vdi@VarDeclInit(x_ident,srt@SimpleSort(x_class), e) -> 
    bstm|[ x_ident = new x_t(); ]|
    with  x_t := <java-type-name> srt

  vardeclinit-to-persist-statement-java-servlet:
    vdi@VarDeclInit(x_ident,SimpleSort(x_class),exps) -> resultwrapped
    with  result := 
          bstm* |[ 
            //hibSession.save(x_ident); no longer necessary here because of generated UUID for keys
            env.putVariable("~x_ident",x_ident); //necessary for set/list creations in globals which use globals
          ]| 
        ; resultwrapped := 
          bstm |[
            if(!applicationContextPropertyExists("~x_ident",res)){
              ~*result
            }
          ]|                 

  vardeclinit-to-id-load:
    vdi@VarDeclInit(x_ident,SimpleSort(x_class),expressions) ->
    bstm |[	
      if(a.getName().equals("~x_ident"))
      {
        x_ident#id = a.getDatabaseId();
      }  
    ]|

  vardeclinit-to-method-call-java-servlet:
    vdi@VarDeclInit(x_ident,SimpleSort(x_class),expressions) ->
    bstm |[	
      if(!applicationContextPropertyExists("~x_ident",res))  
        globalDeclaration#x_ident(); 
    ]|

  vardeclinit-to-method-persist-call-java-servlet:
    vdi@VarDeclInit(x_ident,SimpleSort(x_class),expressions) ->
    bstm |[ 
      if(!applicationContextPropertyExists("~x_ident",res))
        globalDeclaration#x_ident#Persist(); 
    ]|

  vardeclinit-to-method-java-servlet:
    vdi@VarDeclInit(x_ident,srt@SimpleSort(x_class), e) ->
      class-body-dec* |[
        private static t x_ident = null;

        private static void globalDeclaration#x_ident() {
          try {
            x_ident = e_vardeclinit;
          }
          catch(Exception iea) {
            throw new RuntimeException("Catched in globalDeclaration: ",iea);
          }
        }

        private static void globalDeclaration#x_ident#Persist() {
          hibSession.save(x_ident);
          ApplicationContextProperty x_acpident = new ApplicationContextProperty();
          x_acpident.setName("~x_ident");
          x_acpident.setDatabaseId(x_ident.getId());
          hibSession.save(x_acpident);
          GlobalVariables.x_ident#id = x_acpident.getDatabaseId();
          
          //set version property to 1, which indicates a persisted entity
          x_ident.setVersion(1);
          hibSession.flush();

          x_ident = null;
        }
  ]|
  with  x_acpident := <newname> "id"
      ; t := <defined-java-type> srt
      ; x_t := <java-type-name> srt
      ; e_vardeclinit := <expression-to-java-servlet> e

strategies //handle global vars in page classes    

  GenerateCodeJavaServlet = page-class-generation-test; generate-java-servlet-global-vars-page-part; fail
  
  generate-java-servlet-global-vars-page-part :
    def |[ define mod* x(farg*)  req*  { elem* } ]| -> result
    where x := <(JavaThisPage <+ ThisAjaxTemplateName)>
    with result := <generate-java-servlet-partial>
                   class-body-dec|[
                     @Partial
                     protected void initialize() {
                       DispatchServlet.initGlobalVars(env, hibSession);
                     }
                   ]|
