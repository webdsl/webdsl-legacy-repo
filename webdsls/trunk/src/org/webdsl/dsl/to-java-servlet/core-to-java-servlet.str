
module org/webdsl/dsl/to-java-servlet/core-to-java-servlet

imports
  libstrategolib
  Java-15
  libjava-front

imports
  org/webdsl/dsl/to-seam/-
  org/webdsl/dsl/to-java-servlet/-
    org/webdsl/dsl/to-java-servlet/types/-
  org/webdsl/dsl/modules/-
  libwebdsl-front

strategies

  core-to-java-servlet =
    ?Application(qid, sections)
    ; info(|"Now going to do code generation")
    ; rules(
        Package       := <qid-to-package-name> qid        
        BeanPackage   := <qid-to-package-name> Qualified(qid, "beans")
      )
    ; set-DomainPackage(|<qid-to-package-name> Qualified(qid, "domain")) //strategy/rule shared with seam back-end

    ; !Application(qid, sections)
    ; annotate-with-unique-element-ids
    ; generate-code-java-servlet

    // generate the application context java files that initialize global variables
    
    ; ! <get-java-artifacts-java-servlet>

   generate-code-java-servlet =
     if ?Define(mods, name, args, elems); is-page-or-template then //strategy/rule shared with seam back-end
       {| ThisPage, NestedTemplate, ThisTemplateName
        : if ThisPage
          then rules ( NestedTemplate := name ) end
        ; if <elem> (Page(), mods)
          then rules ( ThisPage := name ) end
        ; rules ( ThisTemplateName := name)
        ; not(GenerateCodeJavaServlet)
        ; all(generate-code-java-servlet)
        |}
     else
       if ?Action(_, _, _) then
         {| InAction
         : rules ( InAction := True() )
         ; not(GenerateCodeJavaServlet)
         ; all(generate-code-java-servlet)
         |}
       else
         not(GenerateCodeJavaServlet)
         ; all(generate-code-java-servlet)
       end
     end

// some helping annotations to create uniqueness among the static elements

signature
  constructors
    TemplateCallNumber : String -> Annotation
    FormNumber : String -> Annotation
    InputNumber : String -> Annotation
    ActionNumber : String -> Annotation
        
rules

  get-templatecallnumber :
    _{anno*} -> compid
    where TemplateCallNumber(compid) := <collect(?TemplateCallNumber(_));Hd> anno*
 
  get-formnumber :
    _{anno*} -> compid
    where FormNumber(compid) := <collect(?FormNumber(_));Hd> anno* 
    
  get-inputnumber :
    _{anno*} -> compid
    where InputNumber(compid) := <collect(?InputNumber(_));Hd> anno* 
    
  get-actionnumber :
    _{anno*} -> compid
    where ActionNumber(compid) := <collect(?ActionNumber(_));Hd> anno* 
      
strategies

  annotate-with-unique-element-ids = 
    info(|"applying unique element ids")
    ; topdown(try(java-servlet-element-id))
    ; info(|"applying unique element ids done")

rules

  java-servlet-element-id :
    tc@TemplateCall(name, args, _){anno*} -> tc{TemplateCallNumber(newid),anno*} 
    where is-call-to-toplevel-template 
        ;  newid := <newname> "tcall"
    
  java-servlet-element-id :
    tc@TemplateCall("form", [], elems){anno*} -> tc{FormNumber(newid),anno*} 
    where  newid := <newname> "form"
    
  java-servlet-element-id :
    tc@TemplateCall("action",[e1,e2],[]){anno*} -> tc{ActionNumber(newid),anno*} 
    where  newid := <newname> "action"
    
       
