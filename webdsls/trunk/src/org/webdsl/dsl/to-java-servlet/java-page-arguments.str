module org/webdsl/dsl/to-java-servlet/java-page-arguments

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

rules // page arguments
                                  //JavaThisPage
  GenerateCodeJavaServlet = where((JavaThisPage<+ThisTemplateName);not(InAction)); page-class-generation-test; argument-to-java-servlet-property; fail
   
  /**
   *  GenericSort -List and Set- page arguments 
   */ 
  argument-to-java-servlet-property :
    arg@farg|[ x : srt ]| -> 
     <generate-java-servlet-partial> java|[
         @Partial
         protected void conversion(){
           if(parammap.get("~<OriginalNonUniqueName> x")!=null)
           {
             try{ 
                String[] ids = parammap.get("~<OriginalNonUniqueName> x").split(","); 
                x_ids = e;
                for(String anid : ids){
                  x_ids.add(java.util.UUID.fromString(anid));
                }
             }
             catch(NumberFormatException nfe){System.out.println(nfe.getMessage());}  
           } 
         }
         
         @Partial
         void loadArguments() {
           bstm*
         }
         
         @Partial private void cleanupPageArgAndVar(){ x_ids = null; x = null; }
        
         private t_idstype x_ids;
       
         public void set#x_ids(t_idstype v) {
           this.x_ids = v;
         }
         public t_idstype get#x_ids() {
           return this.x_ids;
         }
        
         private x1<t_class> x;
         
         public void set#x(x1<t_class> x) {
           this.x = x;
         }
         
         public x1<t_class> get#x() {
           return x;
         }
      ]|    
      where <?GenericSort(x1,[x2])> srt
      with  <?SimpleSort(_)> x2
          ; t_class := <java-type> x2    
          ; x_id := <concat-strings>[x, "_id"]
          ; t := SimpleSort("UUID")
          ; t_idstype := <builtin-java-type> GenericSort(x1,[t])
          ; e := <java-type-default-value> GenericSort(x1, [t])
          ; bstm* := <argument-to-java-servlet-initialization> arg
          ; x_ids := <concat-strings>[x,"_ids"]
     
  /**
   *  defined entities and Entity type page arguments
   */  
  argument-to-java-servlet-property :
    arg@farg|[ x : srt ]| ->
    <generate-java-servlet-partial> java|[
       @Partial
       protected void conversion(){
         if(parammap.get("~<OriginalNonUniqueName> x")!=null)
         {
           bstm1*
         } 
       }
       @Partial
       void loadArguments() {
         bstm*
       }

       private t x_id;
       
       public void set#x_id(t v) {
         this.x_id = v;
       }
       public t get#x_id() {
         return this.x_id;
       }

       private t_class x;
       
       @Partial private void cleanupPageArgAndVar(){ x_id = null; x = null; }
       
       public void set#x(t_class x) {
         this.x = x;
       }
       
       public t_class get#x() {
         return x;
       }
    ]|
    where <has-entity-anno> srt
    with  <?SimpleSort(_)> srt
        ; t_class := <java-type> srt
        ; x_id := <concat-strings>[x, "_id"]
        ; t := <java-type> SimpleSort("String")
        ; bstm* := <argument-to-java-servlet-initialization> arg
        ; bstm1* := <java-servlet-conversion-to-object> (t,x,x_id)

  /**
   *  Built-in type page arguments
   */ 
  argument-to-java-servlet-property :
    arg@farg|[ x : srt ]| ->
    <generate-java-servlet-partial> java|[
       @Partial
       protected void conversion(){
         if(parammap.get("~<OriginalNonUniqueName> x")!=null)
         {
           bstm1*
         }
       }
       @Partial
       void loadArguments() {
         bstm*
         bstm2*
       }

       private t_Class x;
       
       @Partial private void cleanupPageArgAndVar(){ x = null; }
       
       public void set#x(t_Class x) {
         this.x = x;
       }
       public t_Class get#x() {
         return x;
       }
    ]|
    where t_Class := <builtin-java-type> srt
        ; not(GenericSort(_,_) := srt)
    with  <?SimpleSort(_)> srt
        ; bstm* := <argument-to-java-servlet-initialization> arg
        ; bstm2* := <string-argument-to-java-servlet-initialization <+ ![]> arg // fix for passing strings decode (eg. %20 -> ' ')
        ; bstm1* := <java-servlet-conversion-to-object> (t_Class,x,x)

rules //initialization

  argument-to-java-servlet-initialization :
    arg@farg|[ x : srt ]| ->
    bstm*|[
      if (x_id != null) {            
        x = (x_class) hibSession.load(x_class.class, java.util.UUID.fromString(x_id));
      }
    ]|
    where <has-entity-anno> srt
        ; not(SimpleSort("Entity") := srt)
        ; not(<get-entity-id-name-with-super> srt)
    with  x_class := <java-type-name> srt
        ; x_id := <concat-strings>[x, "_id"]

  argument-to-java-servlet-initialization :
    arg@farg|[ x : srt ]| ->
    bstm*|[
      if (x_id != null) {   
        String[] x_ar = x_id.split(":");
        String x_type = x_ar[0];
        String x_uuid = x_ar[1];
        try { 
          Class<?> cl = org.hibernate.util.ReflectHelper.classForName(e_pre + x_type);
          x = (x_class) hibSession.load(cl, java.util.UUID.fromString(x_uuid));
        } 
        catch (ClassNotFoundException cnfe) { 
          throw new RuntimeException(cnfe); 
        }       
      }
    ]|
    where SimpleSort("Entity") := srt
    with  x_id := <concat-strings>[x, "_id"]
        ; x_class := <java-type-name> SimpleSort("Entity")
        ; e_pre := java:expr|[ "~<concat-strings> [<pp-java5-to-string> <DomainPackage>, "."]" ]|
        ; x_uuid := <newname> "uuid"
        ; x_type := <newname> "type"
        ; x_ar := <newname> "array"

  argument-to-java-servlet-initialization :
    arg@farg|[ x : srt ]| ->
    bstm*|[
      if (x_id != null) {            
        x_id = utils.URLFilter.unfilter(x_id.toString());
        List<t> results = hibSession.createCriteria( x_class.class )
        .add( org.hibernate.criterion.Restrictions.naturalId().set("~x_natidprop", x_id))
        .list();
        if(results.size() == 0){
          System.out.println("No entity with this identity found: "+x_id);
          throw new RuntimeException("No entity with this identity found: "+x_id);
        }
        else if(results.size() > 1){
          System.out.println("Multiple entities with this identity found: "+x_id) ;
          throw new RuntimeException("Multiple entities with this identity found: "+x_id);
          //x = results.get(0);
        }
        else {
          x = results.get(0);
        }
      }
    ]|
    where <has-entity-anno> srt
        ; not(SimpleSort("Entity") := srt)
        ; x_natidprop := <concat-strings>["_",<get-entity-id-name-with-super> srt]
    with  t := <java-type> srt
        ; x_class := <java-type-name> srt
        ; x_id := <concat-strings>[x, "_id"]

  argument-to-java-servlet-initialization :
    arg@farg|[ x : srt ]| ->
    bstm*|[
      if (x_ids == null) {            
         //x_ids = new x3();
      } 
      else {
        if (x == null) {
          x = e;
          for(java.util.UUID anid: x_ids) {
            x.add((x_class) hibSession.load(x_class.class, anid));
          }
        }
      }
    ]|
    where <?GenericSort(x1,[x2])> srt
        ; <defined-java-type> x2
    with  <?SimpleSort(x_Class)> x2
        ; e :=    <java-type-default-value> srt
        ; x_ids := <concat-strings>[x, "_ids"]
        ; x_class := <java-type-name> x2
        
  argument-to-java-servlet-initialization : //@TODO List arguments with builtin types, eg. list of strings
    arg@farg|[ x : srt ]| ->
    bstm*|[]|
    where <?GenericSort(x1,[x2])> srt; <builtin-java-type> x2

  argument-to-java-servlet-initialization :
    arg@farg|[ x : srt ]| ->
    bstm*|[
          if (x == null) {
            x = e_init;
          }
    ]|
    where <builtin-java-type> srt
    with  <?SimpleSort(x_Class)> srt
        ; e_init := <java-type-default-value> srt
       // ; rules( Parameter :+= x )

  string-argument-to-java-servlet-initialization :
    arg@farg|[ x : srt ]| ->
    bstm*|[
      x = utils.URLFilter.unfilter(x);
    ]|
    where <builtin-java-type> srt
        ; <?SimpleSort("String")> srt

  //hiddens for form
                                   //JavaThisPage
  GenerateCodeJavaServlet =  where((JavaThisPage<+ThisTemplateName);not(InAction)); page-class-generation-test; form-hidden-page-params-java-servlet ; fail
       
  form-hidden-page-params-java-servlet :
    Arg(x_ident, s) -> <generate-java-servlet-partial>
    java |[ 
      @Partial 
      public void hiddenParams(java.lang.StringBuffer combinedparams)
      {
        combinedparams.append("<input type=\"hidden\" name=\""+"~<OriginalNonUniqueName> x_ident"+"\" value=\""+utils.HTMLFilter.filter(x_ident.toString())+"\" />");    
      }
    ]|
    where <builtin-java-type> s
        ; not( GenericSort(_,_) := s)

  form-hidden-page-params-java-servlet :
    Arg(x, s) -> <generate-java-servlet-partial>
    java |[ 
      @Partial 
      public void hiddenParams(java.lang.StringBuffer combinedparams)
      {
        combinedparams.append("<input type=\"hidden\" name=\""+"~<OriginalNonUniqueName> x"+"\" value=\""+utils.HTMLFilter.filter(x.getNaturalId().toString())+"\" />");    
      }
    ]|
    where <defined-java-type> s
    
  form-hidden-page-params-java-servlet :
    Arg(x, srt) -> <generate-java-servlet-partial>
    java |[ 
      @Partial 
      public void hiddenParams(java.lang.StringBuffer combinedparams)
      {       
        combinedparams.append("<input type=\"hidden\" name=\""+"~<OriginalNonUniqueName> x"+"\" value=\""+utils.HTMLFilter.filter(org.webdsl.tools.Utils.encodeIdList(x))+"\" />");    
      }
    ]|
    where GenericSort(_,[s]) := srt
        ; <defined-java-type> s  
       
  GenerateCodeJavaServlet =  where((JavaThisPage<+ThisTemplateName);not(InAction)); page-class-generation-test; form-post-page-params-java-servlet ; fail
       
  form-post-page-params-java-servlet :
    Arg(x_ident, s) -> <generate-java-servlet-partial>
    java |[ 
      @Partial 
      public void hiddenPostParams(java.lang.StringBuffer combinedparams)
      {
        combinedparams.append("~<OriginalNonUniqueName> x_ident"+"="+utils.HTMLFilter.filter(x_ident.toString())+"&");    
      }
    ]|
    where <builtin-java-type> s
        ; not( GenericSort(_,_) := s)

  form-post-page-params-java-servlet :
    Arg(x, s) -> <generate-java-servlet-partial>
    java |[ 
      @Partial 
      public void hiddenPostParams(java.lang.StringBuffer combinedparams)
      {
        combinedparams.append("~<OriginalNonUniqueName> x"+"="+utils.HTMLFilter.filter(x.getNaturalId().toString())+"&");    
      }
    ]|
    where <defined-java-type> s

  form-post-page-params-java-servlet  :
    arg@farg|[ x_ident : srt ]| -> <generate-java-servlet-partial>
    java |[ 
      @Partial 
      public void hiddenPostParams(java.lang.StringBuffer combinedparams)
      {
        combinedparams.append("~<OriginalNonUniqueName> x_ident"+"="+utils.HTMLFilter.filter(org.webdsl.tools.Utils.encodeIdList(x_ident))+"&");
      }
    ]|
    where <?GenericSort(_,[s])> srt
        ; <defined-java-type> s
    