module org/webdsl/dsl/to-java-servlet/data-validation/property-validation-annotation

imports 
  libstrategolib  
  libjava-front 
  
imports 
  libwebdsl-front
  org/webdsl/dsl/to-java-servlet/core-to-java-servlet
  
rules

  validation-annotations-setter :
    (x_old,x_new, annos) -> 
    bstm* |[
      //validate if value changed
      //if(!org.webdsl.tools.Utils.equal(x_old,x_new)){
      bstm*
      //}
    ]|
    with  bstm* := <validation-annotations-entity-helper-no-crud-anno(|x_new)> annos
    
  validation-annotations-collection-operations : //delete annos not checked here, see rules below
    annos -> <validation-annotations-entity-helper-no-crud-anno(|None())> annos
 
  validation-annotations-entity-helper-no-crud-anno(|x) =
    remove-all(?ValidateDeleteAnno(_,_)<+?ValidateCreateAnno(_,_)<+?ValidateUpdateAnno(_,_))
  ; validation-annotations-entity-helper(|x)
    
  validation-annotations-entity-helper(|x) :
    annos ->
    bstm* |[
        java.util.List<utils.ValidationException> exceptions = new java.util.LinkedList<utils.ValidationException>();
       
        bstm_validation*
        bstm_lengthvalidation*
       
        if(exceptions.size() > 0){
          //overwrite relevant objects the place of the validate anno determines this here
          /*
          for(utils.ValidationException ve : exceptions){
            ve.getRelevantObjects().clear();
            ve.getRelevantObjects().add(x_old);
          }*/
          throw new MultipleValidationExceptions(exceptions);
        }
    ]|
    with  bstm_validation* := <collect(validate-to-java-servlet(data-validation-to-java-servlet-collect(|"exceptions")))> annos
        ; if <fetch(?SingleParamAnno("length", x_length))> annos then
            bstm_lengthvalidation* := 
              [ <data-validation-to-java-servlet-collect-jexp(|"exceptions")> 
                  (e |[ null ]|, java:expr|[ x.length() <= x_length ]|, java:expr|[ "value too long (max "+x_length+" characters)" ]| ) ]
          else bstm_lengthvalidation* := [] end
                   
rules //delete anno

   GenerateCodeJavaServlet = entity-to-java-servlet-validation-annotation; fail
   
   entity-to-java-servlet-validation-annotation :
     EntityNoSuper(x_Class, entbodydecs) ->
     <emit-java-code-local> compilation-unit|[
       package pkgname;
       
       @Partial
       public class x_Class
       {
         ~*cbds*
       }
     ]|
     with  pkgname := <DomainPackage>
         ; props := <filter(is-property-cons)> entbodydecs
         ; cbds* := <entity-to-java-servlet-validation-annotation-helper> props
       
   entity-to-java-servlet-validation-annotation :
    SessionEntity(x_ident, entbodydecs) ->
    <emit-java-code-local> compilation-unit|[
       package pkgname;
       
       @Partial
       public class x_Class
       {
         ~*cbds*
       }
     ]|
     with  x_Class := <capitalize-string> x_ident
         ; pkgname := <DomainPackage>
         ; props := <filter(is-property-cons)> entbodydecs
         ; cbds* := <entity-to-java-servlet-validation-annotation-helper> props
        
   entity-to-java-servlet-validation-annotation :
     Entity(x_Class, x_super, entbodydecs) ->
     <emit-java-code-local> compilation-unit|[
       package pkgname;
       
       @Partial
       public class x_Class
       {
         ~*cbds*
       }
     ]|
     with  pkgname := <DomainPackage>
         ; props := <filter(is-property-cons)> entbodydecs
         ; cbds* := <entity-to-java-servlet-validation-annotation-helper> props
         
  entity-to-java-servlet-validation-annotation-helper :
    props ->
    class-body-dec*|[
      public void validateDelete(){
        bstm*
      }
      public void validateSave(){
        bstm1*
      }
    ]|
    with  annos := <collect(?ValidateDeleteAnno(_,_))> props
        ; bstm* := <validation-annotations-entity-helper(|None())> annos
        ; annos1 := <collect(?ValidateAnno(_,_)<+?ValidateCreateAnno(_,_)<+?ValidateUpdateAnno(_,_))> props
        ; bstm1* := <validation-annotations-entity-helper(|None())> annos1

        