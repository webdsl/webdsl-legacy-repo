module org/webdsl/dsl/to-seam/validate-statement

imports 
  libstrategolib  
  Java-15
  libjava-front 
  
imports 
  libwebdsl-front
  
rules

  call-to-java-servlet :
    exp |[ validate(e1,e2) ]| -> 
    bstm* |[
      if(!e3){throw e6;}
    ]|
    with e3 := <expression-to-java-servlet> e1 //expression types checked by typechecker, validate([2]) reserved
       ; e4 := <expression-to-java-servlet> e2
       ; usedobjects := <collect(?FieldAccess(_,_) <+ ?Qualified(_,_) <+ ?Var(_))> e1
       ; e5 := e|[new utils.ValidationException(e4)]|
       ; e6 := <add-relevant-objects(|usedobjects)> e5
    
  add-relevant-objects(|usedobjects) :
    e -> <add-relevant-objects(|xs)> e|[ e.addRelevantObject(e2) ]|
    where not([] := usedobjects)
        ; [e1|xs] := usedobjects
        ; e2 := <expression-to-java-servlet> e1
        
  add-relevant-objects(|usedobjects) :
    e -> e
    where [] := usedobjects
    