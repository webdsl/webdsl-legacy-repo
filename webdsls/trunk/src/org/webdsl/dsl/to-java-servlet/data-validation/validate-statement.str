module org/webdsl/dsl/to-seam/validate-statement

imports 
  libstrategolib  
  Java-15
  libjava-front 
  
imports 
  libwebdsl-front
  
rules

  validate-to-java-servlet(s) :
    stat |[ validate(e1,e2); ]| -> <s> (e |[ null ]|,e1,e2)

  validate-to-java-servlet(s) :
    stat |[ x:validate(e1,e2); ]| -> <s> (e |[ "~x" ]|,e1,e2)
    
  validate-to-java-servlet(s) :
    ValidateCreateAnno(e1,e2) -> 
    bstm |[
      if(this.getVersion() == 0){
        bstm
      }
    ]|  
    with bstm := <s> (e |[ null ]|,e1,e2)
  
  validate-to-java-servlet(s) :
    ValidateUpdateAnno(e1,e2) ->
    bstm |[
      if(this.getVersion() > 0){
        bstm
      }
    ]|  
    with bstm := <s> (e |[ null ]|,e1,e2)
  
  validate-to-java-servlet(s) : //only used in one place, see property-validation-annotation.str
    ValidateDeleteAnno(e1,e2) -> <s> (e |[ null ]|,e1,e2)    
  
  validate-to-java-servlet(s) :
    ValidateAnno(e1,e2) -> <s> (e |[ null ]|,e1,e2)

  validate-to-java-servlet(s) :
    NamedValidateAnno(x,e1,e2) -> <s> (e |[ "~x" ]|,e1,e2)
     
   /**
    *  data invariants: collect all exceptions first   
    *
    *  e_jname: name used to identify validation check for manual connection
    *  e_wcheck: webdsl expression for validate check
    *  e_wmessage: webdsl expression for validate message
    *
    *  e_jcheck: java expression for validate check
    *  e_jexception: java expression for exception object
    */ 
  data-validation-to-java-servlet-collect(|x_collection) :
    (e_jname,e_wcheck,e_wmessage) ->
    //(validatename,condition,message)    
    bstm |[
      if(!e_jcheck){
        x_collection.add(e_jexception);
      }
    ]|
    with  (e_jcheck,e_jexception) := <data-validation-to-java-servlet-helper> (e_jname,e_wcheck,e_wmessage)
   /**
    *  data invariants: collect all exceptions first   
    *
    *  e_jname: name used to identify validation check for manual connection
    *  e_jcheck: java expression for validate check
    *  e_jmessage: java expression for validate message
    *
    *  e_jcheck: java expression for validate check
    *  e_jexception: java expression for exception object
    */ 
  data-validation-to-java-servlet-collect-jexp(|x_collection) :
    (e_jname,e_jcheck,e_jmessage) ->
    bstm |[
      if(!e_jcheck){
        x_collection.add(e_jexception);
      }
    ]|
    with  (e_jcheck,e_jexception) := <data-validation-to-java-servlet-helper-jexp> (e_jname,e_jcheck,e_jmessage)
  
   /**
    *  data assertions: throw right away  
    *
    *  e_jname: name used to identify validation check for manual connection
    *  e_wcheck: webdsl expression for validate check
    *  e_wmessage: webdsl expression for validate message
    *
    *  e_jcheck: java expression for validate check
    *  e_jexception: java expression for exception object
    */ 
  data-validation-to-java-servlet-throw :
    (je_name,we_check,we_message) ->
    bstm |[
      if(!e_jcheck){
        throw e_jexception;
      }
    ]|
    with  (e_jcheck,e_jexception) := <data-validation-to-java-servlet-helper> (je_name,we_check,we_message)
    
   /**
    *  e_jname: name used to identify validation check for manual connection
    *  e_wcheck: webdsl expression for validate check
    *  e_wmessage: webdsl expression for validate message
    *
    *  e_jcheck: java expression for validate check
    *  e_jexception: java expression for exception object
    */ 
   data-validation-to-java-servlet-helper :  
    (e_jname,e_wcheck,e_wmessage) -> (e_jcheck,e_jexception)
    with e_jcheck := <expression-to-java-servlet> e_wcheck //expression types should be checked by typechecker
       ; e_jmessage := <expression-to-java-servlet> e_wmessage
       //; usedobjects := <collect(?FieldAccess(_,_) <+ ?Qualified(_,_) <+ ?Var(_))> e1
       ; e_jexception := e|[new utils.ValidationException(e_jname,e_jmessage)]|
       
   /**
    *  e_jname: name used to identify validation check for manual connection
    *  e_jcheck: java expression for validate check
    *  e_jmessage: java expression for validate message
    *
    *  e_jcheck: java expression for validate check
    *  e_jexception: java expression for exception object
    */ 
   data-validation-to-java-servlet-helper-jexp :  
    (e_jname,e_jcheck,e_jmessage) -> (e_jcheck,e_jexception)
    with e_jexception := e|[new utils.ValidationException(e_jname,e_jmessage)]|
  /*  
  add-relevant-objects(|usedobjects) :
    e -> <add-relevant-objects(|xs)> e|[ ps.getOldValuesForInputs().get(e3)==null?e.addRelevantObject(e3):e.addRelevantObject(ps.getOldValuesForInputs().get(e3)) ]|
    where not([] := usedobjects)
        ; [e1|xs] := usedobjects
        ; e2 := <expression-to-java-servlet> e1
        ; if GenericSort(_,_) := <type-of> e1 
          then e3 := java:expr |[ e2.hashCode() ]|
          else e3 := e2
          end
                  
  add-relevant-objects(|usedobjects) :
    e -> e
    where [] := usedobjects
    */