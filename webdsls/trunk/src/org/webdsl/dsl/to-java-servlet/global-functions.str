module org/webdsl/dsl/to-java-servlet/global-functions

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front
  org/webdsl/dsl/to-java-servlet/to-java-servlet

rules

  GenerateCodeJavaServlet = generate-java-servlet-global-functions; fail

	get-all-global-functions =
		?app@Application(qid, sections)
    ; <collect(\ Section(_,defs) -> <filter(?Function(_, _, _, _))> defs \);concat> sections

  generate-java-servlet-global-functions :
    app@Application(qid, sections) ->
    <emit-java-code> compilation-unit|[
      package utils;
  
      import java.io.*;
      import java.util.HashMap;
  
      import javax.servlet.*;
      import javax.servlet.http.*;
      import java.util.*;
      import utils.*;
      import pkgname.*;
      import pkgname2.*;
      import java.io.PrintWriter;
      
      import org.webdsl.lang.Environment;
  
      public class GlobalFunctions {
        private static org.hibernate.Session hibSession;
        private static TemplateCall templateArg = TemplateCall.None;
        private static HashMap<String, utils.TemplateCall> withcallsmap = new HashMap<String, utils.TemplateCall>();

        ~*cbds*
      }
    ]|
    with  pkgname    := <BeanPackage>
        ; pkgname2   := <DomainPackage>
        ; cbds* := <map(webdsl-function-to-java-servlet-function)> (<get-all-global-functions> app)
        
  // This translation is reused for static entity functions.
  webdsl-function-to-java-servlet-function(|mod*):
    function@Function(x, args, s, Block(stm*)) -> 
    class-body-dec |[
      public mod* t x#_(org.hibernate.Session hibSession, Environment env, param*, PrintWriter out)  {
        try {
          bstm1*
          bstm2*
          bstm*
        } 
        catch(ValidationException ve){
          throw ve;
        }
        catch(MultipleValidationExceptions ve){
          throw ve;
        }
      }
    ]|
    with  {| InFunction:
              rules( InFunction := <id> )
            ; if SimpleSort("Void") := s
              then  t      := Void()
              else  t      := <java-type> s end
            ; param* := <map(arguments-to-java-servlet)> args
            ; bstm*  := <statements-to-java-servlet> stm*
            ; usedvarnames := <collect(?Var(<id>)<+?Qualified(<is-string>,_))> stm*
            ; bstm1* := <get-global-vars-into-java-function(|usedvarnames)>
            ; bstm2* := <get-session-vars-into-java-function(|usedvarnames)>
            |}
  
  webdsl-function-to-java-servlet-function =
    	?Function(x, args, s, Block(stm*))
    ; if JavaInEntity 
      then webdsl-function-to-java-servlet-function(|[])
      else webdsl-function-to-java-servlet-function(|[mod |[ static ]| ])
      end
    
  get-global-vars-into-java-function(|usedvarnames) =
    <filter(get-global-vars-into-java-function-helper(|usedvarnames))> <bagof-AllGlobalVars>
  
  get-global-vars-into-java-function-helper(|usedvarnames) :
    elem |[ var x : srt := e ]| -> bstm |[ t x = (x_class) env.getVariable("~x"); ]| 
    where <elem> (x,usedvarnames)
    with  t := <java-type> srt
        ; x_class := <java-type-name> srt 

  get-session-vars-into-java-function(|usedvarnames) =
    <filter(get-session-vars-into-java-function-helper(|usedvarnames))> <bagof-SessionEntities>
   
  get-session-vars-into-java-function-helper(|usedvarnames) :
    def|[ session x { ebd* } ]| -> bstm |[ t x = (x_ent) env.getVariable("~x"); ]| 
    where <elem> (x,usedvarnames)
    with  x_ent := <capitalize-string> x
        ; t := type |[ x_ent ]|
        
