module org/webdsl/dsl/to-java-servlet/types/entity

imports
  libstrategolib

imports
  libwebdsl-front
  
rules

  builtin-java-type :
    SimpleSort("Entity") -> type|[ org.webdsl.WebDSLEntity ]|

rules

  call-to-java-servlet :
    Call(e1, "save", []) -> expr|[ 
      {| e2.validateSave();  
         //set version property to 1, which indicates a persisted entity
         e2.setVersion(1);
       | e_HibSession.save(e2) |} 
    ]|
    where <get-type-anno;has-entity-anno> e1
    with  e2 := <expression-to-java-servlet> e1

  call-to-java-servlet :
    Call(e1, "delete", []) -> 
    expr|[ {| e2.validateDelete(); 
            | e_HibSession.delete(e2) |} ]|
    where <get-type-anno;has-entity-anno> e1
    with  e2 := <expression-to-java-servlet> e1
    
  call-to-java-servlet :
    exp |[ loadEntity(e1,e2) ]| -> java |[ utils.EntityType.loadEntity(e_HibSession, t.class, e3) ]|
    with  t := <get-type-anno; java-type> e1
        ; e3 := <expression-to-java-servlet> e2