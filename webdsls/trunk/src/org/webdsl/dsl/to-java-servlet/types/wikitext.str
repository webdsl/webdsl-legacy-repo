module org/webdsl/dsl/to-java-servlet/types/wikitext

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules

  builtin-java-type :
    SimpleSort("WikiText") -> type|[ String ]|

  // length annotation now added during desugar
  //builtin-persistence-annotations :
  //  SimpleSort("WikiText") -> [anno|[ @Column(length = 1000000) ]|]  
  
rules 
  java-servlet-element-id : 
    tc@elem|[ inputWikiText(e)[passign*]{elem*} ]| -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"

  handle-actions-input-to-java-servlet :
    tc@elem|[ inputWikiText(e)[passign*]{elem*} ]| ->
    <wrap-input-java-code(|passign*)>
    bstm*|[
      ident += "~inputident"+uniqueid;
      if(ThreadLocalPage.get().getParammap().get(ident)!=null)
      {
        ~*<wrap-validation-context-code-store-inputs> bstm*|[
          //~*<text-length-check-java-servlet(|x_temp,e)>
          String x_temp = ThreadLocalPage.get().getParammap().get(ident);
          bstm* 
        ]|
      }
    ]|
    with  inputident := <get-inputnumber> tc
        ; x_temp := <newname> "temp"
        ; bstm* := <input-assignment-to-java-servlet(|x_temp)> e
    
  elem-to-java-servlet : 
    tc@elem|[ inputWikiText(e)[passign*]{elem*} ]| ->
    <wrap-input-render-java-code>
    bstm* |[ 
      String x_temp = ident+"~inputident"+uniqueid;
      ~*<render-error-messages-with-error-template(|java:expr|[ x_temp ]|,<ErrorTemplateInput>)> bstm*|[
        out.print("<textarea name=\""+x_temp+"\""+e_attrs1);
        ~*<connect-with-enclosing-label>
        out.print(">");
        if(ThreadLocalPage.get().inSubmittedForm())
        {
          out.print(utils.HTMLFilter.filter(ThreadLocalPage.get().getParammap().get(x_temp)));
        }
        else
        {
          bstm_exptostring*
        }
        out.print("</textarea>"); 
      ]|
    ]|
    with  inputident := <get-inputnumber> tc
        ; bstm_exptostring* := <to-page-string-java-servlet> e
        ; x_temp := <newname> "temp"
        ; e_attrs1 := <to-html-attrs(|"inputTextarea inputWikiText")> passign*     
    
  data-validation-to-java-servlet : 
    tc@elem|[ inputWikiText(e)[passign*]{elem*} ]| -> result
    with  result := <validation-context-wrapper-for-input(data-validation-to-java-servlet)> tc    
    
  elem-to-java-servlet :
    elem |[ outputWikiText(e)[passign*]{} ]| ->
    <wrap-common-context-code>
    bstm_exptostring*
    with  bstm_exptostring* := <to-page-string-java-servlet(|"org.webdsl.tools.WikiFormatter.wikiFormat",expr |[ ThreadLocalServlet.getContextPath() ]|)> e
  
