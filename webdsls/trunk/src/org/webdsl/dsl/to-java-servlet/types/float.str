module org/webdsl/dsl/to-java-servlet/types/float

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front

rules

  builtin-java-type :
    SimpleSort("Float") -> type|[ Float ]|
    
rules // java

  expression-to-java-servlet :
    Float(x) -> expr |[ ~float:Float(<concat-strings> [x1,"f"]) ]|
    where x1 := <string-replace(|"f", "");string-replace(|"F", "")> x

rules // conversion

  call-to-java-servlet :
    exp |[ e.round() ]| -> java |[ Math.round(e1) ]|
    where SimpleSort("Float") := <get-type-anno> e
    with  e1    := <expression-to-java-servlet> e

  call-to-java-servlet :
    exp |[ e.floor() ]| -> java |[ new Double(Math.floor(e1)).intValue() ]|
    where SimpleSort("Float") := <get-type-anno> e
    with  e1    := <expression-to-java-servlet> e

  call-to-java-servlet :
    exp |[ e.ceil() ]| -> java |[ new Double(Math.ceil(e1)).intValue() ]|
    where SimpleSort("Float") := <get-type-anno> e
    with  e1    := <expression-to-java-servlet> e
    
  call-to-java-servlet :
    exp |[ e.log() ]| -> java |[ new Double(Math.log(e1)).floatValue() ]|
    where SimpleSort("Float") := <get-type-anno> e
    with  e1 := <expression-to-java-servlet> e
    
  call-to-java-servlet :
    exp |[ e.log2() ]| -> java |[ new Double(Math.log(e1) / Math.log(2)).floatValue() ]|
    where SimpleSort("Float") := <get-type-anno> e
    with  e1 := <expression-to-java-servlet> e
        
rules 

  call-to-java-servlet :
    exp |[ random() ]| -> java |[ new Float(new Double(Math.random()).floatValue()) ]|
