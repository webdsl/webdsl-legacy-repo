module org/webdsl/dsl/to-java-servlet/types/image

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front

rules // types

  builtin-java-type :
    SimpleSort("Image") -> type|[ utils.File ]|

  builtin-java-type-default-value :
    SimpleSort("Image") -> java|[ null ]|

  //used in entity generation
  //hibernate has a built-in mapping for java.sql.Blob
  type-specific-annotations :
    Property(x_prop, k, s, annos) -> <file-specific-annotations>
    where <?SimpleSort("Image")> s

rules

  requires-multipart-encoded-form = ?TemplateCall("inputImage",_,_,_) //?elem|[inputImage(e)[passign*]{}]|
  
  handle-actions-input-to-java-servlet :
    tc@elem|[ inputImage(e)[passign*]{} ]| -> <handle-actions-input-to-java-servlet> TemplateCall("inputFile", [e], passign*, []){anno*} //stratego bug: cannot add annotations with concrete syntax
    where _{anno*} := tc
   
  elem-to-java-servlet :
    tc@elem|[ inputImage(e)[passign*]{} ]| -> <elem-to-java-servlet> TemplateCall("inputFile", [e], passign*, []){anno*} //stratego bug: cannot add annotations with concrete syntax
    where _{anno*} := tc
    
  java-servlet-element-id : 
    tc@elem|[ inputImage(e)[passign*]{} ]| -> <java-servlet-element-id-template-call> tc

  //for displaying images, modified action call is used, the src of the image needs to be a call to the page with the arguments that invoke a download action
   
  java-servlet-element-id : 
    tc@elem|[ outputImage(e,e1)[passign*]{} ]| -> <java-servlet-element-id-template-call> tc
     
  elem-to-java-servlet :
    tc@TemplateCall("outputImage",[e2,ThisCall(name, args)],passign*,[]) ->
    <for-loop-counter-wrap-code> 
    bstm* |[ 
    //out.print("<img alt=\""+e4+"\" "+e_attrs1+" src='javascript:serverInvokeGetImage(\""+"~app"+"/" + ps.getPageName()  +"\",\""+ "~tcident"+ident+e5 +"\", \""+ps.getHiddenPostParams()+"\", this)' >");
    ident += "~tcident"+e5 ;
    out.print("<img alt=\""+e4+"\" "+e_attrs1+" src='"+"~app"+"/" + ps.getPageName()  +"?"+ ident +"=1&"+ps.getHiddenPostParams()+"' >");
    out.print("</img>");
    ]|
    with  tcident := <get-inputnumber> tc   
        ; e_attrs1 := <to-html-attrs> passign* 
        ; e4 := <arg-to-value-string-java-servlet> e2       
        ; e5 := <actual-action-args-to-state-enc> args
        ; app := <AppRoot>
        
  handle-actions-to-java-servlet: 
    tc@TemplateCall("outputImage",[e2,ThisCall(x_name, args)],passign*,[]) ->
    <for-loop-counter-wrap-code> 
    bstm*|[
        ident += "~tcident"+e5;
        if(ps.getParammap().get(ident)!=null)
        {
            x_name temp = (x_name)actionclasses.get("~x_name");
            temp.x_name(e*);
        }
    ]|
    with  tcident := <get-inputnumber> tc   
        ; e* := <map(expression-to-java-servlet)> args
        ; e5 := <actual-action-args-to-state-enc> args

rules //image download   
    
  call-to-java-servlet :
    exp|[ e.download() ]| -> java|[ ps.setDownload(e2) ]|
    where SimpleSort("Image") := <type-of> e
    with  e2 := <expression-to-java-servlet> e
    
  call-to-java-servlet :
    exp  |[ e.fileName() ]| -> java |[ e2.getFileName() ]|
    where SimpleSort("Image") := <type-of> e
    with  e2 := <expression-to-java-servlet> e