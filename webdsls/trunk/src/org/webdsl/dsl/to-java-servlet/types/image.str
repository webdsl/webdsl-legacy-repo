module org/webdsl/dsl/to-java-servlet/types/image

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front

rules // types

  builtin-java-type :
    SimpleSort("Image") -> type|[ utils.File ]|

  //used in entity generation
  //hibernate has a built-in mapping for java.sql.Blob
  type-specific-annotations :
    Property(x_prop, k, s, annos) -> <file-specific-annotations>
    where <?SimpleSort("Image")> s

rules
  
  java-servlet-element-id :
    tc@OutputImage(_,_,_){anno*} -> tc{ActionNumber(newid),anno*} 
    where  newid := <newname> "action"

rules

  requires-multipart-encoded-form = ?TemplateCall("inputImage",_,_,_) //?elem|[inputImage(e)[passign*]{}]|
  
  handle-actions-input-to-java-servlet :
    tc@elem|[ inputImage(e)[passign*]{elem*} ]| -> <handle-actions-input-to-java-servlet> TemplateCall("inputFile", [e], passign*, TemplateBody([])){anno*} //stratego bug: cannot add annotations with concrete syntax
    where _{anno*} := tc
   
  elem-to-java-servlet :
    tc@elem|[ inputImage(e)[passign*]{elem*} ]| -> <elem-to-java-servlet> TemplateCall("inputFile", [e], passign*, TemplateBody([])){anno*} //stratego bug: cannot add annotations with concrete syntax
    where _{anno*} := tc
    
  java-servlet-element-id : 
    tc@elem|[ inputImage(e)[passign*]{elem*} ]| -> <java-servlet-element-id-template-call> tc

  //for displaying images, modified action call is used, the src of the image needs to be a call to the page with the arguments that invoke a download action
     
  elem-to-java-servlet :
    tc@OutputImage(ActionCall(name, args),passign*,elem*) ->
    <wrap-input-render-java-code>
    bstm* |[ 
      //out.print("<img alt=\""+e4+"\" "+e_attrs1+" src='javascript:serverInvokeGetImage(\""+"~app"+"/" + ThreadLocalPage.get().getPageName()  +"\",\""+ "~tcident"+ident+e5 +"\", ["+ThreadLocalPage.get().getHiddenPostParamsJson()+"], this)' >");
      ident += "~tcident"+e5;
      ~*<render-error-messages-with-error-template(|java:expr|[ ident ]|,<ErrorTemplateInput>)> bstm*|[
        out.print("<img alt=\"");
        bstm*
        out.print("\""+e_attrs1+"src='"+ThreadLocalServlet.getContextPath()+"/" + ThreadLocalPage.get().getActionTarget()  +"?"+ ident +"=1&"+"~<action-call-with-get-param-name>"+"=1&"+ThreadLocalPage.get().getHiddenPostParams()+"' >");
        out.print("</img>");
      ]|
    ]|
    with  tcident := <get-actionnumber> tc   
        ; bstm* := <elems-to-java-servlet> elem*      
        ; e5 := <actual-action-args-to-state-enc> args
        ; e_attrs1 := <to-html-attrs(|"outputimage")> passign*
        
  data-validation-to-java-servlet : 
    tc@elem|[ inputImage(e)[passign*]{elem*} ]| -> result
    with  result := <validation-context-wrapper-for-input(data-validation-to-java-servlet)> tc
        
  handle-actions-to-java-servlet: 
    tc@OutputImage(ActionCall(x_name, args),passign*,elem*) ->
    <for-loop-counter-wrap-code> 
    bstm*|[
        ident += "~tcident"+e5;
        if(ThreadLocalPage.get().getParammap().get(ident)!=null)
        {
            x_name temp = (x_name)env.getAction("~x_name");
            temp.x_name(e*,ident);
        }
    ]|
    with  tcident := <get-actionnumber> tc   
        ; e* := <map(expression-to-java-servlet)> args
        ; e5 := <actual-action-args-to-state-enc> args

rules //image download   
    
  call-to-java-servlet :
    exp|[ e.download() ]| -> java|[ ThreadLocalPage.get().setDownload(e2) ]|
    where SimpleSort("Image") := <get-type-anno> e
    with  e2 := <expression-to-java-servlet> e
    
  call-to-java-servlet :
    exp  |[ e.fileName() ]| -> java |[ e2.getFileName() ]|
    where SimpleSort("Image") := <get-type-anno> e
    with  e2 := <expression-to-java-servlet> e

  call-to-java-servlet :
    exp  |[ e.resize(e_width, e_height) ]| -> java |[ utils.ImageSizeUtils.resizeImage(e_HibSession, e2, e3, e4) ]|
    where SimpleSort("Image") := <get-type-anno> e
    with  e2 := <expression-to-java-servlet> e
       ;  e3 := <expression-to-java-servlet> e_width
       ;  e4 := <expression-to-java-servlet> e_height

  call-to-java-servlet :
    exp  |[ e.crop(e_x, e_y, e_width, e_height) ]| -> java |[ utils.ImageSizeUtils.cropImage(e_HibSession, e2, e3, e4, e5, e6) ]|
    where SimpleSort("Image") := <get-type-anno> e
    with  e2 := <expression-to-java-servlet> e
       ;  e3 := <expression-to-java-servlet> e_x
       ;  e4 := <expression-to-java-servlet> e_y
       ;  e5 := <expression-to-java-servlet> e_width
       ;  e6 := <expression-to-java-servlet> e_height

  call-to-java-servlet :
    exp  |[ e.getWidth() ]| -> java |[ utils.ImageSizeUtils.getWidth(e_HibSession, e2) ]|
    where SimpleSort("Image") := <get-type-anno> e
    with  e2 := <expression-to-java-servlet> e
  
  call-to-java-servlet :
    exp  |[ e.getHeight() ]| -> java |[ utils.ImageSizeUtils.getHeight(e_HibSession, e2) ]|
    where SimpleSort("Image") := <get-type-anno> e
    with  e2 := <expression-to-java-servlet> e

  call-to-java-servlet :
    exp  |[ e.clone() ]| -> java |[ e2.makeClone() ]|
    where SimpleSort("Image") := <get-type-anno> e
    with  e2 := <expression-to-java-servlet> e
