module org/webdsl/dsl/to-java-servlet/types/string

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front

rules
  
  builtin-java-type :
    SimpleSort("String") -> type|[ String ]|

rules

  expression-to-java-servlet :
    String(x) -> expr|[ "~x" ]|

rules 

  java-servlet-element-id : 
    tc@elem|[ inputString(e)[passign*]{} ]| -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"

  handle-actions-input-to-java-servlet :
    tc@elem|[ inputString(e)[passign*]{} ]| ->
    <for-loop-counter-wrap-code> 
    bstm*|[
      ident += "~inputident"+uniqueid;
      if (ps.getParammap().get(ident)!=null) {
      
        ~*<string-length-check-java-servlet(|x_temp,e)>
      
        bstm 
      }
    ]|
    with  inputident := <get-inputnumber> tc
        ; x_temp := <newname> "temp"
        ; bstm := <input-assignment-to-java-servlet(|x_temp)> e
        
  string-length-check-java-servlet(|x_temp,e) =
    ! bstm* |[
        String x_temp = ps.getParammap().get(ident);
        if(x_temp.length()>255) { //currently the size of string properties in db (varchar(255))
          ~*<add-conversion-error> (e,e|["input too long (max 255 characters)"]|)
          x_temp = null;
        }
    ]|
    
  elem-to-java-servlet : 
    tc@elem|[ inputString(e)[passign*]{} ]| ->
    <for-loop-counter-wrap-code>
    bstm* |[ 
      String x_temp = ident+"~inputident"+uniqueid;
      out.print("<input name=\""+x_temp+"\" "+e_attrs1+" type=\"text\" value=\"");
      if(inSubmittedForm) {
        out.print(ps.getParammap().get(x_temp));
      } 
      else {
        bstm_exptostring*
      }
      out.print("\" />"); 
    ]|
    with  inputident := <get-inputnumber> tc
        ; bstm_exptostring* := <to-page-string-java-servlet> e
        ; x_temp := <newname> "temp"
        ; e_attrs1 := <to-html-attrs> passign*        
    
  elem-to-java-servlet :
    elem |[ outputString(e)[passign*]{} ]| ->
    bstm_exptostring*
    with  bstm_exptostring* := <to-page-string-java-servlet(|"utils.HTMLFilter.filter")> e
    
rules //native java operations    
    
 call-to-java-servlet :
   exp  |[ e.contains(e1) ]| ->   java |[ e2.contains(e3) ]|
   where SimpleSort("String") := <type-of> e
   with  e2    := <expression-to-java-servlet> e
      ;  e3    := <expression-to-java-servlet> e1

 call-to-java-servlet :
   exp  |[ log(e1) ]| ->   java |[ System.out.println(e2) ]|
   where SimpleSort("String") := <type-of> e1
   with  e2 := <expression-to-java-servlet> e1

 call-to-java-servlet :
   exp  |[ e1.toString() ]| -> java |[ e2.toString() ]|
   with  e2 := <expression-to-java-servlet> e1