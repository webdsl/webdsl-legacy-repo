module org/webdsl/dsl/to-java-servlet/types/date

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  
imports 
  libwebdsl-front  

rules // Peristence annotations

  builtin-persistence-annotations :
    SimpleSort("Time") -> [anno|[ @Temporal(TemporalType.TIME) ]|]
    
  builtin-persistence-annotations :
    SimpleSort("Date") -> [anno|[ @Temporal(TemporalType.DATE) ]|]
    
  builtin-persistence-annotations :
    SimpleSort("DateTime") -> [anno|[ @Temporal(TemporalType.TIMESTAMP) ]|]

rules // Typechecking

  builtin-java-type : 
    SimpleSort("Date") -> type|[ java.util.Date ]|
    
  builtin-java-type : 
    SimpleSort("DateTime") -> type|[ java.util.Date ]|
    
  builtin-java-type : 
    SimpleSort("Time") -> type|[ java.util.Date ]|

rules 
  call-to-java-servlet :
    exp|[ now() ]| -> java|[ new java.util.Date() ]|
    //ThisCall("now", []) -> java|[ new java.util.Date() ]|

  call-to-java-servlet :
    exp|[ today() ]| -> java|[ new java.util.Date() ]|
    //ThisCall("now", []) -> java|[ new java.util.Date() ]|

  call-to-java-servlet :
    exp|[ e1.before(e2) ]| -> java|[ e3.before(e4) ]|
    with e3 := <expression-to-java-servlet> e1
        ; e4 := <expression-to-java-servlet> e2

  call-to-java-servlet :
    exp|[ e1.after(e2) ]| -> java|[ e3.after(e4) ]|
    with e3 := <expression-to-java-servlet> e1
        ; e4 := <expression-to-java-servlet> e2

  call-to-java-servlet :
    exp|[ Date(e1, e2) ]| -> java|[ org.webdsl.tools.Utils.parseDate(e3, e4) ]|
    //ThisCall("Date", [d, f]) -> java|[ org.webdsl.tools.Utils.parseDate(e1, e2) ]|
    with e3 := <expression-to-java-servlet> e1
        ; e4 := <expression-to-java-servlet> e2

  call-to-java-servlet :
    exp|[ Date(e1) ]| -> java|[ org.webdsl.tools.Utils.parseDate(e2, "dd/MM/yyyy") ]|
    //ThisCall("Date", [d]) -> java|[ org.webdsl.tools.Utils.parseDate(e1, "dd/MM/yyyy") ]|
    with e2 := <expression-to-java-servlet> e1

  call-to-java-servlet :
    exp|[ Time(e1, e2) ]| -> java|[ org.webdsl.tools.Utils.parseDate(e3, e4) ]|
    //ThisCall("Time", [d, f]) -> java|[ org.webdsl.tools.Utils.parseDate(e1, e2) ]|
    with SimpleSort("Date") := <type-of> e1
        ; e3 := <expression-to-java-servlet> e1
        ; e4 := <expression-to-java-servlet> e2

  call-to-java-servlet :
    exp|[ Time(e1) ]| -> java|[ org.webdsl.tools.Utils.parseDate(e2, "H:mm") ]|
    //ThisCall("Time", [d]) -> java|[ org.webdsl.tools.Utils.parseDate(e1, "H:mm") ]|
    with e2 := <expression-to-java-servlet> e1

  call-to-java-servlet :
    exp|[ DateTime(e1, e2) ]| -> java|[ org.webdsl.tools.Utils.parseDate(e3, e4) ]|
    //ThisCall("DateTime", [d, f]) -> java|[ org.webdsl.tools.Utils.parseDate(e1, e2) ]|
    with e3 := <expression-to-java-servlet> e1
        ; e4 := <expression-to-java-servlet> e2

  call-to-java-servlet :
    exp|[ DateTime(e1) ]| -> java|[ org.webdsl.tools.Utils.parseDate(e2, "dd/MM/yyyy H:mm") ]|
    //ThisCall("DateTime", [d]) -> java|[ org.webdsl.tools.Utils.parseDate(e1, "dd/MM/yyyy H:mm") ]|
    with e2 := <expression-to-java-servlet> e1

rules

  java-servlet-element-id : //refactoring needed, simply add an input number to any template call in core-to-java-servlet
    tc@elem|[ inputDate(e)[passign*]{} ]| -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"
 java-servlet-element-id :
    tc@elem|[ inputTime(e)[passign*]{} ]| -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"
 java-servlet-element-id :
    tc@elem|[ inputDateTime(e)[passign*]{} ]| -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"
  
  date-format-string = !e|[ "dd/MM/yyyy" ]|
  time-format-string = !e|[ "H:mm" ]|
  datetime-format-string = !e|[ "dd/MM/yyyy H:mm" ]|
  
  handle-actions-input-to-java-servlet :
    tc@elem|[ inputDate(e)[passign*]{} ]| ->
    <handle-actions-input-to-java-servlet-input-date> (inputident,e,passign*,<date-format-string>)    
    with inputident := <get-inputnumber> tc     

  handle-actions-input-to-java-servlet :
    tc@elem|[ inputTime(e)[passign*]{} ]| ->
    <handle-actions-input-to-java-servlet-input-date> (inputident,e,passign*,<time-format-string>)    
    with inputident := <get-inputnumber> tc  
    
  handle-actions-input-to-java-servlet :
    tc@elem|[ inputDateTime(e)[passign*]{} ]| ->   
    <handle-actions-input-to-java-servlet-input-date> (inputident,e,passign*,<datetime-format-string>)    
    with inputident := <get-inputnumber> tc  
    
  handle-actions-input-to-java-servlet-input-date :
    (inputident,e,passign*,e1) ->
    <for-loop-counter-wrap-code> 
    bstm*|[
      ident += "~inputident"+uniqueid;
      if (ps.getParammap().get(ident)!=null) {
      
        java.text.SimpleDateFormat sdf = new java.text.SimpleDateFormat(e1);
        
        java.util.Date x_temp = sdf.parse(ps.getParammap().get(ident),new java.text.ParsePosition(0));
        if(x_temp==null) { 
          ~*<add-conversion-error> (e,e|["not in a valid format"]|)
        }
      
        bstm 
      }
    ]|
    with  x_temp := <newname> "temp"
        ; name := <pp-webdsl> e
        ; bstm := <input-assignment-to-java-servlet(|x_temp)> e 

  elem-to-java-servlet :
    tc@elem|[ inputDate(e)[passign*]{} ]| ->
    <elem-to-java-servlet-input-date> (inputident,e,passign*,<date-format-string>)    
    with inputident := <get-inputnumber> tc     
    
  elem-to-java-servlet :
    tc@elem|[ inputTime(e)[passign*]{} ]| ->
    <elem-to-java-servlet-input-date> (inputident,e,passign*,<time-format-string>)    
    with inputident := <get-inputnumber> tc  
    
  elem-to-java-servlet :
    tc@elem|[ inputDateTime(e)[passign*]{} ]| ->
    <elem-to-java-servlet-input-date> (inputident,e,passign*,<datetime-format-string>)    
    with inputident := <get-inputnumber> tc
    
  elem-to-java-servlet-input-date :
    (inputident,e,passign*,e_formatstring) ->
    <for-loop-counter-wrap-code>
    bstm* |[ 
      String x_temp = ident+"~inputident"+uniqueid;
      out.print("<input name=\""+x_temp+"\" "+e_attrs1+" type=\"text\" value=\"");
      if(inSubmittedForm) {
        out.print(ps.getParammap().get(x_temp));
      } 
      else {
        bstm_exptostring*
      }
      out.print("\" />"); 
    ]|
    with  bstm_exptostring* := <webdsl-exp-to-java-bstms-format-as-date(|e_formatstring)> e
        ; x_temp := <newname> "temp"
        ; e_attrs1 := <to-html-attrs> passign*     
    
  elem-to-java-servlet :
    elem|[ outputDate(e)[passign*]{} ]| ->
    <webdsl-exp-to-java-bstms-format-as-date(|<date-format-string>)> e

  elem-to-java-servlet :
    elem|[ outputTime(e)[passign*]{} ]| ->
    <webdsl-exp-to-java-bstms-format-as-date(|<time-format-string>)> e
    
  elem-to-java-servlet :
    elem|[ outputDateTime(e)[passign*]{} ]| ->
    <webdsl-exp-to-java-bstms-format-as-date(|<datetime-format-string>)> e

  webdsl-exp-to-java-bstms-format-as-date(|e1) :
    e ->
    bstm*|[
      String x_tmpstring;
      try {
        java.text.SimpleDateFormat x_format = new java.text.SimpleDateFormat(e1);
        StringBuffer x_buf = new StringBuffer();
        x_format.format(e0,x_buf,new java.text.FieldPosition(0));
        x_tmpstring = x_buf.toString();
      }
      catch(NullPointerException npe) {
        x_tmpstring = "";
      }
      out.print(x_tmpstring);
    ]|
    with x_tmpstring := <newname> "tmpstring"
       ; x_buf := <newname> "buffer" 
       ; x_format := <newname> "formatter"
       ; e0 := <expression-to-java-servlet> e
      