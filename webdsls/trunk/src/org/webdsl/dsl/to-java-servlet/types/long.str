module org/webdsl/dsl/to-java-servlet/types/long

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules // java type

  builtin-java-type :
    SimpleSort("Long") -> type|[ Long ]|
    
rules // Int literal

   expression-to-java-servlet :
     Long(x) -> Lit(Deci(x))
  
rules // conversion
  
  /*
  call-to-java-servlet :
    exp  |[ e.floatValue() ]| ->   java |[ e1.floatValue() ]|
    where SimpleSort("Int") := <type-of> e
    with  e1    := <expression-to-java-servlet> e  
    */
  
rules
    
  java-servlet-element-id :
    tc@elem|[ inputLong(e)[passign*]{elem*} ]| -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"
          
  //load the input string and try to convert it to int, insert null if it fails and specify an error
  handle-actions-input-to-java-servlet :
    tc@elem|[ inputLong(e)[passign*]{elem*} ]| ->
    <wrap-input-java-code(|passign*)>
    bstm*|[
      ident += "~inputident"+uniqueid;
      if(ps.getParammap().get(ident)!=null)
      {
        ~*<wrap-validation-context-code-store-inputs> bstm*|[
          String name = ps.getParammap().get(ident);
          Long x_temp = null;
          if(!name.matches("-?\\d+")){
            ~*<add-conversion-error> (e,e|["Not a valid number"]|)
          }
          else {
            try {
              x_temp = Long.parseLong(name);
            }
            catch(NumberFormatException nfe) {
              ~*<add-conversion-error> (e,e|["Outside of possible number range"]|)
            }  
          }
          bstm*
        ]|
      }
    ]|
    with  inputident := <get-inputnumber> tc
        ; x_temp := <newname> "temp"
        ; name := <pp-webdsl> e
        ; bstm* := <input-assignment-to-java-servlet(|x_temp)> e      
        
  elem-to-java-servlet :
    tc@elem|[ inputLong(e)[passign*]{elem*} ]| ->
    <wrap-input-render-java-code>
    bstm* |[ 
      String x_temp = ident+"~inputident"+uniqueid;
      ~*<render-error-messages-with-error-template(|java:expr|[ x_temp ]|,<ErrorTemplateInput>)> bstm*|[
        out.print("<input name=\""+x_temp+"\""+e_attrs1);
        ~*<connect-with-enclosing-label>
        out.print(" value=\"");
        if(ps.inSubmittedForm()) {
          out.print(utils.HTMLFilter.filter(ps.getParammap().get(x_temp)));
        }
        else {
          bstm_exptostring*
        }
        out.print("\" />"); 
      ]|
    ]|
    with  inputident := <get-inputnumber> tc
        ; bstm_exptostring* := <to-page-string-java-servlet> e
        ; x_temp := <newname> "temp"
        ; e_attrs1 := <to-html-attrs(|"inputLong")> passign*

  data-validation-to-java-servlet : 
    tc@elem|[ inputLong(e)[passign*]{elem*} ]| -> result
    with  result := <validation-context-wrapper-for-input(data-validation-to-java-servlet)> tc

  elem-to-java-servlet :
    elem|[ outputLong(e)[passign*]{} ]| ->
    <wrap-common-context-code>
    bstm* |[ 
      out.print(utils.HTMLFilter.filter(String.valueOf(e1))); 
    ]|
    with  e1 := <expression-to-java-servlet> e
