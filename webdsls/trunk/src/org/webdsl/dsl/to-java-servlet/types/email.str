module org/webdsl/dsl/to-java-servlet/types/email

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules

  builtin-java-type :
    SimpleSort("Email") -> type|[ String ]|
  
rules

// reuse String input/output generation

  java-servlet-element-id : 
    tc@elem|[ inputEmail(e)[passign*]{} ]| -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"

  handle-actions-input-to-java-servlet :
    tc@elem|[ inputEmail(e)[passign*]{} ]| ->
    <handle-actions-input-to-java-servlet-string-helper(check-email-type-java-servlet)> tc

  check-email-type-java-servlet(|x_temp,e) =
    !bstm* |[
      if(!x_temp.matches("^([a-zA-Z0-9_\\-\\.]+)@((\\[[0-9]{1,3}\\.[0-9]{1,3}\\.[0-9]{1,3}\\.)|(([a-zA-Z0-9\\-]+\\.)+))([a-zA-Z]{2,4}|[0-9]{1,3})(\\]?)")) {
        ~*<add-conversion-error> (e,e|["Not a valid email address"]|)
      }
    ]|

  elem-to-java-servlet : 
    tc@elem|[ inputEmail(e)[passign*]{} ]| ->
    <elem-to-java-servlet-string-helper> tc
    
  elem-to-java-servlet :
    elem|[ outputEmail(e)[passign*]{} ]| ->
    <elem-to-java-servlet> elem|[ outputString(e)[passign*]{} ]|
