module org/webdsl/dsl/to-java-servlet/types/url

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules

  builtin-java-type :
    SimpleSort("URL") -> type|[ String  ]|
  
rules

  java-servlet-element-id : 
    tc@elem|[ inputURL(e)[passign*]{} ]|  -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"

  handle-actions-input-to-java-servlet :
    tc@elem|[ inputURL(e)[passign*]{} ]|  ->
    <for-loop-counter-wrap-code> 
    bstm*|[
          
        ident += "~inputident"+uniqueid;
        
        if(ps.getParammap().get(ident)!=null)
        {
            ~*<string-length-check-java-servlet(|x_temp,name)>
            bstm 
        }
      
    ]|
    with  inputident := <get-inputnumber> tc
        ; x_temp := <newname> "temp"
        ; name := <pp-webdsl> e
        ; bstm := <input-assignment-to-java-servlet(|x_temp)> e
    
  elem-to-java-servlet : 
    tc@elem|[ inputURL(e)[passign*]{} ]|  ->
    <for-loop-counter-wrap-code>
    bstm* |[ 
    String x_temp = ident+"~inputident"+uniqueid;
    out.print("<input name=\""+x_temp+"\" "+e_attrs1+"  type=\"text\" value=\"");
    if(inSubmittedForm)
    {
      out.print(ps.getParammap().get(x_temp));
    }
    else
    {
      out.print(e1);
    }
    out.print("\" />"); 
    ]|
    with  inputident := <get-inputnumber> tc
        ; e1 := <arg-to-value-string-java-servlet> e
        ; x_temp := <newname> "temp"
        ; e_attrs1 := <to-html-attrs> passign*                
    
    

  elem-to-java-servlet :
    TemplateCall("url",[e],[passign*],[]) ->
    bstm|[
      out.print(e1);
    ]|
    //%> <h:outputText value="<%= url %>" /> <%
    where not(ThisCall(p,args) := e)
        ; e1 := <arg-to-value-string-java-servlet> e
