module org/webdsl/dsl/to-java-servlet/types/url

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front
  
rules

  builtin-java-type :
    SimpleSort("URL") -> type|[ String  ]|
    
rules

 call-to-java-servlet :
   exp  |[ url(e1) ]| -> java |[ e2 ]|
   where <type-compatible> (<type-of> e1,  SimpleSort("String"))
   with  e2 := <expression-to-java-servlet> e1
  
rules

  java-servlet-element-id : 
    tc@elem|[ inputURL(e)[passign*]{elem*} ]|  -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"

  handle-actions-input-to-java-servlet :
    tc@elem|[ inputURL(e)[passign*]{elem*} ]|  ->
    <wrap-input-java-code(|passign*)>
    bstm*|[
        ident += "~inputident"+uniqueid;
        if(ps.getParammap().get(ident)!=null)
        {
          ~*<wrap-validation-context-code-store-inputs> bstm*|[
            //~*<string-length-check-java-servlet(|x_temp,e)>
            String x_temp = ps.getParammap().get(ident);
            bstm*
          ]|
        }
    ]|
    with  inputident := <get-inputnumber> tc
        ; x_temp := <newname> "temp"
        ; bstm* := <input-assignment-to-java-servlet(|x_temp)> e
    
  elem-to-java-servlet : 
    tc@elem|[ inputURL(e)[passign*]{elem*} ]|  ->
    <wrap-input-render-java-code>
    bstm* |[ 
      String x_temp = ident+"~inputident"+uniqueid;
      ~*<render-error-messages-with-error-template(|java:expr|[ x_temp ]|,<ErrorTemplateInput>)> bstm*|[
        out.print("<input name=\""+x_temp+"\""+e_attrs1+"type=\"text\"");
        ~*<connect-with-enclosing-label>
        out.print(" value=\"");
        if(ps.inSubmittedForm())
        {
          out.print(utils.HTMLFilter.filter(ps.getParammap().get(x_temp)));
        }
        else
        {
          bstm_exptostring*
        }
        out.print("\" />"); 
      ]|
    ]|
    with  inputident := <get-inputnumber> tc
        ; bstm_exptostring* := <to-page-string-java-servlet> e
        ; x_temp := <newname> "temp"
        ; e_attrs1 := <to-html-attrs(|"inputURL")> passign*     

  data-validation-to-java-servlet : 
    tc@elem|[ inputURL(e)[passign*]{elem*} ]| -> result
    with  result := <validation-context-wrapper-for-input(data-validation-to-java-servlet)> tc

  elem-to-java-servlet :
    TemplateCall("url",[e],[passign*],TemplateBody([])) ->
    <wrap-common-context-code>
    bstm_exptostring*
    where not(ThisCall(p,args) := e)
        ; bstm_exptostring* := <to-page-string-java-servlet> e
