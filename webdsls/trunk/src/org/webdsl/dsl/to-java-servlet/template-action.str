module org/webdsl/dsl/to-java-servlet/template-action

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

rules // action methods

  /**
   * Generate action method code
   */
  GenerateCodeJavaServlet = where(not(ThisEmail));log-time(generate-action-java-servlet-code|"generate-action-java-servlet-code"); fail

  //should only generate an action function
  generate-action-java-servlet-code :
    webdsl |[ action x_action(farg*) { stat* } ]| ->
    (<generate-java-servlet-template-partial>
    java |[
      @Partial
      private void initializePassOn()
      {
        env.putAction(
          "~x_action"
        ,
          new x_action(){  
            boolean isRedirected = false;
            
            public void setIsRedirected(boolean b){
              isRedirected=b;
            }
          
            public void x_action(param*)
            {
              ThreadLocalAction.set(this);
            
              if (!ThreadLocalPage.get().hasExecutedAction)
              {
                
                boolean actionFailed = false;
                ThreadLocalPage.get().hasExecutedAction = true; 
                try {
                  bstm*
                } 
                catch(utils.ValidationException ve){
                  ThreadLocalPage.get().getValidationExceptions().add(ve.setName(actionident));
                  ThreadLocalPage.get().clearHibernateCache();
                  //System.out.println("cache clear on action fail");
                  actionFailed = true;                 
                }
                catch(utils.MultipleValidationExceptions ve){
                  for(utils.ValidationException vex : ve.getValidationExceptions()){
                    ThreadLocalPage.get().getValidationExceptions().add(vex.setName(actionident));
                  }
                  ThreadLocalPage.get().clearHibernateCache();
                  //System.out.println("cache clear on action fail");              
                  actionFailed = true;
                }
                catch(Exception excep)  {
                  System.out.println("exception");
                  ThreadLocalPage.get().clearHibernateCache();
                  //System.out.println("cache clear on action fail");
                  excep.printStackTrace();               
                  ThreadLocalPage.get().getValidationExceptions().add(new ValidationException(actionident,"An error occured while processing your request"));
                  actionFailed = true;
                  //throw new RuntimeException(excep);
                }
                //redirect to current page by default
                finally{
                  if(!isRedirected && !ThreadLocalPage.get().isDownloadSet() && !actionFailed){
                    try {
                      if (!ThreadLocalPage.get().isAjaxActionExecuted()) {
                        ThreadLocalPage.get().setRedirectUrl(ThreadLocalPage.get().getPageUrlWithParams());
                        if(!ThreadLocalPage.get().isActionLinkUsed()){ response.sendRedirect(ThreadLocalPage.get().getRedirectUrl()); }
                      }
                      else if (!isRedirected) { //nothing updated, 
                        out.print("{ action: \"refresh\" },");           
                      }
                    } catch (IOException ioe) {
                      System.out.println("redirect failed");
                      ioe.printStackTrace();
                    }
                  }
                }
              }
            }
          }  
        );
      }
    
    ]|
    ,<emit-java-code>
    compilation-unit|[
      package pkgname3;
      import pkgname2.*;
      public abstract class x_action implements utils.ActionClass{
        public abstract void x_action(param*);
      }
    ]|
    )
    with  
    {| InAction :
         rules(InAction := <id>)
       ; param* := <add-input-id-for-action-as-param;map(arguments-to-java-servlet<+debug(!"map failed in template-action.str"))> farg*
       ; bstm* := <statements-to-java-servlet> stat*
    |}
    ; pkgname2 := <DomainPackage>
    ; pkgname3 := <ActionPackage>
  
  add-input-id-for-action-as-param :
    params -> <concat> [params,[Arg("actionident",SimpleSort("String"))]]
    