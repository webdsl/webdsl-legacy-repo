module org/webdsl/dsl/to-java-servlet/uicomponents/action

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front
    
rules 

  is-classic-action = 
      ?tc@TemplateCall(_, _, passign*, _)
    ; where (<fetch(?PropertyAssignment("classic", "true"))> passign*)

  elem-to-java-servlet :
    tc@TemplateCall("action",[e2,call@ThisCall(name, args)],passign*,TemplateBody([])) ->
    <wrap-action-render-java-code>
    bstm* |[
      ident = e_actionid;
      if(ThreadLocalPage.get().isServingAsAjaxResponse || ThreadLocalPage.get().isAjaxRuntimeRequest() || e_ajaxmodifier || ThreadLocalPage.get().getFormIdent().equals("")){
	~*<render-error-messages-with-error-template(|java:expr|[ ident ]|,<ErrorTemplateAction>)> bstm*|[ 
	  out.print("<input type=\"button\" value=\"");
	  bstm_exptostring*
	  out.print("\" "+e_attrs1+" ");
	  out.print("onclick='javascript:serverInvoke(\""+ThreadLocalServlet.getContextPath()+"/" + ThreadLocalPage.get().getActionTarget()  +"\",\""+ ident +"\", ["+ThreadLocalPage.get().getHiddenPostParamsJson()+"],\""+ThreadLocalPage.get().getFormIdent()+"\", this, true)'/>"); 
	]|
      }
      else{
        ~*<render-error-messages-with-error-template(|java:expr|[ ident ]|,<ErrorTemplateAction>)> bstm*|[
          /*if(ThreadLocalPage.get().getParammap().get(ident)!=null)
          {
            bstm_validation*
          }*/
          out.print("<input type=\"submit\" name=\""+ident+"\" value=\"");
          bstm_exptostring*
          out.print("\" "+e_attrs1+" />"); 
        ]|
      }
    ]|
    with  bstm_exptostring* := <to-page-string-java-servlet> e2
        ; e_attrs1 := <to-html-attrs> [PropertyAssignment("class", "button") | passign*]
        ; e_actionid := <action-id-encoding> (<get-actionnumber> tc, args)
        ; if <fetch(TemplateCallPropertyAjax)> passign*
          then e_ajaxmodifier := java:expr |[ true ]|
          else e_ajaxmodifier := java:expr |[ false ]|
          end
       // ; bstm_validation* := <render-validation-exceptions>  
  
  action-id-encoding : 
//    (actionident,args) -> e|[ ident + "~actionident" + uniqueid + e ]|
    (actionident,args) -> <for-loop-counter-wrap-code-expr> e|[ "~actionident" + uniqueid /*+ e*/ ]|
    //with  e := <actual-action-args-to-state-enc> args   //no longer add action args, this caused issues with inline actions + template vars, the args are changed during data binding and the action isn't found anymore 

  id-encoding :
    inputident -> java:expr |[ ident+"~inputident"+uniqueid ]|
        
  handle-actions-to-java-servlet: 
    tc@TemplateCall("action",[e2,e1],passign*,TemplateBody([])) -> result
    with  actionident := <get-actionnumber> tc   
        ; x_action := <?ThisCall(name,args);!name> e1
        ; e* := <map(expression-to-java-servlet)> args
        ; e4 := <action-id-encoding> (actionident,args)
        ; bstm_passign* := <filter(handle-actions-to-java-servlet); concat> passign*
        ; if <fetch(TemplateCallPropertyAjax)> passign*
          then bstm* := bstm*|[ ThreadLocalPage.get().enableAjaxActionExecuted(); ]|
          else bstm* := []
          end
        ; result :=
            <wrap-action-render-java-code> 
            bstm*|[
              ident = e4;
              if(ThreadLocalPage.get().getParammap().get(ident)!=null)
              {
                bstm*
                x_action temp = (x_action)env.getAction("~x_action");
                temp.x_action(e*,ident);
              }
              bstm_passign*
            ]|    
    
  /*
   * this doesn't allow downloads, use special downloadLink (see types/file.str)
   */
  elem-to-java-servlet :
    tc@TemplateCall("actionLink",[e2,call@ThisCall(name, args)],passign*,TemplateBody([])) ->
    <wrap-action-render-java-code> 
    bstm* |[ 
      ident = e4;
      if(ThreadLocalPage.get().isServingAsAjaxResponse || ThreadLocalPage.get().isAjaxRuntimeRequest() || e_ajaxmodifier || ThreadLocalPage.get().getFormIdent().equals("")){
        ~*<render-error-messages-with-error-template(|java:expr|[ ident ]|,<ErrorTemplateAction>)> bstm*|[
          out.print("<a href=\"javascript:void(0)\" "+e_attrs1+" onclick='javascript:serverInvoke(\""+
            ThreadLocalServlet.getContextPath()+"/" + ThreadLocalPage.get().getActionTarget()+   //TODO getActionTarget is currently used, but the default redirect will not reproduce the page arguments in this case
           "\",\""+ ident+"\", ["+ThreadLocalPage.get().getHiddenPostParamsJson()+"],\""+ThreadLocalPage.get().getFormIdent()+"\", this, true)' >");
          bstm_exptostring*
          out.print("</a>");
        ]|
      }
      else{
        ~*<render-error-messages-with-error-template(|java:expr|[ ident ]|,<ErrorTemplateAction>)> bstm*|[
          /*if(ThreadLocalPage.get().getParammap().get(ident)!=null)
          {
            bstm_validation*
          }*/
          out.print("<a "+e_attrs1+" href=\"javascript:void(0)\" ");
          out.print("onclick='javascript:serverInvoke(\""+utils.HTMLFilter.filter(ThreadLocalPage.get().getPageUrlWithParams())+
            "?__action__link__=1\",\""+ident+"\", ["+ThreadLocalPage.get().getHiddenPostParamsJson()+"],\""+ThreadLocalPage.get().getFormIdent()+"\", this, true)'>"); 
          bstm_exptostring*
          out.print("</a>");
        ]|
      }
    ]|
    with  bstm_exptostring* := <to-page-string-java-servlet> e2
        ; e4 := <action-id-encoding> (<get-actionnumber> tc,args) 
        ; e_attrs1 := <to-html-attrs> passign*  
        ; if <fetch(TemplateCallPropertyAjax)> passign*
          then e_ajaxmodifier := java:expr |[ true ]|
          else e_ajaxmodifier := java:expr |[ false ]|
          end
        //; bstm_validation* := <render-validation-exceptions>     
        
  handle-actions-to-java-servlet: 
    tc@TemplateCall("actionLink",[e2,e1],passign*,TemplateBody([])) -> result
    with  actionident := <get-actionnumber> tc   
        ; x_action := <?ThisCall(name,args);!name> e1
        ; e* := <map(expression-to-java-servlet)> args
        ; e4 := <action-id-encoding> (actionident,args)        
        ; bstm_passign* := <filter(handle-actions-to-java-servlet); concat> passign*
        ; x_temp := <newname> "temp"
        ; if <fetch(TemplateCallPropertyAjax)> passign*
          then bstm* := bstm*|[ ThreadLocalPage.get().enableAjaxActionExecuted(); ]|
          else bstm* := []
          end
        ; result :=
            <wrap-action-render-java-code> 
            bstm*|[
              ident = e4;
              if(ThreadLocalPage.get().getParammap().get(ident)!=null )
              {
                bstm*
                x_action temp = (x_action)env.getAction("~x_action");
                temp.x_action(e*,ident);
              }
              bstm_passign*
            ]|
        