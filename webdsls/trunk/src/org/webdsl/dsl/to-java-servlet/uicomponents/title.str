module org/webdsl/dsl/to-java-servlet/uicomponents/title

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

rules // title

  element-to-java-servlet = ?TemplateCall("title",_,_,_); element-render(|<render-phase-title>)

  render-phase-title :
    TemplateCall("title", _,passign*, elems) ->
    bstm* |[ 
      java.io.StringWriter x_s = new java.io.StringWriter();
      out = new java.io.PrintWriter(x_s);
      ThreadLocalOut.push(out);
      bstm*
      x_s.flush();
      threadLocalPageCached.setPageTitle(x_s.toString());
      ThreadLocalOut.popChecked(out);
      out = ThreadLocalOut.peek();
    ]|
    with bstm* := <elements-to-java-servlet> elems
       ; x_s := <newname> "s"
       ; x_out := <newname> "out"

