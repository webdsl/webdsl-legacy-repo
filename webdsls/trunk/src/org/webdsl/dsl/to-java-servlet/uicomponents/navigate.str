module org/webdsl/dsl/to-java-servlet/uicomponents/navigate

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

rules // links

  // link to a url, maybe constant, or obtained from an entity field
        
  elem-to-java-servlet : //void navigate, useful for ajax
    TemplateCall("navigate", [],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<a class=\"navigate\" href=\"javascript:void(0)\" "+e_attrs1+">");
    bstm*
    out.print("</a>");
    ]|
    with  bstm* := <elems-to-java-servlet> elems
        ; e_attrs1 := <to-html-attrs> passign*
        
        
  elem-to-java-servlet :
    TemplateCall("navigate", [ThisCall("url",[e])],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<a class=\"navigate\" href=\"");
    bstm_exptostring*
    out.print("\" "+e_attrs1+">");
    bstm*
    out.print("</a>");
    ]|
    with  bstm_exptostring* := <to-page-string-java-servlet> e
        ; bstm* := <elems-to-java-servlet> elems
        ; e_attrs1 := <to-html-attrs> passign*

  elem-to-java-servlet :
    TemplateCall("navigate",[call@ThisCall(p,args)],passign*,elems) ->
    <wrap-common-context-code>
    bstm* |[ 
      out.print("<a href=\"");
      if(ThreadLocalEmailContext.inEmailContext()){
        out.print(e_resulturlemail);
      }
      else{
        out.print(e_resulturl);
      }
      out.print("\" "+e_attrs1+" class=\"navigate\">");
      bstm*
      out.print("</a>");
    ]|
    where SimpleSort("Page") := <get-type-anno> call
    with  e_url := <page-thiscall-to-url-string-java-servlet> call
        ; bstm* := <elems-to-java-servlet> elems
        ; e_attrs1 := <to-html-attrs> passign*
        ; e_resulturlemail := expr|[ utils.HTMLFilter.filter(ThreadLocalPage.get().getAbsoluteLocation()) + e_url ]|  
        ; e_resulturl := e_url

  elem-to-java-servlet :
    TemplateCall("navigatebutton",[call@ThisCall(p,args), e],passign*,TemplateBody([])) ->
    <wrap-common-context-code>
    bstm* |[
      out.print("<input type=\"button\" "+e_attrs1+" onclick=\"window.location = '"+e_url+"'\"");
      out.print("value=\"");
      bstm_exptostring*
      out.print("\"");
      out.print(" />");
    ]|
    where SimpleSort("Page") := <get-type-anno> call
    with  e_url := <page-thiscall-to-url-string-java-servlet> call
        ; bstm_exptostring* := <to-page-string-java-servlet> e
        ; e_attrs1 := <to-html-attrs> passign*
      
rules      
       
  page-thiscall-to-url-string-java-servlet :
    ThisCall(p,args) -> e |[ utils.HTMLFilter.filter(e_url) ]|
    with  def |[ define mod* x(farg*) req* { elem* } ]| := <TemplateSignature> (p, <map(get-type-anno);strip-annos> args)
    	; params := <zip(bind-param-java-servlet)> (farg*, args)
        ; e_url := <page-call-to-url-java-servlet> (p, params, farg*)
        
  bind-param-java-servlet :
    (Arg(x, srt), e) -> (x, e|[ utils.URLFilter.filter(e1.getNaturalId()) ]|)
    where <get-type-anno;has-entity-anno> e
        ; not(SimpleSort("Entity") := srt)
    with  SimpleSort(x_class) := srt
        ; e1 := <expression-to-java-servlet> e
 
  bind-param-java-servlet :
    (Arg(x, srt), e) -> (x, e|[ utils.URLFilter.filter(e1.get_WebDslEntityType() + ":" + e1.getId()) ]|)
    where SimpleSort("Entity") := srt
    with  e1 := <expression-to-java-servlet> e
    
  bind-param-java-servlet :
    (Arg(x, SimpleSort(<?"Date"<+?"DateTime"<+?"Time">)), e) -> (x, e|[ utils.URLFilter.filter(e1.getTime()) ]|)
    with  e1 := <expression-to-java-servlet> e
    
  bind-param-java-servlet :
    (Arg(x, srt), e) -> (x, e|[ utils.URLFilter.filter(e1) ]|)
    where <is-simple-sort> srt
    with  e1 := <expression-to-java-servlet> e

  page-call-to-url-java-servlet :
    (fn, [], []) -> java:expr |[ ThreadLocalServlet.getContextPath() + "~s" ]| //java:expr |[ e1 + e2 ]|
    where not("root" := fn)
    with  s := <concat-strings> ["/", fn]
        //; [e1,e2] := <add-success-messages-to-url> [e |[ "~s" ]| ]

  page-call-to-url-java-servlet :
    ("root", [], []) -> java:expr |[ ThreadLocalServlet.getContextPath() + "/" ]|

  page-call-to-url-java-servlet :
    (fn, params, farg*) -> e |[ ThreadLocalServlet.getContextPath() + "~s" + utils.URLFilter.removeTrailingDefaultValues(String.valueOf(e1),e*) ]| 
    where not([] := params)
    with  s  := <concat-strings> ["/", fn]
        ; e1 := <map(param-to-request-param-java-servlet);combine-params-to-exp> params // add-success-messages-to-url;
        ; e* := <map(param-tuple-type-java-string)> farg*
        
  param-tuple-type-java-string :
    Arg(x, srt) -> java:expr|[ "~<pp-webdsl-to-string> srt" ]|

  combine-params-to-exp :
    [e] -> e |[ e ]|
  
  combine-params-to-exp :
    [e|e*] -> e |[ e + "/" + e1 ]|
    with  e1 := <combine-params-to-exp> e*

  param-to-request-param-java-servlet :
    (param, value) -> value
    //    (param, value) -> <concat-strings> [param, "=", value]
