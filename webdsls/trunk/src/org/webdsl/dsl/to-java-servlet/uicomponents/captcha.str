module org/webdsl/dsl/to-java-servlet/uicomponents/captcha

imports
  libstrategolib

imports
  libwebdsl-front

rules // captcha should only be checked when a form is submitted that contained a captcha

  handle-actions-input-to-java-servlet :
    tc@elem|[ captcha()[passign*]{} ]| ->
    <for-loop-counter-wrap-code> 
    bstm*|[
           boolean isResponseCorrect = false;
           //need an id to validate
           //TODO: support multiple captchas on a page, use sessionid + templatecontext var
           String captchaId = request.getSession().getId();
           //retrieve the response
           String response = threadLocalPageCached.getParammap().get("j_captcha_response");
           if(response!=null){
             // Call the Service method
             try {
                  isResponseCorrect = utils.CaptchaServiceSingleton.getInstance().validateResponseForID(captchaId,
                          response);
             } catch (com.octo.captcha.service.CaptchaServiceException cse) {
                   //should not happen, may be thrown if the id is not valid
             }
             if(!isResponseCorrect){
               threadLocalPageCached.failedCaptchaResponses.add(response);
               threadLocalPageCached.setValidated(false);
             }
           }
    ]|
    
  elem-to-java-servlet : 
    tc@elem|[ captcha()[passign*]{} ]| ->
    bstm* |[ 
      out.print("<table><tr><td><img src=\""+threadLocalPageCached.getAbsoluteLocation()+"/jcaptcha\"></td></tr>");//TODO use styling for layout instead of tables
      out.print("<tr><td><input type='text' name='j_captcha_response' value=''");
      bstm_attr
      out.print("></td></tr>");
      if(!threadLocalPageCached.isValid() && threadLocalPageCached.failedCaptchaResponses.size() > 0 ) { //TODO: fix: message will be shown on all rerendered captchas
        out.print("<tr><td>text entered did not match text in image</td></tr>"); //TODO: use a more flexible message mechanism
      }
      out.print("</table>");
    ]|
    with  bstm_attr := <to-html-attrs-include-collection(|"captcha")> passign*
    
  dasmklfkmdslfkslfdsdf=id //avoid segmentation fault http://yellowgrass.org/issue/StrategoXT/802