module org/webdsl/dsl/to-java-servlet/uicomponents/image
imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front
    
rules // images

  elem-to-java-servlet :
    TemplateCall("image",[e],passign*,TemplateBody([])) -> 
    bstm* |[ 
      out.print("<img src=\"");
      bstm_exptostring*
      out.print("\" "+e_attrs1+" />"); 
    ]|
    where String(url) := e
        ; <is-substring(!"://")> url
    with  bstm_exptostring* := <to-page-string-java-servlet> e
        ; e_attrs1 := <to-html-attrs> passign*

  elem-to-java-servlet :
    TemplateCall("image",[e],passign*,TemplateBody([])) -> 
    bstm* |[ 
      out.print("<img src=\""+(e1.toString().startsWith("http://")?e1:threadLocalPageCached.getAbsoluteLocation()+"/"+e1));
      out.print("\" "+e_attrs1+" />"); 
    ]|
    where not(String(url) := e)
    with  e1 := <expression-to-java-servlet> e
        ; e_attrs1 := <to-html-attrs> passign*

  elem-to-java-servlet :
    TemplateCall("image",[e ],passign*,TemplateBody([])) -> 
    bstm* |[ 
      out.print("<img src=\""+threadLocalPageCached.getAbsoluteLocation());
      bstm_exptostring*
      out.print("\" "+e_attrs1+" />"); 
    ]|
    where String(url) := e
        ; not(<is-substring(!"://")> url)
    with  bstm_exptostring* := <to-page-string-java-servlet> e
        ; e_attrs1 := <to-html-attrs> passign*

