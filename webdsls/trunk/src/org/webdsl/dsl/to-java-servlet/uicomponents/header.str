module org/webdsl/dsl/to-java-servlet/uicomponents/header

imports
  libstrategolib

imports
  libwebdsl-front
  
rules 

  element-to-java-servlet = ?TemplateCall("header",[],_,_); element-render(|<render-phase-header>)

  render-phase-header :
    TemplateCall("header", [],passign*, elems) ->
    bstm* |[ 
      int x_temp = threadLocalPageCached.getSectionDepth();
      if(x_temp>0){
        if(x_temp > 6){
          x_temp = 6;
        }
      }
      else{
        x_temp = 1;
      }
      out.print("<h"+x_temp);
      bstm_attr
      out.print(">");
      bstm*
      out.print("</h"+x_temp+">");
    ]|
    with  bstm* := <elements-to-java-servlet> elems
        ; x_temp := <newname> "sec"
        ; bstm_attr := <to-html-attrs(|java:expr|[ "header section"+x_temp ]|)> passign*
