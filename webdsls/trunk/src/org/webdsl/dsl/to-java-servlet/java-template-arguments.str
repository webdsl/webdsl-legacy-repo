module org/webdsl/dsl/to-java-servlet/java-template-arguments

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front


rules // page arguments



  GenerateCodeJavaServlet = not(InAction) ; argument-to-java-servlet-template-property; fail

   argument-to-java-servlet-template-property :
    arg@farg|[ x : srt ]| ->
    <generate-java-servlet-template-partial> java|[

       private x_Class x;
       
       public void set#x(x_Class x) {
         this.x = x;
       }
       
       public x_Class get#x() {
         return x;
       }
    ]|
    where <defined-java-type> srt
        ; <?SimpleSort(x_Class)> srt
      

   argument-to-java-servlet-template-property :
    arg@farg|[ x : srt ]| ->
    <generate-java-servlet-template-partial> java|[

       private t_Class x;

       public void set#x(t_Class x) {
         this.x = x;
       }

       public t_Class get#x() {
         return x;
       }
    ]|
    where t_Class := <builtin-java-type> srt
   
rules

  argument-java-servlet-type:
    arg@farg|[ x : srt ]| -> x_Class
    where <defined-java-type> srt
        ; <?SimpleSort(x_Class)> srt

  argument-java-servlet-type:
    arg@farg|[ x : srt ]| -> x_Class
    where x_Class := <builtin-java-type> srt