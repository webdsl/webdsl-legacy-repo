module org/webdsl/dsl/to-java-servlet/java-template-arguments

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front


rules // page arguments



  GenerateCodeJavaServlet = /*?farg |[ x:srt ]|; debug
  ;debug(JavaThisPage <+ !"thispage not found")
  ;debug(ThisTemplateName <+ !"ThisTemplateName not found" )
  ;debug(InAction <+ !"InAction not found" )
  ;debug(InFunction <+ !"InFunction not found" )
  ;debug(JavaInEntity <+ !"InEntity not found" );
  //;debug(InGlobalVarDef <+ !"InGlobalVarDef not found" ) ;*/
   where(ThisTemplateName;not(InAction)); argument-to-java-servlet-template-property; fail

   argument-to-java-servlet-template-property :
    arg@farg|[ x : srt ]| ->
    <generate-java-servlet-template-partial> java|[

       private t_class x = null;
       
       @Partial private void cleanupTemplateArgAndVar(){ x = null; }
       
       public void set#x(t_class x) {
         this.x = x;
       }
       
       public t_class get#x() {
         return x;
       }
    ]|
    where <defined-java-type> srt
    with  t_class := <java-type> srt      

   argument-to-java-servlet-template-property :
    arg@farg|[ x : srt ]| ->
    <generate-java-servlet-template-partial> java|[

       private t_class x = e;
       
       @Partial private void cleanupTemplateArgAndVar(){ x = null; }

       public void set#x(t_class x) {
         this.x = x;
       }

       public t_class get#x() {
         return x;
       }
    ]|
    where t_class := <builtin-java-type> srt
    with  e := <java-type-default-value> srt
    
//store vars in scope
  GenerateCodeJavaServlet = where(ThisTemplateName;not(InAction)); store-argument-java-servlet; fail
 
  store-argument-java-servlet:
    arg@farg|[ x : srt ]| -> 
    <generate-java-servlet-template-partial> java|[
       @Partial 
       private void initializePassOn() { env.putVariable("~x",x); }
    ]|
    where rules (Variables :+= elem|[ var x : srt ]|)
        //; vars := [elem|[ var x : srt ]| | <Variables<+![]>]
        //; rules( Variables := vars  )
