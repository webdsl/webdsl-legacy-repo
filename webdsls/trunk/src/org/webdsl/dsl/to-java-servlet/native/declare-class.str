module org/webdsl/dsl/to-java-servlet/native/declare-class

rules

  native-class-java-type = NativeClassSortName; pp-webdsl-to-string; ?x; !type|[ x ]|
 
  call-to-java-servlet :
    Call(e,x,args) ->   java |[ e1.x(e*) ]|
    where <type-of; IsNativeClass> e
    with  e1 := <expression-to-java-servlet> e 
        ; e* := <map(expression-to-java-servlet)> args
        
  //static function
  call-to-java-servlet :
    Call(e,x_funcname,args) -> java |[ x_classname.x_funcname(e*) ]|
    where <type-of; ?ClassSort(name)> e
    with  x_classname := <NativeClassName; pp-webdsl-to-string> name
        ; e* := <map(expression-to-java-servlet)> args
        
  call-to-java-servlet :
    ThisCall(x,args) ->   java |[ new x_cons(e*) ]|
    where qualifiedname := <NativeClassConstructorQualifiedName> x
    with  x_cons := <pp-webdsl-to-string> qualifiedname
        ; e* := <map(expression-to-java-servlet)> args
        
  assignment-to-java-servlet-specific :
    Stat(Assignment(q@FieldAccess(qid, x), e1)) -> bstm |[ e2.x = e3; ]|
    where <type-of; IsNativeClass> qid
    with  e2 := <expression-to-java-servlet> qid
        ; e3 := <expression-to-java-servlet> e1
        
  field-access-to-java-servlet :
    FieldAccess(e1, x) -> expr|[ e2.x ]|
    where <type-of; IsNativeClass> e1
    with  e2 := <expression-to-java-servlet> e1
    
    
    
    