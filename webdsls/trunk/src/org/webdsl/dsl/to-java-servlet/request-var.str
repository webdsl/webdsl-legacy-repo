module org/webdsl/dsl/to-java-servlet/request-var

rules

  generate-code-java-servlet-once =
    <emit-java-code; fail> compilation-unit|[
      package utils;
      @Partial
      public abstract class PageServlet {
                
        @Partial
        public void initRequestVars(PrintWriter out){
        }
        
      }
    ]|

  GenerateCodeJavaServlet = generate-java-servlet-request-vars; fail

  generate-java-servlet-request-vars :
    RequestScopeVarDeclInit(x, srt, e) ->
    <emit-java-code> compilation-unit|[
      package utils;
      @Partial
      public abstract class PageServlet {
        @Partial
        public void initRequestVars(PrintWriter out){
          t x = e1;
          addRequestScopedVar("~x",x);
        }
      }
    ]|
    with  e1 := <expression-to-java-servlet> e
        ; t := <java-type> srt
        
  generate-java-servlet-request-vars :
    RequestScopeVarDecl(x, srt) ->
    <emit-java-code> compilation-unit|[
      package utils;
      @Partial
      public abstract class PageServlet {
        @Partial
        public void initRequestVars(PrintWriter out){
          t x = null;
          addRequestScopedVar("~x",x);
        }
      }
    ]|
    with  t := <java-type> srt
        
  expression-to-java-servlet :
    rv@RequestVar(x) -> java|[ (x_class) ThreadLocalPage.get().requestScopedVariables.get("~x") ]|
    with  x_class := <get-type-anno;java-type-name> rv
    
  /**
   *  Need to overwrite direct assignment to request var, since this would otherwise only overwrite a local reference and not the actual request var
   */
  assignment-to-java-servlet-specific:
    Stat(Assignment(RequestVar(x), e1)) -> java|[ ThreadLocalPage.get().addRequestScopedVar("~x",e2); ]|
    with  e2 := <expression-to-java-servlet> e1