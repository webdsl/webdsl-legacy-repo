module org/webdsl/dsl/to-java-servlet/elements-to-java-servlet

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front
    
rules // block

  // @obsolete; should have been removed by sugar, however seems to have to do something with menu
  elem-to-java-servlet :
    TemplateCall("dummy", _,passign*, elems) -> <elems-to-java-servlet> elems

  // @obsolete; use block{ ... } instead of div{ ... }
  elem-to-java-servlet :
    TemplateCall("div", [String(x)],passign* , elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<div "+e_attrs1 + ">");
    bstm*
    out.print("</div>");
    ]|
    with  bstm* := <elems-to-java-servlet> elems 
        ; e_attrs1 := <to-html-attrs> passign* 

         
  elem-to-java-servlet :
    TemplateCall("block", [],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<div "+e_attrs1+" class=\"block\" >");
    bstm*
    out.print("</div>");
    ]|
    with bstm* := <elems-to-java-servlet> elems 
       ; e_attrs1 := <to-html-attrs> passign*

  elem-to-java-servlet :
    TemplateCall("block", [String(x)],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<div class=\"block "+"~x"+"\" "+e_attrs1+" >");
    bstm*
    out.print("</div>");
    ]|
    with bstm* := <elems-to-java-servlet> elems 
        ;e_attrs1 := <to-html-attrs> passign*
    
  elem-to-java-servlet :
    TemplateCall("block", [String(x), String("")],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<div class=\"block "+"~x"+"\" "+e_attrs1+">");
    bstm*
    out.print("</div>");
    ]|
    with bstm* := <elems-to-java-servlet> elems 
        ;e_attrs1 :=<to-html-attrs> passign*
    
  elem-to-java-servlet :
    TemplateCall("block", [String(x), String(y)],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<div class=\""+"~y"+" block "+"~x"+"\"  "+e_attrs1+">");
    bstm*
    out.print("</div>");
    ]|  
     where not( <?""> x ) ; not( <?""> y )
     with  bstm* := <elems-to-java-servlet> elems 
          ;e_attrs1 := <to-html-attrs> passign*
    
  elem-to-java-servlet :
    TemplateCall("block", [String(""), String(y)],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<div class=\""+"~y"+" block\" "+e_attrs1+">"); 
    bstm*
    out.print("</div>");
    ]|
    with bstm* := <elems-to-java-servlet> elems 
        ;e_attrs1 := <to-html-attrs> passign*

strategies // title

  elem-to-java-servlet :
    TemplateCall("title", _,passign*, elems) ->
    bstm |[ 
    {
      java.io.StringWriter s = new java.io.StringWriter();
      java.io.PrintWriter out = new java.io.PrintWriter(s);
      bstm*
      s.flush();
      ps.setPageTitle(s.toString());
    } 
    ]|
    with bstm* := <elems-to-java-servlet> elems

rules // text

  elem-to-java-servlet :
    Text(val) -> 
    <wrap-common-context-code>
    bstm* |[ out.print(utils.HTMLFilter.filter("~val")); ]|

rules // text

  // convert an expression to text (string value of the expression)

  elem-to-java-servlet :
    TemplateCall("text", [arg],passign*, []) -> 
    <wrap-common-context-code>
    bstm_exptostring*
    with  bstm_exptostring* := <to-page-string-java-servlet(|"utils.HTMLFilter.filter")> arg

rules // spacers

  elem-to-java-servlet :
    TemplateCall("spacer", _,passign*, []) -> 
    <wrap-common-context-code>
    bstm* |[ out.print("<hr "+e_attrs1+"/>"); ]|
    with e_attrs1 := <to-html-attrs> passign*
    /*
    %> <rich:spacer id="<%= spacer-id %>" /> <%
    where spacer-id := <newname> "spacer"
    */
  elem-to-java-servlet :
    TemplateCall("horizontalspacer", _,passign*, []) -> 
    <wrap-common-context-code>
    bstm* |[ out.print("<hr "+e_attrs1+" />"); ]|
    with e_attrs1 := <to-html-attrs> passign*
rules // images

  elem-to-java-servlet :
    TemplateCall("image",[e],passign*,[]) -> 
    <wrap-common-context-code>
    bstm* |[ 
      out.print("<img src=\"");
      bstm_exptostring*
      out.print("\" "+e_attrs1+" />"); 
    ]|
    where String(url) := e
        ; <is-substring(!"://")> url
    with  bstm_exptostring* := <to-page-string-java-servlet> e
        ; e_attrs1 := <to-html-attrs> passign*

  elem-to-java-servlet :
    TemplateCall("image",[e ],passign*,[]) -> 
    <wrap-common-context-code>
    bstm* |[ 
      out.print("<img src=\""+"~<AppRoot>");
      bstm_exptostring*
      out.print("\" "+e_attrs1+" />"); 
    ]|
    where String(url) := e
        ; not(<is-substring(!"://")> url)
    with  bstm_exptostring* := <to-page-string-java-servlet> e
        ; e_attrs1 := <to-html-attrs> passign*

rules // lists

  elem-to-java-servlet :
    TemplateCall("list", [],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
      out.print("<ul "+e_attrs1+">");
      bstm*
      out.print("</ul>");
    ]|
    with  bstm* := <elems-to-java-servlet> elems
        ; e_attrs1 := <to-html-attrs> passign*

  elem-to-java-servlet :
    TemplateCall("listitem", [],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
      out.print("<li "+e_attrs1+">");
      bstm*
      out.print("</li>");
    ]|
    with  bstm* := <elems-to-java-servlet> elems
        ; e_attrs1 := <to-html-attrs> passign*

rules
 
  elem-to-java-servlet :
    TemplateCall("par", [],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<p "+e_attrs1+">");
    bstm*
    out.print("</p>");
    ]|
    with  bstm* := <elems-to-java-servlet> elems
        ; e_attrs1 := <to-html-attrs> passign*    
    
  elem-to-java-servlet :
    TemplateCall("container", [],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<span "+e_attrs1+">");
    bstm*
    out.print("</span>");
    ]|
    with  bstm* := <elems-to-java-servlet> elems
        ; e_attrs1 := <to-html-attrs> passign*
        
rules // pre-formatted text

  elem-to-java-servlet :
    TemplateCall("pre", [],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<pre "+e_attrs1+">");
    bstm*
    out.print("</pre>");
    ]|
    with  bstm* := <elems-to-java-servlet> elems
        ; e_attrs1 := <to-html-attrs> passign*

//table moved to uicomponents/table

//for moved to control-flow-to-java-servlet

//select moved to types/collection

//if moved to control-flow-to-java-servlet


//templatecalls moved to control-flow-to-java-servlet

rules // links

  // link to a url, maybe constant, or obtained from an entity field
        
  elem-to-java-servlet : //void navigate, useful for ajax
    TemplateCall("navigate", [],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<a class=\"navigate\" href=\"javascript:void()\" "+e_attrs1+">");
    bstm*
    out.print("</a>");
    ]|
    with  bstm* := <elems-to-java-servlet> elems
        ; e_attrs1 := <to-html-attrs> passign*
        
        
  elem-to-java-servlet :
    TemplateCall("navigate", [ThisCall("url",[e])],passign*, elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<a class=\"navigate\" href=\"");
    bstm_exptostring*
    out.print("\" "+e_attrs1+">");
    bstm*
    out.print("</a>");
    ]|
    with  bstm_exptostring* := <to-page-string-java-servlet> e
        ; bstm* := <elems-to-java-servlet> elems
        ; e_attrs1 := <to-html-attrs> passign*

  elem-to-java-servlet :
    TemplateCall("navigate",[call@ThisCall(p,args)],passign*,elems) ->
    <wrap-common-context-code>
    bstm* |[ 
    out.print("<a class=\"navigate\" href=\""+e_resulturl+"\" "+e_attrs1+">");
    bstm*
    out.print("</a>");
    ]|
    where <IsPage> p
    with  e_url := <page-thiscall-to-url-string-java-servlet> call
        ; bstm* := <elems-to-java-servlet> elems
        ; e_attrs1 := <to-html-attrs> passign*
        ; if ThisEmail 
          then e_resulturl := expr|[ ps.getAbsoluteLocation() + e_url ]|  
          else e_resulturl := e_url end

  elem-to-java-servlet :
    TemplateCall("navigatebutton",[call@ThisCall(p,args), e],passign*,[]) ->
    <wrap-common-context-code>
    bstm* |[
      out.print("<input type=\"button\" "+e_attrs1+" onclick=\"window.location = '"+e_url+"'\"");
      out.print("value=\"");
      bstm_exptostring*
      out.print("\"");
      out.print(" />");
    ]|
    where <IsPage> p
    with  e_url := <page-thiscall-to-url-string-java-servlet> call
        ; bstm_exptostring* := <to-page-string-java-servlet> e
        ; e_attrs1 := <to-html-attrs> passign*
        
  page-thiscall-to-url-string-java-servlet :
    ThisCall(p,args) -> e |[ e_url ]|
    with  def |[ define mod* x(farg*) { elem* } ]| := <TemplateSignature> (p, <map(type-of)> args)
        ; params := <zip(bind-param-java-servlet)> (farg*, args)
        ; e_url := <page-call-to-url-java-servlet> (p, params)
        
  bind-param-java-servlet :
    (Arg(x, srt), e) -> (x, e|[ e1.getNaturalId()]|)
    where <is-entity-type> srt
    with  SimpleSort(x_class) := srt
        ; e1 := <expression-to-java-servlet> e
 
  bind-param-java-servlet :
    (Arg(x, srt), e) -> (x, el)
    where <is-simple-sort> srt
    with  el := <expression-to-java-servlet> e

  page-call-to-url-java-servlet :
    (fn, []) -> java:expr |[ e1 + e2 ]|
    with  s := <concat-strings> [<AppRoot>, "/", fn]
        ; [e1,e2] := <add-success-messages-to-url> [e |[ "~s" ]| ]

  page-call-to-url-java-servlet :
    (fn, params) -> e |[ "~s" + e1 ]| 
    where not([] := params)
    with  s  := <concat-strings> [<AppRoot>, "/", fn, "/"]
        ; e1 := <map(param-to-request-param-java-servlet); add-success-messages-to-url; combine-params-to-exp> params

  combine-params-to-exp :
    [e] -> e |[ e ]|
  
  combine-params-to-exp :
    [e|e*] -> e |[ e + "/" + e1 ]|
    with  e1 := <combine-params-to-exp> e*

  param-to-request-param-java-servlet :
    (param, value) -> value
    //    (param, value) -> <concat-strings> [param, "=", value]

rules // forms

  elem-to-java-servlet : 
    tc@TemplateCall("form", [],passign*, elems) ->
    <wrap-input-render-java-code> 
    bstm* |[
      {
        ident += "~formident"+uniqueid;
        form_ident = ident; //just for actionLink javascript code    
        if(ps.getParammap().get(ident)!=null)
        {
          inSubmittedForm = true; // ok, because no nested forms, otherwise need to count just like inForLoop variable
        }
        
        out.print("<form "+e_enc+" name=\""+ident+"\" id=\""+ident+"\" action=\""+ps.getPageUrlWithParams()+"\" method=\"POST\" "+e_attrs1+">");
        out.print("<input type=\"hidden\" name=\""+ident+"\" value=\"1\" />");
        out.print(ps.getHiddenParams()); // page arguments

        bstm*

        out.print("</form>");

        inSubmittedForm = false;
        form_ident = "";
      }
    ]|
    with  bstm* := <elems-to-java-servlet> elems
        ; formident := <get-formnumber> tc
        ; e_attrs1 := <to-html-attrs> passign*
        ; if <oncetd(requires-multipart-encoded-form)> elems
          then e_enc := java |[ "enctype=\"multipart/form-data\"" ]|
          else e_enc := java |[ "" ]| end
/*        ; if (IsAjaxApp) //TODO: enter afvangen
          then e1 := "onsubmit
          else
          end
*/   
  handle-actions-to-java-servlet: 
    tc@TemplateCall("form", [],passign*, elems) ->
    <wrap-input-render-java-code> 
    bstm*|[
        ident += "~formident"+uniqueid;
        //if(ps.getParammap().get(ident)!=null)
        //{
        bstm1*
        //}
    ]|
    with // bstm* := <collect-om(handle-actions-input-to-java-servlet)> elems
         bstm1* := <collect-om(handle-actions-to-java-servlet); concat-with-sublists> elems
        ; formident := <get-formnumber> tc   
   
  elem-to-java-servlet :
    tc@TemplateCall("action",[e2],passign*,[]) ->
    <wrap-input-render-java-code> 
    bstm* |[ 
      out.print("<input type=\"button\" value=\"");
      bstm_exptostring*
      out.print("\" "+e_attrs1+" />");
    ]|
    where IsAjaxApp
    with  bstm_exptostring* := <to-page-string-java-servlet>  e2
        ; e_attrs1 := <to-html-attrs> passign*    

  elem-to-java-servlet :
    tc@TemplateCall("action",[e2,call@ThisCall(name, args)],passign*,[]) ->
    <wrap-input-render-java-code> 
    bstm* |[ 
      out.print("<input type=\"button\" value=\"");
      bstm_exptostring*
      out.print("\" "+e_attrs1+" ");
      out.print("onclick='javascript:serverInvoke(\""+"~app"+"/" + ps.getPageName()  +"\",\""+ "~actionident"+uniqueid+e3 +"\", \""+ps.getHiddenPostParams()+"\",\""+form_ident+"\", this)'/>"); 
    ]|
    where IsAjaxApp
    with  bstm_exptostring* := <to-page-string-java-servlet> e2
        ; actionident := <get-actionnumber> tc   
        ; app := <AppRoot>
        ; e3 := <actual-action-args-to-state-enc> args
        ; e_attrs1 := <to-html-attrs> passign*
  
  action-id-encoding : //TODO make all id encodings use utility functions so the strategy can be easily modified
    (actionident,args) -> e|[ ident+"~actionident"+uniqueid+e ]|
    with  e := <actual-action-args-to-state-enc> args     
  
  id-encoding :
    inputident -> java:expr |[ ident+"~inputident"+uniqueid ]|
  
  elem-to-java-servlet :
    tc@TemplateCall("action",[e2,e1],passign*,[]) ->
    <wrap-input-render-java-code> 
    bstm* |[ 
      ident = e4;
      if(ps.getParammap().get(ident)!=null)
      {
        bstm_validation*
      }
      out.print("<input type=\"submit\" name=\""+ident+"\" value=\"");
      bstm_exptostring*
      out.print("\" "+e_attrs1+" />"); 
    ]|
    where not(IsAjaxApp)
    with  e3 := <?ThisCall(name,args);!name> e1
        ; bstm_exptostring* := <to-page-string-java-servlet>  e2
        ; actionident := <get-actionnumber> tc   
        ; e_attrs1 := <to-html-attrs> passign*        
        ; e4 := <action-id-encoding> (actionident,args)
        ; bstm_validation* := <render-validation-exceptions>
        
  handle-actions-to-java-servlet: 
    tc@TemplateCall("action",[e2,e1],passign*,[]) ->
    <wrap-input-render-java-code> 
    bstm*|[
      ident = e4;
      if(ps.getParammap().get(ident)!=null)
      {
        x_action temp = (x_action)actionclasses.get("~x_action");
        temp.x_action(e*,ident);
      }
    ]|
    with  actionident := <get-actionnumber> tc   
        ; x_action := <?ThisCall(name,args);!name> e1
        ; e* := <map(expression-to-java-servlet)> args
        ; e4 := <action-id-encoding> (actionident,args)

  elem-to-java-servlet : //actionLink without call only valid for ajax apps
    tc@TemplateCall("actionLink",[e2],passign*,[]) ->
    <wrap-input-render-java-code> 
    bstm* |[ 
      out.print("<a href=\"javascript:void()\" "+e_attrs1+">");
      bstm_exptostring*
      out.print("</a>");
    ]|
    where IsAjaxApp
    with  bstm_exptostring* := <to-page-string-java-servlet>  e2
        ; e_attrs1 := <to-html-attrs> passign*        
    
  elem-to-java-servlet :
    tc@TemplateCall("actionLink",[e2,call@ThisCall(name, args)],passign*,[]) ->
    <wrap-input-render-java-code> 
    bstm* |[ 
      ident += "~actionident"+uniqueid+e3 ;
      out.print("<a href=\"javascript:void()\" "+e_attrs1+" onclick='javascript:serverInvoke(\""+"~app"+"/" + ps.getPageName()  +"\",\""+ ident+"\", \""+ps.getHiddenPostParams()+"\",\""+form_ident+"\", this)' >");
      bstm_exptostring*
      out.print("</a>");
    ]|
    where IsAjaxApp
    with  bstm_exptostring* := <to-page-string-java-servlet> e2
        ; actionident := <get-actionnumber> tc   
        ; app := <AppRoot>
        ; e3 := <actual-action-args-to-state-enc> args
        ; e_attrs1 := <to-html-attrs> passign*        
        
   elem-to-java-servlet :
    tc@TemplateCall("actionLink",[e2,e3@ThisCall(name, args)],passign*,[]) ->
    <wrap-input-render-java-code> 
    bstm* |[ //this doesn't allow downloads, use special downloadLink (see types/file.str)
      ident += "~actionident"+uniqueid+e5;
      if(ps.getParammap().get(ident)!=null)
      {
        bstm_validation*
      }
      out.print("<input type=\"hidden\" name=\""+ident+"\" "+e_attrs1+"/>");
      out.print("<a href=\"#\"");
      out.print("onClick=\"document.getElementsByName('"+ident+"')[0].value='1';"+
                          "document."+form_ident+".submit();"+
                          "document.getElementsByName('"+ident+"')[0].value='0';"+
                          "\">");
      bstm_exptostring*
      out.print("</a>");
    ]|
    where not(IsAjaxApp)
    with  bstm_exptostring* := <to-page-string-java-servlet>  e2
        ; actionident := <get-actionnumber> tc   
        ; e_attrs1 := <to-html-attrs> passign*        
        ; e5 := <actual-action-args-to-state-enc> args
        ; app := <AppRoot>
        ; bstm_validation* := <render-validation-exceptions>
        
  handle-actions-to-java-servlet: 
    tc@TemplateCall("actionLink",[e2,e1],passign*,[]) ->
    <wrap-input-render-java-code> 
    bstm*|[
        ident += "~actionident"+uniqueid+e5;
        if(ps.getParammap().get(ident)!=null ) // not necessary: && ps.getParammap().get(ident).equals("1") )
        {
            x_action temp = (x_action)actionclasses.get("~x_action");
            temp.x_action(e*,ident);
        }
    ]|
    with  actionident := <get-actionnumber> tc   
        ; x_action := <?ThisCall(name,args);!name> e1
        ; e* := <map(expression-to-java-servlet)> args
        ; e5 := <actual-action-args-to-state-enc> args        
