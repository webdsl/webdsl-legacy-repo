module org/webdsl/dsl/to-java-servlet/test/test-all

imports
  libstrategolib
  libjava-front

imports
  libwebdsl-front

strategies

  GenerateCodeJavaServlet = generate-test-all; fail

  generate-test-all :
     Application(_,_) ->
     <emit-java-code> compilation-unit|[
       //package pkgname;
       package utils;
       import pkgname.*;
       import pkgname2.*;
       import pkgname3.*;
       import utils.*;
       import org.webdsl.lang.*;
       import org.webdsl.tools.*;
       import org.hibernate.*;
       
       public class TestAll
       {
          public TestAll () {

          }
          
          public static void clearDB(){
            org.hibernate.Session hibSession = null;
            try{
              hibSession = HibernateUtilConfigured.getSessionFactory().getCurrentSession();
              hibSession.beginTransaction();
              hibSession.setFlushMode(org.hibernate.FlushMode.MANUAL);
           
              bstm1*

              hibSession.flush();
              hibSession.getTransaction().commit();
            }	
            catch(Exception ex)
            { 
              System.out.println("exception occured while clearing database: " + ex.getMessage());
              ex.printStackTrace();
              hibSession.getTransaction().rollback();
            }
          }
          
          public static void reloadGlobalVarsInit(Session hibSession){
            GlobalVariables.load();
            GlobalInit.load();
          }

          public static void main(String[] args) {
            boolean exitWithError = false;
            
            bstm*
            
            if(exitWithError){
              System.exit(1);
            }
          }
       }
     ]|
     with pkgname := <TestPackage>
        ; pkgname2 := <BeanPackage>
        ; pkgname3 := <DomainPackage>
        ; names := <AllTestNames <+ ![]>
        ; bstm* := <map(gen-call-test-class)> names  
        ; bstm1* := <map({\ 
                           x -> bstm|[hibSession.createQuery("delete from "+"~x").executeUpdate(); ]|
                       \})> 
                       ["ApplicationContextProperty"|["File"|<AllEntityNames>]]
       
  gen-call-test-class :
    name -> bstm|[ exitWithError = !(new x_name()).run() || exitWithError; ]|
    with x_name := <concat-strings> ["Test", name]
    