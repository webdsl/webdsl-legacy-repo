module org/webdsl/dsl/to-java-servlet/ajax/desugar
   
imports
  libstrategolib  
  libwebdsl-front

rules


  //desugar InlineAjaxAction
 desugar : 
    Define(mods, name, args, elems) -> <rename-all> Define(mods, name, args, elems2)
    where not(<RemovedInlineAjaxActions> name)
    with rules( RemovedInlineAjaxActions :+= name )
       ; {| InsertedActions  
          : elems1 := <alltd(desugar-inline-action-call)> elems
          ; elems2 := <concat> [elems1, <bagof-InsertedActions>]
         |}

  desugar-inline-action-call :
    InlineAjaxAction(action, div, e1) -> ThisCall(fun, params)
    with  fun := <newname> "inline_action" 
        ; {| Param, IgnoreVars
           : <alltd(find-params)> e1
           ; params := <bagof-Param>
          |}
        ; farg* := <map(forexp-function-param-to-farg)> params
        ; rules( InsertedActions :+= Action(fun, farg*, Block([AjaxAction(action, div, e1)]))    ) 
