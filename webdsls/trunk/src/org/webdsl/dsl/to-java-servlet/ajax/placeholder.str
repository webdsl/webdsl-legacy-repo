module org/webdsl/dsl/to-java-servlet/ajax/placeholder

imports
  libstrategolib
  libwebdsl-front

rules

  element-to-java-servlet :
    Placeholder(name, elems) ->
    bstm* |[
      java.io.StringWriter x_sw = null;
      if(RENDER_PHASE == phase){
        x_sw = new java.io.StringWriter();
        out = new java.io.PrintWriter(x_sw);
        ThreadLocalOut.push(out);
        out.print("<div id=\""+e_name+"\" class=\"webdsl-placeholder\">");
      }
      bstm*
      if(RENDER_PHASE == phase){
        out.print("</div>");
        ThreadLocalOut.popChecked(out);
        out = ThreadLocalOut.peek();
        String x_string = x_sw.toString();
        out.write(x_string); // write placeholder to normal out
        threadLocalPageCached.addReRenderPlaceholdersContent(e_name,x_string); // write placeholder to rerender 'buffer', will only add if this placeholder was marked for refreshing
      }
    ]|
    with bstm* := <elements-to-java-servlet> elems
       ; e_name := <placeholder-id-to-java-exp> name
       ; x_sw := <newname> "tmpstringwriter"
       ; x_string := <newname> "tmpstring"
         
  element-to-java-servlet :
    PlaceholderWithAjaxCall(name, tc) ->
    bstm* |[
      if(RENDER_PHASE == phase){
        out.print("<div id=\""+e_name+"\" class=\"webdsl-placeholder\">");
        bstm_thecall*
        out.print("</div>");
      }
    ]|
    with bstm_thecall* := <call-template-as-ajax-template-helper> tc
       ; e_name := <placeholder-id-to-java-exp> name
