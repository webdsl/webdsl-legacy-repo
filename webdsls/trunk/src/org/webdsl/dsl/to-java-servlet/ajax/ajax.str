module org/webdsl/dsl/to-java-servlet/ajax/ajax
   
imports
  libstrategolib  

imports 
  libwebdsl-front

rules

 
  //check if there is any need for ajax in this application
  check-for-ajax = 
    alltd(item-requires-ajax)
    
  item-requires-ajax =
    ? AjaxAction(_,_,_)
    ; rules(IsAjaxApp := True())

  //event call to java
  assignment-to-html-attr :
    tc@PropertyAssignment(x, call@ThisCall(_, args)) -> (x, e |[ "javascript:serverInvoke(\""+"~app"+"/" + ps.getPageName() +"\",\""+ "~actionident"+uniqueid+e2 +"\", \""+ps.getHiddenPostParams()+"\",\""+form_ident+"\", this)" ]| )
    //TODO: where x <+ "onclick"...
    with app := <AppRoot>
        ;actionident := <get-actionnumber> tc   
        ;e2 := <actual-action-args-to-state-enc> args
        
  //handle actions
  handle-actions-to-java-servlet: 
    tc@PropertyAssignment(x, ThisCall(x_action, args)) ->
    bstm*|[
        ident = "~actionident"+uniqueid+e2;
        if(ps.getParammap().get(ident)!=null)
        {
            x_class temp = (x_class)actionclasses.get("~x_class");
            temp.x_action(e*);
        }
    ]|
    with  actionident := <get-actionnumber> tc   
        ; e* := <map(expression-to-java-servlet)> args
        ; e2 := <actual-action-args-to-state-enc> args
        ; _{x_class} := x_action
        

 //action java implementation
  ajax-to-java-servlet :
    AjaxAction(theaction, thediv, ThisCall(template, args)) -> bstm*|[ 
        Object[] x_args = { e* };
  
        java.io.StringWriter x_s = new java.io.StringWriter();
        java.io.PrintWriter x_out = new java.io.PrintWriter(x_s); 
        
        //check if this action has been executed
        ps.clearHibernateCache(variables);
        try {
          ((PageServlet)DispatchServlet.pages.get("~template").newInstance()).serveAsAjaxResponse(request, x_out, x_args, ps.getParammap(), ps.getParammapvalues(), ps.getFileUploads());
        }
        catch(IllegalAccessException iae)
        {
          System.out.println("Problem in template servlet template lookup: "+iae.getMessage());
        }
        catch(InstantiationException ie)
        {
          System.out.println("Problem in template servlet template lookup: "+ie.getMessage());
        }  
          
        out.print("{ action: \""+"~theaction"+"\", id: \""+"~thediv"+"\", value : \""+org.apache.commons.lang.StringEscapeUtils.escapeJavaScript(x_s.toString())+"\"},"); 
     ]|
    where (theaction := "replace" <+ theaction := "append") 
    with x_args := <newname> "args"
       ; x_out := <newname> "out"
       ; x_s   := <newname> "s"
       ; e* := <map({\e -> <expression-to-java-servlet> e\})> args
    
 ajax-to-java-servlet :
    AjaxAction("visibility", thediv, String(visibility)) -> bstm*|[ 
       out.print("{ action: \"visibility\", id: \""+"~thediv"+"\", value: \""+"~visibility"+"\"},");
     ]|
    with (visibility := "show") <+ (visibility := "hide")  <+ (visibility := "toggle")

 ajax-to-java-servlet :
    AjaxAction("relocate", "this", call@ThisCall(_,_)) -> bstm*|[ 
      ps.clearHibernateCache(variables);
      out.print("{ action: \"relocate\", value: \""+e_url+"\" },\n");
     ]|
    with  e_url := <page-thiscall-to-url-string-java-servlet> call

 ajax-to-java-servlet :
    AjaxAction("restyle", thediv, String(newstyle)) -> bstm*|[ 
       out.print("{ action: \"restyle\", id: \""+"~thediv"+"\", value: \""+"~newstyle"+"\"},");
     ]|
    
ajax-to-java-servlet :
    AjaxAction("clear", thediv, String("")) -> bstm*|[ 
       out.print("{ action: \"clear\", id: \""+"~thediv"+"\"},");
     ]|
    