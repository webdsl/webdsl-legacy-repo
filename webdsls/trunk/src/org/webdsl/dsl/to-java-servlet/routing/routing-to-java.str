module org/webdsl/dsl/to-java-servlet/routing/routing-to-java

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front
  org/webdsl/dsl/to-java-servlet/to-java-servlet
  org/webdsl/dsl/to-java-servlet/core-to-java-servlet

rules

  generate-code-java-servlet: 
  	Routing([RoutingReceive(RoutingArgs([Arg(x_argname,_)]), Block(b1)), 
  	         RoutingConstruct(RoutingArgs([Arg(x_constructarg0,_), Arg(x_constructarg1,_), Arg(x_constructarg2,_)]), Block(b2))]) ->
    <with(emit-java-code); fail> compilation-unit|[
      package utils;

      public class Routing{
         static{
		   org.webdsl.logging.Logger.info("routing customization used: yes");
	     }
         public static java.util.List<String> processRequest(java.util.List<String> x_argname){
           bstm1*
         }
         public static java.util.List<String> processNavigate(String x_constructarg0, String x_constructarg1, java.util.List<String> x_constructarg2){
           bstm2*
         }
       }
    ]|
    with  pkgname    := <TemplatePackage>
        ; pkgtname2   := <DomainPackage>
        ; ambname2 := pkgtname2
        ; {| InFunction:
            rules( InFunction := <id> )
          ; bstm1* := <statements-to-java-servlet> b1
          ; bstm2* := <statements-to-java-servlet> b2
          |}