module aterm-interface

imports
  libstrategolib
  libwebdsl-front

strategies

  builtin-java-type :
    SimpleSort("ATerm") -> type |[ org.spoofax.interpreter.terms.IStrategoTerm ]|

strategies

  call-to-java-servlet :
    exp  |[ e.subterms() ]| ->
    java |[ Arrays.asList(e2.getAllSubterms()) ]|
    where SimpleSort("ATerm") := <type-of> e
    with e2 := <expression-to-java-servlet> e
    
  call-to-java-servlet  :
    exp  |[ e.constructor() ]| ->
    java |[ e2 instanceof org.spoofax.interpreter.terms.IStrategoAppl
              ? ((org.spoofax.interpreter.terms.IStrategoAppl) e2).getConstructor().toString() 
              : "" ]|
    where SimpleSort("ATerm") := <type-of> e
    with e2 := <expression-to-java-servlet> e 

  call-to-java-servlet  :
    exp  |[ e.get(e_index) ]| ->
    java |[ e2.getSubterm(e_index2) ]|
    where SimpleSort("ATerm") := <type-of> e
    with e2       := <expression-to-java-servlet> e
       ; e_index2 := <expression-to-java-servlet> e_index

  call-to-java-servlet :
    exp  |[ e.length() ]| ->
    java |[ e2.getSubtermCount() ]|
    where SimpleSort("ATerm") := <type-of> e
    with e2 := <expression-to-java-servlet> e

  call-to-java-servlet :
    exp  |[ e.toString() ]| ->
    java |[ e2.toString() ]|
    where SimpleSort("ATerm") := <type-of> e
    with e2 := <expression-to-java-servlet> e

  call-to-java-servlet :
    exp  |[ e.toInt() ]| ->
    java |[ Integer.parseInt(e2.toString()) ]|
    where SimpleSort("ATerm") := <type-of> e
    with e2 := <expression-to-java-servlet> e
  