module sdf-interface

imports
  libstrategolib
  libwebdsl-front

strategies
  
  call-to-java-servlet :
    exp  |[ e.parse() ]| ->
    java |[
      org.webdsl.tools.strategoxt.SDF
        .get("~language", utils.AppInfo.class)
        .parse(e2)
    ]|
    where language := <language-of> e
    with e2    := <expression-to-java-servlet> e
       ; x_sdf := <new>
  
  call-to-java-servlet :
    exp  |[ e.isValid() ]| ->
    java |[
      org.webdsl.tools.strategoxt.SDF
        .get("~language", utils.AppInfo.class)
        .isValid(e2)
    ]|
    where language := <language-of> e
    with e2    := <expression-to-java-servlet> e
       ; x_sdf := <new>
  
strategies
    
  java-servlet-element-id : 
    tc@elem|[ inputSDF(e)[passign*]{} ]| -> tc{InputNumber(newid),anno*} 
    with  _{anno*} := tc
          ; newid := <newname> "input"
    
  elem-to-java-servlet : 
    tc@elem |[ inputSDF(e)[passign*]{} ]| -> <elem-to-java-servlet>    TemplateCall("inputText",[e],passign*,[]){anno*}//elem |[ inputText(e)[passign*]{} ]|{anno*} 
    with  _{anno*} := tc
    
  //customized from string
  handle-actions-input-to-java-servlet :
    tc@elem|[ inputSDF(e)[passign*]{} ]| ->
    <wrap-input-java-code(|passign*)>
    bstm*|[
      ident += "~inputident"+uniqueid;
      if (ps.getParammap().get(ident)!=null) {
        ~*<wrap-validation-context-code-store-inputs> bstm*|[
          String x_temp = ps.getParammap().get(ident);
          if(!org.webdsl.tools.strategoxt.SDF
                .get("~language", utils.AppInfo.class)
                .isValid(x_temp)) {
            ~*<add-conversion-error> (e, e|[org.webdsl.tools.strategoxt.SDF.getSGLRErrors().get(x_temp)]| )
            x_temp = null;
          }
          bstm* 
        ]|
      }
    ]|
    with  inputident := <get-inputnumber> tc
        ; x_temp := <newname> "temp"
        ; name := <pp-webdsl> e
        ; bstm* := <input-assignment-to-java-servlet(|x_temp)> e
        ; language := <language-of> e

strategies // Basic type definitions
  
  builtin-java-type :
    GenericSort("SDFInput", [_]) -> type |[ String ]|
