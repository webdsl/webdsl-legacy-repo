module org/webdsl/dsl/to-java-servlet/types-info

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front

/**
 *  A simple class containing meta information about types
 */

rules

  GenerateCodeJavaServlet =
      ?Application(_,_)     // emit only once 
    ; get-string-compatible-types
    ; cached-generate(generate-java-servlet-types-info | "internal_TypesInfo")
    ; fail
      
  generate-java-servlet-types-info :
    string-compatible-types -> <emit-java-code-local> compilation-unit|[
	    package utils;
	    import java.util.*;
	    
	    public class TypesInfo {
	        
	        private static List<String> stringCompatibleTypes = Arrays.asList(e*);
	        
	        public static List<String> getStringCompatibleTypes()
	        { 
	          return stringCompatibleTypes;
	        }
	    
	    }
	    ]|
    with  e* := <map(pp-webdsl-to-string; \x -> java:expr|[ "~x" ]|\)> string-compatible-types
