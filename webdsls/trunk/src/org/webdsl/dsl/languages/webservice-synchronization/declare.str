module org/webdsl/dsl/languages/webservice-synchronization/declare



imports
  libwebdsl-front
  org/webdsl/dsl/generation/webdsl-generator

strategies
	
  declare-synchronization-settings(|ent) = declare-synchronization-topentity(|ent)
  declare-synchronization-settings(|ent) = declare-synchronization-restrictedprops(|ent)
  declare-synchronization-settings(|ent) = declare-synchronization-access-control(|ent)


  declare-synchronization(|ent) : x@SyncConfigEmbedded(content) -> x
  	where <filter(declare-synchronization-settings(|ent))> content
  	with rules (
  		HasSyncConfig :+ ent -> ent
  		SyncEnabled := True()
  	)
  

  declare-synchronization-topentity(|ent): x@SyncTopLevelNameProperty(propertyname) -> x
    with rules (
    	TopEntity:+ ent -> propertyname
    )

  declare-synchronization-restrictedprops(|ent): x@SyncRestrictedProperties(props) -> x
   	where not(<?[]> props)
    with rules (
    	RestrictedProperties:+ ent -> props
    )
 
  declare-synchronization-access-control(|ent): x@SyncACRead(exp) -> exp
  	with rules(
  		SyncACRead:+ ent -> exp
  	)
 
  declare-synchronization-access-control(|ent): x@SyncACWrite(exp) -> exp
  	with rules(
  		SyncACWrite:+ ent -> exp
  	)

  declare-synchronization-access-control(|ent): x@SyncACCreate(exp) -> exp
  	with rules(
  		SyncACCreate:+ ent -> exp
  	)