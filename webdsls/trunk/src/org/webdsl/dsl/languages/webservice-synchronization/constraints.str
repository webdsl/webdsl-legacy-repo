module org/webdsl/dsl/languages/webservice-synchronization/constraints

imports
  libstrategolib

imports
  libwebdsl-front
  
  
  org/webdsl/dsl/languages/data-model/declare
  org/webdsl/dsl/typechecker/error
  
  org/webdsl/dsl/languages/webservice-synchronization/rename
  org/webdsl/dsl/languages/webservice-synchronization/declare

rules

  constraint-error-synchronization: x@SyncTopLevelNameProperty(propertyname) -> x
    where <get-anno(InEntity(?ent))> x
		; not( <get-property> (ent, propertyname) )
        ; < add-error(|$[no property [propertyname] in entity [ent]]) > propertyname
  
  constraint-error-synchronization: x@SyncTopLevelNameProperty(propertyname) -> x
    where <get-anno(InEntity(?ent))> x
		; type := <get-property; get-property-type; remove-sort-constructor> (ent, propertyname)
		; not(<?"String"> type) 
        ; < add-error(|$[name Property type: [type] is not compatible with expected type: String]) > propertyname
  
  constraint-error-synchronization: x@SyncConfigEmbedded(content) -> x
  	where TopLevelEntities* := <all-keys-TopEntity>
  		; <?[]> TopLevelEntities*
  		; < add-error(|$[synchronization framework requires at least one TopLevelEntity]) > x 
   
  constraint-error-synchronization: x@SyncRestrictedProperties(props) -> x
  	where <get-anno(InEntity(?ent))> x
  		; <filter(contraint-error-synchronization-no-property(|ent))> props
  		; <filter(contraint-error-synchronization-no-name-property(|ent))> props
  		; <filter(contraint-error-synchronization-prohibited-property(|ent))> props   
  		; fail  
  
  contraint-error-synchronization-no-property(|ent): propertyname -> propertyname
  	where not( <get-property> (ent, propertyname) )
        ; < add-error(|$[no property [propertyname] in entity [ent]]) > propertyname
  
  contraint-error-synchronization-no-name-property(|ent): propertyname -> propertyname
  	where nameprop := <TopEntity> ent
  		; <?nameprop> propertyname 
        ; < add-error(|$[it is not allowed to restrict the name property])> propertyname
  
  contraint-error-synchronization-prohibited-property(|ent): propertyname -> propertyname
  	where <?"id"> propertyname 
        ; < add-error(|$[it is not allowed to restrict property id])> propertyname
     
  