module org/webdsl/dsl/languages/action/typechecker/expression

imports
  libstrategolib

imports
  libwebdsl-front

signature
  constructors
    Op : String -> Annotation
 
strategies

  type-of = eq-type-of <+ <TypeOf> Var(<id>)

rules // typechecking expressions

  eq-type-of =
    ?OrderAscending(e1)
    ; <type-of> e1

  eq-type-of =
    ?OrderDescending(e1)
    ; <type-of> e1

  eq-type-of =
    ?OrderNonSpecific(e1)
    ; <type-of> e1

  constraint-error-action :
    Var(x) -> <add-error(|["Variable '", x, "' not defined"])>
    where not(type-of)

  eq-type-of :
    Var(x) -> <TypeOf> x

  constraint-error-action : 
    Cast(e, s) -> <add-error(|["Cannot cast ", <pp-webdsl> e, " to type ", <pp-webdsl> s])>
    where not(type-of)

  eq-type-of:
    Cast(e, s) -> s
    where (<type-compatible> (s, <type-of> e) <+ <generic-cast-compatible> (s, <type-of> e) )

  eq-type-of :
    Assignment(y, e) -> SimpleSort("Void") //<type-of> y
    
  eq-type-of :
    PropertyAssignment(y, e) -> SimpleSort("Void") //<type-of> y
    
  eq-type-of = type-of-qualified
  
  type-of-qualified :
    Qualified(x,y) -> <type-of-property> (<type-of-qualified> x, y)
    where <?Qualified(_,_)> x

  type-of-qualified :
    Qualified(x,y) ->  <type-of-property> (<TypeOf;?SimpleSort(<id>)> x, y)
    where not(<?Qualified(_,_)> x)
    
  constraint-error-action :
    FieldAccess(e, f) -> Error(["No property ", f, " defined for ", <type-of> f])
    where not(type-of)

  eq-type-of :
    FieldAccess(e, f) -> <type-of-property> (x_class, f)
    where SimpleSort(x_class) := <type-of> e
    
  eq-type-of :
    FieldAccess(e, f) -> <type-of> Var(f)
    where "Component" := <type-of> e

  eq-type-of :
    InColl(e1, e2) -> SimpleSort("Bool")
    where t := <type-of> e1
        ; GenericSort(_, [lt]) := <type-of> e2
        ; <type-compatible> (lt, t)

  constraint-error-action :
    InColl(e1, e2) -> <add-error(|["Expression ", <pp-webdsl> e1, " has wrong type"])>
    where not(type-of)

  constraint-error-action :
    AndForExp(fe) -> <add-error(|[<pp-webdsl> fe, " should be of some list type of Bool"])>
    where not(type-of)

  eq-type-of :
    AndForExp(fe) -> SimpleSort("Bool")
    where GenericSort(_, [SimpleSort("Bool")]) := <type-of> fe

  constraint-error-action :
    OrForExp(fe) -> <add-error(|[<pp-webdsl> fe, " should be of some list type of Bool"])>
    where not(type-of)

  eq-type-of :
    OrForExp(fe) -> SimpleSort("Bool")
    where GenericSort(_, [SimpleSort("Bool")]) := <type-of> fe

  constraint-error-action :
    ForExp(_, _, _, _, _) -> <add-error(|["type error in ", <pp-webdsl>])>
    where not(type-of)

  eq-type-of :
    ForExp(oe, x, s, ie, Filter(we, obe, l)) -> t
    where tie := <type-of> ie
        ; toe := <type-of> oe
        ; twe := <type-of> we
        ; tobe := <type-of> obe
        ; if <?GenericSort("List", _)> tie then
            t := GenericSort("List", [toe])
          else
            t := GenericSort("Set", [toe])
          end
        ; SimpleSort("Bool") := twe

  eq-type-of :
    ForExp(oe, x, s, ie, FilterNoOrderBy(we, l)) -> <eq-type-of> ForExp(oe, x, s, ie, Filter(we, OrderNonSpecific(Null()), l))

  eq-type-of :
    ForExp(oe, x, s, ie, FilterNoOrderByNoLimit(we)) -> <eq-type-of> ForExp(oe, x, s, ie, Filter(we, OrderNonSpecific(Null()), Limit(Int("1000"), Int("0"))))
  
  eq-type-of :
    ForExp(oe, x, s, ie, FilterNoWhereNoLimit(obe)) -> <eq-type-of> ForExp(oe, x, s, ie, Filter(True(), obe, Limit(Int("1000"), Int("0"))))

  eq-type-of :
    ForExp(oe, x, s, ie, FilterNoWhere(obe, l)) -> <eq-type-of> ForExp(oe, x, s, ie, Filter(True(), obe, l))

  eq-type-of :
    ForExpNoFilter(oe, x, s, ie) -> <eq-type-of> ForExp(oe, x, s, ie, Filter(True(), OrderNonSpecific(Var(x)), Limit(1000, 0)))

  typecheck-binop :
    (op, [e1, e2]) -> t
    where t := <check-signature> (None(), op, [e1, e2])
        
  check-operator-signature : (None(), Op("Add"), [t1, t2]) -> t3 where <type-compatible> (t1, t2); t3 := t1
                                                                    <+ <type-compatible> (SimpleSort("String"),t1); t3 := SimpleSort("String") //all types can be converted to strings implicitly
                                                                    <+ <type-compatible> (SimpleSort("String"),t2); t3 := SimpleSort("String")
  check-operator-signature : (None(), Op("Sub"), [SimpleSort("Int"), SimpleSort("Int")]) -> SimpleSort("Int")
  check-operator-signature : (None(), Op("Sub"), [SimpleSort("Float"), SimpleSort("Float")]) -> SimpleSort("Float")
  check-operator-signature : (None(), Op("Mul"), [SimpleSort("Int"), SimpleSort("Int")]) -> SimpleSort("Int")
  check-operator-signature : (None(), Op("Mul"), [SimpleSort("Float"), SimpleSort("Float")]) -> SimpleSort("Float")
  check-operator-signature : (None(), Op("Div"), [SimpleSort("Int"), SimpleSort("Int")]) -> SimpleSort("Int")
  check-operator-signature : (None(), Op("Div"), [SimpleSort("Float"), SimpleSort("Float")]) -> SimpleSort("Float")
  check-operator-signature : (None(), Op("Mod"), [SimpleSort("Int"), SimpleSort("Int")]) -> SimpleSort("Int")
  check-operator-signature : (None(), Op("Eq"), [t, t2]) -> SimpleSort("Bool") where <type-compatible> (t, t2)
  check-operator-signature : (None(), Op("NotEq"), [t, t2]) -> SimpleSort("Bool") where <type-compatible> (t, t2)
  check-operator-signature : (None(), Op("SmallerThan"), [t, t]) -> SimpleSort("Bool")
  check-operator-signature : (None(), Op("SmallerThanOrEqual"), [t, t]) -> SimpleSort("Bool")
  check-operator-signature : (None(), Op("LargerThan"), [t, t]) -> SimpleSort("Bool")
  check-operator-signature : (None(), Op("LargerThanOrEqual"), [t, t]) -> SimpleSort("Bool")
  check-operator-signature : (None(), Op("And"), [SimpleSort("Bool"), SimpleSort("Bool")]) -> SimpleSort("Bool")
  check-operator-signature : (None(), Op("Or"), [SimpleSort("Bool"), SimpleSort("Bool")]) -> SimpleSort("Bool")
  
  constraint-error-action :
    c#([e1, e2]) -> <add-error(|["Wrong operand types for operator ", c, ": ", <pp-webdsl> e1, " has type ",<type-of<+!"type unknown"> e1, ",", <pp-webdsl> e2, " has type ",<type-of<+!"type unknown"> e2])>
    where not(type-of)
        ; <elem> (c, ["Add", "Sub", "Mul", "Div", "Mod", "Eq", "NotEq", "SmallerThan", "SmallerThanOrEqual", "LargerThan", "LargerThanOrEqual", "And", "Or"])

  eq-type-of :
    c#([e1, e2]) -> t
    where not("IsA" := c)
        ; t := <typecheck-binop> (Op(c), [e1, e2])
  
  constraint-error-action :
    Not(e) -> <add-error(|[<pp-webdsl> e, " is not of type Bool"])>
    where not(type-of)

  eq-type-of :
    Not(e) -> SimpleSort("Bool")
    where SimpleSort("Bool") := <type-of> e

  eq-type-of :
    IsA(e, s) -> SimpleSort("Bool")
    where <is-webdsl-type> s
        ; <type-of> e

  constraint-error-action :
    HqlQuery(q) -> <add-error(|["Type error in query ", <pp-webdsl> q])>
    where not(type-of)

  eq-type-of :
    HqlQuery(q) -> <type-of-query> q

rules // signature

  check-signature :
    (t3, f, e*) -> t
    where t* := <map(type-of)> e*
        ; t := <check-signature-aux> (t3, f, t*)

  check-signature-aux =
    ?sig
    ; (CheckActionSignature
       <+ check-operator-signature
       <+ check-function-signature
       <+ CheckPageSignature
       <+ CheckGlobalFunctionSignature
       <+ CheckGeneratedGlobalFunctionSignature
       <+ check-builtin-signature)

  check-builtin-signature :
    (s@SimpleSort(_), "delete", []) -> SimpleSort("Void")
    where <is-entity-type> s

  check-builtin-signature :
    (s@SimpleSort(_), "save", []) -> SimpleSort("Void")
    where <is-entity-type> s

  check-builtin-signature :
    (s@SimpleSort(_), "persist", []) -> SimpleSort("Void")
    where <is-entity-type> s

  check-builtin-signature :
    (GenericSort(Z, [s@SimpleSort(_)]), "persist", []) -> SimpleSort("Void")
    where <is-entity-type> s
        ; <?"List" <+ ?"Set"> Z

  check-function-signature = CheckFunctionSignature

  check-function-signature :
    (SimpleSort(s), f, args) -> <check-function-signature> (SimpleSort(<Extends> s), f, args)
