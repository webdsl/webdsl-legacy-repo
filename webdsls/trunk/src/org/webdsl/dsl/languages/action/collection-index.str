module org/webdsl/dsl/languages/action/collection-index

rules

  constraint-error-action :
    CollectionIndex(e_col,e_index) -> <add-error(|["Indexed access [] is only allowed for List<type>, type found: ",<pp-webdsl-to-string>srt,"."])>
    where not(type-of)
        ; srt := <type-of> e_col; <type-of> e_index // don't report when sub-expression was already wrong

  constraint-error-action :
    CollectionIndex(e_col,e_index) -> <add-error(|["Index must be of type Int, but found type '",<pp-webdsl-to-string>type,"'."])>
    where type := <type-of> e_index //check that it has a type, otherwise error is already reported
        ; not(SimpleSort("Int") := type) //but not the correct one

  eq-type-of-action :
    CollectionIndex(e_col,e_index) -> srt
    where GenericSort("List",[srt]) := <type-of> e_col
    
  constraint-error-action :
    CollectionIndexAssignment(e_col,e_index,e_val) -> <add-error(|["Indexed access [] in left-hand side of assignment is only allowed for List<type>, type found: ",<pp-webdsl-to-string>srt,"."])>
    where not(<type-of> CollectionIndex(e_col,e_index))
        ; srt := <type-of> e_col; <type-of> e_index; <type-of> e_val // don't report when sub-expression was already wrong
     
  constraint-error-action :
    CollectionIndexAssignment(e_col,e_index,e_val) -> <add-error(|["Index must be of type Int, but found type '",<pp-webdsl-to-string>type,"'."])>
    where type := <type-of> e_index //check that it has a type, otherwise error is already reported
        ; not(SimpleSort("Int") := type) //but not the correct one
   
  constraint-error-action :
    CollectionIndexAssignment(e_col,e_index,e_val) -> 
      <add-error(|["Type of expression being assigned '",<pp-webdsl-to-string>srt2,"' is incompatible with type in collection '",<pp-webdsl-to-string>srt1,"'."])>
    where not(<type-compatible> (<type-of> e_col, <type-of> e_val))
        ; srt1 := <type-of> e_col; <type-of> e_index; srt2 := <type-of> e_val 
        
rules
    
  desugar-action :
    CollectionIndex(e_col,e_index) -> Call(e_col,"get",[e_index])

  desugar-action :
    Stat(CollectionIndexAssignment(e_col,e_index,e_val)) -> Stat(Call(e_col,"set",[e_index,e_val]))
