module org/webdsl/dsl/languages/data-model/name-property

strategies

  extend-property-declare(|x_class) = declare-name-property(|x_class); fail
  
rules

  declare-name-property(|x_class) :
    prop{anno*} -> <fail>
    where not(<fetch(?IsGeneratedProperty())> anno*)
        ; x := <get-property-name> prop // cope with different property constructors
        ; if <?"name"> x then rules ( HasNameProperty : x_class ) end
        ; if <is-namefield> prop then rules ( HasNameAnnotation : x_class ) end

strategies

  /**
   *  Determine whether the entity or its supers has a 'name' property
   */ 
  has-name-property =
    ?x_class
    ; (HasNameProperty <+ <has-name-property> <Extends> x_class)
    
strategies

	// get name of property with name annotation, or return "id"
  get-namefield =
    (   oncetd(\ Property(name, k, type, annos) -> name where <fetch(?SimpleAnno("name"))> annos \; ?name ) 
     <+ oncetd(\ DerivedProperty(name, k, type, annos, e) -> name where <fetch(?SimpleAnno("name"))> annos \; ?name ))
    ; !name
    <+ !"id"

	// succeed if property has name annotation
  is-namefield = 
    ?Property(name, k, type, annos)
    ; where( <fetch(?SimpleAnno("name"))> annos)

  is-namefield =
    ?DerivedProperty(name, k, type, annos, e)
    ; where( <fetch(?SimpleAnno("name"))> annos)
    
rules // entity name property annotation for entities without a property 'name'
  
  // entity with super: generate custom getter if *this class* has a property with name anno, otherwise inherit from super
  add-backend-annotation =
		  ?Entity(class,_,_)
	   ; where(<HasNameAnnotation> class)
	   ; where(namefield := <get-namefield>) 	// will return name anno, not "id"
		 ; add-new-anno(|NamePropertyAnno(namefield))
  
  // entity without super: generate custom getter if this class does not have a name property
	add-backend-annotation =
	   ( ?ent@SessionEntity(class,_) <+ ?ent@EntityNoSuper(class,_) )
	   ; where(<not(HasNameProperty)> class)
	   ; where(namefield := <get-namefield>)	// either a property with name anno, or "id"
		 ; add-new-anno(|NamePropertyAnno(namefield))
  