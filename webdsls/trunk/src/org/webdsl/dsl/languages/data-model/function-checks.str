module org/webdsl/dsl/languages/data-model/function-checks

imports
  libstrategolib

imports
  libwebdsl-front
  org/webdsl/dsl/languages/data-model/property-checks

rules //session entity
  
  constraint-error-data :
    Application(_, sections) -> 
    <constraint-error-data-duplication-in-session-entity(constraint-error-double-func-session)>
  
  get-functions-of-session-entities-by-name(|entities):
    x -> <collect(extract-session-entity-extend-functions-by-name(|x)); flatten-list> entities
    
  extract-session-entity-extend-functions-by-name(|x) =
    (  ?SessionEntity(x, <id>)
    <+ ?ExtendSessionEntity(x, <id>)
    )
  ; filter(is-function-cons)
  
  constraint-error-double-func-session(|entities) :
    x -> <id>
    with  functions := <get-functions-of-session-entities-by-name(|entities)> x
        ; <constraint-error-double-func-common(|functions, "Session Entity")> x

rules //entity

  constraint-error-data :
    Application(_, sections) -> 
    <constraint-error-data-duplication-in-entity(constraint-error-double-func-entity)>
        
  constraint-error-double-func-entity(|entities) :
    x -> <id>
    with  functions := <get-functions-of-entities-by-name(|entities)> x
        ; <constraint-error-double-func-common(|functions, "Entity")> x
  
  get-functions-of-entities-by-name(|entities):
    x -> <collect(extract-entity-extend-functions-by-name(|x)); flatten-list> entities
    
  extract-entity-extend-functions-by-name(|x) =
    (  ?Entity(x, _, <id>)
    <+ ?EntityNoSuper(x, <id>)
    <+ ?ExtendEntity(x, <id>)
    )
  ; filter(is-function-cons)

rules //common  
  
  constraint-error-double-func-common(|functions, entitytype) :
    x -> <id>
    with  functionsignatures := <remove-all(?ExtendFunction(_));map(extract-function-sig)> functions
        ; doublefuncsigs := <find-doubles> functionsignatures
        ; <map(try(constraint-error-double-func-helper(|doublefuncsigs, x, entitytype)))> functions
       
  extract-function-sig = 
     \Function(x,farg,_,_) -> (x, <map(farg-to-type)> farg)\
  <+ \FunctionNoReturn(x,farg,_) -> (x, <map(farg-to-type)> farg)\
  
  farg-to-type = ?Arg(_,<id>)

  constraint-error-double-func-helper(|doublefuncsigs, x, entitytype) :
    f -> <add-error(|["Function with signature ", prettysig," for ", entitytype," ", x, " is defined multiple times."])>
    where sig := <extract-function-sig> f
        ; <fetch(?sig)> doublefuncsigs  
        ; prettysig := <pp-func-sig> sig
        
  pp-func-sig = ?(x,argtypes); <concat-strings> [x,"(",<map(pp-webdsl-to-string);separate-by(|", ");concat-strings> argtypes,")"]
  
rules // generic check entity function traversal

  constraint-error-data = check-all-entity-functions
  
  check-all-entity-functions:
    Application(_, sections) -> <fail>
    // filter over sections, filter over definitions
    where <filter(filter(transform-entity-body(check-entity-functions)))> sections
  
  check-entity-functions(|entityname):
    decls -> <fail>
    // filter over body declarations
    where filter(check-entity-function(|entityname))
    
rules // built-in function signature check
  
  check-entity-function(|entityname):
    decl -> <fail>
    where is-function-cons
	    ; sig := <extract-function-sig> decl
	    ; is-builtin-ent-func-signature
	    ; prettysig := <pp-func-sig> sig
	    ; <add-error(|
	            ["Function with signature ", prettysig, " for ", entityname,
	             " overlaps with built-in function." ] )> decl

rules
  
  is-builtin-ent-func-signature = fail
