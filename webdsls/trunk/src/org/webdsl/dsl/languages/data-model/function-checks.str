module org/webdsl/dsl/languages/data-model/function-checks

imports
  libstrategolib

imports
  libwebdsl-front
  org/webdsl/dsl/generation/webdsl-generator

rules
  
  constraint-error-data = constraint-error-data-general(
  	get-entity-def,
  	traverse-entity-hierarchy(bagof-Functions); flatten-list; unique-locations,
  	constraint-entity-functions)
	
rules	// function duplication

  constraint-entity-functions: (classname, funcs) -> <fail>
  	with signatures := <map(extract-function-sig)> funcs
  	   ; double-sigs := <find-doubles> signatures
  	   ; double-funcs := <filter(where(<member> (<extract-function-sig>, double-sigs)))> funcs
  	   ; <map(add-double-function-error(|classname))> double-funcs

	add-double-function-error(|classname):
		def -> <add-error(|["Function with signature ",pretty-sig," for Entity ", classname, " is defined multiple times."])>    
  		with sig := <extract-function-sig> def
  			; pretty-sig := <pp-func-sig> sig
    
rules // built-in function signature check
  
   constraint-entity-functions:
   	(classname, funcs) -> <fail>
   	where <map(try(constraint-entity-function(|classname)))> funcs
   	
   constraint-entity-function(|classname):
   	decl -> <fail>
   	where sig := <extract-function-sig> decl
   			; msg := <is-builtin-ent-func-signature(|classname)> sig
	    	; prettysig := <pp-func-sig> sig
	    	; <add-error(|
	            ["Function with signature ", prettysig, " for entity ", classname,
	             " ", msg ] )> decl

rules // builtin-function that  cannot be overloaded
  
  is-builtin-ent-func-signature(|entname):
    (name, params) -> msg
    where fargs := <EntityFunctionNoOverloading> (entname, name)
        ; not(params := fargs)
        ; msg := <concat-strings> ["overloads a builtin function."]

rules // utils

	extract-function-sig = 
     \Function(x,farg,_,_) -> (x, <map(farg-to-type)> farg)\
  <+ \FunctionNoReturn(x,farg,_) -> (x, <map(farg-to-type)> farg)\
  
  farg-to-type = ?Arg(_,<id>)
    
  pp-func-sig = ?(x,argtypes); <concat-strings> [x,"(",<map(pp-webdsl-to-string);separate-by(|", ");concat-strings> argtypes,")"]
