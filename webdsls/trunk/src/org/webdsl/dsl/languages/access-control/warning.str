/**

 Security rules for WebDSL

*/

module org/webdsl/dsl/languages/access-control/warning

imports
  libstrategolib
  //Java-15
  libjava-front

imports
  libwebdsl-front
  org/webdsl/dsl/languages/access-control/-

strategies

  warn-for-unused-rules =
    where(with(
      <diff> (<bagof-AccessControlRuleDeclared>,<bagof-AccessControlRuleUsed>)
    ; remove-all(?AccessControlRule("action","*",_,_,_)) //remove "*" action rules, many are generated
    ; map(\   AccessControlRule(checktype,matchstring,fargs,check,acrules) 
           -> <concat-strings> ["rule ",<pretty-print-access-control-warning> (checktype,matchstring,fargs)]
          \) //don't show nested rules and simplify output
    ; (?[] <+ where(newline-for-ac-warnings-once;<debug> "Access control warning: unused rules: ");map(debug)) 
    ))
    
  warn-for-blocked-elements =
    try(
      ?(False(),type,ident,fargs)
    ; where(
        newline-for-ac-warnings-once
      ; <concat-strings;debug> 
          ["Access control warning: access to element always denied: "
          ,<pretty-print-access-control-warning> (type,ident,fargs)
          ]
      )
    )
    
  newline-for-ac-warnings-once =
    where(
      if not(FirstACWarningShown) 
      then <debug> ""
         ; rules(FirstACWarningShown := True())  
      end
    )
    
  pretty-print-access-control-warning :
    (checktype,ident,fargs) -> <concat-strings> [checktype," ",ident,"(",<pp-webdsl-to-string> fargs,")"]
    