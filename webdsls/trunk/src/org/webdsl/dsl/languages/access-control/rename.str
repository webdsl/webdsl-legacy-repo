module org/webdsl/dsl/languages/access-control/rename

imports
  libstrategolib

imports
  libwebdsl-front
  org/webdsl/dsl/languages/access-control/-

rules 

  rename-ac :
    AccessControlRule(checktype,matchstring,fargs1,check1,acrules1){anno*} -> newrule
    with {| Rename, InDef, VarInTemplateContext
           : rules ( InDef := True() )
           ; fargs2 := <rename-all;MatchArgs(map(add-is-template-arg-anno),id)> fargs1
           ; <MatchArgs(declare-args-in-context,id)> fargs2
           ; check2 := <alltd(rename-security-context-properties)> check1
           ; check3 := <rename-all> check2
           ; acrules2 := <rename-all> acrules1
           |}
         ; newrule := AccessControlRule(checktype,matchstring,fargs2,check3,acrules2){anno*}
         ; rules( AccessControlRule :+ checktype -> newrule )
           
  // SpecialAccessControlRule, e.g. rule logsql { e }
  rename-ac :
    SpecialAccessControlRule(checktype,check1){anno*} -> SpecialAccessControlRule(checktype,check3){anno*}
    with  {| Rename, InDef
           : rules ( InDef := True() )
           ; check2 := <alltd(rename-security-context-properties)> check1
           ; check3 := <rename-all> check2
           |}
           
  rename-security-context-properties :
    Var(x){anno*} -> Var(y){anno*}
    where srt := <TypeOfProperty> ("SecurityContext",<strip-annos>x) //relies on declaration of securitycontext, problem with current typechecker
        ; y := x{<newname-webdsl> x}
        ; rules ( TypeOf : y -> srt 
                  SecurityContextProperty : Var(y) -> |[ securityContext.x ]|)

  rename-ac :
    Predicate(x, args1, b1){anno*} -> Predicate(x, args2, b3){anno*}
    with  {| Rename, InDef, VarInTemplateContext
           : rules ( InDef := True() )
           ; b2 := <alltd(rename-security-context-properties)> b1
           ; args2 := <map(rename)> args1
           ; <declare-args-in-context> args2
           ; b3 := <rename-all> b2
           |} 

strategies

  rename-ac =
    ?webdsl|[ access control policy pexp ]|
  ; rules(AC-Policy-Expression := pexp)

  ///declare-definition:
   // AccessControlDefinition(name,defs) -> AccessControlDefinition(name,defs)
   // where <alltd(declare-definition)> defs // declare-definition is done with alltd, which doesn't go into the tree after a match, explicitly search deeper
        
  // declare-ac-principal :
  //  AccessControlPrincipal(ident,props) -> AccessControlPrincipal(ident,props)
  //nothing to declare, gets translated to securityContext which should be declared
 
 // declare-ac-rule = 
 //   ?AccessControlRule(_,_,_,_,_)
    
 
  //@TODO currently broken, can probably be deprecated
  rename-ac :
    AccessControlPointcut(pname,args1,b1){anno*} -> <declare-ac-pointcut> AccessControlPointcut(pname,args2,b2){anno*}
    with  {| Rename, InDef, VarInTemplateContext
           : rules ( InDef := True() )
           ; args2 := <map(rename)> args1
           ; <declare-args-in-context> args2
           ; b2 := <alltd(rename-pc-elem-idents)> b1
           //; b2 := <rename-all> b1
          |}

  rename-pc-elem-idents :
    x -> y
    where Var(y) := <Rename> Var(x)
    
  declare-ac-pointcut:
    acp@AccessControlPointcut(pname,fa,elems) -> acp
    with 
         {| pointcut-formal-argument :
             <filter(\ ar@Arg(ident,thesort)->ar with rules (pointcut-formal-argument : ident -> ar) \)> fa
           ; t1* := <map(?Arg(_,<id>))> fa
           ; <filter(declare-ac-pointcut-element(|pname,t1*))> elems
           ; rules(  MatchesAccessControlPointcut : (x_ident, t1*) 
                     PointcutArguments : (x_ident, t1*) -> fa
           )
         |}  
   
  declare-ac-pointcut-element(|pname,fa):
    acpe@AccessControlPointcutElement(ident,matchident,faidents,wc) -> acpe
    with  faidentsargs := MatchArgs(<map(pointcut-formal-argument <+ id)> faidents,wc)
        ; newPointcutList := <concat> [<AccessControlPointcuts<+![]> (pname,fa),[(ident,matchident,faidentsargs)]]
        ; rules(
            AccessControlPointcuts: (pname,fa) -> newPointcutList
          ) 
/*
          
  // uses: declare-global-funcs = ?fu@Function(f, args, s, b) ...
  declare-definition:
    p@Predicate(f,args,b) -> p//<declare-global-funcs> Function(f, args, SimpleSort("Bool"), b)
    where rules (IsPredicate : (f,args)
                 IsPredicate : f ) //TODO remove need for this one
  */        
