module org/webdsl/dsl/languages/access-control/rename

imports
  libstrategolib

imports
  libwebdsl-front
  org/webdsl/dsl/languages/access-control/-

rules 

  rename-ac :
    AccessControlRule(checktype,matchstring,fargs1,check1,acrules1){anno*} -> AccessControlRule(checktype,matchstring,fargs2,check3,acrules2){anno*}
    with  {| Rename
           : fargs2 := <rename-all> fargs1
           ; check2 := <rename-all> check1
           ; check3 := <alltd(rename-security-context-properties)> check2
           ; acrules2 := <rename-all> acrules1
           |}
           
  rename-security-context-properties :
    Var(x){anno*} -> Var(y){anno*}
    where srt := <TypeOfProperty> ("SecurityContext",x) //relies on declaration of securitycontext, problem with current typechecker
        ; y := x{<newname> x}
        ; rules ( TypeOf : y -> srt 
                  SecurityContextProperty : Var(y) -> |[ securityContext.x ]|)
  
  rename-ac :
    Predicate(x, args1, b1){anno*} -> <declare-global-pred> Predicate(x, args2, b3){anno*}
    with  {| Rename
           : b2 := <alltd(rename-security-context-properties)> b1
           ; args2 := <map(rename)> args1
           ; b3 := <rename-all> b2
           |}
  declare-global-pred =
    ?Predicate(f, args, b)
    ; with(<declare> Function(f, args, SimpleSort("Bool"), b))
    ; where(t1* := <map(?Arg(_,<id>))> args
          ; fname := <strip-annos> f 
          ; rules (IsPredicate : (fname,t1*))
      )
    
    
  declare-ac = declare-ac-principal
    
  declare-ac-principal : 
    acp@AccessControlPrincipal(x_ident,props) -> acp
    with <declare> def |[
            session securityContext {
              principal   -> x_ident
              loggedIn    :: Bool //for backwards comp
            }
          ]|
        ; x_loginfun := "loggedIn"
        ; <declare-global-pred> def |[
            predicate x_loginfun()
            {
              securityContext.principal != null
            }
          ]|
        ; <register-reserved-function> (x_loginfun,0)
        ; try(
            not(<IsPage> "accessDenied") // don't overwrite the custom def
          ; <with(declare;once-AccessDeniedPageDefined)> 
              DefaultAccessDeniedPage()
          )
        ; rules (
            AccessControlEnabled :+= True()
          )
        ; <declare-login> acp
  
  check-access-denied-page-redefinition =
    try(
      ?def@Define(mod*, "accessDenied", _, _, _)
      ; where(<fetch(?Page())> mod*)
      ; rules( AccessDeniedPageDefined :+= True() )
    )

overlays

  DefaultAccessDeniedPage =
    def |[
      define ignore-access-control page accessDenied()
      {
        title{"Access Denied"}
        text("Access Denied: ")
        navigate(root()) { "return to home page" }
      }
    ]|

strategies

  rename-ac =
    ?webdsl|[ access control policy pexp ]|
  ; rules(AC-Policy-Expression := pexp)

  ///declare-definition:
   // AccessControlDefinition(name,defs) -> AccessControlDefinition(name,defs)
   // where <alltd(declare-definition)> defs // declare-definition is done with alltd, which doesn't go into the tree after a match, explicitly search deeper
        
  // declare-ac-principal :
  //  AccessControlPrincipal(ident,props) -> AccessControlPrincipal(ident,props)
  //nothing to declare, gets translated to securityContext which should be declared
 
 // declare-ac-rule = 
 //   ?AccessControlRule(_,_,_,_,_)
    
 
  rename-ac :
    AccessControlPointcut(pname,args1,b1){anno*} -> <declare-ac-pointcut> AccessControlPointcut(pname,args2,b2){anno*}
    with  {| Rename
           : args2 := <map(rename)> args1
           ; b2 := <alltd(rename-pc-elem-idents)> b1
           //; b2 := <rename-all> b1
          |}

  rename-pc-elem-idents :
    x -> y
    where Var(y) := <Rename> Var(x)
    
  declare-ac-pointcut:
    acp@AccessControlPointcut(pname,fa,elems) -> acp
    with 
         {| pointcut-formal-argument :
             <filter(\ ar@Arg(ident,thesort)->ar with rules (pointcut-formal-argument : ident -> ar) \)> fa
           ; t1* := <map(?Arg(_,<id>))> fa
           ; <filter(declare-ac-pointcut-element(|pname,t1*))> elems
           ; rules(  MatchesAccessControlPointcut : (x_ident, t1*) 
                     PointcutArguments : (x_ident, t1*) -> fa
           )
         |}  
   
  declare-ac-pointcut-element(|pname,fa):
    acpe@AccessControlPointcutElement(ident,matchident,faidents,wc) -> acpe
    with  faidentsargs := MatchArgs(<map(pointcut-formal-argument)> faidents,wc)
        ; newPointcutList := <concat> [<AccessControlPointcuts<+![]> (pname,fa),[(ident,matchident,faidentsargs)]]
        ; rules(
            AccessControlPointcuts: (pname,fa) -> newPointcutList
          ) 
/*
          
  // uses: declare-global-funcs = ?fu@Function(f, args, s, b) ...
  declare-definition:
    p@Predicate(f,args,b) -> p//<declare-global-funcs> Function(f, args, SimpleSort("Bool"), b)
    where rules (IsPredicate : (f,args)
                 IsPredicate : f ) //TODO remove need for this one
  */        
