module org/webdsl/dsl/languages/access-control/login

signature
constructors

  GeneratedTemplate : Term

overlays
  
  LoginTemplateForDeclare  = Define([GeneratedTemplate()],"login",[],[],[])
  LogoutTemplateForDeclare = Define([GeneratedTemplate()],"logout",[],[],[])
  AuthenticationTemplateForDeclare = Define([GeneratedTemplate()],"authentication",[],[],[])
  
rules
  get-login-template  = !<IsPage; TemplateDefinition> "login" <+ <TemplateSignature> ("login",[])
  get-logout-template = !<IsPage; TemplateDefinition> "logout" <+ <TemplateSignature> ("logout",[])
  get-auth-template   = !<IsPage; TemplateDefinition> "authentication" <+ <TemplateSignature> ("authentication",[])

  declare-login : 
    acp@AccessControlPrincipal(x_ident,props) -> acp
    with  farg_loginfun* := <map(create-farg-for-login-fun(|x_ident))> props
        ; len := <length> props
        ; <register-reserved-function> ("authenticate",len)
        ; <register-reserved-function> ("logout",0)
        ; <map(declare)> def* |[
            function authenticate(farg_loginfun*):Bool{}
            function logout(){}
          ]|
        ; (  get-login-template
          <+ // in first declare step, it doesn't matter whether a login definition is in the app as well, nor which order they are handled,
             // as long as there is a declare of the login template.
             // if this one is declared first, the actual login page/template will override it, since that one is unconditional
             // if the real one is declared first, this declare will not happen due to get-login-template check above
             <declare> LoginTemplateForDeclare()
          )
        ; if not(InInitialDeclareStepBeforeRename) //there will be another declare step after rename
           ; LoginTemplateForDeclare() := <get-login-template> //check whether the declared template is the generated one
          then rules(LoginTemplateGenerated  := True()) end
        ; (  get-logout-template
          <+ <declare> LogoutTemplateForDeclare()
          )
        ; if not(InInitialDeclareStepBeforeRename) 
           ; LogoutTemplateForDeclare() := <get-logout-template>
          then rules(LogoutTemplateGenerated := True()) end
        ; (  get-auth-template 
          <+ <declare> AuthenticationTemplateForDeclare()
          )
        // declaring a login page will remove the login template, only generate authentication when both login and logout are generated
        ; if not(InInitialDeclareStepBeforeRename) 
           ; AuthenticationTemplateForDeclare() := <get-auth-template>
           ; LoginTemplateGenerated
           ; LogoutTemplateGenerated 
          then 
            rules(AuthenticationTemplateGenerated := True())
          end
        
          
  create-farg-for-login-fun(|x_ident) :
    x -> result
    with  type := <entity-prop-to-type(|x_ident)> x
        ; result := Arg(<credential-to-var-name> x, type) 
      
  credential-to-var-name = <concat-strings> ["cred",<id>]     
          
  entity-prop-to-type(|x_ident) :
    x -> result
    with result := <TypeOfProperty <+ !"Undefined"> (x_ident,x) 
    //Undefined when the principal declaration was incorrect, e.g. principal type or credential property doesn't exist
    //doesn't overlap with an entity name Undefined, since that would be type SimpleSort("Undefined")

  desugar-login : 
    acp@AccessControlPrincipal(x_ident,props) -> acp
    with  create-login-template-if-not-defined(|x_ident,props)
        ; create-authenticate-function(|x_ident,props)
        ; create-logout-template-if-not-defined(|x_ident,props)
        ; create-logout-function(|x_ident,props)
        ; create-auth-template-if-not-defined(|x_ident,props)
    
  create-login-template-if-not-defined(|x_ident,props) = 
    try(where(LoginTemplateGenerated); default-login-template(|x_ident,props))
  
  
  default-login-template(|x_ident,props) =
    with(
      e_loginfunarg* := <map(!Var(<credential-to-var-name>))> props
    ; elem_loginvar* := <map(create-vars-for-login-tpl(|x_ident))> props
    ; elem_logininput* := <map(create-inputs-for-login-tpl(|x_ident))> props
    ; <emit-webdsl-code> def|[
      define login() {
      
        elem_loginvar*
      
        form { 
          group("Login") {
            table {
              elem_logininput*
            }
            action("Login", signinAction())
          }
        }
        
        action signinAction() {
          validate(authenticate(e_loginfunarg*), "The login credentials are not valid.");
          message("You are now logged in.");
          return root();
        }
      }
      ]|
    ; <emit-webdsl-section> webdsl |[ 
      access control rules anonymous
        rule template login(){
          true
        }
      ]|
    )
    
  create-authenticate-function(|x_ident,props) =
    with(
      farg_loginfun* := <map(create-farg-for-login-fun(|x_ident))> props 
    ; x_firstprop := <Hd> props
    ; x_firstpropvarname := <credential-to-var-name> x_firstprop
    ; x_findfun := <concat-strings> ["find",x_ident,"By",<capitalize-string> x_firstprop]
    ; srt_ent := SimpleSort(x_ident)
    ; stat_auth* := <Tl; map(cred-prop-to-auth-stat(|x_ident))> props 
    ; <emit-webdsl-code> def|[
      function authenticate(farg_loginfun*) : Bool {
        var tmpentitylist : List<srt_ent> := x_findfun(x_firstpropvarname);
        if(tmpentitylist.length > 0) {
          for(tmpentity : srt_ent in tmpentitylist){
            var allow : Bool := true;
            
            stat_auth*
            
            if(allow){
              securityContext.principal := tmpentity;
              securityContext.loggedIn := true;
              return true;
            }
          }
        }
        securityContext.principal := null;
        securityContext.loggedIn := false;
        return false;
      }
      ]|
    )
    
  cred-prop-to-auth-stat(|x_ident) =
     cred-prop-to-auth-stat-secret(|x_ident)
  <+ cred-prop-to-auth-stat-stringcomp(|x_ident)
  
  cred-prop-to-auth-stat-secret(|x_ident) :
    x -> stat*
    where SimpleSort("Secret") := <entity-prop-to-type(|x_ident)> x
    with  x_varname := <credential-to-var-name> x
        ; stat* := webdsl |[
            if(!tmpentity.x.check(x_varname)) {
              allow := false;
            }
          ]|
          
  cred-prop-to-auth-stat-stringcomp(|x_ident) :
    x -> stat*
    where <type-compatible> (<entity-prop-to-type(|x_ident)> x, SimpleSort("String"))
    with  x_varname := <credential-to-var-name> x
        ; stat* := webdsl |[
            if(tmpentity.x != x_varname){
              allow := false;
            }
          ]|
 
  create-vars-for-login-tpl(|x_ident) :
    x -> result
    with  type := <entity-prop-to-type(|x_ident)> x
        ; result := VarDecl(<credential-to-var-name> x,type) 
 
  create-inputs-for-login-tpl(|x_ident) :
    x -> result
    with  x1 := <credential-to-var-name> x
        ; str := <concat-strings> [<make-human-readable> x , ": "]
        ; result := elem |[ groupitem{ label(str){input(x1)} } ]|
    
  create-logout-template-if-not-defined(|x_ident,props) = 
    try(where(LogoutTemplateGenerated); default-logout-template(|x_ident,props))

  default-logout-template(|x_ident,props) =
    with(
      x := <Hd> props
    ; <emit-webdsl-code> def|[
        define logout() {
          "Logged in as: " output(securityContext.principal.x)
          form{ 
            actionLink("Logout", signoffAction())
            action signoffAction() { logout(); }
          }
        }
      ]|
    ; <emit-webdsl-section> webdsl |[ 
        access control rules anonymous
          rule template logout(){
            true
          }
      ]|
    )
    
  create-logout-function(|x_ident,props) =
    with(
      <emit-webdsl-code> def|[
        function logout() {
          securityContext.loggedIn := false;
          securityContext.principal := null;
        }
      ]|
    )
    
  create-auth-template-if-not-defined(|x_ident,props) = 
    try(where(AuthenticationTemplateGenerated); default-auth-template(|x_ident,props))
  
  default-auth-template(|x_ident,props) =
    <emit-webdsl-code> def|[
      define authentication() {
        if(loggedIn()){
          logout()
        }
        else{
          login()
        }
      }
    ]|
  ; <emit-webdsl-section> webdsl |[ 
      access control rules anonymous
        rule template authentication(){
          true
        }
    ]|
  
    