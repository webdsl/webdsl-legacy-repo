module org/webdsl/dsl/languages/ajax/typechecker

imports
  libstrategolib

imports
  libwebdsl-front

strategies

  eq-type-of-ajax :
    EventCall(_,_) -> SimpleSort("String")

  eq-type-of-ajax :
    InlineAction(_) -> SimpleSort("Void")

rules // constraints

/*    propertyassignment-check-id-is-single(|tc) :
      p@PropertyAssignment("id", x) -> p
      where [other|_] := <bagof-HtmlEntityId> x  
          ; other2    := <strip-annos> other
          ; tc2 := <strip-annos> tc
          ; not(other2 := tc2)
          ; !<add-error(|["Templatecall with id ", x, " is defined multiple times: ",tc2," and ",other2])>
*/          
    propertyassignment-check-id-is-string :
      p@PropertyAssignment("id", x) -> p
      where not(<is-string> x)
          ; !<add-error(|["Templatecall id's can only be plain id's. "])>

    constraint-error-check-propertyassignments(|tc) :
      p@PropertyAssignment(name,val) -> p
      where "id":= name
          ; try(<propertyassignment-check-id-is-string> p)
//          ; try(<propertyassignment-check-id-is-single(|tc)> p)

    constraint-error-ajax =
      (  ?a@AjaxReplace(thediv, _) 
      <+ ?a@AjaxAppend(thediv, _) 
      <+ ?a@AjaxVisibility(thediv,_)
      <+ ?a@AjaxRestyle(thediv,_)
      <+ ?a@AjaxClear(thediv)
      )
      ; where(<is-string> thediv) // only check placeholder Id {}, not placeholder Exp {}
      ; where (<check-id-existence> thediv)
      ; !a
      
    check-id-existence = 
      ? theid
      ;( //id attribute or placeholder exists?
        ( divs := <bagof-HtmlEntityId> theid
        ; 0 := <length> divs
        ; not("this" := theid)
        )
      <+ //tempalte with that name exists?
        ( <OriginalNonUniqueName> theid )
      )
      ; !<add-error(|["Ajax action refers to unknown object with id, please create a template, placeholder or element with proper 'id' attribute.", theid])>

/*
    constraint-error-ajax :
      a@InlineAjaxAction(_, thediv, _) -> a
      where divs := <bagof-HtmlEntityId> thediv
          ; 0 := <length> divs
          ; not("this" := thediv)
          ; !<add-error(|["Ajax action refers to unknown object with id ", thediv])>
*/
   
    constraint-error-ajax =
      (  ?tc@TemplateCall(_,_,passign*,_) 
        <+ ?tc@TemplateCallPropsNoBody(_, _,passign*) 
        <+ ?tc@TemplateCallProps(_, passign*)
        <+ ?tc@TemplateCallPropsNoArgs(_, passign*, _)
      )
      ; where (
          props := <map(\ PropertyAssignment(x,_) -> x \)> passign*
        ; doubles := <find-doubles> props
        ; if (not([] := doubles))
          then !<add-error(|["Properties are defined multiple times - ", <separate-by(|", "); concat-strings> doubles, " in ",tc])>
          else <filter(constraint-error-check-propertyassignments(|tc))> passign*
          end
       )
     ; fail
       
rules

  constraint-error-ajax =
   (  ?a@AjaxReplace(thediv, tcall) 
   <+ ?a@AjaxAppend(thediv,  tcall)
   )
  ; where(<is-defined-tc-get-called-tc; not(IsDefinedAsAjaxTemplate)> tcall)
  ; add-error(|["An ajax-enabled template must be called here, adding the 'ajax' modifier to a template definition makes it ajax-enabled. Note that this template will then be directly accessible from a URL."])
  
