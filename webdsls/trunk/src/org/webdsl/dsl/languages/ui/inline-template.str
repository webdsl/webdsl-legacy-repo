module org/webdsl/dsl/languages/ui/inline-template

rules

  rename-ui :
    InlineTemplateCall(tbody){anno*} -> InlineTemplateCall(tbody1){anno1*}
    with  anno1* := <put-closure-in-anno> anno*
        ; tbody1 := <rename-all> tbody

  desugar-ui : 
    Define(mods, name, args, targs, elems) -> Define(mods, name, args, targs, <rename-all> elems1)
    where <oncetd(?InlineTemplateCall(_))> elems
    with {| InsertedTemplates
          : elems1 := <alltd(desugar-inline-template-call)> elems
         |}

  desugar-inline-template-call :
    InlineTemplateCall(tbody){anno*} -> TemplateCall(template, arg*, [], TemplateBody([
      TemplateCallNoArgs("elements") //make elements() usable in inline templates
    ])) 
    with  template := <toplevel-rename> "inline_template" 
        ; (arg*,farg*) := <get-args-fargs-from-closure-in-anno-filtered> (anno*,tbody)
        ; <emit-webdsl-code> Define([], template, farg*, None(), tbody)
    
rules //TODO disable inline templates for now, needs more work, e.g. ac rules for page should be applied to lifted template  
        
  constraint-error-ui :
    InlineTemplateCall(_) -> <add-error(|["Inline templates are disabled in this version of WebDSL."])>