module org/webdsl/dsl/languages/ui/request-var-template
//similar to org/webdsl/dsl/languages/action/request-var but inside template, var that is not affected by validation reset

imports
  libwebdsl-front

rules

  desugar-action :
    RequestScopeTemplate(TemplateVarDeclInit(x, srt, e)) -> RequestScopeTemplateVarDeclInit(x, srt, e)

  desugar-action :
    RequestScopeTemplate(TemplateVarDecl(x, srt)) -> RequestScopeTemplateVarDecl(x, srt)

rules

  // The initial rename for typecheck will use the rename rules for TemplateVarDeclInit and TemplateVarDecl.
  // Rename rules are still required because after the above desugaring, rename can still be called
  // to retrieve information about the template.
  // Without the rename rules, request vars are not part of the variables passed on to lifted templates

  rename-action :
    v@RequestScopeTemplateVarDecl(x, srt){anno*} -> RequestScopeTemplateVarDecl(y, srt2){anno*}
    where InDef
    with  (y, srt2) := <vardecl-rename-common> (v, x, srt)

  rename-action :
    v@RequestScopeTemplateVarDeclInit(x, srt, e){anno*} -> RequestScopeTemplateVarDeclInit(y, srt2, e'){anno*}
    where InDef
    where (y, srt2, e') := <vardeclinit-rename-common> (v, x, srt, e)
