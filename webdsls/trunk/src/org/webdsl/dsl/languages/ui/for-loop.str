module org/webdsl/dsl/languages/ui/for-loop

rules

  normalize-ui :
    ForNoFilter(x, s, e, te*, sep) -> For(x, s, e, Filter(True(), OrderAscending(Null()), Limit(Int("1000"), Int("0"))), te*, sep)

  normalize-ui :
    ForAllNoFilter(x, s, te*, sep) -> ForAll(x, s, Filter(True(), OrderAscending(Null()), Limit(Int("1000"), Int("0"))), te*, sep)

rules

  rename-ui :
    def@For(x, srt, e1, f1, elems1, elems3) -> For(y, srt2, e2, f2, elems2, elems4)
    with e2 := <rename-all> e1
       ; {| Rename, VarInTemplateContext
          : srt2   := <rename-all> srt
          ; y      := <rename-bound(|srt2)> x
          ; rules(VarDecl :+ y -> def)
          ; <declare-vars-in-context> y
          ; f2     := <rename-all> f1
          ; elems2 := <rename-all> elems1
          |}
       ; elems4 := <rename-all> elems3 // separator cannot refer to loop index

  rename-ui :
    def@ForNoFilter(x, srt, e1, elems1, elems3) -> ForNoFilter(y, srt2, e2, elems2, elems4)
    with e2 := <rename-all> e1
       ; {| Rename, VarInTemplateContext
          : srt2   := <rename-all> srt
          ; y      := <rename-bound(|srt2)> x
          ; rules(VarDecl :+ y -> def)
          ; <declare-vars-in-context> y
          ; elems2 := <rename-all> elems1
          |}
       ; elems4 := <rename-all> elems3 // separator cannot refer to loop index

  rename-ui :
    def@ForAll(x, srt, f1, elems1, elems3) -> ForAll(y, srt2, f2, elems2, elems4)
    with {| Rename, VarInTemplateContext
          : srt2   := <rename-all> srt
          ; y      := <rename-bound(|srt2)> x
          ; rules(VarDecl :+ y -> def)
          ; <declare-vars-in-context> y
          ; f2     := <rename-all> f1
          ; elems2 := <rename-all> elems1
          |}
        ; elems4 := <rename-all> elems3 // separator cannot refer to loop index

  rename-ui :
    def@ForAllNoFilter(x, srt, elems1, elems3) -> ForAllNoFilter(y, srt2, elems2, elems4)
    with {| Rename, VarInTemplateContext
          : srt2   := <rename-all> srt
          ; y      := <rename-bound(|srt2)> x
          ; rules(VarDecl :+ y -> def)
          ; <declare-vars-in-context> y
          ; elems2 := <rename-all> elems1
          |}
        ; elems4 := <rename-all> elems3 // separator cannot refer to loop index

  rename-ui :
    def@ForCount(x, e1, e2, elems1, elems3) -> ForCount(y, e3, e4, elems2, elems4)
    with  e3 := <rename-all> e1
        ; e4 := <rename-all> e2 
        ; {| Rename, VarInTemplateContext
          : y := <rename-bound(|SimpleSort("Int"))> x
          ; rules(VarDecl :+ y -> def)
          ; <declare-vars-in-context> y
          ; elems2 := <rename-all> elems1
          |}  
        ; elems4 := <rename-all> elems3 // separator cannot refer to loop index

rules

  constraint-error-ui : For(_, srt, e, _, _, _)            -> <for-constraint-error(|srt, e)>
  constraint-error-ui : ForNoFilter(_, srt, e, _, _)       -> <for-constraint-error(|srt, e)>
     
  for-constraint-error(|srt, e) :
    x -> <add-error(|[<pp-webdsl> e, " should be a collection of type ", <pp-webdsl> srt])>
    where not(GenericSort(_, [srt]) := <type-of> e)  
    
rules    

  constraint-error-ui : ForCount(x, e1, e2, elems1, sep) -> <expression-type-constraint(|SimpleSort("Int"))> e1
  constraint-error-ui : ForCount(x, e1, e2, elems1, sep) -> <expression-type-constraint(|SimpleSort("Int"))> e2
    