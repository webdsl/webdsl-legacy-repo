module org/webdsl/dsl/languages/ui/declare

strategies

  declare-ui = initial-declare-template-definition 
  declare-ui = initial-declare-page-definition 

strategies // inital declare for globally visible pages/templates

  initial-declare-page-definition :
    Define(mod*, x, farg1*, req*, elem1*){anno*} -> <with(<declare-page-definition> Define(mod*, x, farg1*, req*, elem1*){anno*} )>
    where <fetch(?Page())> mod*

  initial-declare-template-definition :
    Define(mod*, x, farg1*, req*, elem1*){anno*} -> <with(<declare-toplevel-template-definition> Define(mod*, x, farg1*, req*, elem1*){anno*} )>
    where not(<fetch(?Page())> mod*)
        ; not(<fetch(?Email())> mod*)

   /* no local declares here
  declare-template-definition :
    def |[ define mod* x(farg1*) { elem1* } ]| -> <declare-local-template-definition> def |[ define mod* x(farg1*) { elem1* } ]| 
    where (InPage <+ InTemplate)
        ; not(<fetch(?Page())> mod*)
        ; not(<fetch(?Email())> mod*)
    with {| InTemplate
          : <debug(!"local")> x;rules ( InTemplate := x )
          ; <alltd(declare-template-definition)> elem1*
          |}*/
          
rules

  declare-page-definition =
    ?def@Define(mod*, name, args, targs, body)
    ; where(<fetch(?Page())> mod*)
    ; where(check-access-denied-page-redefinition)
    ; with(common-declare-template-rules)
    ; where(with (
      t1* := <map(?Arg(_,<id>))> args
      ; rules (
          IsPage : name
          PageNames :+= name
          CheckPageSignature : (None(), name, t1*) -> SimpleSort("Page")
        )
    ))

  declare-toplevel-template-definition =
    ?Define(mod*, name, fargs, targs, body)
    ; where(not(<fetch(?Page())> mod*))
    ; where(not(<fetch(?Email())> mod*))
    ; with(common-declare-template-rules)
    ; where(with(
        t* := <map(?Arg(_,<id>))> fargs
      ; n := <strip-annos> name
      ; if not(<elem> ((n, t*), <bagof-TopLevelTemplateDefinitions> n)) then
          y := <newname> n
          ; rules (
            TopLevelTemplateNames :+= n
            TopLevelTemplateDefinitions :+ n -> (n, t*)
            OverloadedTemplateSignature : (n, t*) -> y
            CheckPageSignature : (None(), n, t*) -> SimpleSort("Page") // for ajax
            TopLevelTemplateArguments :+ n -> t* // used for overload resolution
          )
        end
    ))

  declare-local-template-definition =
    ?def@Define(mod*, name, fargs, targs, body)
    ; where(not(<fetch(?Page())> mod*))
    ; where(not(<fetch(?Email())> mod*))
    ; with(common-declare-template-rules)
    ; where(with( 
        page := <InPage <+ InTemplate>
        ; t1* := <map(?Arg(_,<id>))> fargs
        ; n := <strip-annos> name
        ; rules (
            LocalTemplateNames :+ page -> name
            LocalTemplateSignature : (n, t1*) -> def
            EnclosingDefinition : name -> page
          )
      ))
 
  declare-template-arg :
    Arg(x, s) -> Arg(x, s)
    with template := <InTemplate>
        ; rules ( TemplateArguments :+ template -> Arg(x, s) ) 
     
  common-declare-template-rules :
    def@Define(mods, name, fargs, targs, body){anno*} -> Define(mods, name, fargs, targs, body){anno*}
    with  t* := <map(?Arg(_,<id>))> fargs
        ; n := <strip-annos> name
        ; genname := <generate-template-name> (n,t*)
        ; rules( TemplateSignature  : (n, t*) -> def
                 TemplateNewName : (n, t*) -> genname //used in overload resolution
                 TemplateDefinition : n -> def // only for use in code gen when renaming is applied which makes the names unique
                 TemplateArguments  : n -> fargs //''
                 TemplateRequires   : (n, t*) -> targs
                 TemplateDefinitionFromAnnoName : genname -> def
               )
        //template arguments
        ; actions := <collect(?Action(_,_,_)<+?Define(_,_,_,_,_));remove-all(?Define(_,_,_,_,_))> body
        ; rules( TemplateActions : (n, t*) -> actions )
        ; if <oncetd( // check for each variant of elements for robustness
            ?TemplateCallNoBody("elements",_)
            <+?TemplateCallNoArgs("elements")            
            <+?TemplateCallNoAssigns("elements",_,_)  
            <+?TemplateCallPropsNoBody("elements",_,_)
            <+?TemplateCallProps("elements",_)        
            <+?TemplateCallPropsNoArgs("elements",_,_)
            <+?TemplateCallBody("elements",_)   
            <+?TemplateCall("elements",_,_,_) 
            )> body then rules( TemplateContainsElementsCall : genname ) end
        // generics    TODO: simplified lookup for generics
        ; if <oncetd(?SimpleSort("EntityType"))> fargs then rules (TemplateWithGenerics : name) end

rules //utility rule to easily get the unique name generated for a define constructor

  define-cons-to-genname : 
    def@Define(mods, name, fargs, targs, body){anno*} -> result
    with  t* := <map(?Arg(_,<id>))> fargs
        ; n := <strip-annos> name
        ; result := <TemplateNewName> (n, t*)
        
rules 

  generate-template-name :
    (n,t*) -> name
    with arg_part := <map(generate-template-name-helper)> t*
       ; name := <concat-strings> [n | arg_part]
       
  generate-template-name-helper:
    SimpleSort(s) -> s
    
  generate-template-name-helper:
    GenericSort(x,[SimpleSort(s)]) -> <concat-strings> [x,s]
        
strategies

  declare-page-action(|direct-enclosing-define-name) =
    ?def@Action(name, fargs, body)
    ; with( 
        t1* := <map(?Arg(_,<id>))> fargs 
      )
      ; rules(
          ActionDefinition  : name -> def
          ActionArguments  : name -> fargs
  
          CheckActionSignature :
            (direct-enclosing-define-name, name, t2*) -> SimpleSort("Void")
            where <zip(type-compatible)> (t1*, t2*)
        )

