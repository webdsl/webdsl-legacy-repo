module org/webdsl/dsl/languages/ui/template-ref-arguments

imports
  libstrategolib
  libwebdsl-front
  org/webdsl/dsl/languages/ui/templatecall-utils

rules
  
  //RefSort only allowed in template arguments	
  constraint-error-ui :
    Arg(_,RefSort(_)){anno*} -> <add-error(|["Reference argument Ref<Type> only allowed in formal parameters of templates (neither page nor ajax)"])>
    where <not(fetch(IsNonPageNonAjaxTemplateArg))> anno*
    
  //only pass lvalue to ref argument
  constraint-error-ui :
    x -> <fail>
    where uniqtname := <is-defined-tc-get-called-tc> x
        ; fargs := <TemplateArguments> uniqtname
        ; args := <get-tc-args> x
        ; <zip(try(constraint-error-ui-ref-arg))> (fargs,args)
          
  constraint-error-ui-ref-arg :
    (Arg(_,RefSort(_)), e) -> <add-error(|["Invalid argument: '",epp,"' is not assignable, which is required for reference argument"])> e
    where <type-of> e
        ; not(<is-lvalue> e)
    with  epp := <pp-webdsl-to-string> e