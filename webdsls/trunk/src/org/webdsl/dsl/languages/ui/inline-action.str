module org/webdsl/dsl/languages/ui/inline-action

rules

  rename-ui :
    InlineAction(blk){anno*} -> InlineAction(blk1){anno1*}
    with  anno1* := <put-closure-in-anno> anno*
        ; blk1 := <rename-all> blk
        
  desugar-ui : 
    Define(mods, name, args, targs, elems) -> result
    where not(<RemovedInlineActions> name)
    with {| InsertedActions  
          : elems1 := <alltd(desugar-inline-action-call)> elems
          ; elems2 := <concat> [elems1, <bagof-InsertedActions>]
         |}
       ; result@Define(_, renamedname, _, _, _) := <rename> Define(mods, name, args, targs, elems2)
       ; rules( RemovedInlineActions : renamedname )      
  
  desugar-inline-action-call :
    InlineAction(Block(e1)){anno*} -> ThisCall(fun, arg*)
    with  fun := <newname> "inline_action"
        ; (arg*,farg*) := <get-args-fargs-from-closure-in-anno-filtered> (anno*,e1)
        ; newaction := Action(fun, farg*, Block(e1))
        ; rules( InsertedActions :+= newaction )
        