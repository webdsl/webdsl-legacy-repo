module org/webdsl/dsl/languages/ui/inline-action

rules

  rename-ui :
    a@InlineAction(blk){anno*} -> InlineAction(blk1){anno1*}
    with  {| InActionContext
        : rules( InActionContext := a)
        ; anno1* := <put-closure-in-anno> anno*
        ; blk1 := <rename-all> blk
          |}
        
  desugar-ui : 
    Define(mods, name, args, targs, elems){anno*} -> result
    where <oncetd(?InlineAction(_))> elems
    with {| InsertedActions  
          : elems1 := <alltd(desugar-inline-action-call)> elems
          ; elems2 := <concat> [elems1, <bagof-InsertedActions>]
         |}
       ; result := <rename> Define(mods, name, args, targs, elems2){anno*}
  
  desugar-inline-action-call :
    InlineAction(Block(e1)){anno*} -> ActionCall(fun, arg*)
    with  fun := <toplevel-rename> "inline_action"
        ; (arg*,farg*) := <get-args-fargs-from-closure-in-anno-filtered> (anno*,e1)
        ; newaction := Action(fun, <strip-ref-sort-from-fargs> farg*, Block(e1)) // strip ref sort, currently ref arg is not implemented for actions, a ref arg is always dereferenced and passed by value
        ; rules( InsertedActions :+= newaction )
        
  strip-ref-sort-from-fargs = map(strip-ref-sort-from-farg)
  strip-ref-sort-from-farg : Arg(name,srt) -> Arg(name,<strip-ref-sort> srt)