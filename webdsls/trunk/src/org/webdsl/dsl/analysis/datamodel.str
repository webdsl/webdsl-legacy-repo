module org/webdsl/dsl/analysis/datamodel

imports
  libstrategolib
  Java-15
  libjava-front

imports
  libwebdsl-front

strategies
 
  analyze =
    (?For(x, srt, _, _, _) <+ ?ForAll(x, srt, _, _) <+ ?ForStmt(x, srt, _, _, _) <+ ?ForAllStmt(x, srt, _, _))
    ; ?t
    //; debug(!"Checking for joinable stuff: ")
    ; {| IteratedVars, IteratedVar
       : rules ( IteratedVars :+= (x, srt)
                 IteratedVar : Var(x) -> srt )
       //; info(|["Looking for property use of variable ", x, " of type ", srt])
       ; fas := <collect-potential-joins> t
       //; info(|["Field accesses to analyze from ", x, ": ", fas])
       ; joincandidates := <filter(an-requires-joining); make-set> fas
       //; debug(!"Join candidates: ")
       ; cleant := <topdown(try(strip-annos))> t
       ; rules ( PropertiesToJoin : cleant -> joincandidates )
       
       //; info(|["These should be inlined: ", inlinecandidates])
      |}

  collect-potential-joins =
    collect(an-potential-joins)
    ; flatten-list
  
  an-potential-joins =
    where(
      ?FieldAccess(l, _)
      ; oncetd(where(IteratedVar); not(IteratedVarChild))
    )

  an-potential-joins =
    ?fa@FieldAccess(l, _)
    ; repeat(oncetd(IteratedVarChild))
    ; not(?fa)

  an-potential-joins =
    ?tc@TemplateCall(_, args,_, _)
    //; debug(!"Template call found: ")
    //; info(|["IteredVars: ", <dr-all-keys(|"IteratedVar")>])
    ; not([] := <collect(\ Var(x) -> <IteratedVar> \)> args)
    ; find-definition
    //; debug(!"Definition found: ")
    ; not(?Define([Page()], _, _, _))
    ; ?Define(_, _, fargs, body)
    ; <an-potential-joins-with-args(|args, fargs)> body

  an-potential-joins =
    ?tc@ThisCall(_, args)
    //; debug(!"Function call found: ")
    ; not([] := <collect(\ Var(x) -> <IteratedVar> \)> args)
    ; find-definition
    //; debug(!"Searching in function: ")
    ; ?Function(_, fargs, _, body)
    ; <an-potential-joins-with-args(|args, fargs)> body

  an-potential-joins-with-args(|args, fargs) =
    ?body
    ; {| IteratedVarChild
       : with(id
         ; <filter({vars: 
                     \ (e, Arg(x, srt)) ->  <id>
                       where not([] := <collect(where(IteratedVar))>)
                       with rules( IteratedVarChild : Var(x) -> e 
                                   IteratedVar      : Var(x) -> srt
                                 )
                     \})> <zip> (args, fargs)
         ) 
       ; <collect-potential-joins> body
     |}


strategies // checks for potential inlining

  an-find-entity-property-pair =
    ?fa@FieldAccess(l, fld)
    ; if Var(x) := l then
        !<concat-strings> ["_", fld]
      else
        !<concat-strings> [<an-find-entity-property-pair> l, "._", fld]
      end
  
  an-requires-joining =
    ?FieldAccess(fa@FieldAccess(_, _), _)
    ; <an-find-entity-property-pair> fa
