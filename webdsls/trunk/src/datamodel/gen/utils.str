module utils

strategies

  /* Create a nested directory structure. Works on a
     list containing the directories to be created,
     returns the path to the last (innermost) directory
     Current working directory remains unchanged.

     @type List(String) -> String
  */
  
  create-dirs = 
    where(root := <getcwd>) 
	; map((file-exists <+ mkdir(|"w")) ; chdir) 
	; getcwd // return path to innermost created dir.
    ; where (<chdir> root) // restore old cwd'
    
  capitalize-string =
    explode-string
    ; [to-upper | id]
    ; implode-string
    
    signature
  constructors
    JavaFile : String * String * CompilationUnit -> File

strategies

  java-file-to-file =
    ?JavaFile(pkg, name, cu)
    ; where(
        path := <string-tokenize(|['.']); create-dirs> pkg
	    ; file := <fopen> (<concat-strings> [path, "/", name, ".java"], "w")
	    ; content := <pp-java5-to-string> cu
	    ; <fputs> (content, file)
	  )