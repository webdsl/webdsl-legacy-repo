module analysis-manual

imports
  include/WebDSL
  lib/index-library
  lib/analysis-library
  names
    
rules // Adjust diff
    
  // Add Type to the constructors to check for differences.
  index-diff-constructors = ?DefData(_, Type(), _)
  
  // Type definitions are equal if their URI is equal and if their values are equal.
  index-diff-compare:
    (DefData(u1, Type(), v1), DefData(u2, Type(), v2)) -> <id>
    where
       <index-uri-eq> (u1, u2);
       <index-key-eq> (v1, v2)
       
// rules // Primitives
//   
//   // Return primitives as well.
//   adjust-index-lookup(target|namespace, path, prefix): 
//     Type(<target>) -> <concat> [primitives, [[class() | path]]]
//     with
//       primitives := <map(type-of; type-to-def)> <primitive-types>

rules // Import adjust rule for naming language
     
  import-transitive =
    collect-one(?Transitive())
  import-standard =
    not(import-transitive)
    
  import-values(|namespace, path) =
    <index-get-all-values> DefData([namespace| path], Import(), ())
    
  import-currentfile-values(|namespace) =
    <index-get-all-values> DefData([namespace|[<index-file-to-string> <index-get-current-file>]], Import(), ())  
    
  adjust-index-import(|namespace, path):
    _ -> <concat> [[[namespace | path]], currentFileImportDefs, importDefs]
    with
      uri := [namespace | path]
    where
      allImports := <import-values(|namespace, path)> ;
      importDefs := <mapconcat(import-defs(|namespace, [uri]))> allImports
    where
      currentfileImports := <import-currentfile-values(|namespace)> ;
      currentFileImportDefs := <mapconcat(import-defs(|namespace, [uri]))> currentfileImports
    
  import-defs(|namespace, seen):
    (key, <import-standard>) -> [uri]
    with
      uri := [namespace | <index-uri-path> key]
      
  import-defs(|namespace, seen):
    (key, <import-transitive>) -> <conc> ([uri], transitiveUris)
    with
      path := <index-uri-path> key ;
      uri := [namespace | path] ;
      
      if not(<elem> (uri, seen)) then
        allImports := <import-values(|namespace, path)> ;
        transitiveUris := <mapconcat(import-defs(|namespace, <conc> (seen, [uri])))> allImports
      else
        transitiveUris := []
      end
      